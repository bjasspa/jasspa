; -!- emf -!-
; This is part of the JASSPA MicroEmacs macro files
; Copyright (C) 1999-2004 JASSPA (www.jasspa.com)
; See the file me.emf for copying and conditions.
;
; Synopsis:    Java doc hilighting and extensions.
; Authors:     Jon Green
;
!if &and &sin "h" .fhook-java.setup &band .hilight.flags 0x02
    ; Create a new javadoc hilighting scheme
    !if &not &exi .hilight.javadoc
        set-variable .hilight.javadoc &pinc .hilight.next 1
    !endif
    ; Add a hook into the normal Java hilighing. We jump into
    ; javadoc when we see an opening "/**" .
    hilight .hilight.java 0x80 "/\\*\\*" .hilight.javadoc .scheme.comment
    ; Javadoc hilighting starts here
    0 hilight .hilight.javadoc  2 50                      .scheme.comment
    ; &nbsp; etc.
    hilight .hilight.javadoc    0 "&\\w\\w;"              .scheme.variable
    hilight .hilight.javadoc    0 "&\\w\\w\\w;"           .scheme.variable
    hilight .hilight.javadoc    0 "&\\w\\w\\w\\w;"        .scheme.variable
    hilight .hilight.javadoc    0 "&\\w\\w\\w\\w\\w;"     .scheme.variable
    hilight .hilight.javadoc    0 "&\\w\\w\\w\\w\\w;"     .scheme.variable
    hilight .hilight.javadoc    0 "&\\w\\w\\w\\w\\w\\w;"  .scheme.variable
    ; "<pre> ... </pre>"
    hilight .hilight.javadoc    4 "<pre>" "</pre>" ""     .scheme.variable
    ; <anyWord>
    hilight .hilight.javadoc    4 "</?\\w+" ">" ""        .scheme.variable
    ; {@link URL}
    hilight .hilight.javadoc    4 "{@link" "}" ""         .scheme.link
    ; "@anyWord" - Use branch token and close when not a word!
    hilight .hilight.javadoc    4 "@" "\\}\\W" ""         .scheme.keyword
    ; We must return to Java when we see the close of comment "*/"
    hilight .hilight.javadoc 0x80 "\\*/" .hilight.java    .scheme.comment
!endif

; Hilight curly braces - add to myjava.emf
!if &and &sin "h" .fhook-java.setup &band .hilight.flags 0x02
    ; In keyword colour
    hilight .hilight.java 1 "{"                           .scheme.keyword
    hilight .hilight.java 1 "}"                           .scheme.keyword
!endif

; Hilight TODO in a comment - Examples 
; i.e. /* TODO I need to do this */
!if &and &sin "h" .fhook-java.setup &band .hilight.flags 0x02
    hilight .hilight.java   20 "/\\*\\s +[Tt][Oo][Dd][Oo]" "*/" "" .scheme.error
!endif
