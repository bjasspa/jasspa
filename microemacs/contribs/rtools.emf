;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;
;
;  Author        : Detlef Groth, University of Potsdam, Germany
;  Created By    : Detlef Groth
;  Created       : Wed Dec 4 17:39:58 2024
;  Last Modified : <241214.2155>
;
;  Description   : functions useful to be used for R development
;
;  Notes         : - r-format requires the R package formatR to be installed
;                  - r-lint   requires the R package lintr to be installed
;
;  History       : 2024-12-04 - first version
;
;  Copyright (c) 2024 Detlef Groth, University of Potsdam, Germany
;  License       : Same as MicroEmacs - GPL 2
;
;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;
define-macro r-format
  !if &bmod "edit"
    set-variable #l0 @mc5 "Save buffer first (?/y/n) ? " "nNyY" "(Y)es, (N)o, (C-g)Abort ? "
    !if &iseq #l0 "y" 
      save-buffer 
    !endif
  !endif 
  set-variable #l0 &reg &spr "/history/%s/char-set" %platform ""
  set-variable #l0 &xre #l0 "iso" "ISO-" #l0
  set-variable #l0 &xre #l0 "cp" "CP" #l0    
  set-variable #l0 &xre #l0 "microsoft-" "" #l0        
  2 pipe-shell-command &spr "Rscript  -e 'options(\"encoding\"=\"%s\");formatR::tidy_file(\"%s\")'" #l0 $buffer-fname "*command*"
  !if &equ $result 1
    ml-write "Error: It seems that R package formatR is not installed!"
  !else
    0 reread-file
  !endif
!emacro

define-help "r-format" "3"
## NAME

__r-format__ - format an R script file using the R package formatR
$a

## SYNOPSIS

__r-format__  
__%rscript-com__ "string" ; Default "Rscript -e"

## DESCRIPTION

The __r-format__ macro formats the currently edited file using the R library
formatR which must be installed by the user for instance using the usual 
`install.packages('formatR')` syntax from within a R console.

## EXAMPLE

Here an example to format the currently edited R script file.

```{.emf}
    ; during editing an R file press Esc x and then write
    r-format
    ; and then press the ENTER key
```


## NOTES

The __r-format__ macro is implemented in the file _rtools.emf_ which is currently 
placed in the `microemacs/contribs` project folder. For installation copy that 
file into your user-path folder - usually `~/.config/jasspa` and make the macro
available by placing the following line into your `~/.config/jasspa/myr.emf` file.

```{.emf}
    ; within myr.emf add this line 
    ; or extend the rtools line
    define-macro-file rtools r-format
```
    
## SEE ALSO

[r(9)](r\(9\)), [r-doc(3)](r-doc\(3\)), [r-exec(3)](r-exec\(3\)) and [r-lint(3)](r-lint\(3\)).

!ehelp

define-macro r-lint
  !force set-variable #l1 .lint
  set-variable #l0 &reg &spr "/history/%s/char-set" %platform ""
  set-variable #l0 &xre #l0 "iso" "ISO-" #l0
  set-variable #l0 &xre #l0 "cp" "CP" #l0    
  set-variable #l0 &xre #l0 "microsoft-" "" #l0        
  !if &seq #l1 "ERROR"
    set-variable .lint "Rscript -e 'options(\"encoding\"=\"%s\");library(lintr);lintr::lint(\"%s\",linters = linters_with_defaults( indentation_linter = indentation_linter(indent = 4L) ))'" #l0
    set-variable #l1 .lint
  !endif
  !if &bmod "edit"
    set-variable #l2 @mc5 "Save buffer first (?/y/n) ? " "nNyY" "(Y)es, (N)o, (C-g)Abort ? "
    !if &iseq #l2 "y" 
      save-buffer 
    !endif
  !endif 
  2 pipe-shell-command &spr #l1 #l0 $buffer-fname "*lintr*"
  !if &equ $result 1
    ml-write "Error: It seems that R package lintr is not installed!"
  !else
    set-variable .buffer $buffer-bname
    next-window-find-buffer "*lintr*"
    buffer-init "r-lint"
    set-variable :mouse-word-select r-lint-find-line
    buffer-bind-create "bio" "return" "0" r-lint-find-line
  !endif
!emacro

0 define-macro r-lint-find-line
  set-variable #l0 @wl
  !if &xse #l0 "\\(.+\\):\\([0-9]+\\):\\([0-9]+\\):.+"
    next-window-find-buffer .r-lint.buffer
    goto-line @s2
    beginning-of-line
    @s3 forward-char
  !else
    ml-write "Not a error line!"
  !endif
!emacro

define-macro fhook-r-lint
  buffer-init-hooks
!emacro

set-variable .fhook-r-lint.name "R Lint"
set-variable .fhook-r-lint.setup &reg "/history/fhook/r-lint" "acfhmw"
set-variable .fhook-r-lint.setup-mask "acfhmw"

!if &and &sin "h" .fhook-r-lint.setup &band .hilight.flags 0x02
  !iif &not &exi .hilight.r-lint  set-variable .hilight.r-lint &pinc  .hilight.next 1
  0 hilight .hilight.r-lint 2 200                                     $global-scheme
  hilight .hilight.r-lint   0  "[/\\.A-Za-z0-9]+:[0-9]+:[0-9]+:"      .scheme.link  
  hilight .hilight.r-lint   1 "commas_linter"                         .scheme.operator
  hilight .hilight.r-lint   1 "infix_spaces_linter"                   .scheme.operator
  hilight .hilight.r-lint   1 "function_left_parentheses_linter"      .scheme.operator
  hilight .hilight.r-lint   1 "indentation_linter"                    .scheme.operator  
  ;hilight .hilight.r-lint   0 "\[.+\]"   .scheme.operator  ; does not work
!endif

add-file-hook "*lintr*" fhook-r-lint

define-help "r-lint" "3"
## NAME

__r-lint__       - check an R script file for possible problems
_.r-lint.lint_ - linting command to be executed   
$a

## SYNOPSIS

__r-lint__   
__set-variable__ _.r-lint.lint_ "Rscript -e '...'"

## DESCRIPTION

The __r-lint__ macro checks the currently edited file using the R package
`lintr` which must be installed by the user for instance using the usual 
`install.packages('lintr)` syntax from within a R console. 

After executing the macro a popup-window (`*lintr*`) displays the warning
messages of the linter package, the user can click on the hilighted lines
showing  warnings and jump directly to the problematic line. The same can
be achieved by pressing th ENTER key on the hilighted line without using
a mouse.

Setting the variable _.r-lint.lint_ allows you to customize the linter settings.

## EXAMPLE

Here an example to lint the currently edited R script file.

```{emf}
    ; inside myr.emf file configure your linter to use 2 char indentation instead of default 4
    ; by modifying the macro variable .r-lint.lint
    set-variable .r-lint.lint "Rscript -e 'library(lintr);lintr::lint(\"%s\",linters = linters_with_defaults( indentation_linter = indentation_linter(indent = 2L) ))'"
    ; during editing an R file press Esc x and then write
    r-lint
```


## NOTES

The __r-lint__ macro is implemented in the file _rtools.emf_ which is currently 
placed in the `microemacs/contribs` project folder. For installation copy that 
file into your user-path folder - usually `~/.config/jasspa` and make the macro
available by placing the following line into your `~/.config/jasspa/myr.emf` file.

```{.emf}
    ; within myr.emf add this line 
    ; or extend the rtools line
    define-macro-file rtools r-lint
```
    
## SEE ALSO

[r(9)](r\(9\)), [r-doc(3)](r-doc\(3\)), [r-exec(3)](r-exec\(3\)) and [r-format(3)](r-format\(3\)).

!ehelp

define-macro r-rscript
  !force set-variable #l0 %rscript-com
  !if &and &seq #l0 "ERROR" &not &seq $platform "windows"
    !if &not &seq &which "Rscript" ""
      set-variable %rscript-com "Rscript"
    !endif
  !else 
    set-variable $file-names "C:/Program Files/R/R-.*"
    !while &not &seq &set #l0 $file-names ""
      !if &xse #l0 "R-[3-9].[0-9].[0-9].+"
        set-variable %rscript-com &spr "\"C:/Program Files/R/%sbin/Rscript.exe\"" #l0
      !endif
    !done
  !endif
  !if &seq &which %rscript-com ""
    1000 ml-write &spr "Error: Executable '%s' is not in the path! Please install it!" %rscript-com
    !abort
  !else
    ml-write &spr "OK: Executable is '%s'!" %rscript-com
  !endif
!emacro

; execute a R file using Rscript executable directly from MicroEmacs using the current
; ISO or Windows encodings
define-macro r-exec
  !if &bmod "edit"
    set-variable #l0 @mc5 "Save buffer first (?/y/n) ? " "nNyY" "(Y)es, (N)o, (C-g)Abort ? "
    !if &iseq #l0 "y" 
      save-buffer 
    !endif
  !endif 
  ; command line arguments
  set-variable #l1 ""
  !if @?
    set-variable #l1 @ml "Add command line arguments" 
  !endif
  set-variable #l0 &reg &spr "/history/%s/char-set" %platform ""
  set-variable #l0 &xre #l0 "iso" "ISO-" #l0
  set-variable #l0 &xre #l0 "cp" "CP" #l0    
  set-variable #l0 &xre #l0 "microsoft-" "" #l0        
  0 pipe-shell-command &spr "Rscript  --encoding=%s %s %s" #l0 $buffer-fname #l1 "*command*"
!emacro

define-help "r-exec" "3"
## NAME

__r-exec__ - executes the current R script with or without command line arguments
$a

## SYNOPSIS

_n_ __r-exec__   

## DESCRIPTION

The __r-exec__ macro executed the currently edited file using the Rscript command line
interpreter. If any numeric argument is given before calling the script, then the user
is asked for command line arguments which should be given on the message line.

After executing the macro a popup-window (`*command*`) displays the results of the script
execution. The script executes the R file using the current character encodings.


## NOTES

The __r-exec__ macro is implemented in the file _rtools.emf_ which is currently 
placed in the `microemacs/contribs` project folder. For installation copy that 
file into your user-path folder - usually `~/.config/jasspa` and make the macro
available by placing the following line into your `~/.config/jasspa/myr.emf` file.

```{.emf}
    ; within myr.emf add this line 
    ; or extend the rtools line
    define-macro-file rtools r-exec
```
    
## SEE ALSO

[r(9)](r\(9\)), [r-doc(3)](r-doc\(3\)), [r-format(3)](r-format\(3\)), [r-lint(3)](r-lint\(3\)).

!ehelp

