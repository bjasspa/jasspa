; -!- emf -!-
; Copyright (C) 2023-2024 JASSPA (www.jasspa.com)
;
; This is part of JASSPA's MicroEmacs, see the LICENSE file for licensing and
; copying information.
;
; Synopsis:    ME single executable boot-strap
; Authors:     Steven Phillips
;
; Notes:       If it exists add .about.userpath first to search path so user can copy new & override files into directory
;
!if &set .about.hasssn &seq "R" &stat t &spr "%s%s.emf" &set .about.userpath &lef $progname &rsin "/" $progname $user-name
  set-variable $search-path &spr "%s%s%s" &set $user-path .about.userpath &con &band $system 0x80 ":" ";" $search-path
!elif &set .about.hasssn &seq "D" &stat t &set .about.userpath &spr "%s%s/" $progdata $user-name
  set-variable $search-path &spr "%s%s%smacros%s%sspelling%s%s" &set $user-path .about.userpath &set #l1 &con &band $system 0x80 ":" ";" $progdata #l1 $progdata #l1 $search-path
  set-variable .about.hasssn &seq "R" &stat t &spr "%s%s.emf" $user-path $user-name
!endif
execute-file "me"
!iif .about.hasssn  !return

define-macro init-session
  set-position "\x81"
  !if &seq "X" &stat "t" &set #l1 &cat "file:" &lef .about.userpath -1
    !if &seq "X" &stat "t" &set #l2 &lef #l1 &sub &rsin "/" #l1 1
      ; On UNIX try to create the ~/.config directory in case it doesn't exist yet
      !iif &band $system 0x80  !force 0x100 file-op &lef #l2 &rsin "/" #l2
      !force 0x100 file-op #l2
    !endif
    !force 0x100 file-op #l1
  !endif
  !if &not &seq "D" &stat t #l1
    ml-write &spr "[Error: Cannot create config path: %s]" .about.userpath
    !abort
  !elif &seq "X" &stat "t" &set #l0 &spr "file:%s%s.emf" &set $user-path &stat a .about.userpath $user-name
    !force 0 delete-buffer "*mes-tmp*"
    find-buffer "*mes-tmp*"
    insert-file "tfs://meuser.emf"
    !force write-buffer #l0
    !iif &not $status  ml-write &spr "[Error: Cannot write to file %s]" #l0
    !force delete-buffer $buffer-bname
  !endif
  set-variable $search-path &spr "%s%s%smacros%s%sspelling%s%s" $user-path &set #l0 &con &band $system 0x80 ":" ";" $user-path #l0 $user-path #l0 $search-path
  set-variable .session.update 1
  0 update-session
  goto-position "\x81"
!emacro
!iif &seq $buffer-bname "*scratch*" ml-write &spr "[Run init-session to create session in: %s]" .about.userpath
