; -!- emf -!-
; This is part of the JASSPA MicroEmacs macro files
; Copyright (C) 2002-2009 JASSPA (www.jasspa.com)
; See the file me.emf for copying and conditions.
;
; Created:     Fri May 24 2002
; Synopsis:    Standard basic toolbar tools
; Authors:     Steven Phillips
;
0 define-macro buffer-info-mouse-drop-1
    set-variable #l0 0
    !if &equ $window-line 4
        ; on the attr line
        !if &gre $window-col 10
            ; on the changeable file fmod bits
            !if &les &set #l1 &sub $window-col 10 &cond &band $system 0x80 10 5
                set-variable #l0 1
            !endif
        !elif &gre $window-col 8
        !elif &gre $window-col 5
            ; on the file type
            set-variable #l0 2
        !endif
    !endif
    goto-position "\x82"
    !if #l0
        !if &equ #l0 1
            ; on the changeable file fmod bits
            !if &band $system 0x80
                set-variable $buffer-fmod &bxor $buffer-fmod &lget "|256|128|64|32|16|8|4|2|1|" #l1
            !else
                set-variable $buffer-fmod &bxor $buffer-fmod &lget "|1|2|4|32|" #l1
            !endif
        !elif &not &band $buffer-fmod 0x10000
            ; on the file type
            set-variable #l1 &lget "|1|2|3|7|1|" &add 1 &lfind "|1|2|3|7|" &add &add &bmod "cr" &mul 2 &bmod "lf" &mul 4 &bmod "ctrlz"
            &con &band 1 #l1 1 -1 buffer-mode "cr"
            &con &band 2 #l1 1 -1 buffer-mode "lf"
            &con &band 4 #l1 1 -1 buffer-mode "ctrlz"
        !endif
        ; 1 buffer-mode "edit"
        buffer-info-create
    !endif
!emacro

0 define-macro buffer-info-create
    set-position "\x86"
    set-variable #l0 $buffer-fname
    !if &band $buffer-fmod 0x10000
        set-variable #l2 "Dir d"
    !else
        set-variable #l2 &lget "|?0?|Unx|Mac|Win|?4?|?5?|?6?|Dos|" &add 1 &add &add &bmod "cr" &mul 2 &bmod "lf" &mul 4 &bmod "ctrlz"
        set-variable #l2 &cat #l2 " -"
    !endif
    !if &band $system 0x80
        ; unix system
        set-variable #l2 &cat #l2 &cond &band $buffer-fmod 256 "r" "-"  
        set-variable #l2 &cat #l2 &cond &band $buffer-fmod 128 "w" "-"  
        set-variable #l2 &cat #l2 &cond &band $buffer-fmod  64 "x" "-"  
        set-variable #l2 &cat #l2 &cond &band $buffer-fmod  32 "r" "-"  
        set-variable #l2 &cat #l2 &cond &band $buffer-fmod  16 "w" "-"  
        set-variable #l2 &cat #l2 &cond &band $buffer-fmod   8 "x" "-"  
        set-variable #l2 &cat #l2 &cond &band $buffer-fmod   4 "r" "-"  
        set-variable #l2 &cat #l2 &cond &band $buffer-fmod   2 "w" "-"  
        set-variable #l2 &cat #l2 &cond &band $buffer-fmod   1 "x" "-"  
    !else
        ; MS system (probably)
        set-variable #l2 &cat #l2 &cond &band $buffer-fmod  1 "-" "w"
        set-variable #l2 &cat #l2 &cond &band $buffer-fmod  2 "h" "-"
        set-variable #l2 &cat #l2 &cond &band $buffer-fmod  4 "s" "-"
        set-variable #l2 &cat #l2 &cond &band $buffer-fmod 32 "a" "-"
    !endif
    set-variable #l1 ""
    set-variable #l3 ""
    !if &seq "" #l0
        ; no file name
    !elif &seq &lef .toolbar-redraw.bn 1 "*"
        ; A '*' buffer like *grep* - ingore the 'file'
    !elif &seq &lef #l0 6 "ftp://"
        ; a ftp file, &stat 'm' & 's' does not work on these files
    !elif &seq &lef #l0 7 "http://"
        ; a http file, &stat 'm' & 's' does not work on these files
    !elif &or &not &len &lget :buffer-info 2 &band @# 0x013
        set-variable #l1 &stat "m" $buffer-fname
        set-variable #l1 &spr "%d-%02d-%02d" &lef #l1 4 &mid #l1 7 2 &mid #l1 9 2
        set-variable #l3 &stat "s" $buffer-fname
        set-variable #l3 &cond &les #l3 0 "-" #l3
        set-variable :buffer-info &spr "|%s|%s|%s|" &lget :buffer-info 1 #l1 #l3
    !else
        set-variable #l1 &lget :buffer-info 2
        set-variable #l3 &lget :buffer-info 3
    !endif
    !if &seq $buffer-fhook ""
        set-variable #l4 "<unknown>"
    !else
        set-variable #l4 &ind &spr ".%s.name" $buffer-fhook
        !if &seq #l4 "ERROR"
            set-variable #l4 &cat &sup &mid $buffer-fhook 6 1 &rig $buffer-fhook 7
        !endif
        set-variable #l4 &spr "%s (%s)" #l4 &rig $buffer-fhook 6
    !endif
    find-buffer "*buffer-info*" 
    -1 buffer-mode "undo"
    -1 buffer-mode "view"
    beginning-of-buffer
    set-mark
    end-of-buffer
    -1 kill-region
    insert-string &spr "File Info - %s\n\n" .toolbar-redraw.bn
    ;insert-string &spr "State:  %s\n" &cond &nbm .toolbar-redraw.bn "edit" "Modified" "Unchanged"
    insert-string &spr "Type: %s\n" #l4
    insert-string &spr "Attr: %s\n" #l2
    insert-string &spr "Date: %s\n" #l1
    insert-string &spr "Size: %s\n" #l3
    insert-string &spr "Name: %s\n" &rig #l0 &rsin "/" #l0
    insert-string &spr "Path: %s\n" &lef #l0 &rsin "/" #l0
    beginning-of-buffer
    set-variable $line-scheme .scheme.header
    set-variable :mouse-pick-1 void
    set-variable :mouse-drop-1 buffer-info-mouse-drop-1
    -1 buffer-mode "edit"
    1 buffer-mode "view"
    goto-position "\x86"
!emacro

0 define-macro buffer-list-create-single
    set-variable #l1 &lget &ind &set #l2 &spr ":%s:buffer-info" #p9 1
    !if &sin "D" #l1
        set-variable #l4 "D"
    !elif &nbmod #p9 "nact"
        set-variable #l4 " "
    !elif &exi &spr ":%s:watch" #p9
        set-variable #l4 "W"
    !else
        set-variable #l4 "@"
    !endif
    !if &nbmod #p9 "edit"
        set-variable #l4 &cat #l4 &con &sin "S" #l1 "S " "* "
    !else
        !if &sin "S" #l1
            set-variable &ind #l2 &lset &ind #l2 1 &rep #l1 "S" ""
        !endif
        !if &nbmod #p9 "view"
            set-variable #l4 &cat #l4 "% "
        !else
            set-variable #l4 &cat #l4 "  "
        !endif
    !endif
    insert-string &cat #l4 #p9
    insert-newline
!emacro

0 define-macro buffer-list-command
    !if &equ @# -2
        goto-position "\x82"
        set-variable $buffer-names ".*"
        !while &not &seq &set #l0 $buffer-names ""
            !if &sin "S" &lget &ind &spr ":%s:buffer-info" #l0 1
                find-buffer #l0
                save-buffer @mna
            !endif
        !done
        set-variable $buffer-names ".*"
        !while &not &seq &set #l0 $buffer-names ""
            !if &not &nbmod #l0 "nact"
            !elif &sin "D" &lget &ind &spr ":%s:buffer-info" #l0 1
                delete-buffer #l0 @mna
            !endif
        !done
        set-variable $buffer-names ".*"
        !while &not &seq &set #l0 $buffer-names ""
            !if &sin "D" &lget &ind &spr ":%s:buffer-info" #l0 1
                delete-buffer #l0 @mna
            !endif
        !done
        ; force a toolbar update on buffers
        set-variable .toolbar-redraw.bn "\CH"
    !elif &les $window-line 2
    !elif &seq &set #l9 &rig @wl 3 ""
    !elif &equ @# -1
        !force !force delete-buffer #l9 @mna
        !if $status
            ; force a toolbar update on buffers
            set-variable .toolbar-redraw.bn "\CH"
        !endif
    !elif &not @#
        set-variable #l1 "D"
        !jump 3
    !elif &equ @# 1
        set-variable #l1 "S"
        !if &not &exi &set #l2 &spr ":%s:buffer-info" #l9
            set-variable &ind #l2 &lset "||" 1 #l1
        !elif &sin #l1 &set #l3 &lget &ind #l2 1
            set-variable &ind #l2 &lset &ind #l2 1 &rep #l3 #l1 ""
        !else
            set-variable &ind #l2 &lset &ind #l2 1 &cat #l3 #l1
        !endif
        -1 buffer-mode "view"
        beginning-of-line
        set-mark
        forward-line
        -1 kill-region
        buffer-list-create-single
        backward-line
        -1 buffer-mode "edit"
        1 buffer-mode "view"
    !elif &gre @# 2
        goto-position "\x82"
        find-buffer #l9
        !return
    !endif
    goto-position "\x82"
!emacro

0 define-macro buffer-list-mouse-pick-1
    set-variable .buffer-list-create.line $window-line
    goto-position "\x82"
!emacro

0 define-macro buffer-list-mouse-drop-1
    !if &gre $window-line 1
        !if &equ .buffer-list-create.line $window-line
            $window-col buffer-list-command
            !return
        !endif
    !endif
    goto-position "\x82"
!emacro

0 define-macro buffer-list-mouse-drop-3
    -1 osd .osd.tmp
    osd .osd.tmp 0 bBo -1 1
    !if &gre $window-line 1
        osd .osd.tmp 10 "" "Switch to \HBuffer"   3 buffer-list-command
        osd .osd.tmp 20 "" "\HClose Buffer"      -1 buffer-list-command
        osd .osd.tmp 30 "" "Toggle \HSave Flag"   1 buffer-list-command
        osd .osd.tmp 40 "" "Toggle \HDelete Flag" 0 buffer-list-command
    !else
        osd .osd.tmp 10 "" "Switch to Buffer"
        osd .osd.tmp 20 "" "Close Buffer"
        osd .osd.tmp 30 "" "Toggle Save Flag"
        osd .osd.tmp 40 "" "Toggle Delete Flag"
    !endif
    osd .osd.tmp 50 "" "E\Hxecute Flags" -2 buffer-list-command
    !force !force .osd.tmp osd
    !if &not $status
        goto-position "\x82"
    !endif
!emacro

0 define-macro buffer-list-create
    set-position "\x86"
    set-variable #l0 $buffer-bname
    goto-position "\x85"
    -1 buffer-mode "undo"
    -1 buffer-mode "view"
    beginning-of-buffer
    set-mark
    end-of-buffer
    -1 kill-region
    insert-string "AC Buffer List\n"
    set-variable #l1 1
    set-variable $buffer-names ".*"
    !while &not &seq &set #l9 $buffer-names ""
        !if &seq #l9 #l0
            set-variable #l1 $window-line
            buffer-list-create-single
        !elif &not &nbmod #l9 "hide"
            buffer-list-create-single
        !endif
    !done
    backward-delete-char
    beginning-of-buffer
    set-variable $line-scheme .scheme.header
    set-variable $window-line #l1
    set-variable :mouse-pick-1 buffer-list-mouse-pick-1
    set-variable :mouse-drop-1 buffer-list-mouse-drop-1
    set-variable :mouse-pick-3 void
    set-variable :mouse-drop-3 buffer-list-mouse-drop-3
    -1 buffer-mode "edit"
    1 buffer-mode "view"
    goto-position "\x86"
!emacro

0 define-macro buffer-tab-list-mouse-pick-1
    !if &seq @wc &lef $box-chars 1
        set-variable .buffer-tab-list-create.col1 0
    !else
        !repeat
            backward-char
        !until &seq @wc &lef $box-chars 1
        forward-char
        set-variable .buffer-tab-list-create.col1 $window-col
        !repeat
            forward-char
        !until &seq @wc &lef $box-chars 1
        set-variable .buffer-tab-list-create.col2 $window-col
    !endif
    goto-position "\x82"
!emacro

0 define-macro buffer-tab-list-mouse-drop-1
    !if .buffer-tab-list-create.col1
        !if &les $window-col .buffer-tab-list-create.col1
        !elif &les $window-col .buffer-tab-list-create.col2
            set-variable $window-col .buffer-tab-list-create.col1
            set-mark
            set-variable $window-col .buffer-tab-list-create.col2
            copy-region
            set-variable #l1 &trr @y
            -1 yank
            set-variable $window-col .buffer-tab-list-create.col1
            goto-position "\x82"
            find-buffer #l1
            !return
        !endif
    !endif
    goto-position "\x82"
!emacro

0 define-macro buffer-tab-list-mouse-drop-2
    !if .buffer-tab-list-create.col1
        !if &les $window-col .buffer-tab-list-create.col1
        !elif &les $window-col .buffer-tab-list-create.col2
            set-variable $window-col .buffer-tab-list-create.col1
            set-mark
            set-variable $window-col .buffer-tab-list-create.col2
            copy-region
            set-variable #l1 &trr @y
            -1 yank
            set-variable $window-col .buffer-tab-list-create.col1
            goto-position "\x82"
            delete-buffer #l1 @mna
            ; successful - flag that the current buffer has changed to get the tools updated
            set-variable .toolbar-redraw.bc &bor .toolbar-redraw.bc 0x08
            !return
        !endif
    !endif
    goto-position "\x82"
!emacro

0 define-macro buffer-tab-list-create
    set-position "\x86"
    find-buffer "*toolbar-buffer-tab*" 
    -1 buffer-mode "undo"
    -1 buffer-mode "view"
    beginning-of-buffer
    set-mark
    end-of-buffer
    -1 kill-region
    set-variable #l0 &lef $box-chars 1
    insert-string #l0
    set-variable $buffer-names ".*"
    !while &not &seq &set #l9 $buffer-names ""
        !if &not &nbmod #l9 "hide"
            insert-string &spr "%s%n%s" #l9 &sub 12 &len #l9 " " #l0
        !endif
    !done
    beginning-of-buffer
    set-variable :mouse-pick-1 buffer-tab-list-mouse-pick-1
    set-variable :mouse-drop-1 buffer-tab-list-mouse-drop-1
    set-variable :mouse-pick-2 buffer-tab-list-mouse-pick-1
    set-variable :mouse-drop-2 buffer-tab-list-mouse-drop-2
    -1 buffer-mode "edit"
    1 buffer-mode "view"
    goto-position "\x86"
!emacro

0 define-macro toolbar-dir-list-create
    set-variable #l0 $buffer-fname
    set-position "\x86"
    find-buffer "*directory*"
    !if &seq $buffer-fname ""
        !if &lfind "|http:/|ftp://|" &lef #l0 6
            set-variable #l0 &stat "a" "."
        !elif &seq "" &set #l0 &lef #l0 &rsin "/" #l0
            set-variable #l0 &stat "a" "."
        !endif
        !force 0x0f directory-tree #l0
    !endif
    1 buffer-mode "hide"
    goto-position "\x86"
!emacro
