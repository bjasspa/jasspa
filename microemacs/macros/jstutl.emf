; -!- emf -!-
; This is part of the JASSPA MicroEmacs macro files
; Copyright (C) 2002-2005 JASSPA (www.jasspa.com)
; See the file me.emf for copying and conditions.
;
; Created:     Mon Jan 14 2002
; Synopsis:    JST export macros
; Authors:     Steven Phillips
;
; Notes:
;
;  Neutral Format:
;
;   Temporary tags are used during conversion but will not exist in the output.
;
;    \bF?....\bf?   - Font style change (? can be B,I,U,M,p,b)
;    \bDD\bDF<color>\bDB<color>\bdD....\bDd - Foreground or background color change
;                <color> = hex RRGGBB, either \bDF<c> or \bDB<c> could be missing.
;    \bIT<image-name>\biT
;    \bIT<image-name>\bID<depth>\biD\bIW<width>\biW\biT
;    \bTA<anchor-name>\btA
;    \bTL<link-url>\btL<link-text>\bTl
;    \bTV<variable-name>_<variable-ref>\btV (temporary)
;    \bSF  - Start new file
;    \bSL  - Start new line
;    \bSP  - Start new page
;    \bST  - Insert the table of content (temporary)
;    \bPI  - Indent level
;    \bPB....\bpB  - Paragraph - justification both
;    \bPC....\bpC  - Paragraph - justification center
;    \bPF....\bpF  - Paragraph - File style block
;    \bPL....\bpL  - Paragraph - justification left
;    \bPN....\bpN  - Paragraph - justification none
;    \bPR....\bpR  - Paragraph - justification right
;    \bH#....\bh#  - Heading, e.g. \bH1....\bh1
;    \bBI\bB?...\bb?\bB?...\bb?\bbI  - Item list start and end tags 
;    \bBB....\bbB  - Bullet item
;    \bBN#\bBn....\bbN - item # in numbered item list
;    \bBU#\bBu....\bbU - item # in uppercase letter item list
;    \bBL#\bBl....\bbL - item # in lowercase letter item list
;    \bBR#\bBr....\bbR - item # in uppercase roman numeral item list
;    \bBS#\bBs....\bbS - item # in lowercase roman numeral item list
;    \bCT<flags>\bCt<widths>\bct....\bcT  - Table - Entire table (contains rows)
;                    <flags> can be any of h, c or r
;                    <widths> is an ME list "|no col|width table|width of col 1|width of col 2|...|"
;    \bCR....\bcR  - Table - Row (contains columns)
;    \bCC....\bcC  - Table - column (single cell)
;
;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;

0 define-macro jst-to-neutral-symbol
    set-variable #p9 "Unknown symbol problem occurred"
    backward-delete-char
    backward-delete-char
    set-mark
    !force search-buffer "e" "]]"
    !if &not $status
        set-variable #p9 "Badly formed [[...]] symbol"
        !return 0
    !endif
    kill-region
    set-variable #l1 &lef @y -2
    -1 yank
    !if &seq #l1 "toc"
        insert-string "\bST"
    !elif &seq #l1 "nl"
        insert-string "\bSL"
    !elif &seq #l1 "np"
        insert-string "\bSP"
    !elif &seq #l1 "nf"
        insert-string "\bSF"
    !else
        set-variable #p9 &spr "Unknown symbol \"[[%s]]\"" #l1
        !return 0
    !endif
!emacro

0 define-macro jst-to-neutral-tag
    set-variable #p9 "Unknown tag problem occurred"
    backward-char
    backward-delete-char
    set-variable #l0 0
    !repeat
        set-mark
        !if &seq @wc "\""
            !force goto-matching-fence
            !if &not $status
                set-variable #p9 "Quoted tag argument is missing closing \""
                !return 0
            !endif
            forward-char
        !else
            search-buffer "me" "[]\\s]"
            backward-char
        !endif
        kill-region
        set-variable #l1 @y
        !if &seq &lef #l1 1 "\""
            set-variable #l1 &xrep &mid #l1 1 &sub &len #l1 2 "\\\\\\(.\\)" "\\1"
        !endif
        set-variable &cat ".a" &pinc #l0 1 #l1
        -1 yank
        set-mark
        !force search-buffer "me" "\\S"
        !if &not $status
            set-variable #p9 "Tag missing closing ]"
            !return 0
        !endif
        backward-char
        -1 kill-region
    !until &seq @wc "]" 
    forward-delete-char
    set-variable #l1 &lef .a0 1
    !if &seq #l1 "c"
        ; comment - ignore
    !elif &seq #l1 "a"
        ; anchor
        !if &les #l0 2
            set-variable #p9 "Anchor name missing"
            !return 0
        !endif
        insert-string &spr "\bTA%s\btA" .a1
    !elif &seq #l1 "d"
        ; dump 
        !if &sub #l0 3
            set-variable #p9 "Wrong number of arguments for dump tag"
            !return 0
        !elif &seq .a1 .jst-to-neutral.target
            !jump 2
        !elif &seq .a1 "all"
            set-alpha-mark "D"
            insert-string .a2
            goto-alpha-mark "D"
        !endif
    !elif &seq #l1 "b"
        ; background color
        !jump 2
    !elif &seq #l1 "f"
        ; foreground color
        !if &sin "s" .a0
            ; defining a color
            !if &sub #l0 3
                set-variable #p9 "Wrong number of arguments for color definition tag"
                !return 0
            !elif &not &xse .a2 "#\\h\\h\\h\\h\\h\\h"
                set-variable #p9 "Invalid color value"
                !return 0
            !endif
            set-variable &cat ":JC" .a1 &rig .a2 1
        !else
            !if &not &and &seq "" .fcol &seq "" .bcol
                insert-string "\bDd"
            !endif
            !if &gre #l0 1
                !if &seq &lef .a1 1 "#"
                    !if &not &xse .a1 "#\\h\\h\\h\\h\\h\\h"
                        set-variable #p9 "Invalid color value"
                        !return 0
                    !endif
                    set-variable #l2 &rig .a1 1
                !else
                    !if &exi &cat ":JC" .a1
                        set-variable #l2 &ind &cat ":JC" .a1
                    !else
                        set-variable #p9 &spr "Undefined color \"%s\"" .a1
                        !return 0
                    !endif
                !endif
            !else
                set-variable #l2 ""
            !endif
            !if &seq #l1 "b"
                set-variable .bcol #l2
            !else
                set-variable .fcol #l2
            !endif
            !if &not &and &seq "" .fcol &seq "" .bcol
                insert-string "\bDD"
                !if &not &seq "" .fcol
                    insert-string &cat "\bDF" .fcol
                !endif
                !if &not &seq "" .bcol
                    insert-string &cat "\bDB" .bcol
                !endif
                insert-string "\bdD"
            !endif
        !endif
    !elif &seq #l1 "i"
        ; image
        !if &les #l0 2
            set-variable #p9 "Image name missing"
            !return 0
        !endif
        insert-string &cat "\bIT" .a1
        set-variable #l1 2
        !if &sin "d" .a0
            !if &equ #l1 #l0
                set-variable #p9 "Image depth missing"
                !return 0
            !endif
            insert-string &spr "\bID%s\biD" &ind &cat ".a" &pinc #l1 1
        !endif
        !if &sin "w" .a0
            !if &equ #l1 #l0
                set-variable #p9 "Image width missing"
                !return 0
            !endif
            insert-string &spr "\bIW%s\biW" &ind &cat ".a" &pinc #l1 1
        !endif
        insert-string "\biT"
    !elif &seq #l1 "l"
        ; link
        !if .link
            !if &not #l0 1 
                set-variable #p9 "Probable missing close link - no args should be given"
                !return 0
            !endif
            insert-string "\bTl"
            set-variable .link 0
        !elif &les #l0 2
            set-variable #p9 "Link URL missing"
            !return 0
        !else
            insert-string &spr "\bTL%s\btL" .a1
            set-variable .link 1
        !endif
    !elif &seq #l1 "o"
        ; output dump 
        !if &sub #l0 3
            set-variable #p9 "Wrong number of arguments for output dump tag"
            !return 0
        !elif &seq .a1 .jst-to-neutral.target
            insert-string .a2
        !elif &seq .a1 "all"
            insert-string .a2
        !endif
    !elif &seq #l1 "u"
        ; use (or insert)
        !if &sub #l0 2
            set-variable #p9 "Wrong number of arguments for use tag"
            !return 0
        !endif
        insert-newline
        !force insert-file .a1
        !if &not $status
            set-variable #p9 &spr "Failed to load use file \"%s\"" .a1
            !return 0
        !endif
        exchange-point-and-mark
        backward-delete-char
    !elif &seq #l1 "v"
        ; variable
        !if &les #l0 2
            set-variable #p9 "Variable name missing"
            !return 0
        !elif &sin "r" .a0
            !if &not &seq .a0 "vr"
                set-variable #p9 &cat "Variable tag has illegal options combination " .a0
                !return 0
            !elif &not &equ #l0 3
                ; wrong number of args
                set-variable #p9 "Wrong number of arguments for a variable reference tag"
                !return 0
            !endif
            insert-string &spr "\bTV%s_%s\btV" .a1 .a2
        !else
            set-variable #l1 2
            !if &sin "s" .a0
                !if &equ #l1 #l0
                    set-variable #p9 "Variable set value missing"
                    !return 0
                !endif
                set-variable &cat ":JV" .a1 &ind &cat ".a" &pinc #l1 1
            !endif
            !if &sin "i" .a0
                !if &equ #l1 #l0
                    set-variable #p9 "Variable increment value missing"
                    !return 0
                !endif
                set-variable &cat ":JV" .a1 &add &ind &cat ":JV" .a1 &ind &cat ".a" &pinc #l1 1
            !endif
            !if &sin "d" .a0
                !if &equ #l1 #l0
                    set-variable #p9 "Variable reference definition name missing"
                    !return 0
                !elif &exi &set #l2 &spr ":JV%s_%s" .a1 &ind &cat ".a" &pinc #l1 1
                    set-variable #p9 &spr "Variable \"%s\" already defined" &rep &rig #l2 4 "_" "\" reference \""
                    !return 0
                !endif
                set-variable &ind #l2 &ind &cat ":JV" .a1
            !endif
            !if &not &exi &cat ":JV" .a1
                set-variable #p9 &spr "Variable \"%s\" not defined" .a1
                !return 0
            !elif &not &sin "n" .a0
                insert-string &ind &cat ":JV" .a1
            !endif
        !endif
    !else
        set-variable #p9 &spr "Unknown tag \"%s\"" .a0
        !return 0
    !endif
!emacro

0 define-macro jst-to-neutral-par
    set-variable #p9 "Unknown paragraph problem occurred"
    set-variable #l0 &div &add $window-acol &sub $buffer-indent-width 1 $buffer-indent-width
    set-mark
    beginning-of-line
    -1 kill-region
    !if &exi .b0
        set-variable .eop .b4
        !if &seq @wl "\b"
            forward-delete-char
            forward-delete-char
            backward-delete-char
            unset-variable .b0
            !return
        !elif &sin @wc "=>!"
            set-variable #p9 "Can not have a block inside another block"
            !return 0
        !endif
        !if &les #l0 .b0
            set-variable #l0 .b0
        !endif
        set-variable #l1 .b1
        set-variable #l2 .b2
        set-variable #l3 .b3
        set-variable #l4 .b4
        set-variable #l5 0
    !elif &seq @wc "]"
        set-variable #l1 -1
        set-variable #l2 ""
        set-variable #l3 ""
        set-variable #l4 ""
        set-variable #l5 -1
    !elif &sin @wc "=>!"
        set-mark
        set-variable #l2 &trr &rig @wl $window-col
        !if &set #l1 &sin " " &rep #l2 "\t" " "
            set-variable #l2 &lef #l2 &sub #l1 1
            &len #l2 forward-char
            !if &sin @wc " \t"
                forward-char
                !jump -2
            !endif
        !else
            beginning-of-line
            forward-line
        !endif
        -1 kill-region
        !if &xseq #l2 ">\\(.*\\)>"
            set-variable #l3 "\bPF"
            set-variable #l4 "\bpF"
            set-variable #l5 1
        !else
            !if &sin "c" #l2
                set-variable #l3 "C"
            !elif &sin "r" #l2
                set-variable #l3 "R"
            !elif &seq ">" &lef #l2 1
                set-variable #l3 "N"
            !else
                set-variable #l3 "L"
            !endif
            set-variable #l4 &cat "\bp" #l3
            set-variable #l3 &cat "\bP" #l3
            !if &seq "=" &lef #l2 1
                set-variable #l5 &sub &len #l2 &len &rep #l2 "=" ""
                set-variable #l3 &spr "%s\bH%s" #l3 #l5
                set-variable #l4 &spr "\bh%s%s" #l5 #l4
            !endif
            set-variable #l5 0
            !while &not &seq "" &set #l6 &lget "|*|/|_|@|`|,|" &inc #l5 1
                !if &sin #l6 #l2
                    set-variable #l3 &spr "%s\bF%s" #l3 &lget "|B|I|U|M|p|b|" #l5
                    set-variable #l4 &spr "\bf%s%s" &lget "|B|I|U|M|p|b|" #l5 #l4
                !endif
            !done
            set-variable #l5 &sin "\"" #l2
        !endif
    !else
        set-variable #l1 -1
        set-variable #l2 ""
        set-variable #l3 "\bPL"
        set-variable #l4 "\bpL"
        set-variable #l5 0
    !endif
    ; #l0 is the indent level
    ; #l1 is flag, if 0 this is a section with a terminating '!end!' or '>end>' tag
    ; #l2 is a string giving the par flags (i.e. "", ">*" or ">emf>" etc)
    ; #l3 is the string to be inserted at the start of each paragraph
    ; #l4 is the string to be inserted at the end of each paragraph
    ; #l5 is a flag, if -ve its a table cell, elif not 0 the paragraph is to be quoted
    
    ; close down any item lists and tables that the indent level of this paragraph closes
    set-variable #l8 #l0
    !if &gre $window-line 2
        !if &not #l5
            !if &xseq &rig @wl $window-col "\\.[ \t].*"
                set-variable #l8 &add #l8 1
            !endif
        !endif
        set-variable #l7 .item-count
        !if #l7
            !if &not &les &lget .item-ind #l7 #l8
                set-variable #l7 &sub #l7 1
                !jump -3
            !endif
        !endif
        backward-line
        !if &not &seq "" &trr @wl
            set-variable #p9 "Expected blank line before paragraph"
            !return 0
        !endif
        set-variable @wl ""
        backward-line
        !while &seq "" &trr @wl ""
            set-mark
            forward-line
            -1 kill-region
            backward-line
        !done
        end-of-line
        !if &exi .eop
            insert-string .eop
            unset-variable .eop
        !endif
        !if &sub #l7 .item-count
            !repeat
                insert-string &spr "\bb%s\bbI" &lget .item-type .item-count
            !until &equ #l7 &dec .item-count 1
        !endif
        !if &gre #l8 #l0
            insert-string &spr "\bb%s" &lget .item-type .item-count
        !endif
        !if &set #l7 .tbl-count
            !if &not &les &lget .tbl-ind #l7 #l0
                !if &set #l6 &lget .tbl-cell #l7
                    insert-string "\bcC"
                    !if &not &mod #l6 &lget .tbl-col #l7
                        insert-string "\bcR"
                        !if &equ #l6 &mul &lget .tbl-row #l7 &lget .tbl-col #l7
                            insert-string "\bcT"
                            set-variable .tbl-count &sub #l7 1
                            !jump -10
                        !endif
                    !endif
                !endif
                set-variable .tbl-cell &lset .tbl-cell #l7 &add #l6 1
            !endif
            !if &les #l0 &lget .tbl-ind #l7
                !jump 2
            !elif &and &equ #l0 &lget .tbl-ind #l7 &gre #l5 -1
                set-variable #p9 &spr "Table cells missing, another %d required" &sub &mul &lget .tbl-row #l7 &lget .tbl-col #l7 &lget .tbl-cell #l7
                !return 0
            !endif
        !endif
        2 forward-line
    !endif
    beginning-of-line
    &add 1 &sub #l8 .item-count insert-string "\bPI"
    insert-string #l3
    set-alpha-mark "P"
    ; handle tables
    !if &les #l5 0
        !if &not .tbl-count
            !jump 2
        !elif &sub #l0 &lget .tbl-ind .tbl-count
            ; starting a new table
            set-variable #l1 &cat &xrep &trr &rig @wl $window-col "\\s+" "]" "]"
            set-variable #l2 &add .tbl-count 1
            !if &les &lget #l1 2 1
                set-variable #p9 "Invalid table column size"
                !return 0
            !elif &les &lget #l1 3 1
                set-variable #p9 "Invalid table row size"
                !return 0
            !endif
            !if &sin "w" &lget #l1 1
                set-variable #l3 "|"
                set-variable #l4 &seq "%" &rig &lget #l1 4 -1
                set-variable #l5 0
                set-variable #l6 0
                !while &les &pinc #l6 1 &lget #l1 2
                    set-variable #l7 &lget #l1 &add #l6 3
                    !if &not &gre #l7 0
                        set-variable #p9 "Invalid column width"
                        !return 0
                    !elif &not &equ #l4 &seq "%" &rig #l7 -1
                        set-variable #p9 "Cannot mix %% column widths with pixel widths"
                        !return 0
                    !endif
                    set-variable #l5 &add #l5 #l7
                    set-variable #l3 &lin #l3 -1 #l7 
                !done
                !if #l4
                    !if &gre #l5 100
                        set-variable #p9 "Sum of column widths is greater than 100%%"
                        !return 0
                    !endif
                !endif
                set-variable #l3 &spr "|%s|%s%s%s" &lget #l1 2 #l5 &con #l4 "%" "" #l3
            !else
                set-variable #l3 ""
            !endif
            set-variable .tbl-ind &lset .tbl-ind #l2 #l0
            set-variable .tbl-col &lset .tbl-col #l2 &lget #l1 2
            set-variable .tbl-row &lset .tbl-row #l2 &lget #l1 3
            set-variable .tbl-cell &lset .tbl-cell #l2 0
            set-variable .tbl-count #l2
            insert-string &spr "\bCT%s\bCt%s\bct" &lget #l1 1 #l3
            set-mark
            end-of-line
            -1 kill-region
        !else
            set-variable #l2 &lget .tbl-cell .tbl-count
            insert-string "\bPI"
            !if &equ 1 &mod #l2 &lget .tbl-col .tbl-count
                insert-string "\bCR"
            !endif
            insert-string "\bCC"
            forward-delete-char
            set-mark
            !if &seq @wc "j"
                insert-string "\bCJ"
                forward-paragraph
                backward-char
                -1 kill-region
            !else
                search-buffer "me" "\\S"
                backward-char
                -1 kill-region
                insert-newline
                insert-newline
                &mul &add #l0 1 $buffer-indent-width insert-space
                goto-alpha-mark "P"
            !endif
        !endif
        !return
    !endif
    ; handle quoted paragraphs
    !if #l5
        !if #l1
            forward-paragraph
            backward-char
            insert-string #l4
        !else
            !force search-buffer "me" &spr "^%send%s[ \t]*$" &lef #l2 1 &lef #l2 1
            !if &not $status
                set-variable #p9 &spr "Failed to find block terminating \"%send%s\"" &lef #l2 1 &lef #l2 1
                !return 0
            !endif
            set-mark
            beginning-of-line
            -1 kill-region
            !if &sin "\bPF" #l3
                backward-char
                insert-string #l4
            !else
                insert-string "\n\b\n"
                goto-alpha-mark "P"
                !repeat
                    !if &sin "\bPL" #l3
                        -1 fill-paragraph
                    !else
                        forward-paragraph
                    !endif
                    backward-char
                    insert-string #l4
                    search-buffer "me" "\\S"
                    !if &not &seq @wl "\b"
                        backward-char
                        set-mark
                        beginning-of-line
                        -1 kill-region
                        &add 1 &sub #l8 .item-count insert-string "\bPI"
                        insert-string #l3
                    !endif
                !until &seq @wl "\b"
                backward-delete-char
                backward-delete-char
                forward-delete-char
            !endif
        !endif
        !return
    !endif
    
    !if &gre #l8 #l0
        !if &sub &lget .item-ind .item-count #l0
            set-variable #p9 "Isolated item list item"
            !return 0
        !endif
        set-variable .item-num &lset .item-num .item-count &add 1 &lget .item-num .item-count
        !goto insert-bullet
    !elif &xseq &mid @wl $window-col 3 "[*1AaIi:]\\.\\s"
        set-variable .item-count &add .item-count 1
        set-variable .item-type &lset .item-type .item-count &lget "|B|N|U|L|R|S|D|" &sin @wc "*1AaIi:"
        set-variable .item-ind &lset .item-ind .item-count #l0
        set-variable .item-num &lset .item-num .item-count 1
        insert-string "\bBI"
        forward-delete-char
*insert-bullet
        forward-delete-char
        !while &sin @wc " \t"
            forward-delete-char
        !done
        !if &seq "B" &set #l8 &lget .item-type .item-count
            insert-string "\bBB"
        !elif &seq "D" #l8
            insert-string "\bBD"
            end-of-line
            insert-string "\bBd"
            set-mark
            search-buffer "me" "\\S"
            backward-char
            -1 kill-region
        !else
            insert-string &spr "\bB%s%d\bB%s" #l8 &lget .item-num .item-count &slo #l8
        !endif
    !elif &xseq &rig @wl $window-col "---+"
        set-variable @wl &cat &lef @wl $window-col "\bSH"
    !endif
    !if &sin "\bPL" #l3
        -1 fill-paragraph
    !endif
    goto-alpha-mark "P"
    !if #l1
        set-variable .eop #l4
    !elif &not &exi .b0
        !force search-buffer "me" &spr "^%send%s[ \t]*$" &lef #l2 1 &lef #l2 1
        !if &not $status
            set-variable #p9 &spr "Failed to find block terminating \"%send%s\"" &lef #l2 1 &lef #l2 1
            !return 0
        !endif
        set-mark
        beginning-of-line
        -1 kill-region
        insert-string "\n\b\n"
        goto-alpha-mark "P"
        set-variable .b0 #l0
        set-variable .b1 #l1
        set-variable .b2 #l2
        set-variable .b3 #l3
        set-variable .b4 #l4
    !endif
!emacro


0 define-macro jst-to-neutral-gen
    set-variable #p9 "Unknown problem occurred"
    end-of-buffer
    insert-string "\n\b"
    beginning-of-buffer
    set-mark
    !force search-buffer "me" "\\S"
    beginning-of-line
    -1 kill-region
    insert-newline
    beginning-of-buffer
    ; #l9 stores the current style and item state
    set-variable .style "|0|0|0|0|0|0|0|"
    !force unset-variable .jst-to-neutral-par.eop
    !force unset-variable .jst-to-neutral-par.b0
    set-variable .jst-to-neutral-par.item-ind "|"
    set-variable .jst-to-neutral-par.item-type "|"
    set-variable .jst-to-neutral-par.item-count "0"
    set-variable .jst-to-neutral-par.tbl-ind "|"
    set-variable .jst-to-neutral-par.tbl-row "|"
    set-variable .jst-to-neutral-par.tbl-col "|"
    set-variable .jst-to-neutral-par.tbl-cell "|"
    set-variable .jst-to-neutral-par.tbl-count "0"
    set-variable .jst-to-neutral-tag.link 0
    set-variable .jst-to-neutral-tag.fcol ""
    set-variable .jst-to-neutral-tag.bcol ""
    !repeat
        !force search-buffer "me" "\\\\\\|\\([\"*/_@`,]\\)\\1\\|\\(https?\\|ftp\\|mailto\\|news\\)[-@~_a-zA-Z#%\\.:\\/0-9]+\\|\\[[^]\\s]\\|^[ \t]*\n[ \t]*\\S"
        !if $status
            !if &seq @s0 "\\"
                ; '\' quote char
                backward-delete-char
                forward-char
            !elif &not &seq @s1 ""
                ; double letter font styling
                backward-delete-char
                backward-delete-char
                !if &equ 1 &set #l0 &sin @s1 "\"*/_@`,"
                    ; '""' quote string
                    !force search-buffer "me" "\"\""
                    !if &not $status
                        set-variable #p9 "Missing \"\" terminator"
                        !return 0
                    !endif
                    backward-delete-char
                    backward-delete-char
                !else
                    set-variable .style &lset .style #l0 &set #l1 &bxor 1 &lget .style #l0
                    insert-string &spr "\b%s%s" &con #l1 "F" "f" &lget "|Q|B|I|U|M|p|b|" #l0
                !endif
            !elif &not &seq @s2 ""
                ; Automatic URL
                set-variable #l0 &len @s0
                set-mark
                #l0 backward-char
                copy-region
                insert-string "\bTL"
                exchange-point-and-mark
                insert-string "\btL"
                yank
                insert-string "\bTl"
                -1 yank
            !elif &seq &lef @s0 1 "["
                ; Got a tag or special symbol
                !if &seq @s0 "[["
                    !force jst-to-neutral-symbol
                !else
                    !force jst-to-neutral-tag
                !endif
                !if &not $status
                    set-variable #p9 #l9
                    !return 0
                !endif
            !else
                ; beginning of a new paragraph
                backward-char
                !force jst-to-neutral-par
                !if &not $status
                    set-variable #p9 #l9
                    !return 0
                !endif
            !endif
        !endif
    !until &not $status
    ; remove the temporary start and end markers
    end-of-buffer
    set-mark
    3 backward-line
    -1 kill-region
    beginning-of-buffer
    set-mark
    forward-line
    -1 kill-region
    ; replace any variable references with the correct value
    !repeat
        !force search-buffer "me" "\bTV\\([^\b\n]+\\)\btV"
        !if $status
            !if &not &exi &cat ":JV" &set #l0 @s1
                set-variable #p9 &spr "Attempt to insert undefined variable \"%s\"" &rep #l0 "_" "\" reference \""
                !return 0
            !endif
            &len @s0 backward-delete-char
            -1 yank
            insert-string &ind &cat ":JV" #l0
        !endif
    !until &not $status
!emacro

0 define-macro jst-get-output-name
    set-variable #l0 @2
    !if &les @# 0
        set-variable @1 &lef $buffer-fname &rsin "/" $buffer-fname
        !return
    !elif &band @# 2
        set-variable #l1 &stat "a" &cat &lef $buffer-fname &rsin "/" $buffer-fname "."
    !else
        set-variable #l1 @ml01 &spr "Export to %s file" #l0
    !endif
    !if &seq &rig #l1 &rsin "/" #l1 ""
        ; directory given
        !if &seq $buffer-fname ""
            set-variable #l2 $buffer-bname
        !else
            set-variable #l2 &rig $buffer-fname &rsin "/" $buffer-fname
        !endif
        !if &set #l3 &risin ".jst" #l2
            set-variable #l2 &lef #l2 &sub #l3 1
        !endif
        set-variable #l1 &spr "%s%s.%s" #l1 #l2 #l0
    !endif
    !if &not &stat "w" #l1
        ml-write &spr "[Invalid file name \"%s\"]" #l1
        !abort
    !elif &band @# 1
        !if &seq &stat "t" #l1 "R"
            !if &iseq @mc1 &spr "Over-write file %s (y/n) ? " #l1 "yYnN" "n"
                !abort
            !endif
        !endif
    !endif
    set-variable @1 #l1 
    ml-write &spr "[Exporting to %s]" #l1
!emacro

define-macro jst-to-neutral
    set-variable .target @1
    set-position "\x82"
    @# jst-get-output-name #l0 @2
    !force 0 delete-buffer "*jst-conv*"
    beginning-of-buffer
    set-mark
    end-of-buffer
    copy-region
    goto-position "\x82"
    find-buffer "*jst-conv*"
    set-variable $buffer-fname #l0
    yank
    -1 yank
    -1 buffer-mode "edit"
    1 buffer-mode "exact"
    1 buffer-mode "magic"
    1 buffer-mode "undo"
    set-variable #l1 $fill-mode
    set-variable $fill-mode "o"
    !force jst-to-neutral-gen
    set-variable #l2 $status
    set-variable $fill-mode #l1
    !if &les @# 0
        ml-write &spr "Error: %s" #l9
        !return 0
    !elif &not #l2
        set-alpha-mark "E"
        0 undo
        goto-alpha-mark "E"
        set-variable #l0 $window-line
        set-variable #l1 $window-col
        !force 0 delete-buffer "*jst-conv*"
        goto-position "\x82"
        !force set-variable $window-line #l0
        !force set-variable $window-col #l1
        ml-write &spr "Error:%d,%d: %s" #l0 #l1 #l9
        !abort
    !endif
    beginning-of-buffer
!emacro

0 define-macro jst-to-html-int
    set-variable #l1 $buffer-bname
    @# jst-to-neutral "html" @1
    ; special chars
    beginning-of-buffer
    replace-string "&" "&amp;"
    beginning-of-buffer
    replace-string "<" "&lt;"
    beginning-of-buffer
    replace-string ">" "&gt;"
    ; Handle tables first - there is an extra \bPI for each para in a table - remove
    end-of-buffer
    !repeat
        !force search-buffer "meb" "\bCT.*\bCt\\(.*\\)\bct"
        !if $status
            set-variable #l3 @s0
            set-variable #l4 @s1
            set-mark
            &len #l3 forward-char
            -1 kill-region
            !if &sin "c" #l3
                insert-string "<CENTER>"
            !elif &sin "r" #l3
                insert-string "<DIV ALIGN=\"RIGHT\">"
            !endif
            insert-string "<TABLE"
            !if &sin "w" #l3
                insert-string &cat " width=" &lget #l4 2
            !endif
            !if &sin "h" #l3
                insert-string " border=0 cellpadding=6 cellspacing=0>"
            !else
                insert-string " border=1 cellpadding=4 cellspacing=0>"
            !endif
            !if &sin "w" #l3
                set-variable #l6 0
                !if &seq &rig &lget #l4 2 -1 "%"
                    !if &equ 100 &set #l6 &lget #l4 2
                        set-variable #l6 0
                    !endif
                !endif
                set-variable #l5 0
                !while &les &pinc #l5 1 &lget #l4 1
                    !if #l6
                        insert-string &spr "<COL width=%d%%>" &div &mul 100 &lget #l4 &add #l5 2 #l6
                    !else
                        insert-string &spr "<COL width=%s>" &lget #l4 &add #l5 2
                    !endif
                !done
            !endif
            set-variable #l2 $window-line
            search-buffer "me" "\bcT"
            backward-delete-char
            backward-delete-char
            backward-delete-char
            insert-string "</TABLE>"
            !if &sin "c" #l3
                insert-string "</CENTER>"
            !elif &sin "r" #l3
                insert-string "</DIV>"
            !endif
            set-variable #l3 $window-line
            set-variable $window-line &add #l2 1
            &sub #l2 #l3 replace-string  "\bPI\\(\bPI\\)*" "\\1"
            set-variable $window-line #l2
        !endif
    !until &not $status
    beginning-of-buffer
    replace-string "\bCR" "<TR>"
    beginning-of-buffer
    replace-string "\bcR\n?" "</TR>"
    beginning-of-buffer
    replace-string "\bCC\n?" "<TD valign=top>"
    beginning-of-buffer
    replace-string "\bcC\n?" "</TD>"
    ; fonts and color
    beginning-of-buffer
    replace-string "\bFB" "<B>"
    beginning-of-buffer
    replace-string "\bfB" "</B>"
    beginning-of-buffer
    replace-string "\bFI" "<I>"
    beginning-of-buffer
    replace-string "\bfI" "</I>"
    beginning-of-buffer
    replace-string "\bFU" "<U>"
    beginning-of-buffer
    replace-string "\bfU" "</U>"
    beginning-of-buffer
    replace-string "\bFp" "<SUP>"
    beginning-of-buffer
    replace-string "\bfp" "</SUP>"
    beginning-of-buffer
    replace-string "\bFb" "<SUB>"
    beginning-of-buffer
    replace-string "\bfb" "</SUB>"
    beginning-of-buffer
    set-variable #l2 ""
    set-variable #l3 ""
    set-variable #l4 ""
    set-variable #l5 ""
    !repeat
        !force search-buffer "me" "\b\\(FM\\|fM\\|DD\\(\bDF\\h+\\)?\\(\bDB\\h+\\)?\bdD\\|Dd\\)"
        !if $status
            !if &seq "FM" &set #l6 &lef @s1 2 
                set-variable #l3 "font-family: monospace"
            !elif &seq "fM" #l6
                set-variable #l3 ""
            !elif &seq "DD" #l6
                !if &not &seq "" &set #l4 @s2
                    set-variable #l4 &cat "color:#" &rig #l4 3
                !endif
                !if &not &seq "" &set #l5 @s3
                    set-variable #l5 &cat "background-color:#" &rig #l5 3
                !endif
            !elif &seq "Dd" #l6
                set-variable #l4 ""
                set-variable #l5 ""
            !endif
            set-mark
            &len @s0 backward-char
            -1 kill-region
            set-variable #l6 #l3
            !if &not &seq "" #l4
                !if &seq "" #l6
                    set-variable #l6 #l4
                !else
                    set-variable #l6 &spr "%s; %s" #l6 #l4
                !endif
            !endif
            !if &not &seq "" #l5
                !if &seq "" #l6
                    set-variable #l6 #l5
                !else
                    set-variable #l6 &spr "%s; %s" #l6 #l5
                !endif
            !endif
            !if &not &seq #l2 #l6
                !if &not &seq "" #l2
                    insert-string "</SPAN>"
                !endif
                !if &not &seq "" #l6
                    insert-string &spr "<SPAN style=\"%s\">" #l6
                !endif
                set-variable #l2 #l6
            !endif
        !endif
    !until &not $status
    ; Special symbols
    beginning-of-buffer
    replace-string "\bST" "<BR><B>TOC Not yet implemented</B><BR>"
    beginning-of-buffer
    replace-string "\bSH" "<HR>"
    beginning-of-buffer
    replace-string "\bSL" "<BR>"
    beginning-of-buffer
    replace-string "\\(\bPL\\)?\bSP\\(\bpL\\)?" "<BR><HR><BR>"
    beginning-of-buffer
    replace-string "\\(\bPL\\)?\bSF\\(\bpL\\)?" "<BR><B>New-file Not yet implemented</B><BR>"
    ; images
    beginning-of-buffer
    replace-string "\bIT" "<IMG SRC=\""
    beginning-of-buffer
    replace-string "\biT" "\">"
    beginning-of-buffer
    replace-string "\bID\\([^\b]*\\)\biD" "\" height=\"\\1"
    beginning-of-buffer
    replace-string "\bIW\\([^\b]*\\)\biW" "\" width=\"\\1"
    ; items
    beginning-of-buffer
    replace-string "\bBI\bBB" "<UL>\bBB"
    beginning-of-buffer
    replace-string "\bbB\bbI" "\bbB</UL>"
    beginning-of-buffer
    replace-string "\bBB" "<LI>"
    beginning-of-buffer
    replace-string "\bbB" "</LI>"
    beginning-of-buffer
    replace-string "\bBI\bBN" "<OL>\bBN"
    beginning-of-buffer
    replace-string "\bbN\bbI" "\bbN</OL>"
    beginning-of-buffer
    replace-string "\bBN[^\b]*\bBn" "<LI>"
    beginning-of-buffer
    replace-string "\bbN" "</LI>"
    beginning-of-buffer
    replace-string "\bBI\bBU" "<OL TYPE=\"A\">\bBU"
    beginning-of-buffer
    replace-string "\bbU\bbI" "\bbU</OL>"
    beginning-of-buffer
    replace-string "\bBU[^\b]*\bBu" "<LI>"
    beginning-of-buffer
    replace-string "\bbU" "</LI>"
    beginning-of-buffer
    replace-string "\bBI\bBL" "<OL TYPE=\"a\">\bBL"
    beginning-of-buffer
    replace-string "\bbL\bbI" "\bbL</OL>"
    beginning-of-buffer
    replace-string "\bBL[^\b]*\bBl" "<LI>"
    beginning-of-buffer
    replace-string "\bbL" "</LI>"
    beginning-of-buffer
    replace-string "\bBI\bBR" "<OL TYPE=\"I\">\bBR"
    beginning-of-buffer
    replace-string "\bbR\bbI" "\bbR</OL>"
    beginning-of-buffer
    replace-string "\bBR[^\b]*\bBr" "<LI>"
    beginning-of-buffer
    replace-string "\bbR" "</LI>"
    beginning-of-buffer
    replace-string "\bBI\bBS" "<OL TYPE=\"i\">\bBS"
    beginning-of-buffer
    replace-string "\bbS\bbI" "\bbS</OL>"
    beginning-of-buffer
    replace-string "\bBS[^\b]*\bBs" "<LI>"
    beginning-of-buffer
    replace-string "\bbS" "</LI>"
    beginning-of-buffer
    replace-string "\bBI\bBD" ""
    beginning-of-buffer
    replace-string "\bBd" "<BR><UL>"
    beginning-of-buffer
    replace-string "\bbD\bbI" "</UL>"
    ; paragraphs and justification
    beginning-of-buffer
    replace-string "\bPF" "<PRE>"
    beginning-of-buffer
    replace-string "\bpF" "</PRE>"
    beginning-of-buffer
    replace-string "\bPL" "<P>"
    beginning-of-buffer
    replace-string "\bpL" "</P>"
    beginning-of-buffer
    !repeat
        !force search-buffer "me" "\bP[CNR]"
        !if $status
            set-variable #l2 @s0
            set-mark
            3 backward-char
            -1 kill-region
            !if &seq "\bPN" #l2
                insert-string "<P>"
            !elif &seq "\bPC" #l2
                insert-string "<P ALIGN=\"center\">"
            !else
                insert-string "<P ALIGN=\"right\">"
            !endif
            set-variable #l3 $window-line
            1 replace-string &cat &slo &lef #l2 2 &rig #l2 2  "</P>"
            set-variable #l4 $window-line
            set-variable $window-line #l3
            &sub #l3 #l4 replace-string "$" "<BR>"
        !endif
    !until &not $status
    beginning-of-buffer
    replace-string "\bH\\(\\d\\)" "<H\\1>"
    beginning-of-buffer
    replace-string "\bh\\(\\d\\)" "</H\\1>"
    ; anchors & links
    beginning-of-buffer
    replace-string "\bTA" "<A NAME=\""
    beginning-of-buffer
    replace-string "\btA" "\">"
    beginning-of-buffer
    replace-string "\bTL\\(file:\\)?" "<A HREF=\""
    beginning-of-buffer
    replace-string "\btL" "\">"
    beginning-of-buffer
    replace-string "\bTl" "</A>"
    beginning-of-buffer
    set-variable #l2 1
    !repeat
        !force search-buffer "me" "\\(\bPI\\)+"
        !if $status
            set-variable #l3 &len @s0
            set-mark
            #l3 backward-char
            -1 kill-region
            set-variable #l3 &div #l3 3
            !if &gre #l3 #l2
                &sub #l3 #l2 insert-string "<DIR>"
            !elif &les #l3 #l2
                &sub #l2 #l3 insert-string "</DIR>"
            !endif
            set-variable #l2 #l3
        !endif
    !until &not $status
    end-of-buffer
    #l2 insert-string "</DIR>"
    insert-string "</BODY>\n</HTML>"
    beginning-of-buffer
    insert-string "<HTML>\n<HEAD>\n"
    !if &exi :JVtitle
        insert-string &spr "<TITLE>%s</TITLE>\n" :JVtitle
    !else
        insert-string &spr "<TITLE>%s</TITLE>\n" #l1
    !endif
    !if &exi :JVauthor
        insert-string &spr "<META NAME=\"Author\" CONTENT=\"%s\">\n" :JVauthor
    !endif
    insert-string "<META NAME=\"Generator\" CONTENT=\"MicroEmacs JST\">\n</HEAD>\n<BODY>\n"
    !force !force 0 write-buffer $buffer-fname @mna
    set-variable #l3 $status
    0 delete-buffer $buffer-bname
    goto-position "\x82"
    !if &not #l3
        !abort
    !endif
    ml-write "[Export complete]"
!emacro

define-macro jst-to-htm
    @# jst-to-html-int "htm"
!emacro

define-macro jst-to-html
    @# jst-to-html-int "html"
!emacro

; this generates a html file with an rtf extension, this fools windows into loading
; it into Word but word still loads the html file okay.
define-macro jst-to-rtf
    @# jst-to-html-int "rtf"
!emacro

define-macro jst-to-latex
    ml-write "Not yet implemented!"
    !abort
!emacro
