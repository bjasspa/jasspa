;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;
;
;  Created By    : Steven Phillips
;  Created       : Wed Feb 9 18:31:42 2000
;  Description   : Silly game
;
;  Notes
;
;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;

!if &not &exi .osd.fahtzee
  set-variable .osd.fahtzee  &pinc .osd.next 1
  set-variable .osd.fahtzeed &pinc .osd.next 1
  set-variable .osd.fahtzeen &pinc .osd.next 1
  set-variable .osd.fahtzeec &pinc .osd.next 1
  set-variable .osd.fahtzee1 &pinc .osd.next 1
  set-variable .osd.fahtzee2 &pinc .osd.next 1
  set-variable .osd.fahtzee3 &pinc .osd.next 1
  set-variable .osd.fahtzee4 &pinc .osd.next 1
!endif

define-macro Fahtzee
  !if &seq &reg "/fahtzee" "" ""
    set-variable .file &find "fahtzee" ".eof"
    !if &seq &set .file &find "fahtzee" ".eof" "ERROR"
      set-variable #l0 &reg "/history" ""
      set-variable .file &spr "%sfahtzee.eof" &lef #l0 &rsin "/" #l0
    !endif
    set-variable .dice "|6|6|6|6|6|"
    read-registry "fahtzee" .file "cb"
    !if &seq &reg "/fahtzee/nop" "" ""
      !if &seq &reg "/fahtzee/noh" "" ""
        set-registry "/fahtzee/noh" "1"
        set-registry "/fahtzee/h1" ""
      !endif
      set-variable .nop &reg "/fahtzee/noh" ""
      set-variable .curp 0
      set-variable .visp 1
      set-variable .throw 0
    !else
      set-variable .nop &reg "/fahtzee/nop" ""
      set-variable .curp &reg "/fahtzee/curp" "1"
      set-variable .curg &reg "/fahtzee/curg" "1"
      set-variable .throw &reg "/fahtzee/throw" "1"
      set-variable .dice &reg "/fahtzee/dice" "|1|2|3|4|5|"
      set-variable .held &reg "/fahtzee/held" "|0|0|0|0|0|"
      set-variable .visp .curp
    !endif
  !endif
  fahtzee-setup
  .osd.fahtzee osd
!emacro

set-variable .Fahtzee.dices "|\n  X|   X\n\n X|   X\n  X\n X| X X\n\n X X| X X\n  X\n X X| X X\n X X\n X X|"

0 define-macro fahtzee-setup
  set-variable #l1 &cond .Fahtzee.curp "Player " "High "
  -1 osd .osd.fahtzeen
  osd .osd.fahtzeen 0  "N" 
  set-variable #l0 1
  !repeat
    osd .osd.fahtzeen #l0 "Psf" &cat #l1 #l0 &add .osd.fahtzeec #l0
  !until &gre &inc #l0 1 .Fahtzee.nop
  .Fahtzee.visp fahtzee-set-player
  fahtzee-set-dice
!emacro

0 define-macro fahtzee-undo
  set-variable .Fahtzee.curp &lget .Fahtzee.undo 2
  !iif &not &equ .Fahtzee.visp .Fahtzee.curp  .Fahtzee.curp fahtzee-set-player
  set-variable .Fahtzee.dice &lget .Fahtzee.undo 1
  set-variable .Fahtzee.throw &lget .Fahtzee.undo 3
  set-registry &cat .Fahtzee.visr &cat "box" .Fahtzee.curg &lget .Fahtzee.undo 4
  set-registry &cat .Fahtzee.visr &lget .Fahtzee.undo 5 &lget .Fahtzee.undo 6
  set-registry &cat .Fahtzee.visr &add 70 .Fahtzee.curg &lget .Fahtzee.undo 7
  set-registry &cat .Fahtzee.visr &add 80 .Fahtzee.curg &lget .Fahtzee.undo 8
  set-registry &cat .Fahtzee.visr &add 90 .Fahtzee.curg &lget .Fahtzee.undo 9
  set-registry &cat .Fahtzee.visr &add 170 .Fahtzee.curg &lget .Fahtzee.undo 10
  set-registry &cat .Fahtzee.visr &add 180 .Fahtzee.curg &lget .Fahtzee.undo 11
  osd .osd.fahtzee 630 "BxhcfH" .scheme.osd-dbtt " Undo "
  fahtzee-set-dice
!emacro

0 define-macro fahtzee-dice-info
  set-variable #p9 0
  set-variable #p8 "|0|0|0|0|0|0|"
  set-variable #l0 5
  !repeat
    set-variable #l1 &lget .Fahtzee.dice #l0
    set-variable #p9 &add #p9 #l1
    set-variable #p8 &lset #p8 #l1 &add &lget #p8 #l1 1
  !until &not &dec #l0 1
!emacro

0 define-macro fahtzee-entry
  !if &les @# 0
    set-variable @# &abs @#
    set-variable #l1 &div @# 10
    osd .osd.fahtzee 630 "BxhcfH" .scheme.osd-ebtt " \HUndo " f fahtzee-undo
    set-variable .Fahtzee.undo &spr "\b%s\b%s\b%s\b%s\b%s\b%s\b%s\b%s\b%s\b%s\b%s\b" .Fahtzee.dice .Fahtzee.curp .Fahtzee.throw &reg &cat .Fahtzee.visr &cat "box" .Fahtzee.curg "" @# &reg &cat .Fahtzee.visr @# "" &reg &cat .Fahtzee.visr &add 70 .Fahtzee.curg "" &reg &cat .Fahtzee.visr &add 80 .Fahtzee.curg "" &reg &cat .Fahtzee.visr &add 90 .Fahtzee.curg "" &reg &cat .Fahtzee.visr &add 170 .Fahtzee.curg "" &reg &cat .Fahtzee.visr &add 180 .Fahtzee.curg ""
    fahtzee-dice-info
    !if &les #l1 10
      ; 1, 2, 3, 4, 5, 6's
      set-variable #l0 &mul &lget #l8 #l1 #l1
    !elif &equ #l1 10
      ; 3 of a kind
      !if &xse #l8 ".*[345].*"
        set-variable #l0 #l9
      !else
        set-variable #l0 0
      !endif
    !elif &equ #l1 11
      ; 4 of a kind
      !if &xse #l8 ".*[45].*"
        set-variable #l0 #l9
      !else
        set-variable #l0 0
      !endif
    !elif &equ #l1 12
      ; full house
      !if &xse #l8 ".*2.*3.*"
        set-variable #l0 25
      !elif &xse #l8 ".*3.*2.*"
        set-variable #l0 25
      !elif &xse #l8 ".*5.*"
        set-variable #l0 25
      !else
        set-variable #l0 0
      !endif
    !elif &equ #l1 13
      ; low straight
      set-variable #l0 30
      !if &xse #l8 ".*[12]|[12]|[12]|[12].*"
        set-variable #l0 30
      !else
        set-variable #l0 0
      !endif
    !elif &equ #l1 14
      ; high straight
      !if &xse #l8 ".*1|1|1|1|1.*"
        set-variable #l0 40
      !else
        set-variable #l0 0
      !endif
    !elif &equ #l1 15
      ; fahtzee
      !if &xse #l8 ".*5.*"
        !if &gre &reg &cat .Fahtzee.visr @# "" 0
          set-variable #l0 100
          set-registry &cat .Fahtzee.visr @# &spr "%4d" &add &reg &cat .Fahtzee.visr @# "" #l0
          !goto double-fahtzee
        !endif
        set-variable #l0 50
      !else
        set-variable #l0 0
      !endif
    !else
      ; chance
      set-variable #l0 &add &lget .Fahtzee.dice 1 &add &lget .Fahtzee.dice 2 &add &lget .Fahtzee.dice 3 &add &lget .Fahtzee.dice 4 &lget .Fahtzee.dice 5
    !endif
    set-registry &cat .Fahtzee.visr &cat "box" .Fahtzee.curg &sub &reg &cat .Fahtzee.visr &cat "box" .Fahtzee.curg "1" 1
    !if #l0
      set-registry &cat .Fahtzee.visr @# &spr "%4d" #l0
*double-fahtzee
      set-variable #l2 &reg &cat .Fahtzee.visr &add 170 .Fahtzee.curg ""
      !if &less @# 100
        set-variable #l1 &add #l0 &reg &cat .Fahtzee.visr &add 70 .Fahtzee.curg ""
        set-registry &cat .Fahtzee.visr &add 70 .Fahtzee.curg &spr "%4d" #l1
        !if &gre #l1 62
          set-registry &cat .Fahtzee.visr &add 80 .Fahtzee.curg "  35"
          set-variable #l1 &add #l1 35
        !endif
        set-registry &cat .Fahtzee.visr &add 90 .Fahtzee.curg &spr "%4d" #l1
      !else
        set-variable #l1 &reg &cat .Fahtzee.visr &add 90 .Fahtzee.curg ""
        set-variable #l2 &add #l0 #l2
        set-registry &cat .Fahtzee.visr &add 170 .Fahtzee.curg &spr "%4d" #l2
      !endif
      set-registry &cat .Fahtzee.visr &add 180 .Fahtzee.curg &spr "%4d" &add #l1 #l2
    !else
      set-registry &cat .Fahtzee.visr @# "   -"
    !endif
    set-variable .Fahtzee.throw 1
    set-variable #l0 .Fahtzee.curp
    set-variable #l1 0
    !repeat
      set-variable #l0 &add #l0 1
      !if &gre #l0 .Fahtzee.nop
        set-variable #l0 1
        !if &equ 2 &inc #l1 1
          set-variable .Fahtzee.curg &add .Fahtzee.curg 1
          set-registry "/fahtzee/curg" .Fahtzee.curg
          !goto break-out
        !endif
      !endif
      set-variable #l2 &reg &spr "/fahtzee/%s/box%s" #l0 .Fahtzee.curg ""
    !until #l2
*break-out
    !if &not &equ #l0 .Fahtzee.curp
      set-registry "/fahtzee/curp" #l0
      set-variable .Fahtzee.curp #l0
    !endif
    fahtzee-set-dice
  !elif &equ @# 8
    set-variable #l0 &add &add &add &add &add &reg &cat .Fahtzee.visr 181 "0" &reg &cat .Fahtzee.visr 182 "0" &reg &cat .Fahtzee.visr 183 "0" &reg &cat .Fahtzee.visr 184 "0" &reg &cat .Fahtzee.visr 185 "0" &reg &cat .Fahtzee.visr 186 "0"
    set-variable $result &cond #l0 &spr "%5d" #l0 ""
  !else
    set-variable $result &reg &cat .Fahtzee.visr @# ""
  !endif
!emacro
0 define-macro fahtzee-set-player-name
  !if &les @# 0
    set-variable #l0 &reg .Fahtzee.visr ""
    set-variable #l0 @ml2 "" #l0
    set-registry .Fahtzee.visr #l0
  !else
    set-variable $result &reg .Fahtzee.visr ""
  !endif
!emacro

0 define-macro fahtzee-dice-turn
  set-variable #l0 6
  !repeat
    !iif &not &lget .Fahtzee.held #l0  set-variable .Fahtzee.dice &lset .Fahtzee.dice #l0 &add &mod $random 6 1
  !until &not &dec #l0 1
!emacro

0 define-macro fahtzee-dice-shake
  fahtzee-dice-turn
  osd .osd.fahtzeed 10 "Gfzc" 5 3 &lget .Fahtzee.dices &lget .Fahtzee.dice 1
  osd .osd.fahtzeed 20 "Gfzc" 5 3 &lget .Fahtzee.dices &lget .Fahtzee.dice 2
  osd .osd.fahtzeed 30 "Gfzc" 5 3 &lget .Fahtzee.dices &lget .Fahtzee.dice 3
  osd .osd.fahtzeed 40 "Gfzc" 5 3 &lget .Fahtzee.dices &lget .Fahtzee.dice 4
  osd .osd.fahtzeed 50 "Gfzc" 5 3 &lget .Fahtzee.dices &lget .Fahtzee.dice 5
  -2 osd
  50 create-callback fahtzee-dice-shake
!emacro

0 define-macro fahtzee-throw
  !iif &or &not .Fahtzee.throw &gre .Fahtzee.throw 3  !return
  !iif &equ .Fahtzee.curg 7  fahtzee-abort
  osd .osd.fahtzee 630 "BxhcfH" .scheme.osd-dbtt " Undo "
  !iif &not &equ .Fahtzee.visp .Fahtzee.curp  .Fahtzee.curp fahtzee-set-player
  fahtzee-dice-shake
  !repeat
    set-variable #l0 @cgk
  !until &not &seq #l0 "mouse-move"
  ; if pick then get the drop!
  !iif &seq &lef #l0 10 "mouse-pick"  set-variable #l0 @cgk
  -1 create-callback fahtzee-dice-shake
  !if &and &exi .fixed &seq #l0 "f"
    set-variable .Fahtzee.dice .fixed
  !else
    fahtzee-dice-turn
  !endif
  set-variable .Fahtzee.throw &add .Fahtzee.throw 1
  fahtzee-set-dice
  -2 osd
!emacro

set-variable .fahtzee-throw.fixed "|5|5|5|5|5|"

0 define-macro fahtzee-hold
  set-variable .Fahtzee.held &lset .Fahtzee.held @# &bxor 1 &lget .Fahtzee.held @# 
!emacro

0 define-macro fahtzee-set-dice
  osd .osd.fahtzeed  3 "c" "\n"
  !if &not .Fahtzee.throw
    osd .osd.fahtzeed  6 "BxcfH" .scheme.osd-dbtt " THROW "
    osd .osd.fahtzeed 60 "BxcfH" .scheme.osd-dbtt " THROW "
  !else
    ; one gotcha is if the player gets a second Fahtzee, the box must be enabled
    set-variable #l0 &add .Fahtzee.curg 150
    !if &gre &reg &cat .Fahtzee.visr #l0 "" 0
      !if &xse .Fahtzee.dice "|\\(.\\)|\\1|\\1|\\1|\\1|"
        osd .osd.fahtzeec #l0 "ExGfh" "     " #l0 fahtzee-entry
      !else
        osd .osd.fahtzeec #l0 "ESGfh" "     " #l0 fahtzee-entry
      !endif
    !endif
    !if &les .Fahtzee.throw 4
      osd .osd.fahtzeed  6 "BxcfH" .scheme.osd-ebtt " \HTHROW "   f fahtzee-throw
      osd .osd.fahtzeed 60 "BxcfH" .scheme.osd-ebtt " \HTHROW "   f fahtzee-throw
      !if &gre .Fahtzee.throw 1
        osd .osd.fahtzeed  3 "c" &cat &sub .Fahtzee.throw 1 "\n"
        set-variable #l0 5
        !repeat
          osd .osd.fahtzeed &mul #l0 10 "RGHfxzc" &cond &lget .Fahtzee.held #l0 .scheme.osd-dbtt $osd-scheme 5 3 &lget .Fahtzee.dices &lget .Fahtzee.dice #l0 #l0 fahtzee-hold
        !until &not &dec #l0 1
        !return
      !endif
      !if &equ .Fahtzee.curg 7
        osd .osd.fahtzeed  6 "BxcfH" .scheme.osd-ebtt " \HCONT. "   f fahtzee-throw
        osd .osd.fahtzeed 60 "BxcfH" .scheme.osd-ebtt " \HCONT. "   f fahtzee-throw
      !endif
    !else
      osd .osd.fahtzeed  6 "BxcfH" .scheme.osd-dbtt " THROW "
      osd .osd.fahtzeed 60 "BxcfH" .scheme.osd-dbtt " THROW "
    !endif
  !endif
  set-variable .Fahtzee.held "|0|0|0|0|0|"
  osd .osd.fahtzeed 10 "Gfzc" 5 3 &lget .Fahtzee.dices &lget .Fahtzee.dice 1
  osd .osd.fahtzeed 20 "Gfzc" 5 3 &lget .Fahtzee.dices &lget .Fahtzee.dice 2
  osd .osd.fahtzeed 30 "Gfzc" 5 3 &lget .Fahtzee.dices &lget .Fahtzee.dice 3
  osd .osd.fahtzeed 40 "Gfzc" 5 3 &lget .Fahtzee.dices &lget .Fahtzee.dice 4
  osd .osd.fahtzeed 50 "Gfzc" 5 3 &lget .Fahtzee.dices &lget .Fahtzee.dice 5
!emacro

0 define-macro fahtzee-set-player
  set-variable .Fahtzee.visp @#
  osd .osd.fahtzeen 10 "I" &add .osd.fahtzeec @#
  !if .Fahtzee.curp
    osd .osd.fahtzeec 4 "EHfxh"  .scheme.osd-entry "###################" f fahtzee-set-player-name
    set-variable .Fahtzee.visr &spr "/fahtzee/%s/" @#
  !else
    osd .osd.fahtzeec 4 "EHfSh" .scheme.osd-entry "###################" f fahtzee-set-player-name
    set-variable .Fahtzee.visr &spr "/fahtzee/h%s/" @#
  !endif
  !if .curp-last
    set-variable #l0 &add .curg-last 10
    !repeat
      osd .osd.fahtzeec #l0 "ESGfh" "     " #l0 fahtzee-entry
    !until &gre &inc #l0 10 190
  !endif
  !if &set .curp-last &equ .Fahtzee.curp @#
    fahtzee-set-dice
    set-variable .curg-last .Fahtzee.curg
    !if &gre .Fahtzee.throw 1
      set-variable #l0 &add .Fahtzee.curg 10
      !repeat
        !iif &seq &reg &cat .Fahtzee.visr #l0 "" ""  osd .osd.fahtzeec #l0 "ExGfh" "     " #l0 fahtzee-entry
      !until &gre &inc #l0 10 190    
    !endif
  !endif
!emacro

0 define-macro fahtzee-set-player1
  1 fahtzee-set-player
!emacro
0 define-macro fahtzee-set-player2
  2 fahtzee-set-player
!emacro
0 define-macro fahtzee-set-player3
  3 fahtzee-set-player
!emacro
0 define-macro fahtzee-set-player4
  4 fahtzee-set-player
!emacro

0 define-macro fahtzee-new
  set-variable #l0 1
  osd-entry "Fahtzee" "\HNumber of Players : " #l0 4
  !if &or &gre #l0 4 &les #l0 1
    osd-dialog "Fahtzee" "Error: Invalid number of players!" "  \HOK  "
    !return
  !endif
  set-registry "/fahtzee/nop" #l0
  set-registry "/fahtzee/curp" 1
  set-registry "/fahtzee/curg" 1
  set-variable .Fahtzee.nop #l0
  set-variable .Fahtzee.curp 1
  set-variable .Fahtzee.curg 1
  set-variable .Fahtzee.visp 1
  set-variable .Fahtzee.throw 1
  !repeat
    !force delete-registry &set #l2 &spr "/fahtzee/%s/" #l0
    set-registry #l2 ""
    set-variable #l1 7
    !while &dec #l1 1
      set-registry &cat #l2 &cat "box" #l1 "13"
      set-registry &cat #l2 &add #l1 "70" " "
      set-registry &cat #l2 &add #l1 "80" " "
      set-registry &cat #l2 &add #l1 "90" " "
      set-registry &cat #l2 &add #l1 "170" " "
      set-registry &cat #l2 &add #l1 "180" " "
    !done
  !until &not &dec #l0 1
  osd .osd.fahtzee 620 "BxhcfH" .scheme.osd-ebtt " Abort Game " f fahtzee-abort
  fahtzee-setup
!emacro

0 define-macro fahtzee-abort
  set-variable #l0 .Fahtzee.nop
  !repeat
    !force delete-registry &cat "/fahtzee/" #l0
  !until &not &dec #l0 1
  !force delete-registry "/fahtzee/curp"
  !force delete-registry "/fahtzee/curg"
  !force delete-registry "/fahtzee/nop"
  osd .osd.fahtzee 620 "BxhcfH" .scheme.osd-dbtt " Abort Game "
  osd .osd.fahtzee 630 "BxhcfH" .scheme.osd-dbtt " Undo "
  set-variable .Fahtzee.nop &reg "/fahtzee/noh" ""
  set-variable .Fahtzee.curp 0
  set-variable .Fahtzee.visp 1
  fahtzee-setup
!emacro

-1 osd .osd.fahtzeec
osd .osd.fahtzeec   0 "Gk"
osd .osd.fahtzeec   2 "fh" "    Player's Name  "
osd .osd.fahtzeec   6 "fh" "    "
osd .osd.fahtzeec   8 "EHSf" .scheme.osd-entry "      " 8 fahtzee-entry
osd .osd.fahtzeec  10 "Gfh" "             Ones  "
osd .osd.fahtzeec  20 "Gfh" "             Twos  "
osd .osd.fahtzeec  30 "Gfh" "           Threes  "
osd .osd.fahtzeec  40 "Gfh" "            Fours  "
osd .osd.fahtzeec  50 "Gfh" "            Fives  "
osd .osd.fahtzeec  60 "Gfh" "            Sixes  "
osd .osd.fahtzeec  70 "Gfh" "            Total  "
osd .osd.fahtzeec  80 "Gfh" "            Bonus  "
osd .osd.fahtzeec  90 "Gfh" "    Section Total  "
osd .osd.fahtzeec 100 "Gfh" "  Three of a Kind  "
osd .osd.fahtzeec 110 "Gfh" "   Four of a Kind  "
osd .osd.fahtzeec 120 "Gfh" "       Full House  "
osd .osd.fahtzeec 130 "Gfh" "     Low Straight  "
osd .osd.fahtzeec 140 "Gfh" "    High Straight  "
osd .osd.fahtzeec 150 "Gfh" "          Fahtzee  "
osd .osd.fahtzeec 160 "Gfh" "           Chance  "
osd .osd.fahtzeec 170 "Gfh" "    Section Total  "
osd .osd.fahtzeec 180 "Gfh" "       Game Total  "

osd .osd.fahtzee1 0  "" fahtzee-set-player1
osd .osd.fahtzee1 10 "It" .osd.fahtzeec

osd .osd.fahtzee2 0  "" fahtzee-set-player2
osd .osd.fahtzee2 10 "It" .osd.fahtzeec

osd .osd.fahtzee3 0  "" fahtzee-set-player3
osd .osd.fahtzee3 10 "It" .osd.fahtzeec

osd .osd.fahtzee4 0  "" fahtzee-set-player4
osd .osd.fahtzee4 10 "It" .osd.fahtzeec

-1 osd .osd.fahtzeed
osd .osd.fahtzeed  0 "Gs" 11 0 -1 -1
osd .osd.fahtzeed  1 ""
osd .osd.fahtzeed  3 "c" "\n"
osd .osd.fahtzeed  6 "BxcfH" .scheme.osd-dbtt " THROW "
osd .osd.fahtzeed  9 "Sfc" "\H1" 10
osd .osd.fahtzeed 15 "Sfc" "\H2" 20
osd .osd.fahtzeed 25 "Sfc" "\H3" 30
osd .osd.fahtzeed 35 "Sfc" "\H4" 40
osd .osd.fahtzeed 45 "Sfc" "\H5" 50
osd .osd.fahtzeed 55 "" 
osd .osd.fahtzeed 60 "BxcfH" .scheme.osd-dbtt " THROW "

osd .osd.fahtzee 00 "batcH" 6 3 .scheme.osd-title "Fahtzee"
osd .osd.fahtzee 10 "Ih" .osd.fahtzeen
osd .osd.fahtzee 20 "I"  .osd.fahtzeed
osd .osd.fahtzee 600 ""
osd .osd.fahtzee 610 "BxhcfH" .scheme.osd-ebtt " \HNew Game "   f fahtzee-new
osd .osd.fahtzee 620 "BxhcfH" .scheme.osd-dbtt " Abort Game "
osd .osd.fahtzee 630 "BxhcfH" .scheme.osd-dbtt " Undo "
osd .osd.fahtzee 640 "BcfH"   .scheme.osd-ebtt " E\Hxit "       1 void

set-variable #l0 10
!repeat
  set-variable #l1 0
  !while &les &inc #l1 1 7
    osd .osd.fahtzeec &add #l0 #l1 "ESGfh" "     " &add #l0 #l1 fahtzee-entry
  !done
  osd .osd.fahtzeec &add #l0 7 "f"
!until &equ &inc #l0 10 190    

osd-bind-key .osd.fahtzee void "mouse-pick-3"
osd-bind-key .osd.fahtzee fahtzee-throw "mouse-drop-3"

