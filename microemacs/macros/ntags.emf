; -!- emf -!-
;
; Copyright (C) 1998-2024 JASSPA (www.jasspa.com)
;
; This is part of JASSPA's MicroEmacs, see the LICENSE file for licensing and
; copying information.
;
; Synopsis:    Command-line macro for creating an nroff tags file
; Authors:     Steven Phillips
;
; Notes:       See gentagsc.emf for usage & info
;
set-variable %tag-id "n"
set-variable %tag-filemask ":*.nrs:*.[1-9]:*.n:"
!iif &not &exi gentags  execute-file "gentagsc"

define-macro ntags-add-file
  set-variable #l9 $buffer-fname
  !iif &seq &lef #l9 %tag-plen %tag-path  set-variable #l9 &rig #l9 %tag-plen
  ml-write &spr "[Parsing file %s]" #l9
  beginning-of-buffer
  !repeat
    !force search-buffer "me" "^\.XI[ \t]+\\(\"[^\n\"]+\"\\|[^\" \t\n]+\\)[ \t]"
    !iif &not $status !break
    !iif &seq "\"" &lef &set #l1 @s1 1  set-variable #l1 &mid #l1 1 -1
    gentags-add-tag #l1
  !done
!emacro

; load in user extensions if found
!force execute-file "myntags"
