; -!- emf -!-
; This is part of the JASSPA MicroEmacs macro files
; Copyright (C) 1998-2004 JASSPA (www.jasspa.com)
; See the file me.emf for copying and conditions.
;
; Created:     1998
; Synopsis:    Command-line macro for creating an nroff tags file
; Authors:     Steven Phillips
;
; Usage:
;      me "@ntags"
;      me "@ntags" <files> <dirs>
;      me "@ntags" "-v%tag-option=rm"
;      me "@ntags" "-v%tag-option=r" <files> <dirs>
;
; Options:
;      r - recurse, a - append, m - multiple links to a tag
set-variable %tag-option   &cond &exi %tag-option   %tag-option   ""
set-variable %tag-filemask &cond &exi %tag-filemask %tag-filemask ":*.nrs:*.[1-9]:*.n:"
set-variable %tag-ignore   &cond &exi %tag-ignore   %tag-ignore   ":SCCS/:CVS/:"

define-macro ntags-add-file
    set-variable #l2 $buffer-fname
    !if &seq &lef #l2 #g1 #g2
        set-variable #l2 &rig #l2 #g1
    !endif
    ml-write &spr "[Parsing file %s]" #l2
    1 buffer-mode "magic"
    beginning-of-buffer
    !repeat
        !force search-forward "^\.XI"
        !if $status
            !while &sin @wc " \t\""
                forward-char
            !done
            set-variable #l1 $window-col
            !while &not &sin @wc " \t\""
                forward-char
            !done
            set-variable #l1 &mid @wl #l1 &sub $window-col #l1
            set-variable #l3 @wl
            next-window
            !tjump &sin "m" %tag-option 4
            beginning-of-buffer
            !force search-forward &spr "^%s\t" #l1
            !if &not $status
                end-of-buffer
                insert-string #l1
                insert-string "\t"
                insert-string #l2
                insert-string "\t/^"
                insert-string #l3
                insert-string "$/"
            !endif
            next-window
        !endif
    !until &not $status
!emacro

define-macro ntags-parse-tags
    ml-write "All done! Parsing tags file."
    beginning-of-buffer
    set-mark
    replace-string "\\\\" "\\\\\\\\"
    end-of-buffer
    sort-lines
    beginning-of-buffer
!emacro
    
define-macro ntags-process-buffers
    !repeat
        set-variable #l0 $buffer-bname
        !if &xse #l0 "^.*<[0-9]*>$"
            set-variable #l1 &lef #l0 &sub &rsin "<" #l0 1
        !else
            set-variable #l1 #l0
        !endif
        !if &iseq #l1 "tags"
            !return
        !elif &lfind %tag-ignore #l1
        !elif &bmod "dir"
            set-variable #l2 $buffer-fname
            set-variable #l3 0
            !while &not &seq &set #l4 &lget %tag-filemask &inc #l3 1 ""
                !force find-file &cat #l2 #l4 
            !done
        !else
            beginning-of-buffer
            !force ntags-add-file
            !if &not $status
                ml-write &spr "[Warning: ntags-add-file failed on \"%s\"]" $buffer-fname
                !bell
                ;set-variable #l9 @cq
            !endif
        !endif
        0 delete-buffer #l0
    !until 0
!emacro

define-macro ntags
    !if &seq &lget %tag-filemask 1 ""
        ml-write "[No input file mask set!]"
        !abort
    !endif
    delete-other-windows
    split-window-vertically
    find-buffer "*scratch*"
    find-file "tags"
    !force 0 delete-buffer "*scratch*"
    set-variable #g1 &rsin "/" $buffer-fname
    set-variable #g2 &lef $buffer-fname #g1
    !if &not &sin "a" %tag-option
        beginning-of-buffer
        set-mark
        end-of-buffer
        -1 kill-region
    !else
        end-of-buffer
    !endif
    !if &sin "r" %tag-option
        set-variable %tag-filemask &lins %tag-filemask -1 "[^.]*/"
    !endif
    1 buffer-mode "magic"
    1 buffer-mode "exact"
    next-window
    !if &iseq $buffer-bname "tags"
        !force find-file "."
    !endif
    ntags-process-buffers
    ntags-parse-tags
!emacro
  
define-macro start-up
    ntags
    quick-exit
!emacro

; load in user extensions if found
!force execute-file "myntags"
