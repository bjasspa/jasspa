; -!- emf -!-
;
; Copyright (C) 2001-2024 JASSPA (www.jasspa.com)
;
; This is part of JASSPA's MicroEmacs, see the LICENSE file for licensing and
; copying information.
;
; Synopsis:    Create a list of abbreviations for the toolbar
; Authors:     Steven Phillips
;
; Notes:       To use this tool via key bindings add a binding eaf-list-goto, i.e.
;
;    global-bind-key eaf-list-goto "f9"
;
; to get into the abbrev-list, then use 'return' to insert an abbrev and f2 to open/close a fold
;
!iif &not &exi meth-add-format  execute-file "meth"

0 define-macro eaf-list-fold-find-close
  set-variable #l1 $window-aline
  set-variable #l0 1
  !repeat
    !force search-buffer "me" "\CE[OCF]"
    !iif &not $status  -8 ml-write &spr  "[missing </FOLD> for <FOLD+> on line %d]" &sub #l1 1
    !if &seq @s0 "\CEF"
      !iif &not &dec #l0 1  !return
    !else
      set-variable #l0 &add #l0 1
    !endif
  !done
!emacro

define-macro eaf-list-execute
  0 show-region
  set-variable #l5 $result
  set-variable #l0 &add &sin "\CEO" @wl &sin "\CEC" @wl
  !if @?
  !elif &not &sin "\CEL" @wl
    set-variable @# 2
  !elif &not #l0
    set-variable @# 1
  !else
    set-variable @# &con &les #l0 $window-col 1 2
  !endif
  !if &band @# 2
    !if &not #l0
      !iif #l8  goto-position "\x82"
      !return
    !endif
    set-variable #l1 &con &seq &mid @wl #l0 1 "O" "C" "O"
    -1 buffer-mode "view"
    set-variable #l2 $window-line
    set-variable #l9 $buffer-bname
    !force 0 find-buffer :eaf-list
    !if $status
      set-variable $window-line #l2
      set-variable $window-col #l0
      forward-delete-char
      insert-string #l1
      !if &seq #l1 "C"
        set-variable $window-line &add #l2 1
        set-mark
        !force eaf-list-fold-find-close
        !if $status
          set-variable #l3 $window-line
          4 narrow-buffer
          find-buffer #l9
          set-variable $window-line &add #l2 1
          set-mark
          set-variable $window-line #l3
          -1 kill-region
        !endif
      !else
        set-variable $window-line &add #l2 1
        !force 2 narrow-buffer
        !if $status
          set-mark
          set-variable $window-line &add #l2 1
          0 copy-region
          find-buffer #l9
          -1 buffer-mode "view"
          set-variable $window-line &add #l2 1
          yank
          -1 yank
        !endif
      !endif
      find-buffer #l9
      set-variable $window-line #l2
    !endif
    set-variable $window-col #l0
    forward-delete-char
    insert-string #l1
    -1 buffer-mode "edit"
    1 buffer-mode "view"
  !elif &set #l0 &sin "\CEL" @wl
    !iif #l8  set-variable #l2 &or &seq &trb &lef &rep @wl "\CEF" "" &add $window-col 1 "" &seq &trb &mid @wl $window-col &sub &sub #l0 1 $window-col ""
    set-variable #l0 &rig @wl &add #l0 1
    !if #l8
      goto-position "\x82"
    !else
      set-variable #l5 -1
      !if &len &set #l3 &rig :eaf-list 10
        set-variable #l4 $window-id
        !repeat
          3 next-window
          !iif &equ #l4 $window-id  !break
          !iif &or &seq :chook #l3 &seq $buffer-major-mode #l3  set-variable #l5 &con &equ #l5 -1 $window-id -2
        !done
      !endif
      !if &les #l5 0
        -8 ml-write &cat "Abbrev Error: " &con &equ #l5 -1 "Failed to find suitable window to insert into" "More than 1 suitable window to insert into"
      !else
        6 next-window
        !while &sub #l5 $window-id
          2 next-window
        !done
      !endif
    !endif
    !iif &band #l5 1  !force show-region
    !iif &not &and #l8 #l2  execute-line &spr "execute-string \"%s\"" #l0
    !return
  !endif
  !iif #l8  goto-position "\x82"
!emacro

define-macro eaf-list-goto
  0x21 set-position "\x82"
  !force toolbar-make-tool-visible "*abbrev-list*"
  set-variable #l0 $window-id
  !repeat
    3 next-window
    !iif &equ #l0 $window-id  -8 ml-write "[Failed to locate *abbrev-list* window]"
  !until &seq $buffer-bname "*abbrev-list*"
!emacro

0 define-macro eaf-list-create
  set-position "\x86"
  !if &exi :chook
    set-variable #l9 :chook
  !elif &seq &set #l9 $buffer-major-mode ""
    set-variable #l9 "default"
  !endif
  set-variable #l0 &cat "*eaf-list*" &cat @3 #l9
  find-buffer @1
  -1 buffer-mode "undo"
  -1 buffer-mode "view"
  beginning-of-buffer
  set-mark
  end-of-buffer
  -1 kill-region
  !if &band @# 0x003
    ; If this is a tool create or toolbar refresh then force a complete update
    !force 0 delete-buffer #l0
  !endif
  !force 0 find-buffer #l0
  !if &not $status
    find-buffer #l0
    1 buffer-mode "hide"
    1 buffer-mode "magic"
    -1 buffer-mode "undo"
    0 major-mode #l9
    insert-string &spr "%s %s\n" &lget $result 5 @2
    !force 0 delete-buffer &set #l1 "*eaf-list-tmp*"
    find-buffer #l1
    !force insert-file &find &cat @3 #l9 ".eaf"
    ; process inserts first
    beginning-of-buffer
    !repeat
      !force search-buffer "me" " +<INCLUDE: *\\([^>\n]+\\)>"
      !iif &not $status  !break
      beginning-of-line
      set-mark
      forward-line
      -1 kill-region
      !force insert-file &find @s1 ".eaf"
      exchange-point-and-mark
    !done
    set-variable #l2 1
    beginning-of-buffer
    !while &not &seq @wc ""
      set-variable #l3 &xre @wl "^\\([^ ]*\\)? +\"\\(\\(\\\\.\\|[^\\\\\"\n]\\)*\\)\" +\"\\(\\(\\\\.\\|[^\\\\\"\n]\\)*\\)\".*" "\\4"
      !if &not &seq #l3 @wl
        set-variable #l4 &xre @wl "^\\([^ ]*\\)? +\"\\(\\(\\\\.\\|[^\\\\\"\n]\\)*\\)\" +\"\\(\\(\\\\.\\|[^\\\\\"\n]\\)*\\)\".*" "\\2"
        find-buffer #l0
        end-of-buffer
        !if &seq #l3 ""
          insert-newline
        !else
          execute-line &spr "insert-string \"%s\"" #l3
          !iif &not &seq #l4 ""  insert-string &cat "\CEL" #l4
        !endif
        find-buffer #l1
      !endif
      set-variable $window-line &inc #l2 1
    !done
    !force 0 delete-buffer #l1
    find-buffer #l0
    end-of-buffer
    !repeat
      !force search-buffer "meb" "<\\(/FOLD\\|FOLD[-+]\\)>"
      !iif &not $status  !break
      &len @s0 forward-delete-char
      -1 yank
      !if &seq "+" &set #l2 &rig @s1 -1
        insert-string "\CEC"
        set-variable #l1 $window-line
        set-variable #l3 $window-col
        beginning-of-line
        forward-line
        set-mark
        !force eaf-list-fold-find-close
        !iif $status  4 narrow-buffer
        set-variable $window-line #l1
        set-variable $window-col #l3
      !else
        insert-string &con &seq #l2 "-" "\CEO" "\CEF"
      !endif
    !done
    -1 buffer-mode "edit"
  !endif
  beginning-of-buffer
  set-mark
  end-of-buffer
  0 copy-region
  -1 find-buffer #l0
  !force 0 popup-window @1
  !iif &not $status  find-buffer @1
  yank
  -1 yank
  !force forward-delete-char
  beginning-of-buffer
  set-variable $line-scheme .scheme.header
  set-variable $buffer-hilight .hilight.meth
  set-variable :mouse-pick-1 mouse-void
  set-variable :mouse-drop-1 eaf-list-execute
  set-variable :eaf-list #l0
  1 buffer-bind-key eaf-list-execute "return"
  2 buffer-bind-key eaf-list-execute "space"
  2 buffer-bind-key eaf-list-execute "f2"
  -1 buffer-mode "edit"
  1 buffer-mode "view"
  goto-position "\x86"
!emacro

0 define-macro abbrev-list-create
  @# eaf-list-create "*abbrev-list*" "Abbrevs" ""
!emacro

0 define-macro tool-list-create
  @# eaf-list-create "*tool-list*" "Tools" "hk"
!emacro

define-macro abbrev-list
  set-position "\x81"
  !force &cond @? 1 0 abbrev-list-create
  !if &not $status
    goto-position "\x81"
    -8 ml-write "[Failed to load abbreviation list for current buffer]"
  !endif
  !force 0 popup-window "*abbrev-list*"
  !if $status
    goto-position "\x81"
  !else
    delete-other-windows
    !if &equ @# 2
      split-window-vertically
    !else
      split-window-horizontally
      change-window-width .width
    !endif
    find-buffer "*abbrev-list*"
    3 next-window
  !endif
!emacro
!iif &not &exist .abbrev-list.width  set-variable .abbrev-list.width 25
