; -!- emf -!-
; This is part of the JASSPA MicroEmacs macro files
; Copyright (C) 2000-2004 JASSPA (www.jasspa.com)
; See the file me.emf for copying and conditions.
;
; Created:     Wed Jan 12 2000
; Synopsis:    File-browser extensions to support ftp
; Authors:     Steven Phillips
;
!if &not &exi dirlst-sort-list
    0 exec-file "hkdirlst"
!endif

define-macro ftp
    !if &band $window-flags 0x1000
        ; if this is a toolbar window move to the next normal window
        !force next-window
    !endif
    @# ftp-create-layout
    popup-window "*ftp-files*"
    !if &seq $buffer-fname ""
        screen-update
        ftp-connect
    !endif
!emacro

!if &not &exi .osd.ftp
    set-variable .osd.ftp &pinc .osd.next 1
    set-variable .ftp.name ""
    set-variable %ftp-flags &reg  "/" "url/ftp-flags" "csp"
!endif

define-macro ftp-connect
    ftp-data-entry-setup
    .osd.ftp osd
!emacro

0 define-macro ftp-rename-buffer
    set-variable #l0 &len $buffer-fname
    !if &equ #l0 0
        delete-buffer $buffer-bname
    !else
        1 buffer-mode "hide"
        !if &seq &rig $buffer-fname &sub #l0 1 "/"
            set-variable #l1 &rig $buffer-fname &rsin "/" &lef $buffer-fname &sub #l0 1
        !else
            set-variable #l1 &rig $buffer-fname &rsin "/" $buffer-fname
        !endif
        set-variable $buffer-names #l1
        !if &not &seq $buffer-names ""
            set-variable #l0 0
            set-variable #l2 &cat #l1 "<%d>"
            !repeat
                set-variable #l1 &spr #l2 &inc #l0 1
                set-variable $buffer-names #l1
            !until &seq $buffer-names ""
        !endif
        change-buffer-name #l1
    !endif
!emacro

0 define-macro ftp-create-layout
    !if &band $window-flags 0x1000
        ; if this is a toolbar window move to the next normal window
        !force next-window
    !endif
    !if &seq $buffer-fname ""
        set-variable #l0 ""
        set-variable #l1 ""
    !elif &seq &lef $buffer-fname 6 "ftp://"
        set-variable #l0 ""
        set-variable #l1 &lef $buffer-fname &rsin "/" $buffer-fname
    !else
        set-variable #l0 &stat "t" $buffer-fname
        !if &seq #l0 "D"
            set-variable #l0 $buffer-fname
        !elif &seq #l0 "R"
            set-variable #l0 &lef $buffer-fname &rsin "/" $buffer-fname
        !else
            set-variable #l0 ""
        !endif
        set-variable #l1 ""
    !endif
    !if &seq #l0 ""
        set-variable #l0 &stat "a" "."
    !endif
    1 set-position "\x81"
    !if &sin "s" %ftp-flags
        !if .toolbar.open
            !force toolbar-make-tool-visible "*ftp-console*"
            !tjump &not $status 2
        !else
            !force 0 popup-window "*ftp-console*"
            !if &not $status
                delete-other-windows
                2 split-window-vertically
                change-window-depth 15
                find-buffer "*ftp-console*"
                buffer-bind-create "bio" "delete" "" file-browser-close
                3 previous-window
                1 set-position "\x81"
            !else
                goto-position "\x81"
            !endif
        !endif
    !endif
    !if @#
        ; setup the local *files* directory
        !force 0 popup-window "*files*"
        find-buffer "*files*"
        !if &not &and &seq #l0 $buffer-fname &seq .fhook-dirlst.mode :dirlst-mode
            read-file #l0
            !if &not &seq .fhook-dirlst.mode :dirlst-mode
                ; old buffer with the wrong processing - refresh
                !force 0 delete-buffer $buffer-bname
                find-file #l0
            !endif
            change-buffer-name "*files*"
        !endif
        !if &not &band $window-flags 0x1000
            buffer-bind-create "bio" "delete" "" file-browser-close
        !endif
        buffer-bind-create "bio" "tab" "" file-browser-swap-buffers
    !endif
    goto-position "\x81"
    ; setup the ftp *ftp-files* directory
    !force 0 popup-window "*ftp-files*"
    !if &not $status
        !if &seq $buffer-bname "*files*"
            2 split-window-horizontally
        !endif
        find-buffer "*ftp-files*"
    !endif
    !if &seq #l1 $buffer-fname
        !if &seq #l1 ""
            set-variable :mouse-pick-1 void
            set-variable :mouse-drop-1 void
            set-variable :mouse-pick-3 void
            set-variable :mouse-drop-3 ftp-connect
        !endif
    !elif &not &seq #l1 ""
        ftp-rename-buffer
        read-file #l1
        change-buffer-name "*ftp-files*"
    !endif
    !if &not &band $window-flags 0x1000
        buffer-bind-create "bio" "delete" "" file-browser-close
    !endif
    buffer-bind-create "bio" "tab" "" file-browser-swap-buffers
!emacro

0 define-macro ftp-console-tool
    !if &sin "s" %ftp-flags
        set-variable %ftp-flags &rep %ftp-flags "s" ""
    !endif
!emacro

0 define-macro ftp-regfile-entry
    !if &les @# 0
        set-variable #l2 &reg "/history" "url-file" ""
        set-variable #l0 @ml2 "" #l2
        !if &seq #l0 #l2
            !return
        !endif
        !if &seq #l0 ""
            ; user has removed the file name
            delete-registry "/history/url-file"
            set-registry "/" "url" ""
            mark-registry "/url" "F"
            !return
        !endif
        ; me file name entered remove the .e?f extension, if found must force an osd update
        !if &xis &rig #l0 &sub &len #l0 4 "\\.e.f"
            set-variable #l0 &lef #l0 &sub &len #l0 4
            -1 osd
        !endif
        ; is it a base name or full path? set #l0 to base and #l1 to full file name
        !if &sin "/" #l0
            set-variable #l1 &cat #l0 ".erf"
            set-variable #l0 &rig #l0 &rsin "/" #l0
        !else
            ; try to find it, if not add to $user-path
            !if &seq &set #l1 &find #l0 ".erf" "ERROR"
                set-variable #l1 &stat "a" &spr "%s%s.erf" $user-path #l0
            !endif
        !endif
        set-variable #l3 &stat "r" #l1
        ; we now have #l0 = new base, #l1 new file name, #l2 old file name, #l3 new file exist?
        !if #l3
            ; has the current url file been changed
            set-variable $result ""
            !force mark-registry "/url" "g"
            !if &sin "u" $result
                ; file changed, confirm action
                osd-xdialog "FTP" "Current URL registry file has been changed" 1 " \HSave " " \HDiscard " " \HCancel "
                !if &equ $result 1
                    !force save-registry "/url" "" @mna
                    !if &not $status
                        -1 osd
                        !return
                    !endif
                !endif
                !if &equ $result 3
                    -1 osd
                    !return
                !endif
            !endif
            ; delete the existing registry
            !force mark-registry "/url" "U"
            !force 0 delete-registry "/url" @mna
            ; read the registry
            read-registry "url" #l1 "cby"
            ; clear the current entry
            set-variable .ftp.name ""
        !else
            ; new file does not exist, write it now to create it and change the
            ; current file to the new name
            set-registry "/" "url" #l1
            mark-registry "/url" "fyb"
            !force save-registry "/url" #l1 @mna
        !endif
        ; can we store just the base name or the whole path?
        !if &seq &find #l0 ".erf" #l1
            set-variable #l1 #l0
        !endif
        set-registry "/history" "url-file" #l1
        ; force an osd update
        -1 osd
    !else
        set-variable $result &reg "/history" "url-file" ""
    !endif
!emacro

0 define-macro ftp-console-checkbox
    set-variable #l0 &sin "s" %ftp-flags
    !if &les @# 0
        set-variable %ftp-flags &cond #l0 "cp" "csp"
        set-registry "/url" "ftp-flags" %ftp-flags
        mark-registry "/url" "u"
        previous-window
        ftp-create-layout
        screen-update
    !elif &not #l0
        !abort
    !endif
!emacro

0 define-macro ftp-site-name-set
    set-variable #l0 @1
    set-variable .ftp.name #l0
    !if &not &seq #l0 ""
        !force find-registry "/" "url/ftp" 0
        !if &not $status
            set-registry "/" "url/ftp" ""
        !endif
        !force find-registry "/url/ftp" #l0 0
        !if &not $status
            set-registry "/url/ftp" #l0 ""
            set-registry "/url/ftp" &cat #l0 "/path" "/"
            mark-registry "/url" "u"
        !endif
    !endif
!emacro

0 define-macro ftp-data-entry-setup
    ; if we have just changed the name or host,
    ; should the user/pass/path be enabled/disabled
    !if &seq &reg "/url/ftp" &cat .ftp.name "/host" "" ""
        osd .osd.ftp 100 "Shf" "    User Name"
        osd .osd.ftp 101 "Sf" "       Password"
        osd .osd.ftp 110 "SExHhf" .scheme.osd-entry "############"
        osd .osd.ftp 130 "SExHfR" .scheme.osd-entry "############"
        osd .osd.ftp 140 "Sf" "    Initial Host Path"
        osd .osd.ftp 150 "SExHf"  .scheme.osd-entry "############################"
    !else
        osd .osd.ftp 100 "Shf" "    \HUser Name" 110
        osd .osd.ftp 101 "Sf" "       \HPassword" 130
        osd .osd.ftp 110 "EtxHhf" 11 .scheme.osd-entry "############" 3 ftp-data-entry
        osd .osd.ftp 130 "EtxHfR" 13 .scheme.osd-entry "############" 4 ftp-data-entry
        osd .osd.ftp 140 "Sf" "    \HInitial Host Path" 150
        osd .osd.ftp 150 "EtxHf"  15 .scheme.osd-entry "############################" 5 ftp-data-entry
    !endif
!emacro

0 define-macro ftp-host-setup
    -1 osd .osd.tmp
    osd .osd.tmp 0 "bso" -28 0 30 0 -1 -1
    set-variable #l0 0
    !while 1
        !force find-registry "/url" "ftp" &pinc #l0 1
        !if &not $status
            !if &equ #l0 1
                osd .osd.tmp 1 "" "<None Defined>"
            !endif
            !return
        !endif
        osd .osd.tmp #l0 "" $result 1 ftp-host-set
    !done
!emacro

0 define-macro ftp-host-set
    ftp-site-name-set $result
    ftp-data-entry-setup
!emacro

0 define-macro ftp-data-entry
    set-variable #l2 &lget "|name|host|user|pass|path|" &abs @#
    !if &equ &abs @# 1
        set-variable #l1 .ftp.name
    !else
        set-variable #l1 &reg "/url/ftp" &spr "%s/%s" .ftp.name #l2 ""
    !endif
    !if &les @# 0
        !if &equ @# -4
            set-variable #l0 @ml8 ""
        !else
            set-variable #l0 @ml2 "" #l1
        !endif
        !if &seq #l0 #l1
            !return
        !endif
        !if &equ @# -1
            ; entered a name, cannot be empty
            !if &seq #l0 ""
                !return
            !endif
            !force find-registry "/url/ftp" #l0 0
            !if $status
                ; if we already have an ftp entry of this name, simply change to it
            !elif &not &or &seq .ftp.name "" &seq .ftp.name #l0
                ; if not and we are already looking at an ftp entry, does
                ; the user want to rename this one or create anew one?
                set-variable #l9 &spr "  Rename \"%s\" to \"%s\" or create new?  " .ftp.name #l0
                osd-xdialog "FTP" #l9 1 " \HRename " " Create \HNew " " \HCancel "
                !if &equ $result 3
                    !return
                !endif
                !if &equ $result 1
                    copy-registry &cat "/url/ftp/" .ftp.name &cat "/url/ftp/" #l0
                    delete-registry &cat "/url/ftp/" .ftp.name
                !endif
            !endif
            ftp-site-name-set #l0
        !else
            !if &and &equ @# -2 &seq .ftp.name ""
                !if &seq #l0 ""
                    -1 osd
                    !return
                !endif
                ftp-site-name-set #l0
                -1 osd
            !endif
            !if &equ @# -5
                !if &not &seq &lef #l0 1 "/"
                    set-variable #l0 &cat "/" #l0
                !endif
            !endif
            !if &seq #l0 ""
                delete-registry "/url" &spr "ftp/%s/%s" .ftp.name #l2
            !else
                set-registry "/url" &spr "ftp/%s/%s" .ftp.name #l2 #l0
                !if &equ @# -4
                    mark-registry &spr "/url/ftp/%s/%s" .ftp.name #l2 "!"
                !endif
            !endif
        !endif
        mark-registry "/url" "u"
        ftp-data-entry-setup
    !elif &equ @# 4
        set-variable $result &spr "%n" &len #l1 "*"
    !else
        set-variable $result #l1
    !endif
!emacro

0 define-macro ftp-do-connect
    !if &seq .ftp.name ""
        !abort
    !endif
    set-variable #l0 &reg "/url/ftp" &cat .ftp.name "/path" "/"
    find-file &spr "ftp://%s%s" .ftp.name #l0 @mna
    !if &not &seq $buffer-bname "*ftp-files*"
        set-variable #l1 $buffer-bname
        !force 0 find-buffer "*ftp-files*"
        !if $status
            ftp-rename-buffer
        !endif
        find-buffer #l1
        !force change-buffer-name "*ftp-files*"
    !endif
    buffer-bind-create "bio" "delete" "" file-browser-close
    buffer-bind-create "bio" "tab" "" file-browser-swap-buffers
!emacro

-1 osd .osd.ftp
osd .osd.ftp 00  "batcdHs" 3 6 39 0 -1 -1 630 .scheme.osd-title "FTP Connection"
osd .osd.ftp 10  ""
osd .osd.ftp 20  "Sf" "    \HRegistry File" 30
osd .osd.ftp 25  "hf" "    "
osd .osd.ftp 30  "EtxHf" 3 .scheme.osd-entry "############################" f ftp-regfile-entry
osd .osd.ftp 35  ""
osd .osd.ftp 39  "Sfh" "    \HDisplay FTP Console: " 40
osd .osd.ftp 40  "Ctfx" 4 "^^NY^^" 1 ftp-console-checkbox
osd .osd.ftp 45  ""
osd .osd.ftp 50  "Sf" "    \HSite Name" 70
osd .osd.ftp 55  "hf" "    "
osd .osd.ftp 60  "EtxHfhR" 6 .scheme.osd-entry "############################" 1 ftp-data-entry
osd .osd.ftp 70  "MdmxstfHiR" 7 .scheme.osd-ebtt &mid $window-chars 10 1 .osd.tmp ftp-host-setup
osd .osd.ftp 80  "Sf" "    \HHost Address" 90
osd .osd.ftp 85  "hf" "    "
osd .osd.ftp 90  "EtxHfR"  9 .scheme.osd-entry "############################" 2 ftp-data-entry
osd .osd.ftp 105 "hf" "    "
osd .osd.ftp 120 "hf" "    "
osd .osd.ftp 145 "hf" "    "
osd .osd.ftp 600 ""
osd .osd.ftp 610 ""
osd .osd.ftp 620 "BthcfH" 62 .scheme.osd-ebtt " \HConnect " f ftp-do-connect
osd .osd.ftp 630 "BtcfH"  63 .scheme.osd-ebtt " E\Hxit  "   f void
ftp-site-name-set ""
