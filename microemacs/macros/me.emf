; -!- emf -!-
; Copyright (C) 1988-2025 JASSPA (www.jasspa.com).
;
; This is part of JASSPA's MicroEmacs, see the LICENSE file for licensing and
; copying information.
;
; Synopsis:    Main start-up file
; Authors:     Steven Phillips & Jon Green
;
; Is this macro release compatible with the binary?
!if &les $version "20260100"
  5000 ml-write "[Warning: The MicroEmacs executable is not compatible with this macro release]"
!endif
!if &band @# 2
  ; This is a reinit via user-setup, no point reloading erf (and if readonly it may be wrong)
!elif &seq &set #l0 &find $user-name ".erf" "ERROR"
  ; If <$user-name>.erf registry file isn't found then this is a new user
  !force read-registry "/history" "newuser" "cb"
  define-macro-file newuser new-user
  define-macro start-up
    ; Startup for a new user.
    !iif &seq $buffer-bname "*scratch*"  new-user
  !emacro
!else
  !force read-registry "/history" #l0 "cb"
  ml-write &cat "Setting up for user " $user-name
!endif
;
; Define extern file macros first
0 define-macro-file langutl spell-rules-init
0 define-macro-file filetool file-tool-run file-tool-exec file-tool-exec-default
0 define-macro-file osd osd-main osd-find-buffer osd-dialog osd-xdialog osd-entry osd-close osd-pinstall me-find-file
0 define-macro-file utils frame-handle ascii-time str-to-regex filemask-to-regex file-list macro-history-add copy-registry create-file-path reread-settings return-get-next-line mouse-get-next-line
define-macro-file format clean sort-lines-ignore-case transpose-region-line normal-tab tabs-to-spaces paragraph-to-line delete-indentation restyle-region restyle-buffer ifill-paragraph indent-increase indent-decrease
define-macro-file tools change-company compile keep-next-buffer which diff rdiff xdiff diff-changes execute-tool uniq find-bfile find-cfile find-hfile find-lfile find-ufile reread-file reread-all save-kbd-macro
define-macro-file clearcs cc-state cc-update cc-checkout cc-checkin cc-add cc-diff cc-uncheckout cc-annotate cc-describe cc-delete cc-move cc-merge cc-vtree cc-explorer cc-add-view cc-setup
define-macro-file svn svn svn-setup svn-add svn-blame svn-cat svn-checkout svn-commit svn-commit-log svn-copy svn-delete svn-diff svn-ignore svn-info svn-log svn-move svn-status svn-update
define-macro-file misc save-all set-title encase open-line get-next-indent-change check-line-length number-items time compare-windows-exact display-whitespaces write-region execute-region
define-macro-file cvs cvs cvs-setup cvs-add cvs-annotate cvs-checkout cvs-commit cvs-diff cvs-gdiff cvs-log cvs-remove cvs-resolve-conflicts cvs-state cvs-status cvs-tag cvs-update
define-macro-file git git git-setup git-add git-blame git-checkout git-commit git-diff git-fetch git-log git-move git-pull git-push git-remove git-show git-status
define-macro-file osdmisc insert-symbol buffer-setup alarm browse alias-path-setup file-type-setup file-tool-setup file-tool-menu shell-tool-setup find-setup
define-macro-file abbrev expand-abbrev-handle expand-iso-accents expand-look-back expand-word iso-accents-mode
define-macro-file comment comment-line uncomment-line comment-to-end-of-line comment-start comment-restyle
define-macro-file games cross-word Brag Fahtzee Mahjongg Match-It Matrix Metris Patience Triangle Typing
define-macro-file find find grep grep-for-region replace-string-in-files query-replace-string-in-files
define-macro-file rect string-rectangle space-rectangle copy-rectangle yank-rectangle-overwrite
define-macro-file favorite favorites-add favorites-edit favorites-goto favorites
define-macro-file password password-to-phonic generate-password insert-password
define-macro-file toolbar toolbar toolbar-close toolbar-open toolbar-mode-line
define-macro-file majormod major-mode-setup major-mode-help major-mode-tool
define-macro-file spell spell-word spell-buffer spell-edit-word find-word
define-macro-file narrow narrow-search-forward narrow-search-backward
define-macro-file notes notes notes-file notes-instant notes-context
define-macro-file charsutl buffer-is-utf8 change-buffer-charset
define-macro-file itemlist item-list item-list-close occur
define-macro-file hktools file-exec file-format file-lint
define-macro-file fileopen osd-file-open osd-get-file
define-macro-file spellaut auto-spell auto-spell-menu
define-macro-file pagefile page-file page-bfile
define-macro-file calc calc-op calc insert-calc
define-macro-file zfile find-zfile zfile-setup
define-macro-file gentags generate-tags-file
define-macro-file dmf display-matching-fence
define-macro-file etfinsrt insert-template
define-macro-file xfind xfind xgrep rgrep
define-macro-file schemosd scheme-editor
define-macro-file mouseosd context-menu
define-macro-file printstp print-setup
define-macro-file abbrlist abbrev-list
define-macro-file diffline diff-lines
define-macro-file fattrib file-attrib
define-macro-file organize organizer
define-macro-file userstp user-setup
define-macro-file word describe-word
define-macro-file watch
define-macro-file gdiff
define-macro-file draw
define-macro-file ftp
define-macro-file xrdb xrdb-scheme
;
; Define list of supported major-modes, start with modes that define content-ids as the order is more important
29 major-mode "sh" ".sh .ksh .csh .login .bashrc .cshrc .tcshrc .zshrc .profile" "UNIX Shell script" 1 "^#![ \t]*/.*sh"
16 major-mode "\\1" 1 "^#![ \t]*/.*\\(perl\\|awk\\|python\\|ruby\\)"
23 major-mode "make" "makefile" "makefile .mak .mk .gmk" 1 "^#![ \t]*/.*make"
29 major-mode "tcl" ".tcl .tk" "Tcl/Tk" 1 "^#![ \t]*/.*\\(wish\\|tclsh\\)"
29 major-mode "r" ".r .rtk" "R/S-Plus" -1 "^#![ \t]*/.*Rscript" r-doc
16 major-mode "\\1" -2 "-[*!]-[ \t]*\\([^ \t\n:;]+\\).*?-[*!]-"
16 major-mode "\\1" -2 "-[*!]-[ \t]*mode:[ \t]*\\([^ \t\n:;]+\\).*?-[*!]-"
21 major-mode "xml" ".xml .xsl .xul .xaml" -1 "<\\?xml"
5 major-mode "ada" ".ada .adb .ads"
5 major-mode "adoc" ".tdoc .adoc .asciidoc"
; TODO httpd.conf & smb.conf are very specific and apache can used different names, would be better to have more generic hondlers
5 major-mode "apache" "httpd.conf"
9 major-mode "apt" "Maven APT"
7 major-mode "asm" "assembler" ".asm .x86"
1 major-mode "asn1"
9 major-mode "asp" "ASP"
13 major-mode "autoit" ".au3" "AutoIt"
5 major-mode "awk" ".awk .nawk .gawk"
13 major-mode "bat" ".bat .btm .cmd" "Win/DOS Batch"
13 major-mode "bibtex" ".bib" "BibTeX"
5 major-mode "binary" ""
13 major-mode "bison" ".l .ll .lxx .y .yy yxx" "Bison (Lex/Yacc)"
13 major-mode "blist" "*buffers*" "Buffer List"
1 major-mode "bnf"
5 major-mode "c" ".c .h .def .i"
1 major-mode "c3"
5 major-mode "cfml" ".cfm .cfml .cfc"
5 major-mode "cobol" ".cbl"
7 major-mode "cpp" "c++" ".cc .cpp .cxx .hh .hpp .hxx .rc"
1 major-mode "cs"
1 major-mode "css"
1 major-mode "dart"
29 major-mode "diff" "*diff* .diff .dif" "Diff File" 1 "^diff"
13 major-mode "dirlst" "/ *files*" "Directory List" copy-file delete-file file-browser file-browser-close file-browser-swap-buffers
13 major-mode "dirtre" "*directory*" "Directory Tree"
; TODO Need to sort out doc & txt for best support across editors.
7 major-mode "doc" "document" ".doc"
9 major-mode "dot" "Dot/Neato"
13 major-mode "ehf" "*help* *about*" "ME Help"
9 major-mode "emf" "ME Macro"
9 major-mode "erf" "ME Registry"
7 major-mode "euphor" "euphoria" ".ex .exw .exu .e .ew .eu"
7 major-mode "f" "fortran" ".f .f77 .f90"
5 major-mode "fusion" ".fu"
5 major-mode "fvwm" ".fvwm .fvwm2rc"
1 major-mode "go"
5 major-mode "groovy" ".gy .gsh .gvy .groovy"
5 major-mode "hask" ".hs"
5 major-mode "html" ".htm .html .htp .hts"
9 major-mode "idl" "OMG IDL"
3 major-mode "imake" "imakefile"
13 major-mode "info" ".info /dir" "GNU Info" info info-on info-goto-link
5 major-mode "ini" ".ini .hpj .reg .rgy .toml"
5 major-mode "ipipe" "*compile* *icommand* *shell*"
9 major-mode "iss" "Inno Setup Script"
5 major-mode "java" ".java .jav"
9 major-mode "js" "JavaScript"
13 major-mode "json" ".json" "JSON" json-parse
1 major-mode "jsp"
1 major-mode "jst"
5 major-mode "julia" ".jl"
5 major-mode "kotlin" ".kt .ktm .kts"
13 major-mode "latex" ".tex .sty" "LaTeX"
13 major-mode "lhtml" ".lhtm .lhtml" "LUA-HTML"
5 major-mode "lisp" ".el"
13 major-mode "lists" "*bindings* *commands* *variables*" "ME List"
1 major-mode "lua"
1 major-mode "m4"
15 major-mode "man" "" "" "UNIX Man page" man aman
7 major-mode "md" "markdown" ".md .tmd .rmd .tqd .qd" md-to-html md-to-htm md-to-latex md-process
15 major-mode "mereg" "" "*registry*" "ME Registry"
13 major-mode "meta" ".mp .mf" "MetaPost/Font"
13 major-mode "mhg" ".mhg .mhg5" "MHEG"
7 major-mode "mod2" "modula-2" ".mod"
1 major-mode "nim"
5 major-mode "nroff" ".n .1 .2 .3 .4 .5 .6 .7 .8 .9 .so .tni .sm"
5 major-mode "octave" ".m"
5 major-mode "pascal" ".p .pas"
5 major-mode "perl" ".pl .pm"
5 major-mode "php" ".php .php3 .php4 .php5 .phtml .inc"
9 major-mode "phps" "PHP Script"
13 major-mode "plsql" ".pls" "PL/SQL"
15 major-mode "ps1" "powershell" ".ps1 .psm1" "PowerShell Script"
1 major-mode "psf"
5 major-mode "python" ".py" py-doc
5 major-mode "rbin" ""
1 major-mode "rd"
5 major-mode "ruby" ".rb"
9 major-mode "rul" "Install Shield Rul"
5 major-mode "rust" ".rs"
5 major-mode "samba" "smb.conf"
5 major-mode "scala" ".sc .scala"
5 major-mode "scheme" ".scm .sch"
5 major-mode "sgml" ".sgm .sgml .dtd"
5 major-mode "slang" ".sl"
1 major-mode "spec"
1 major-mode "sql" sql-format
1 major-mode "swift"
15 major-mode "tcldoc" "tcl-doctools" ".tman" "TCL doctools"
3 major-mode "texi" "texinfo"
9 major-mode "ts" "TypeScript"
3 major-mode "txt" "text"
7 major-mode "typst" "typescript" ".typ .typst .ttyp .ttypst"
1 major-mode "vala"
15 major-mode "vb" "visual-basic" ".bas .cls" "Visual Basic"
; TODO need ftest-verlg & ftest-vlang to determine which a .v file is
13 major-mode "vlang" ".v" "V"
7 major-mode "verilg" "verlog" ".v"
5 major-mode "vhdl" ".vhdl .vhd"
5 major-mode "vrml" ".wrl"
13 major-mode "wiki" "" "GNU Wiki"
5 major-mode "yaml" ".yml .yaml"
1 major-mode "zig"

set-variable %platform &cat $platform &cond &band $system 0x01 "c" ""
set-variable $system &bor &band $system &bnot 0x0030f000 &band 0x0030f000 &reg "/history/system" 0x007000
set-variable $system &bor &band $system &bnot 0x2c8e0414 &band 0x2c8e0414 &reg &spr "/history/%s/system" %platform 0x040010
set-variable $mouse &reg &spr "/history/%s/mouse" %platform $mouse
set-variable $scroll &reg &spr "/history/%s/scroll" %platform $scroll
set-variable $show-modes &reg "/history/show-modes" $show-modes
set-variable $cursor-blink &reg &spr "/history/%s/blink" %platform $cursor-blink
; setup the display scheme, char set, keyboard, language and buffer initialiser
execute-file "scheme"
execute-file "charset"
execute-file "keyboard"
execute-file "language"
execute-file "buffinit"
; setup the displaying of fences
set-variable #l0 &reg &spr "/history/%s/fence" %platform "1"
&cond &equ #l0 1 1 -1 global-mode "fence"
!if &gre #l0 1
  set-variable .display-matching-fence.dmf &lget "|0|37|45|61|" #l0
  0 global-bind-key display-matching-fence "idle-pick"
!endif
; key bindings - all prefixes first in case an emulation is in use
1 global-bind-key prefix               "esc"
2 global-bind-key prefix               "C-x"
3 global-bind-key prefix               "C-h"
4 global-bind-key prefix               "C-c"
0x1 global-bind-key spell-word         "esc $"
global-bind-key frame-handle           "C-x 5"
global-bind-key find-zfile             "C-x 6"
9 global-bind-key find-file            "C-x 7"
global-bind-key find-hfile             "C-x C-h"
global-bind-key grep                   "C-c g"
global-bind-key grep-for-region        "C-c C-g"
global-bind-key describe-word          "C-h w"
global-bind-key open-line              "C-o"
ml-bind-key insert-newline             "C-o"
global-bind-key narrow-search-forward  "C-c s"
global-bind-key narrow-search-backward "C-c r"
global-bind-key replace-string-in-files "A-C-r"
global-bind-key query-replace-string-in-files "esc A-C-r"

global-bind-key restyle-buffer         "esc C-\\"
9 global-bind-key compile              "esc C-c"
global-bind-key compile                "esc c"
global-bind-key major-mode-help        "esc h"
1 global-bind-key ifill-paragraph      "esc q"
global-bind-key delete-indentation     "esc ^"
-1 global-bind-key delete-indentation  "esc %"
0xa0000 global-bind-key osd            "esc ="
global-bind-key context-menu           "esc +"
global-bind-key context-menu           "menu"
global-bind-key context-menu           "S-f1"
0xa0000 global-bind-key osd            "f1"
global-bind-key browse                 "f3"
global-bind-key diff-changes           "f4"
global-bind-key reread-file            "f5"
global-bind-key item-list              "f6"
2 global-bind-key occur                "S-f6"
global-bind-key item-list-close        "esc f6"
global-bind-key spell-buffer           "f7"
0 global-bind-key auto-spell           "S-f7"
global-bind-key auto-spell-menu        "C-f7"
global-bind-key notes                  "f8"
global-bind-key notes-instant          "S-f8"
global-bind-key notes-context          "C-f8"
global-bind-key file-browser           "f10"
global-bind-key ftp                    "S-f10"
3 global-bind-key file-browser         "C-f10"
global-bind-key display-whitespaces    "f11"
global-bind-key toolbar                "S-f11"
global-bind-key expand-abbrev-handle   "esc esc"
ml-bind-key     tab                    "esc esc"
global-bind-key me-find-file           "find-file"
!if &reg "/history/clearcase/rep-rcs" "0"
  0 define-macro-file clearcs cc-coci
  global-bind-key cc-coci              "C-x C-q"
!endif
; if using MS friendly key bindings create them now
!if &reg "/history/mskeys" "0"
  global-bind-key beginning-of-line    "home"
  global-bind-key beginning-of-buffer  "C-home"
  global-bind-key end-of-line          "end"
  global-bind-key end-of-buffer        "C-end"
!endif
; execute msshift if using MS Shift style region definition, otherwise create standard S-* bindings
!if &reg "/history/msshift" "0"
  execute-file "msshift"
!else
  1 global-bind-key transpose-region-line "C-S-down"
  -1 global-bind-key transpose-region-line "C-S-up"
  global-bind-key indent-increase "C-S-right"
  global-bind-key indent-decrease "C-S-left"
!endif
!if &reg "/history/bndfav" "0"
  set-variable #l0 9
  !repeat
    #l0 global-bind-key favorites-add &cat "C-c " #l0
    #l0 global-bind-key favorites-goto &cat "C-" #l0
  !until &not &dec #l0 1
  global-bind-key favorites-add "C-c 0"
  global-bind-key favorites-goto "C-0"
!endif
;
; Setup modes
&cond &reg "/history/backup" "1" 1 -1 global-mode "backup"
&cond &reg "/history/exact" "1" 1 -1 global-mode "exact"
&cond &reg "/history/magic" "1" 1 -1 global-mode "magic"
&cond &reg "/history/tab" "1" 1 -1 global-mode "tab"
&cond &reg "/history/undo" "1" 1 -1 global-mode "undo"
&cond &gre &set #l0 &reg "/history/autosv" "300" "0" 1 -1 global-mode "autosv"
!iif &gre #l0 "0"  set-variable $auto-time #l0
; Setup variables
set-variable $quiet &reg "/history/quiet" "1"
set-variable $kept-versions &reg &spr "/history/%s/kept-vers" $platform "0"
set-variable $file-ignore &reg &spr "/history/%s/file-ignore" $platform ""
set-variable $scroll-bar &reg &spr "/history/%s/scroll-bar" %platform "0"
set-variable %os-chars "/ :;"
set-variable %tag-file "tags"
set-variable %tag-template "%[^\t]\t%[^\t]\t%[^\n]\n"
set-variable .osd.tmp 40
set-variable .osd.next &cond &exi .osd.next .osd.next &add .osd.tmp 1
set-variable .expand-iso-accents.on &reg "/history/expaccent" "1"
set-variable .expand-look-back.on &reg "/history/explookbk" "1"
set-variable .expand-word.on &reg "/history/expword" "0"

; Initialize the top menu line
osd 0 0  "asM" 0 0 -1 0 0 0 osd-main
osd 0 10 "Mdfsh" " \HFile  "     1
osd 0 20 "Mdfsh" " \HEdit  "     2
osd 0 30 "Mdfsh" " \HSearch  "   3
osd 0 40 "Mdfsh" " \HView  "     4
osd 0 50 "Mdfsh" " Fo\Hrmat  "   5
osd 0 60 "Mdfsh" " \HTools  "    6
osd 0 70 "Mdfsh" " \HAdvanced  " 7
osd 0 80 "Mdfsh" " \HWindow  "   8
osd 0 90 "Mdfsr" " \HHelp "      9
!force osd -1 &set .osd.menu &reg "/history/main-menu" "1" "1"
; Load in the mouse handling routines if required
!iif &band $mouse 0x10  execute-file "mouse"
0 add-next-line "*grep*"
add-next-line "*grep*" "%f:%l:"
; load in the platform specific stuff if found
execute-file $platform
; execute any emulation file, e.g. Emacs etc
!if &not &seq &set #l0 &reg "/history/emulate" "" ""
  set-variable %emulate #l0
  !force execute-file &cat "me" #l0
!endif
; load ftp/http registry file
!if &seq &set #l0 &reg "/history/url-file" "" ""
!elif &not &seq &find #l0 ".erf" "ERROR"
  !force read-registry "/url" #l0 "y"
!endif
; execute company setup
!iif &not &sin &set #l0 &lget &spr " %s " &reg "/history/company" "" 1 "-"  !force execute-file #l0
; excute own setup
!if &not &seq &set #l1 &reg "/history/setup-file" "" ""
  !force execute-file #l1
  !if &sin #l0 "-"
    !iif &exi %no-company-url  set-variable %company-url %no-company-url
    !iif &exi %no-company-license  set-variable %company-license %no-company-license
  !endif
!endif
!if &not &exi session
  execute-file session
  set-variable #l1 &bor &mul 16 &ban @# 1 &con &exi %session-name &con &seq %session-name "" 42 34 62
  !force #l1 read-session
  !if &band #l1 12
    !iif &set #l0 &reg &spr "/history/%s/frame-width" %platform "0"  change-frame-width #l0
    !iif &set #l0 &reg &spr "/history/%s/frame-depth" %platform "0"  change-frame-depth #l0
  !endif
!else
  !force 0 change-scheme
!endif
; setup the toolbar if not already done by the session
!if .toolbar.open
!elif &equ $frame-id .notes.frame-id
!elif &reg &spr "/history/%s/toolbar" %platform "0"
  toolbar-open
!endif
;
4 popup-window
; Setup the Client Server
!if &band $system 0x20000
  define-macro-file meserver server-input
  find-buffer "*server*"
  set-variable :last-line 2
  set-variable :client-list ":"
  set-variable $buffer-ipipe server-input
  beginning-of-buffer
  goto-alpha-mark  "I"
  -1 find-buffer "*server*"
!endif
