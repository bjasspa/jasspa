; -!- emf -!-
; This is part of the JASSPA MicroEmacs macro files
; Copyright (C) 1998-2009 JASSPA (www.jasspa.com)
; See the file me.emf for copying and conditions.
;
; Created:     Sat Jul 18 1998
; Synopsis:    Miscellaneous macros
; Authors:     Steven Phillips & Jon Green
;
!if &not &exi %alarm-numb
!endif
; save-all - saves any changes to any buffer, registry or dictionary
define-macro save-all
    @# save-some-buffers @mna
    &bor 2 @# save-registry @mna
    &bor 2 @# save-dictionary @mna
    !if &not @?
        set-variable #l0 @mc5 "Save session (?/y/n) ? " "nNyY" "(Y)es, (N)o, (C-g)Abort ? "
        !if &iseq #l0 "y"
            0 save-session
        !endif
    !endif
    ml-write "[Save all complete]"
!emacro
; Emacs open-line implementation
define-macro open-line
    @# insert-newline
    @# backward-char
!emacro

define-macro encase
    0 show-region
    set-variable #l0 &equ &band $result 5 5
    !if &not &exi .s
        set-variable .s @ml00 "Starting text"
    !endif
    !if &not &exi .e
        set-variable .e @ml00 "Ending text"
        !if &not #l0
            !return
        !endif
    !endif
    ; is a region is hilighted
    !if &not #l0
        set-variable .s @ml10 "Starting text" .s
        set-variable .e @ml10 "Ending text" .e
        !return
    !endif
    !if &not &band $result 2
        3 show-region
    !endif
    ; Find start/end of region.
    -2 show-region
    set-variable #l0 $window-line
    set-variable #l1 $window-col
    set-position "\x82"
    2 show-region
    set-position "\x83"
    set-variable #l2 $window-line
    ; Determine start of region.
    !if &or &gre #l0 #l2 &gre $window-col $window-col
        set-variable #l3 1
    !else
        set-variable #l3 0
    !endif
    ; italic the region.
    goto-position &cond #l3 "\x83" "\x82"
    insert-string .s
    goto-position &cond #l3 "\x82" "\x83"
    insert-string .e
!emacro

define-macro check-line-length
    !repeat
        end-of-line
        !if &gre $window-acol $buffer-fill-col
            ml-write "[line too long]"
            !abort
        !endif
        !force forward-line
    !until &not $status
!emacro

define-macro number-items
    set-variable #l1 @ml "Number offset"
    set-variable #l2 @ml "Chars per Number"
    set-variable #l3 @ml "Number of lines"
    set-variable #l4 &spr "%%%dd" #l2
    !while &gre &pdec #l3 1 0
        set-variable #l5 $window-col
        insert-string &spr #l4 &pinc #l1 1
        set-variable #l5 &sub $window-col #l5
        set-variable #l6 #l5
        !if &pdec #l5 1
            !if &not &seq @wc "\n"
                forward-delete-char
                !jump -3
            !endif
        !endif
        #l6 backward-char
        forward-line
    !done
!emacro

define-macro time
    !force set-variable #l2 @1
    !if &not $status
        set-variable #l2 @ml00 "Time command"
    !endif
    set-variable #l0 $time
    !force execute-line #l2
    set-variable #l1 $time
    set-variable #l2 &add &mid #l0 16 2 &mul 60 &add &mid #l0 14 2 &mul 60 &mid #l0 12 2
    set-variable #l3 &add &mid #l1 16 2 &mul 60 &add &mid #l1 14 2 &mul 60 &mid #l1 12 2
    !if &les &set #l4 &sub &rig #l1 18 &rig #l0 18 0
        set-variable #l2 &add #l2 1
        set-variable #l4 &add 1000 #l4
    !endif
    set-variable .msec &add &mul &sub #l3 #l2 1000 #l4
    ml-write &spr "Command took %d.%03d sec" &sub #l3 #l2 #l4
!emacro

; The default compare-windows ignores white space. The -exact extension checks
; white space as well.
define-macro compare-windows-exact
    0 compare-windows
!emacro

; toggles the displaying of white characters
define-macro display-whitespaces
    set-variable $system &bxor $system 0x80000
    screen-update
!emacro

; Steve Philips - mailed to Jasspa group on 28th Sept 2004.
; Writes a region to a file. If invoked from another macro then a string
; paramter specifies the name of the file to write, if the name is not
; specified then the user is prompted for the name. 
define-macro write-region
    copy-region
    set-variable #l0 $buffer-fname
    set-position "\x88"
    !force delete-buffer "*write-region-tmp*"
    find-buffer "*write-region-tmp*"
    yank
    set-variable $buffer-fname #l0
    !force set-variable #l1 @1
    !if $status
        !force !force @# write-buffer #l1 @mna
    !else
        !force !force @# write-buffer @mna
    !endif
    !if &not $status
        !force delete-buffer $buffer-bname
        goto-position "\x88"
        !abort
    !endif        
    delete-buffer $buffer-bname
    goto-position "\x88"
!emacro
