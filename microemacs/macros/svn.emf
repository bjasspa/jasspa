; -!- emf -!-
; This is part of the JASSPA MicroEmacs macro files
; Copyright (C) 2010 JASSPA (www.jasspa.com)
; See the file me.emf for copying and conditions.
;
; Created:     2010-08-28 13:11:22
; Synopsis:    Subversion support macros for MicroEmacs
; Authors:     Steven Phillips
;
!iif &not &exi dirlst-sort-list  0 execute-file "hkdirlst"
!iif &not &exi %diff-com  0 execute-file "tools"
!if &not &exi .osd.svn-osd
  set-variable .osd.svn-osd &pinc .osd.next 1
  set-variable .osd.svn-hlp &pinc .osd.next 1
  set-variable .osd.svn-tlc &pinc .osd.next 1
  set-variable .osd.svn-tll &pinc .osd.next 1
  set-variable .osd.svn-tlo &pinc .osd.next 1
  set-variable .osd.svn-tl  &pinc .osd.next 1
  set-variable .osd.svn-gen &pinc .osd.next 1
  set-variable .osd.svn-nb  &pinc .osd.next 1
  set-variable .osd.svn-stp &pinc .osd.next 1
  define-macro svn
  !emacro
!endif
!if &not &lfind .fhook-dirlst.sc-name "Subversion"
  set-variable #l0 &lef .fhook-dirlst.col-name 1
  set-variable .fhook-dirlst.col-name &spr "%sFlags  %sWork Rev%sComt Rev%sComt Author%s" .fhook-dirlst.col-name #l0 #l0 #l0 #l0
  set-variable .fhook-dirlst.col-flag &spr "%s1%1%s1%s1%s" .fhook-dirlst.col-flag #l0 #l0 #l0 #l0
  set-variable .fhook-dirlst.sc-name &lins .fhook-dirlst.sc-name -1 "Subversion"
  set-variable .fhook-dirlst.sc-menu &lins .fhook-dirlst.sc-menu -1 "osd-svn-file-menu"
  set-variable .fhook-dirlst.sc-detect &lins .fhook-dirlst.sc-detect -1 "svn-dirlst-detect"
  set-variable .fhook-dirlst.sc-format &lins .fhook-dirlst.sc-format -1 "svn-dirlst-format"
  set-variable .fhook-dirlst.modefunc-lbl &lins .fhook-dirlst.modefunc-lbl -1 "SVN:Quick"
  set-variable .fhook-dirlst.modefunc-cmd &lins .fhook-dirlst.modefunc-cmd -1 "svn-full-reread"
!endif
!if &band .hilight.flags 0x02
  !iif &not &exi .hilight.diff  !force execute-file "hkdiff"
  hilight .hilight.diff 20 "\\[Diff-" "]" "" $mode-line-scheme
  !if &not &exist .hilight.svn-con
    set-variable .hilight.svn-con &pinc .hilight.next 1
    set-variable .hilight.svn-status &pinc .hilight.next 1
    set-variable .hilight.svn-blm &pinc .hilight.next 1
    set-variable .hilight.svn-log &pinc .hilight.next 1
  !endif
  0 hilight .hilight.svn-status 0 $global-scheme
  hilight .hilight.svn-status 0x102 "[ *]A" .scheme.add
  hilight .hilight.svn-status 0x102 "[ *]C" .scheme.hlred
  hilight .hilight.svn-status 0x102 "[ *]D" .scheme.rmv
  hilight .hilight.svn-status 0x102 "[ *]I" .scheme.hide
  hilight .hilight.svn-status 0x102 "[ *]M" .scheme.chg
  hilight .hilight.svn-status 0x102 "[ *]R" .scheme.prepro
  hilight .hilight.svn-status 0x102 "[ *]X" .scheme.hlred
  hilight .hilight.svn-status 0x102 "[ *]!" .scheme.no1
  hilight .hilight.svn-status 0x102 "[ *]~" .scheme.hlred
  hilight .hilight.svn-status 0x102 "[ *] M" .scheme.chg
  hilight .hilight.svn-status 0x102 "[ *] C" .scheme.hlred
  hilight .hilight.svn-status 0x102 " Flags  " .scheme.header
  hilight .hilight.svn-status 0x102 "\\[\\*\\*\\*\\* SVN [^ ]+ completed successfully" .scheme.no1
  hilight .hilight.svn-status 0x102 "\\[\\*\\*\\*\\* SVN" .scheme.error
  hilight .hilight.svn-status 0x102 "[ *]D.......\\*" .scheme.hlred
  hilight .hilight.svn-status 0x102 "[ *]M.......\\*" .scheme.hlred
  hilight .hilight.svn-status 0x102 "[ *]R.......\\*" .scheme.hlred
  hilight .hilight.svn-status 0x102 "[ *] M......\\*" .scheme.hlred
  hilight .hilight.svn-status 0x102 "[ *]  ......\\*" .scheme.prepro

  0 hilight .hilight.svn-con 0 $global-scheme
  hilight .hilight.svn-con 0x102 "\\[\\*\\*\\*\\* SVN [^ ]+ completed successfully" .scheme.no1
  hilight .hilight.svn-con 0x102 "\\[\\*\\*\\*\\* SVN Server [^ ]+ completed successfully" .scheme.no1
  hilight .hilight.svn-con 0x102 "\\[\\*\\*\\*\\* SVN" .scheme.error

  0 hilight .hilight.svn-log 0 $global-scheme
  hilight .hilight.svn-log 0x100 "\\[Hide]" $mode-line-scheme
  hilight .hilight.svn-log 0x100 "\\[Show]" $mode-line-scheme
  hilight .hilight.svn-log 0x102 "   A" .scheme.add
  hilight .hilight.svn-log 0x102 "   D" .scheme.rmv
  hilight .hilight.svn-log 0x102 "   M" .scheme.chg

  ; fix the *files* buffer hilighting for svn additions
  hilight .hilight.dirlst 0x112 " dA"       .scheme.add
  hilight .hilight.dirlst 0x112 " -A"       .scheme.add
  hilight .hilight.dirlst 0x112 " dC"       .scheme.hlred
  hilight .hilight.dirlst 0x112 " -C"       .scheme.hlred
  hilight .hilight.dirlst 0x112 " dD"       .scheme.rmv
  hilight .hilight.dirlst 0x112 " -D"       .scheme.rmv
  hilight .hilight.dirlst 0x112 " dI"       .scheme.hide
  hilight .hilight.dirlst 0x112 " -I"       .scheme.hide
  hilight .hilight.dirlst 0x112 " dM"       .scheme.chg
  hilight .hilight.dirlst 0x112 " -M"       .scheme.chg
  hilight .hilight.dirlst 0x112 " dR"       .scheme.prepro
  hilight .hilight.dirlst 0x112 " -R"       .scheme.prepro
  hilight .hilight.dirlst 0x112 " dX"       .scheme.hlred
  hilight .hilight.dirlst 0x112 " d\\?"     .scheme.type
  hilight .hilight.dirlst 0x112 " -\\?"     .scheme.type
  hilight .hilight.dirlst 0x112 " d!"       .scheme.no1
  hilight .hilight.dirlst 0x112 " -!"       .scheme.no1
  hilight .hilight.dirlst 0x112 " d~"       .scheme.hlred
  hilight .hilight.dirlst 0x112 " -~"       .scheme.hlred
!endif
0 define-macro fhook-svn-status
!emacro
set-variable .fhook-svn-status.name "Subversion Status"
set-variable .fhook-svn-status.setup &reg "/history/fhook/svn-status" "ghmop"
set-variable .fhook-svn-status.setup-mask    "aceghlopq"
set-variable .fhook-svn-status.command-flag  &rig &lset &lset &lset &lset &lset &lset .fhook-dirlst.command-flag  1 "" 2 "" 3 "t" 4 "thbio" 5 "thbio" 6 "bio" 2
set-variable .fhook-svn-status.command-name  &rig &lset &lset &lset &lset &lset &lset .fhook-dirlst.command-name  1 "" 2 "" 3 "osd" 4 "svn-context-menu" 5 "svn-status" 6 "svn-status" 2
set-variable .fhook-svn-status.command-nbind &rig &lset &lset &lset &lset &lset &lset .fhook-dirlst.command-nbind 1 "" 2 "" 3 .osd.svn-hlp 4 "" 5 "" 6 "" 2
set-variable .fhook-svn-status.command-kbind &rig &lset &lset &lset &lset &lset &lset .fhook-dirlst.command-kbind 1 "" 2 "" 3 "" 4 "k" 5 "C-return" 6 "f5" 2
set-variable .fhook-svn-status.command-desc  &rig &lset &lset &lset &lset &lset &lset .fhook-dirlst.command-desc  1 "" 2 "" 3 "Help on Subversion Status" 4 "Open Subversion menu" 5 "Rerun svn-status" 6 "" 2
set-variable .fhook-svn-status.refresh "svn-status-refresh"

; * = Console, ! = wait, $ = no file select
set-variable .svn.cnames  "|Add|Blame|Checkout|Commit|Copy|Delete|Diff|Log|Move|Status|Update|Server Copy|Server Move|Resolve|Ignore|Info|XDiff|Commit Log|"
set-variable .svn.cflags  "|RIS*!|Vhpvs|UR$|CRS*!|NPrS*!|KrS*!|ZRxWwpvS|ZVpf|NPrS*!|ZRAIuwS|ZRMS*|nCPs*!|nCPs*!|ZRaS*|iD!|sD^!|ZtS|pv$|"
; 1 Add - flags: RIS*!
set-variable .svn.cdefs1  "|1|0|"
; 2 Blame - flags: Vhpvs
set-variable .svn.cdefs2  "|0|0|1|3|"
; 3 Checkout - flags: UR$
set-variable .svn.cdefs3  "||1|0|"
; 4 Commit - flags: CRS*!
set-variable .svn.cdefs4  "||1|0|"
; 5 Copy - flags: NPrS*!
set-variable .svn.cdefs5  "||1|0|"
; 6 Delete - flags: KrS*!
set-variable .svn.cdefs6  "|0|0|"
; 7 Diff - flags: ZRxWwpvS
set-variable .svn.cdefs7  "|0|1|0|1|0|1|3|"
; 8 Log - flags: ZVpf
set-variable .svn.cdefs8  "|0|0|1|"
; 9 Move - flags: NPrS*!
set-variable .svn.cdefs9  "||1|0|"
; 10 Status - flags: ZRAIuwS
set-variable .svn.cdefs10 "|1|1|0|0|1|0|"
; 11 Update - flags: ZRMS*
set-variable .svn.cdefs11 "|1|1|0|"
; 12 Server Copy - flags: nCPs*!
set-variable .svn.cdefs12 "|||1|"
; 13 Server Move - flags: nCPs*!
set-variable .svn.cdefs13 "|||1|"
; 14 Resolve - flags: ZRaS*
set-variable .svn.cdefs14 "|0|0|2|"
; 15 Ignore - flags: iD!
set-variable .svn.cdefs15 "||"
; 16 Info - flags: sD^!
set-variable .svn.cdefs16 "|"
; 17 XDiff - flags: ZtS
set-variable .svn.cdefs17 "|0|1|"
; 18 Commit Log - flags: pv$
set-variable .svn.cdefs18 "|1|6|"
set-variable .svn.ccount  18

set-variable .svn.com &reg &spr "/history/%s/svn/com" $platform "svn"
!iif &sin " " .svn.com  set-variable .svn.com &spr "\"%s\"" .svn.com
!iif &not &seq "" &set #l9 &trb &reg &spr "/history/%s/svn/garg" $platform "--non-interactive"  set-variable .svn.com &cat &cat .svn.com " " #l9
set-variable .svn.def-rep-url &reg &spr "/history/%s/svn/def-rep-url" $platform ""
set-variable .svn.browse-mode &reg "/history/svn/browse-mode" "0"
set-variable #l9 .svn.ccount
!repeat
  set-variable #l8 &lget .svn.cflags #l9
  set-variable #l7 &ind &cat ".svn.cdefs" #l9
  set-variable #l0 0
  !while &not &seq "" &set #l1 &mid #l8 &pinc #l0 1 1
    !if &seq #l1 "C"
    !elif &not &seq "" &set #l2 &reg &spr "/history/svn/%d_%s" #l9 #l1 ""
      set-variable #l7 &lset #l7 #l0 #l2
    !endif
  !done
  set-variable &ind &cat ".svn.cdefs" #l9 #l7
  set-variable &ind &cat ".svn.cvalues" #l9 #l7
!until &not &dec #l9 1

0 define-macro svn-dirlst-detect
  !iif &seq .ld &lef &set #l0 $buffer-fname &len .ld  !return
  !repeat
    !iif &seq &stat "t" &cat #l0 ".svn/." "D"  !break
    !iif &gre 5 &set #l1 &rsin "/" &lef #l0 -1  !abort
    set-variable #l0 &lef #l0 #l1
  !done
  set-variable .ld #l0
!emacro
0 define-macro svn-full-reread
  set-variable #l0 .svn.browse-mode
  set-variable .svn.browse-mode 0
  !force !force dirlst-refresh
  set-variable .svn.browse-mode #l0
!emacro
0 define-macro svn-dirlst-format
  !if @#
    ml-write "[SVN: processing directory...]"
    set-position "\x83"
    set-variable #l1 $buffer-bname
    0x46 pipe-shell-command &spr "%s status -v %s%s--depth=immediates ." .svn.com &con .svn.browse-mode "" "-u " &con &lget .fhook-dirlst.mode 2 "" "--no-ignore " "*svn-temp*"
    find-buffer "*svn-temp*"
    beginning-of-buffer
    !if &seq &lef @wl 4 "svn:"
      set-variable #l2 @wl
      ; not a CC directory
      goto-position "\x83"
      !force 0 delete-buffer "*svn-temp*"
      !force !force file-browser-filter
      ml-write &spr "[SVN Error: %s]" #l2
      !return
    !endif
    find-buffer #l1
    2 goto-line
    end-of-line
    -1 buffer-mode "view"
    1 buffer-mode "edit" @mna
    insert-string &spr " (SVN:%s - %sFiltered)" &con .svn.browse-mode "Quick" "Full" &con &lget .fhook-dirlst.mode 2 "" "Not-"
    4 goto-line
    6 forward-char
    insert-string " Work Rev Comt Rev Comt Author "
    beginning-of-line
    forward-line
    !while &not &seq @wl ""
      set-variable #l7 "-"
      set-variable #l3 &rig @wl :fmpf
      !iif &set #l4 &sin " -> " #l3  set-variable #l3 &lef #l3 &sub #l4 1
      !if &seq &rig #l3 &set #l4 &sub &len #l3 1 "/"
        !if &seq #l3 "./"
          !goto rmv-line
        !elif &seq #l3 "../"
          set-variable #l7 "-"
          !goto pad-line
        !elif &seq #l3 ".svn/"
          !tgoto &lget .fhook-dirlst.mode 2 rmv-line
          set-variable #l7 "I"
          !goto pad-line
        !endif
        set-variable #l3 &lef #l3 #l4
      !endif
      str-to-regex #l3
      find-buffer "*svn-temp*"
      beginning-of-buffer
      !force search-buffer "me" &spr "^.\\{41\\}%s$" #l3
      !if $status
        set-variable #l7 &lef @wl 1
        set-variable #l7 &con &seq #l7 " " &con .svn.browse-mode "Q" "-" #l7
        set-variable #l8 &mid @wl 9 30
        find-buffer #l1
        2 forward-char
        insert-string #l7
        3 forward-char
        insert-string #l8
        beginning-of-line
        forward-line
      !elif &lget .fhook-dirlst.mode 2
        find-buffer #l1
*rmv-line
        beginning-of-line
        set-mark
        forward-line
        -1 kill-region
      !else
        set-variable #l7 "E"
        find-buffer #l1
*pad-line
        2 forward-char
        insert-string #l7
        4 forward-char
        insert-string "                              "
        beginning-of-line
        forward-line
      !endif
    !done
    goto-position "\x83"
    -1 buffer-mode "edit"
    set-variable :fmpf &add :fmpf 31
    !force 0 delete-buffer "*svn-temp*"
    0 ml-write
  !else
    0 file-browser-filter
  !endif
  buffer-bind-create "bio" "C-delete" "" svn-delete
  buffer-bind-create "bio" "C-f5"   "" svn-full-reread
  buffer-bind-create "bio" "S-f5" "" svn-status
  buffer-bind-create "b"   "esc f5" "" svn-diff
  buffer-bind-create "bio" "k" "" svn-context-menu
!emacro

;; Some useful macros ;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;

0 define-macro svn-get-base-url
  !if &seq .ld &lef &set #l1 $buffer-fname &len .ld
  !elif &and &seq #l1 &lef .ld &len #l1 &not &sin "/" &mid .ld &len #l1 -1
  !else
    !force svn-dirlst-detect
    !if $status
      set-variable #l3 &set .ld .svn-dirlst-detect.ld
    !else
      ; see if given file is already in an SVN checkout area, if so get the URL
      set-variable #l1 $buffer-fname
      set-variable $file-names &cat &lef #l1 &rsi "/" #l1 ".*/"
      set-variable #l2 $result
*gbu-loop
      !if &seq &set #l3 $file-names ""
        !abort
      !elif &seq #l3 "../"
        !goto gbu-loop
      !elif &not &seq &stat "t" &cat &cat #l2 #l3 ".svn" "D"
        !goto gbu-loop
      !endif
      set-variable .ld &cat #l2 #l3
      set-variable #l3 &lef #l3 -1
    !endif
    set-variable .svn.crep-url ""
    set-variable .svn.crep-rot ""
    !force 0x46 pipe-shell-command &spr "%s info %s" .svn.com #l3 "*svn-gburl*"
    !iif &not $status  !abort
    find-buffer "*svn-gburl*"
    beginning-of-buffer
    !force search-buffer "me" "^Repository Root: +\\(.*[^/\n]\\)/?"
    !iif $status  set-variable .svn.crep-rot @s1
    beginning-of-buffer
    !force search-buffer "me" &con &band @# 1 "^URL: +\\(.*\\)" "^Repository Root: +\\(.*\\)"
    !if $status
      set-variable #l4 @s1
      set-variable .svn.crep-url &con &seq &cat "/" #l3 &rig #l4 &sub -1 &len #l3  &lef #l4 &neg &len #l3  &cat #l4 "/"
    !endif
    delete-buffer "*svn-gburl*"
    goto-position "\x82"
  !endif
!emacro

;; SVN main GUI macros ;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;

0 define-macro svn-osd-init-comment
  set-variable .svn.ccomment ""
  set-variable .svn.ccominit ""
  !iif &exi &cat ".svn.ccomment" @#  set-variable .svn.ccomment &ind &cat ".svn.ccomment" @#
  !if &seq "" &set #l0 &reg &spr "/history/svn/%d_C" @# ""
  !elif &not &seq "ERROR" &set #l0 &find #l0 ""
    !force 0 find-file #l0
    !if $status
      !iif &not &seq .svn.ccomment ""  set-variable @h0 .svn.ccomment
      beginning-of-buffer
      set-mark
      end-of-buffer
      backward-char
      copy-region
      set-variable .svn.ccomment @y
      -1 yank
      0 delete-buffer $buffer-bname
      set-variable .svn.ccominit .svn.ccomment
      goto-position "\x82"
    !endif
  !endif
!emacro
0 define-macro svn-osd-mec
  !if &les @# 0
    set-variable .svn.ccomment @ml2 "" .svn.ccomment
  !else
    set-variable $result .svn.ccomment
  !endif
!emacro
0 define-macro svn-osd-init-ignore-mask
  !if &sub &set #l1 .find-file.file-count 1
  !elif &not &seq &rig $buffer-fname -1 "/"
  !elif &seq &cat .find-file.file-dir .find-file.file1 $buffer-fname
    set-variable .find-file.file-dir $buffer-fname
    set-variable .find-file.file-count &set #l1 0
  !endif
  0x404 pipe-shell-command &spr "%s propget svn:ignore \"%s\"" .svn.com .find-file.file-dir #l2
  !if &not $status
    set-variable .svn.cignrmsk ""
  !else
    set-variable #g0 #l2
    set-variable #l2 &spr "\n%s\n\n" &rep &trb #l2 "\r\n" "\n"
    set-variable #g1 #l2
    !if #l1
      set-variable #l0 0
      !while &les &pinc #l0 1 #l1
        !iif &not &lfin #l2 &set #l3 &ind &cat ".find-file.file" #l0  set-variable #l2 &spr "%s%s\n" #l2 #l3
      !done
    !endif
    set-variable #g2 #l2
    set-variable .svn.cignrmsk &cat &trb #l2 "\n"
  !endif
!emacro
0 define-macro svn-osd-meim
  !if &les @# 0
    set-variable .svn.cignrmsk @ml2 "" .svn.cignrmsk
  !else
    set-variable $result .svn.cignrmsk
  !endif
!emacro
0 define-macro svn-osd-rep-url
  !if &not &len &set #l1 .svn.crep-url
    set-variable #l1 .svn.def-rep-url
  !elif &gre @# 0
  !elif &len .svn.def-rep-url
    set-variable @h0 .svn.def-rep-url
  !endif
  !if &les @# 0
    set-variable .svn.crep-url @ml2 "" #l1
  !else
    set-variable $result #l1
  !endif
!emacro
0 define-macro svn-osd-name
  !if &les @# 0
    set-variable .svn.cname @ml24 "" .svn.cname
  !else
    set-variable $result .svn.cname
  !endif
!emacro
0 define-macro svn-osd-logfs
  !if &les @# 0
    set-variable .svn.logfs @ml2 "" .svn.logfs
  !else
    set-variable $result .svn.logfs
  !endif
!emacro
set-variable .svn.logfs ""
0 define-macro svn-osd-fver
  !if &les @# 0
    set-variable .svn.cfver @ml2 "" .svn.cfver
  !else
    set-variable $result .svn.cfver
  !endif
!emacro
set-variable .svn.cfver ""
0 define-macro svn-osd-cbc
  !if &les @# 0
    set-variable #l0 &abs @#
    set-variable .svn-osd.values &lset .svn-osd.values #l0 &bxor 1 &lget .svn-osd.values #l0
  !elif &not &lget .svn-osd.values @#
    !abort
  !endif
!emacro
0 define-macro svn-revision-op
  !if &les @# 0
    -1 osd .osd.tmp
    osd .osd.tmp 0 "bs" 15 0 0 0
    set-variable #l1 .si
    !while &not &seq "" &set #l2 &lget .lbl &inc #l1 1
      osd .osd.tmp #l1 "" #l2 #l1 svn-revision-ops
    !done
  !else
    set-variable $result &rep &lget .lbl &lget .svn-osd.values .svn-osd.o-v "\H" ""
  !endif
!emacro
set-variable .svn-revision-op.lbl "|Base \Hto Head|\HHead|\HBase|\HCommitted|\HPrevious|\HSpecific|"
0 define-macro svn-revision-ops
  set-variable .svn-osd.values &lset .svn-osd.values .svn-osd.o-v @#
!emacro
0 define-macro svn-resolve-a
  !if &les @# 0
    set-variable #l0 &abs @#
    -1 osd #l0
    osd #l0 0 "bs" 20 0 0 0
    set-variable #l1 0
    !while &not &seq "" &set #l2 &lget .svn-resolve.al &inc #l1 1
      osd #l0 #l1 "" #l2 #l1 svn-resolve-as
    !done
  !else
    set-variable $result &rep &lget .svn-resolve.al &lget .svn-osd.values .svn-osd.o-a "\H" ""
  !endif
!emacro
0 define-macro svn-resolve-as
  set-variable .svn-osd.values &lset .svn-osd.values .svn-osd.o-a @#
!emacro
0 define-macro svn-osd
  set-position "\x82"
  !if &exi .svn.lock
    osd-xdialog &cat "Subversion - " &lget .svn.cnames @# &spr "    SVN %s command currently in progress!    " &lget .svn.cnames .svn.lock 2 "  \HIgnore  " "  \HAbort  "
    !iif &not &equ $result 1  !abort
  !endif
  set-variable #l9 &lget .svn.cflags @#
  set-variable .values &ind &cat ".svn.cvalues" @#
  !if &seq $buffer-fname ""
    osd-dialog &cat "Subversion - " &lget .svn.cnames @# "    Current buffer has no file name!    " "  \HAbort  "
    !abort
  !endif
  set-variable .svn.cwd &lef $buffer-fname &rsin "/" $buffer-fname
  !if &not &seq &stat "t" .svn.cwd "D"
    osd-dialog &cat "Subversion - " &lget .svn.cnames @# "    Current buffer is not located in a directory!    " "  \HAbort  "
    !abort
  !endif
  !if &sin "$" #l9
  !elif &exi :fmpf
    10 dirlst-tag-list
    goto-position "\x82"
  !elif &seq &lef $buffer-bname 1 "*"
    set-variable .find-file.file-count 0
    set-variable .find-file.file-dcount 0
  !else
    !if &sin "S" #l9
      !if &bmod "edit"
        set-variable #l0 @mc5 "Save buffer first (?/y/n) ? " "nNyY" "(Y)es, (N)o, (C-g)Abort ? "
        !iif &iseq #l0 "y"  save-buffer @mna
      !endif
    !endif
    set-variable .find-file.file-count 1
    set-variable .find-file.file-dcount &con &seq &rig $buffer-fname -1 "/" 1 0
    set-variable .find-file.file-dir &lef $buffer-fname &set #l0 &rsin "/" &lef $buffer-fname -1
    set-variable .find-file.file1 &rig $buffer-fname #l0
  !endif
  !if &sin "$" #l9
  !elif &sin "s" #l9
    !if &not &set #l1 &equ .find-file.file-count 1
      !if &not &sin "D" #l9
      !elif .find-file.file-count
      !elif &set #l1 &exi :fmpf
        set-variable .find-file.file-count 1
        set-variable .find-file.file-dcount 1
        set-variable .find-file.file-dir &lef $buffer-fname &set #l0 &rsin "/" &lef $buffer-fname -1
        set-variable .find-file.file1 &rig $buffer-fname #l0
      !endif
      !if &not #l1
        osd-dialog &cat "Subversion - " &lget .svn.cnames @# "    This tool can only opperate on one specific file at a time!    " "  \HAbort  "
        !abort
      !endif
    !endif
  !elif &not &or &sin "Z" #l9 &sin "D" #l9
    !if &not .find-file.file-count
      osd-dialog &cat "Subversion - " &lget .svn.cnames @# "    This tool can only opperate on selected files!    " "  \HAbort  "
      !abort
    !endif
  !endif
  !iif &sin "^" #l9  !return
  -1 osd .osd.svn-osd
  osd .osd.svn-osd 0 ""
  set-variable #l0 0
  set-variable #l3 0
  !while &not &seq "" &set #l1 &mid #l9 &pinc #l0 1 1
    !if &not &sin #l1 "DSs*!$"
      set-variable #l2 &mul #l0 100
      osd .osd.svn-osd &inc #l2 1 ""
      !if &seq #l1 "A"
        osd .osd.svn-osd &inc #l2 1 "fh" "    "
        osd .osd.svn-osd &inc #l2 1 "Cptfx" &cat .osd.checkbox-chars "\} List \Hall items with revision info" #l0 svn-osd-cbc
        set-variable #l3 &con #l3 #l3 #l2
      !elif &seq #l1 "a"
        set-variable .X 0
        osd .osd.svn-osd &inc #l2 1 "Sfh" "        \HAccept:    " &add #l2 2
        osd .osd.svn-osd &inc #l2 1 "OtxmsfhHR" .scheme.osd-entry "############" .osd.tmp svn-resolve-a
        osd .osd.svn-osd &inc #l2 1 "BdxfHR" .scheme.osd-ebtt &mid $window-chars 10 1 &sub #l2 1
        set-variable .o-a #l0
      !elif &seq #l1 "C"
        osd .osd.svn-osd &inc #l2 1 "Sfh" "    Co\Hmment:    " &add #l2 2
        @# svn-osd-init-comment
        osd .osd.svn-osd &inc #l2 1 "fh" "      "
        osd .osd.svn-osd &inc #l2 1 "EtNxHfhz" .scheme.osd-entry 60 10 "" f svn-osd-mec
        set-variable #l3 #l2
        osd .osd.svn-osd &inc #l2 1 "f"  "    "
      !elif &seq #l1 "f"
        osd .osd.svn-osd &inc #l2 1 "Sfh" "    \HFilter:  " &add #l2 1
        osd .osd.svn-osd &inc #l2 1 "EtxHfz" .scheme.osd-entry 20 1 "" #l0 svn-osd-logfs
        set-variable #l3 &con #l3 #l3 #l2
      !elif &seq #l1 "h"
        osd .osd.svn-osd &inc #l2 1 "fh" "    "
        osd .osd.svn-osd &inc #l2 1 "Cptfx" &cat .osd.checkbox-chars "\} R\Haw out, remove header etc" #l0 svn-osd-cbc
        set-variable #l3 &con #l3 #l3 #l2
      !elif &seq #l1 "I"
        osd .osd.svn-osd &inc #l2 1 "fh" "    "
        osd .osd.svn-osd &inc #l2 1 "Cptfx" &cat .osd.checkbox-chars "\} \HInclude ignored files" #l0 svn-osd-cbc
        set-variable #l3 &con #l3 #l3 #l2
      !elif &seq #l1 "i"
        svn-osd-init-ignore-mask
        osd .osd.svn-osd &inc #l2 1 "Sf" "    \HIgnore file masks (one per line):" &add #l2 2
        osd .osd.svn-osd &inc #l2 1 "fh" "      "
        osd .osd.svn-osd &inc #l2 1 "EtNxHfhz" .scheme.osd-entry 60 10 "" f svn-osd-meim
        set-variable #l3 &con #l3 #l3 #l2
        osd .osd.svn-osd &inc #l2 1 "f"  "    "
      !elif &seq #l1 "K"
        osd .osd.svn-osd &inc #l2 1 "fh" "    "
        osd .osd.svn-osd &inc #l2 1 "Cptfx" &cat .osd.checkbox-chars "\} \HKeep local file(s)" #l0 svn-osd-cbc
        set-variable #l3 &con #l3 #l3 #l2
      !elif &seq #l1 "M"
        osd .osd.svn-osd &inc #l2 1 "fh" "    "
        osd .osd.svn-osd &inc #l2 1 "Cptfx" &cat .osd.checkbox-chars "\} Checkout \Hmissing items" #l0 svn-osd-cbc
        set-variable #l3 &con #l3 #l3 #l2
      !elif &seq #l1 "N"
        !if &equ .find-file.file-count 1
          osd .osd.svn-osd &inc #l2 1 "Sf" "    \HDestination Name or Directory:" &add #l2 2
          set-variable .svn.cname .find-file.file1
        !else
          osd .osd.svn-osd &inc #l2 1 "Sf" "    \HDestination Directory:" &add #l2 2
          set-variable .svn.cname ""
        !endif
        osd .osd.svn-osd &inc #l2 1 "fh" "      "
        osd .osd.svn-osd &inc #l2 1 "EtxHfhz" .scheme.osd-entry 60 1 "" #l0 svn-osd-name
        set-variable #l3 &con #l3 #l3 #l2
        osd .osd.svn-osd &inc #l2 1 "f"  "    "
      !elif &seq #l1 "n"
        !force svn-get-base-url
        !if &equ .find-file.file-count 1
          osd .osd.svn-osd &inc #l2 1 "Sf" "    Source item repository URL (please check):"
          osd .osd.svn-osd &inc #l2 1 "Sf" &cat "      " &set .svn.crep-url &cat .svn.crep-url .find-file.file1
          osd .osd.svn-osd &inc #l2 1 "f"  ""
          osd .osd.svn-osd &inc #l2 1 "Sf" "    \HDestination item repository URL:" &add #l2 2
        !else
          osd .osd.svn-osd &inc #l2 1 "Sf" "    Source base repository URL (please check):"
          osd .osd.svn-osd &inc #l2 1 "Sf" &cat "      " .svn.crep-url
          osd .osd.svn-osd &inc #l2 1 "f"  ""
          osd .osd.svn-osd &inc #l2 1 "Sf" "    \HDestination base repository URL:" &add #l2 2
        !endif
        set-variable .svn.crep-surl .svn.crep-url
        osd .osd.svn-osd &inc #l2 1 "fh" "      "
        osd .osd.svn-osd &inc #l2 1 "EtxHfhz" .scheme.osd-entry 60 1 "" #l0 svn-osd-rep-url
        set-variable #l3 &con #l3 #l3 #l2
        osd .osd.svn-osd &inc #l2 1 "f"  "    "
      !elif &seq #l1 "P"
        osd .osd.svn-osd &inc #l2 1 "fh" "    "
        osd .osd.svn-osd &inc #l2 1 "Cptfx" &cat .osd.checkbox-chars "\} Create \Hintermediate directories" #l0 svn-osd-cbc
        set-variable #l3 &con #l3 #l3 #l2
      !elif &seq #l1 "p"
        osd .osd.svn-osd &inc #l2 1 "fh" "    "
        osd .osd.svn-osd &inc #l2 1 "Cptfx" &cat .osd.checkbox-chars "\} \HProcess output - adds buttons etc" #l0 svn-osd-cbc
        set-variable #l3 &con #l3 #l3 #l2
      !elif &seq #l1 "R"
        osd .osd.svn-osd &inc #l2 1 "fh" "    "
        osd .osd.svn-osd &inc #l2 1 "Cptfx" &cat .osd.checkbox-chars "\} \HRecurse into sub-directories" #l0 svn-osd-cbc
        set-variable #l3 &con #l3 #l3 #l2
      !elif &seq #l1 "r"
        osd .osd.svn-osd &inc #l2 1 "fh" "    "
        osd .osd.svn-osd &inc #l2 1 "Cptfx" &cat .osd.checkbox-chars "\} Refresh directory \Hlisting" #l0 svn-osd-cbc
        set-variable #l3 &con #l3 #l3 #l2
      !elif &seq #l1 "t"
        osd .osd.svn-osd &inc #l2 1 "fh" "    "
        osd .osd.svn-osd &inc #l2 1 "Cptfx" &cat .osd.checkbox-chars "\} Use new \Htab option for multiple files    " #l0 svn-osd-cbc
        set-variable #l3 &con #l3 #l3 #l2
      !elif &seq #l1 "U"
        !force svn-get-base-url
        osd .osd.svn-osd &inc #l2 1 "Sf" "    Repository \HURL:" &add #l2 2
        osd .osd.svn-osd &inc #l2 1 "fh" "      "
        osd .osd.svn-osd &inc #l2 1 "EtxHfhz" .scheme.osd-entry 60 1 "" #l0 svn-osd-rep-url
        set-variable #l3 &con #l3 #l3 #l2
        osd .osd.svn-osd &inc #l2 1 "f"  "    "
      !elif &seq #l1 "u"
        osd .osd.svn-osd &inc #l2 1 "fh" "    "
        osd .osd.svn-osd &inc #l2 1 "Cptfx" &cat .osd.checkbox-chars "\} Get o\Hut-of-date info from server" #l0 svn-osd-cbc
        set-variable #l3 &con #l3 #l3 #l2
      !elif &seq #l1 "V"
        osd .osd.svn-osd &inc #l2 1 "fh" "    "
        osd .osd.svn-osd &inc #l2 1 "Cptfx" &cat .osd.checkbox-chars "\} \HVerbose output" #l0 svn-osd-cbc
        set-variable #l3 &con #l3 #l3 #l2
      !elif &seq #l1 "W"
        osd .osd.svn-osd &inc #l2 1 "fh" "    "
        osd .osd.svn-osd &inc #l2 1 "Cptfx" &cat .osd.checkbox-chars "\} Ignore \Hwhite spaces" #l0 svn-osd-cbc
        set-variable #l3 &con #l3 #l3 #l2
      !elif &seq #l1 "w"
        osd .osd.svn-osd &inc #l2 1 "fh" "    "
        osd .osd.svn-osd &inc #l2 1 "Cptfx" &cat .osd.checkbox-chars "\} Use \Hsingle window" #l0 svn-osd-cbc
        set-variable #l3 &con #l3 #l3 #l2
      !elif &seq #l1 "v"
        set-variable .svn-revision-op.si &con &seq &set #l4 &lget .svn.cnames @# "Diff" 0 1
        osd .osd.svn-osd &inc #l2 1 "Sfh" &spr "    %s re\Hvision:  " #l4 &add #l2 1
        osd .osd.svn-osd &inc #l2 1 "OtxmsfhHR" .scheme.osd-entry "#############" .osd.tmp svn-revision-op
        osd .osd.svn-osd &inc #l2 1 "BdxfHR" .scheme.osd-ebtt &mid $window-chars 10 1 &sub #l2 1
        osd .osd.svn-osd &inc #l2 1 ""
        osd .osd.svn-osd &inc #l2 1 "Sfh" &spr "      %n Number:  " &len #l4 " " &add #l2 1
        osd .osd.svn-osd &inc #l2 1 "EtxHfz" .scheme.osd-entry 14 1 "" #l0 svn-osd-fver
        set-variable #l3 &con #l3 #l3 #l2
        set-variable .o-v #l0
      !elif &seq #l1 "x"
        osd .osd.svn-osd &inc #l2 1 "fh" "    "
        osd .osd.svn-osd &inc #l2 1 "Cptfx" &cat .osd.checkbox-chars "\} Use \HME diff tool" #l0 svn-osd-cbc
        set-variable #l3 &con #l3 #l3 #l2
      !elif &seq #l1 "Z"
        !if .find-file.file-count
          osd .osd.svn-osd &inc #l2 1 "fh" "    "
          osd .osd.svn-osd &inc #l2 1 "Cptfx" &cat .osd.checkbox-chars "\} Operate on \Hdirectory (ignore selection)    " #l0 svn-osd-cbc
          set-variable #l3 &con #l3 #l3 #l2
        !else
          osd .osd.svn-osd #l2 "D"
        !endif
      !else
        osd .osd.svn-osd &inc #l2 1 "" &cat "flag " #l1
      !endif
    !endif
  !done
  osd .osd.svn-osd 2000 ""
  osd .osd.svn-osd 2010 ""
  osd .osd.svn-osd 2020 "BtcfHh" .scheme.osd-ebtt " \HOkay " f void
  set-variable #l3 &con #l3 #l3 #l2
  osd .osd.svn-osd 2030 "BtcfH"  .scheme.osd-ebtt " \HCancel " 0 void
  osd .osd.svn-osd 0 "batcDIHs" 9 3 47 4 -1 -1 2020 #l3 .scheme.osd-title &cat "Subversion " &lget .svn.cnames @#
  !force !force .osd.svn-osd osd
  !iif &not $status  !abort
  !if &sin "C" #l9
    !iif &seq .svn.ccominit .svn.ccomment  set-variable .svn.ccomment ""
    set-variable .svn.ccomquot &rep &rep &trb .svn.ccomment "\\" "\\\\" "\"" "\\\""
    !iif &seq &rig $SHELL -3 "csh"  set-variable .svn.ccomquot &rep .svn.ccomquot "\n" "\\\n"
  !endif
  set-variable &ind &cat ".svn.cvalues" @# .values
!emacro

; @# svn-get-file-list 0/1 0/1 [<string>]
; If @1 = 1 return an empty buffer with no files
; If @2 = 1 return 1 file '.' (and 1 dir if using 0x08)
; @# bits are as follows:
; 0x01 Quote names with spaces
; 0x02 Generate space separated lists (default is \b separated me lists)
; 0x04 Insert given string (@3) at start of the buffer
; 0x08 List directories again on a second line
; 0x10 Don't add directories to the file list (first line)
0 define-macro svn-get-file-list
  !force 0 delete-buffer "*svn-cmdline*"
  find-buffer "*svn-cmdline*"
  1 buffer-mode "magic"
  !if @1
    set-variable .fcount 0
  !elif &or &not .find-file.file-count @2
    insert-string "\b."
    set-variable .fcount 1
    !if &band @# 8
      insert-string "\n\b."
      set-variable .dcount 1
    !endif
  !else
    2 insert-newline
    beginning-of-buffer
    set-variable #l0 0
    set-variable .dcount 0
    !while &les &pinc #l0 1 .find-file.file-count
      set-variable #l4 &ind &cat ".find-file.file" #l0
      !if &seq "/" &rig #l4 -1
        set-variable #l4 &lef #l4 -1
        !if &and &band @# 1 &sin " " #l4
          set-variable #l4 &rep &rep #l4 "\\" "\\\\" "\"" "\\\""
          set-variable #l4 &cat &cat "\"" #l4 "\""
        !endif
        !if &band @# 8
          set-variable $window-line 2
          end-of-line
          -1 insert-string &cat "\b" #l4
          set-variable $window-line 1
          end-of-line
          set-variable .dcount &add .dcount 1
          !iif &band @# 16  !continue
        !endif
      !elif &and &band @# 1 &sin " " #l4
        set-variable #l4 &cat &cat "\"" &rep &rep #l4 "\\" "\\\\" "\"" "\\\"" "\""
      !endif
      -1 insert-string &cat "\b" #l4
    !done
    set-variable .fcount .find-file.file-count
  !endif
  beginning-of-buffer
  !if &band @# 2
    replace-string "\b" " "
    beginning-of-buffer
    replace-string "^ +" ""
  !else
    &con &band @# 8 2 1 replace-string "$" "\b"
  !endif
  !if &band @# 4
    beginning-of-buffer
    -1 insert-string @3
  !endif
  beginning-of-buffer
  goto-position "\x82"
!emacro

;; SVN Exec macros ;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;

0 define-macro svn-exec-end
  unset-variable .svn.lock
!emacro

0 define-macro svn-exec-complete
  !if &set #l9 &sin "!" &lget .svn.cflags :lock
    set-variable .svn-exec.wait 0
  !else
    !force unset-variable .svn.lock
    set-variable $buffer-dhook ""
  !endif
  end-of-buffer
  set-mark
  !repeat
    backward-line
  !until &not &seq &trb @wl "" -1
  !if &seq @wl "[TERMINATED]"
    set-variable .svn-exec.exit -9999
    set-variable #l0 "was terminated"
  !elif &xse @wl "\\[EXIT \\(.*\\)]"
    set-variable .svn-exec.exit @s1
    !if @s1
      set-variable #l0 &cat "exited with code " @s1
    !else
      set-variable #l0 "completed successfully"
    !endif
  !else
    set-variable .svn-exec.exit -9998
    set-variable #l0 "exited with unknown status"
    forward-line
  !endif
  -1 buffer-mode "view"
  -1 kill-region
  !if &or &not #l9 .svn-exec.exit
    ; move back a char to force any anchors set to this point to move forward.
    backward-char
    insert-string &spr "%s\n[**** SVN %s %s ****]" @wc &lget .svn.cnames :lock #l0
    forward-delete-char
  !endif
  !if &sin "*" &lget .svn.cflags :lock
    end-of-buffer
    set-mark
    beginning-of-buffer
    copy-region
    set-variable #l1 &spr "*svn-%s*" &slo &lget .svn.cnames :lock
    !force 0 delete-buffer #l1
    change-buffer-name #l1
    find-buffer "*svn-console-safe*"
    change-buffer-name "*svn-console*"
    !iif .toolbar.open  set-variable .toolbar-redraw.bc &bor .toolbar-redraw.bc 0x080
    end-of-buffer
    -1 buffer-mode "view"
    !iif &gre $window-line 1  insert-newline
    yank
    -1 yank
    backward-line
    -1 buffer-mode "edit"
    1 buffer-mode "view"
    !iif &exi .hilight.svn-con  set-variable $buffer-hilight .hilight.svn-con
    find-buffer #l1
  !else
    beginning-of-line
  !endif
  !iif &exi &cat "svn-exec-complete" :lock  !force execute-named-command &cat "svn-exec-complete" :lock
!emacro

0 define-macro svn-exec-ipipe
  !if &not &exi :lock
    set-variable :lock .svn.lock
    !if &sin "%c" $mode-line
      ; on unix the cursor can be temporarily left in mid line, if the column is used in the mode-line this leads to annoying refreshes.
      set-variable $buffer-mode-line &rep $mode-line "%c" "C"
    !endif
    set-variable #l0 &con @# 1 3
  !else
    set-variable #l0 &con @# 0 2
  !endif
  !iif &exi &cat "svn-exec-ipipe" :lock  !force #l0 execute-named-command &cat "svn-exec-ipipe" :lock
  !if &not @#
    !force set-variable $buffer-mode-line $mode-line
    svn-exec-complete
  !endif
!emacro

0 define-macro svn-exec-input
  !if &seq @cck "redraw"
    @# screen-update
    !return
  !elif &seq @cck "callback"
    !force execute-named-command @cc
    !return
  !elif &seq @cck "idle-pick"
    !abort
  !elif &lfin "|abort-command|delete-buffer|ipipe-kill|" @cc
    !force !force set-variable #l0 @mc5 "Abort svn command (?/y/n) ? " "nNyY" "(Y)es, (N)o ? "
    !iif &and &iseq #l0 "y" .svn-exec.wait  !force ipipe-kill
  !elif &lfin "|kill-region|" @cc
    !force ipipe-kill
    set-variable .svn-exec.wait 0
    set-variable .svn-exec.exit -9999
  !endif
!emacro

0 define-macro svn-exec
  set-variable .svn.lock @#
  set-variable #l1 @1
  set-variable #l2 @2
  ml-write &spr "[SVN %s: Processing...]" &lget .svn.cnames @#
  !if &set #l9 &sin "*" &lget .svn.cflags @#
    set-variable #l0 "*svn-console*"
  !else
    set-variable #l0 &spr "*svn-%s*" &slo &rep &lget .svn.cnames @# " " "-"
  !endif
  !force 0 popup-window #l0
  !if &not $status
    !if &seq #l0 "*svn-console*"
      !if .toolbar.open
        !force toolbar-make-tool-visible "*svn-console*"
        !force 0 popup-window "*svn-console*"
      !endif
    !endif
    !if &not &seq $buffer-bname #l0
      1 goto-position "\x82"
      delete-other-windows
      !iif &set #l3 &sin "w" &lget .svn.cflags @#  set-variable #l3 &lget &ind &cat ".svn.cvalues" @# #l3
      !if #l3
        set-position "\x82"
      !else
        !force 2 split-window-vertically
        !iif #l9  !force change-window-depth 15
        previous-window
        set-position "\x82"
        next-window
      !endif
      find-buffer #l0
    !endif
  !elif &set #l3 &sin "w" &lget .svn.cflags @#
    !if &lget &ind &cat ".svn.cvalues" @# #l3
      delete-other-windows
      set-position "\x82"
    !endif
  !endif
  !if #l9
    !force 0 delete-buffer "*svn-console-safe*"
    change-buffer-name "*svn-console-safe*"
  !endif
  find-buffer "*svn-cmdline*"
  !force 0 delete-buffer #l0
  set-variable $buffer-fname #l2
  set-variable .wait &sin "!" &lget .svn.cflags @#
  !force !force !force 0x386 ipipe-shell-command "*svn-cmdline*" #l0 svn-exec-ipipe
  set-variable #l2 $status
  !iif &band #l1 1  !force 0 delete-buffer "*svn-cmdline*"
  find-buffer #l0
  !if &not #l2
    1 goto-position "\x82"
    ml-write "[SVN Warning: failed to run svn]"
    !abort
  !elif &sin "!" &lget .svn.cflags @#
    set-variable $buffer-input svn-exec-input
    screen-update
    !force !force !force command-wait
    set-variable $buffer-input ""
    !if #l9
      !force 0 delete-buffer $buffer-bname
      find-buffer "*svn-console*"
    !endif
    !if &equ .exit -9999
      !jump 2
    !elif &not &or &band #l1 4 &seq .exit "0"
      unset-variable .svn.lock
      1 goto-position "\x82"
      !if &not &exi .exit
        ml-write "[SVN Error: svn exit status not set]"
      !elif &equ .exit -9999
        ml-write &spr "[SVN Warning: %s operation terminated]" &lget .svn.cnames @#
      !elif &equ .exit -9998
        ml-write &spr "[SVN Warning: %s operation exited with unknown status]" &lget .svn.cnames @#
      !else
        ml-write &spr "[SVN Warning: svn exited with code %d]" .exit
      !endif
      !return 0
    !elif &band #l1 2
      !force unset-variable .svn.lock
      !if &seq .exit "0"
        end-of-buffer
        -1 buffer-mode "view"
        insert-string &spr "\n[**** SVN %s completed successfully ****]" &lget .svn.cnames @#
        beginning-of-line
        -1 buffer-mode "edit"
        1 buffer-mode "view"
      !endif
      ml-write &spr "[SVN %s successful]" &lget .svn.cnames @#
    !endif
  !else
    set-variable $buffer-dhook svn-exec-end
  !endif
  1 goto-position "\x82"
!emacro

;; SVN Add ;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;

define-macro svn-add
  1 svn-osd
  set-variable #l2 &spr "%s add %s%s" .svn.com &con &lget .svn.cvalues1 1 "" "--depth=empty " &con &lget .svn.cvalues1 2 "--no-ignore " ""
  7 svn-get-file-list 0 0 #l2
  1 svn-exec 3 $buffer-fname
!emacro

;; SVN Blame ;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;

0 define-macro svn-exec-ipipe2
  !if &band @# 1
    beginning-of-buffer
    !if &lget .svn.cvalues2 2
      set-mark
      !force 3 forward-line
      -1 kill-region
    !else
      set-variable $line-scheme .scheme.header
      forward-line
      set-variable $line-scheme .scheme.header
    !endif
    goto-alpha-mark  "I"
  !endif
!emacro
0 define-macro svn-exec-complete2
  set-variable $buffer-hilight &con .svn-exec.exit 0 .hilight.svn-blm
  end-of-buffer
  backward-line
  !if &not &seq &lef @wl 9 "[**** SVN"
  !elif &lget .svn.cvalues2 2
    backward-line
    !iif &not &seq @wl ""  forward-line
    set-mark
    end-of-buffer
    -1 kill-region
  !else
    set-variable $line-scheme &con &sin "completed successfully" @wl .scheme.no1 .scheme.error
  !endif
  0 hilight .hilight.svn-blm 0 $global-scheme
  !if &not &lget .svn.cvalues2 3
    hilight .hilight.svn-blm 0x400  0 &con &lget .svn.cvalues2 1 62 17 .scheme.no2
  !else
    beginning-of-buffer
    hilight .hilight.svn-blm 0x024 "\\[" "]" "" $mode-line-scheme
    !if &lget .svn.cvalues2 1
      replace-string "^\\( *\\)\\(\\d+\\)\\( +[^ \n]+ +[\\d-]+ +[\\d:]+\\) +\\+\\d+ +([^\n)]+)" "\\1[\\2]\\3 "
      hilight .hilight.svn-blm 0x400 9 40 .scheme.no2
    !else
      replace-string "^\\( *\\)\\(\\d+\\)\\( +[^ \n]+\\)" "\\1[\\2]\\3 "
      hilight .hilight.svn-blm 0x400 9 20 .scheme.no2
    !endif
    buffer-bind-key svn-blame-cmd "return"
    buffer-bind-key svn-blame-cmd "space"
    set-variable :mouse-word-select "svn-blame-cmd"
    -1 buffer-mode "edit"
  !endif
  1 buffer-mode "view"
  beginning-of-buffer
  set-variable $buffer-hilight .hilight.svn-blm
!emacro
0 define-macro svn-blame-cmd
  !if &not &xseq @wl "^\\( *\\)\\[\\(\\d+\\)\\] .*$"
    ml-write "[ERROR: Invalid blame line]"
    !abort
  !elif &or &les $window-col &set #l1 &len @s1 &gre $window-col &add &add #l1 &len &set #l2 @s2 1
    ml-write "[ERROR: Not in blame revision number]"
    !abort
  !endif
  set-position "\x82"
  svn-diff-file .svn-blame.fnm &spr "%s:%s" &sub #l2 1 #l2
!emacro
define-macro svn-blame
  2 svn-osd
  !if &les &lget .svn.cvalues2 4 5
    set-variable #l3 &lget "|HEAD|HEAD|BASE|COMMITTED|PREV|" &lget .svn.cvalues2 4
  !elif &les &set #l3 .svn.cfver 1
    osd-dialog "Subversion - Blame" "    The specific revision number must be given!    " "  \HAbort  "
    !abort
  !endif
  set-variable #l2 &spr "%s blame -r %s %s" .svn.com #l3 &con &lget .svn.cvalues2 1 "-v " ""
  7 svn-get-file-list 0 0 #l2
  find-buffer "*svn-cmdline*"
  set-variable .fnm &rig @wl &len #l2
  goto-position "\x82"
  2 svn-exec 1 $buffer-fname
  popup-window "*svn-blame*"
  beginning-of-buffer
!emacro

;; SVN Checkout ;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;

define-macro svn-checkout
  3 svn-osd
  !if &seq "" &set #l2 &trr .svn.crep-url
    osd-dialog "Subversion - Checkout" "    A valid Repository URL must be given!    " "  \HAbort  "
    !abort
  !endif
  set-variable #l2 &spr "%s checkout %s%s" .svn.com &con &lget .svn.cvalues3 2 "" "--depth=empty " #l2
  7 svn-get-file-list 1 0 #l2
  3 svn-exec 1 $buffer-fname
!emacro

;; SVN Commit ;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;

define-macro svn-commit
  4 svn-osd
  set-variable #l2 &spr "%s commit %s" .svn.com &con &lget .svn.cvalues4 2 "" "--depth=empty "
  set-variable #l2 &spr "%s-m \"%s\" " #l2 .svn.ccomquot
  set-variable .svn.ccomment4 .svn.ccomment
  7 svn-get-file-list 0 0 #l2
  4 svn-exec 3 $buffer-fname
!emacro

;; SVN Commit Log ;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;

0 define-macro svn-log-process
  beginning-of-buffer
  !force search-buffer "me" "^--+\n\\(r\\d+\\) | \\([^ \n]+\\) | \\([\\d-]+ +[\\d:]+\\) "
  !iif &not $status  !return
  set-variable #l0 $window-line
  !tgoto &not @# single
  !force search-buffer "me" "^--+\nr\\d+ | \\([^ \n]+\\) | \\([\\d-]+ +[\\d:]+\\) "
  !if $status
    goto-line #l0
    !while &xse @wl "^\\(r\\d+\\) | \\([^ \n]+\\) | \\([\\d-]+ +[\\d:]+\\) .*"
      set-variable @wl &spr "[Show]  %s %n%s %s" @s1 &sub 16 &add &len @s1 &len @s2 " " @s2 @s3
      set-variable #l2 $window-line
      forward-line
      !if &seq @wl "Changed paths:"
        set-variable @wl ""
        forward-line
        search-buffer "Me" "\n\n"
        set-variable #l1 &sub &sub $window-line #l2 3
        set-mark
        search-buffer "Me" "\n------------------------------------------------------------------------\n"
        2 backward-line
        !if &len &trr @wl
          end-of-line
          insert-newline
        !endif
        forward-line
        beginning-of-line
        set-variable #l3 $window-line
        kill-region
        goto-line #l2
        end-of-line
        !iif &gre &len &set #l4 &trb &rep @y "\n" " " 100  set-variable #l4 &cat &lef #l4 97 "..."
        insert-string &spr "  Items: %s%n %s" #l1 &sub 5 &len #l1 " " #l4
        beginning-of-line
        2 forward-line
        yank
        -1 yank
        goto-line &add #l2 1
        set-mark
        goto-line &add #l3 1
        4 narrow-buffer
      !else
        beginning-of-line
        set-mark
        search-buffer "Me" "\n------------------------------------------------------------------------\n"
        backward-line
        copy-region
        !iif &gre &len &set #l4 &trb &rep @y "\n" " " 100  set-variable #l4 &cat &lef #l4 97 "..."
        -1 yank
        forward-line
        4 narrow-buffer
        backward-line
        end-of-line
        insert-string &cat "  " #l4
        beginning-of-line
        forward-line
      !endif
    !done
  !elif &xse @wl "^\\(r\\d+\\) | \\([^ \n]+\\) | \\([\\d-]+ +[\\d:]+\\) .*"
*single
    set-variable @wl &spr "%s %n%s %s" @s1 &sub 10 &len @s2 " " @s2 @s3
    forward-line
    !if &seq @wl "Changed paths:"
      set-variable @wl ""
      forward-line
      search-buffer "Me" "\n\n"
      set-variable #l1 &sub &sub $window-line #l0 3
      set-mark
      backward-char
      search-buffer "Me" "\n------------------------------------------------------------------------\n"
      2 backward-line
      !if &len &trr @wl
        end-of-line
        insert-newline
      !endif
      forward-line
      beginning-of-line
      kill-region
      goto-line #l0
      end-of-line
      insert-string &spr "  Items: %s" #l1
      beginning-of-line
      2 forward-line
      yank
      -1 yank
    !endif
  !endif
  goto-line #l0
  set-mark
  backward-line
  -1 kill-region
  -1 buffer-mode "edit"
  1 buffer-mode "view"
  buffer-bind-key svn-log-cmd "return"
  buffer-bind-key svn-log-cmd "space"
  set-variable :mouse-word-select "svn-log-cmd"
!emacro
0 define-macro svn-log-cmd
  !if &xseq @wl "^\\[\\(\\u\\l+\\)\\] .*$"
    !if &gre $window-col &add &len &set #l2 @s1 2
      ml-write "[ERROR: Not in log show/hide button]"
      !abort
    !elif &seq #l2 "Show"
      -1 buffer-mode "view"
      set-variable @wl &rep @wl "[Show]" "[Hide]"
      set-variable #l1 $window-line
      forward-line
      !force 2 narrow-buffer
      -1 buffer-mode "edit"
      1 buffer-mode "view"
      goto-line #l1
    !elif &seq #l2 "Hide"
      -1 buffer-mode "view"
      set-variable @wl &rep @wl "[Hide]" "[Show]"
      set-variable #l1 $window-line
      forward-line
      set-mark
      !force search-buffer "Me" "\n------------------------------------------------------------------------\n"
      !iif $status  !force 4 narrow-buffer
      -1 buffer-mode "edit"
      1 buffer-mode "view"
      goto-line #l1
    !else
      ml-write "[ERROR: Unexpected log button]"
      !abort
    !endif
  !elif &xseq @wl "^   [MAD] \\(.*\\)$"
    set-position "\x82"
    set-variable #l1 @s1
    !force search-buffer "meb" "^\\(\\[\\(Show\\|Hide\\)]  \\)?r\\(\\d+\\) "
    !if &not $status
      ml-write "[ERROR: Failed to find revision number]"
      !abort
    !endif
    set-variable #l2 @s3
    goto-position "\x82"
    !force svn-get-base-url
    !if &not $status
      ml-write "[ERROR: Failed to get svn base url]"
      !abort
    !endif
    svn-diff-file &cat .svn.crep-rot #l1 &spr "%s:%s" &sub #l2 1 #l2
  !else
    ml-write "[ERROR: Invalid log line]"
    !abort
  !endif
!emacro
0 define-macro svn-exec-complete18
  set-variable $buffer-hilight &con .svn-exec.exit 0 .hilight.svn-log
  !iif &lget .svn.cvalues18 1  0 svn-log-process
  beginning-of-buffer
!emacro
define-macro svn-commit-log
  18 svn-osd
  !if &les &lget .svn.cvalues18 2 5
    set-variable #l3 &lget "|HEAD|HEAD|BASE|COMMITTED|PREV|" &lget .svn.cvalues18 2
  !elif &les &set #l3 .svn.cfver 1
    osd-dialog "Subversion - Commit Log" "    The specific revision number must be given!    " "  \HAbort  "
    !abort
  !endif
  !force svn-get-base-url
  set-variable #l2 &spr "%s log -v -r %s %s" .svn.com #l3 .svn.crep-rot
  7 svn-get-file-list 1 0 #l2
  18 svn-exec 1 $buffer-fname
  popup-window "*svn-commit-log*"
  beginning-of-buffer
!emacro

;; SVN Copy ;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;

define-macro svn-copy
  5 svn-osd
  !if &seq "" &trr .svn.cname
    osd-dialog "Subversion - Copy" "    A valid destination name must be given!    " "  \HAbort  "
    !abort
  !endif
  set-variable #l2 &spr "%s copy %s" .svn.com &con &lget .svn.cvalues5 2 "--parents " ""
  7 svn-get-file-list 0 0 #l2
  find-buffer "*svn-cmdline*"
  end-of-line
  !if &sin " " .svn.cname
    -1 insert-string &cat &cat " \"" &rep &rep .svn.cname "\\" "\\\\" "\"" "\\\"" "\""
  !else
    -1 insert-string &cat " " .svn.cname
  !endif
  goto-position "\x82"
  5 svn-exec 3 $buffer-fname
  !if &seq $buffer-fname ""
  !elif &not &band $buffer-fmod 0x10000
  !elif &lget .svn.cvalues5 3
    dirlst-refresh
  !endif
!emacro

;; SVN Delete ;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;

define-macro svn-delete
  6 svn-osd
  set-variable #l2 &spr "%s delete %s" .svn.com &con &lget .svn.cvalues6 1 "--keep-local " ""
  7 svn-get-file-list 0 0 #l2
  6 svn-exec 3 $buffer-fname
  !if &seq $buffer-fname ""
  !elif &not &band $buffer-fmod 0x10000
  !elif &lget .svn.cvalues6 2
    dirlst-refresh
  !endif
!emacro

;; SVN Diff ;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;

0 define-macro svn-exec-ipipe7
  !iif &band @# 1  set-variable $buffer-hilight .hilight.diff
!emacro
0 define-macro svn-exec-complete7
  set-variable $buffer-hilight .hilight.diff
  !if &lget .svn.cvalues7 6
    beginning-of-buffer
    !if &les $window-eline 7
      forward-line
      !if &xseq @wl ".* diff -r \\(\\d+\\)\\(:\\d+\\)? \\(-\\l \\)*\\(.*\\)"
        set-variable #l2 &rig @s2 1
        beginning-of-line
        forward-line
        insert-string "\nIndex: "
        insert-string &set #l1 @s4
        insert-string "\n===================================================================\n"
        insert-string &spr "--- %s\t(revision %s)  [Diff-Prev]  [Diff-Prev-%s]\n" #l1 @s1 &con &len #l2 #l2 "Working"
        insert-string &spr "+++ %s\t(%s\n" #l1 &con &len #l2 &cat &cat "revision " #l2 ")  [Diff-Next]  [Diff-Working]" "working copy)"
      !endif
    !else
      !repeat
        !force search-buffer "me" "^=+\n--- \\([^\n(]+\\)\t(\\([^)\n]+\\))\n\\+\\+\\+ \\([^\n(]+\\)\t(\\([^)\n]+\\))$"
        !if $status
          !if &seq @s4 "working copy"
            set-variable #l1 "Working"
          !elif &seq &lef @s4 9 "revision "
            set-variable #l1 &rig @s4 9
            insert-string "  [Diff-Next]  [Diff-Working]"
          !else
            set-variable #l1 ""
          !endif
          !if &not &seq @s2 "nonexistent"
            backward-line
            end-of-line
            insert-string "  [Diff-Prev]"
            !iif &len #l1  insert-string &spr "  [Diff-Prev-%s]" #l1
          !endif
        !endif
      !until &not $status
    !endif
  !endif
  end-of-buffer
  backward-line
  !iif &seq &lef @wl 9 "[**** SVN"  set-variable $line-scheme &con &sin "completed successfully" @wl .scheme.no1 .scheme.error
  -1 buffer-mode "edit"
  1 buffer-mode "view"
  buffer-bind-key svn-diff-cmd "return"
  buffer-bind-key svn-diff-cmd "space"
  set-variable :mouse-word-select "svn-diff-cmd"
!emacro
0 define-macro svn-diff-file
  !iif &sin " " &set #l9 &set #l8 @1  set-variable #l9 &spr "\"%s\"" #l9
  set-variable #l2 &spr "%s diff -r %s " .svn.com @2
  !if &lget .svn.cvalues7 3
    !if &xse &set #l0 %diff-com "\\( *\\(\"[^\"]+\"\\|[^\" ][^ ]*\\)\\) .*"
      set-variable #l1 &len @s1
      set-variable #l3 &rig #l0 #l1
      set-variable #l0 &lef #l0 #l1
      !if &lget .svn.cvalues7 4
      !elif &xse #l3 ".* -w\\>.*"
        ; this assumes that -w is a standard option and is set in %diff-com
        set-variable #l3 &rep #l3 " -w" ""
      !endif
      set-variable #l3 &trb #l3
    !endif
    set-variable #l2 &spr "%s--diff-cmd %s " #l2 &trb #l0
    !iif &len #l3  set-variable #l2 &spr "%s-x \"%s\" " #l2 #l3
  !elif &lget .svn.cvalues7 4
    set-variable #l2 &cat #l2 "-x -w "
  !endif
  set-variable #l2 &cat #l2 #l9
  7 svn-get-file-list 1 0 #l2
  7 svn-exec 1 $buffer-fname
  popup-window "*svn-diff*"
  set-variable :fn #l8
  beginning-of-buffer
!emacro
0 define-macro svn-diff-cmd
  !if &not &xseq @wl "^\\(---\\|\\+\\+\\+\\) \\([^\n(]+\\)\t(\\([^)\n]+\\))\\(  \\[[-\\m]+\\]\\)+$" &lef @wl 4
    ml-write "[ERROR: Invalid diff line]"
    !abort
  !elif &or &sin @wc " \n" &not &seq "[Diff-" &lef &set #l0 &rig @wl &rsin " " &lef @wl $window-col 6
    ml-write "[ERROR: Not in diff command]"
    !abort
  !endif
  set-variable #l9 &con &exi :fn :fn @s2
  set-variable #l2 @s3
  !iif &set #l3 &sin " " #l0  set-variable #l0 &lef #l0 &sub #l3 1
  set-position "\x82"
  !if &not &seq &lef #l2 9 "revision "
    ml-write &spr "[ERROR: Unexpected revision for diff: %s]" #l2
    !abort
  !elif &seq &lef #l0 10 "[Diff-Prev"
    !if &not &seq &lef #l2 9 "revision "
      ml-write &spr "[ERROR: Unexpected revision for diff-prev: %s]" #l2
      !abort
    !endif
    4 pipe-shell-command &spr "%s log -l 3 -r %s:1 \"%s\"" .svn.com &add 1 &set #l4 &rig #l2 9 #l9 "*svn-tmp*"
    !force search-buffer "me" &spr "^r%s | " #l4
    !if &not $status
      !force search-buffer "me" &spr "^r%s | " &add #l4 1
      !if $status
        !force search-buffer "me" &spr "^r\\(\\d+\\) | "
        !if $status
          set-variable #l4 @s1
        !else
          set-variable #l4 &add #l4 1
        !endif
      !endif
    !endif
    !force search-buffer "me" &spr "^r\\(\\d+\\) | "
    !if $status
      set-variable #l5 @s1
    !elif &les #l4 2
      goto-position "\x82"
      !force delete-buffer "*svn-tmp*"
      ml-write &spr "[ERROR: Cannot diff previous - r%s is the first committed version of %s]" #l4 #l9
      !abort
    !else
      set-variable #l5 &sub #l4 1
    !endif
    !if &seq #l0 "[Diff-Prev]"
      set-variable #l5 &spr "%s:%s" #l5 #l4
    !elif &xseq #l0 "\\[Diff-Prev-\\(\\d+\\)\\]"
      set-variable #l5 &spr "%s:%s" #l5 @s1
    !endif
    goto-position "\x82"
    !force delete-buffer "*svn-tmp*"
  !elif &seq #l0 "[Diff-Working]"
    set-variable #l5 &rig #l2 9
  !else
    4 pipe-shell-command &spr "%s log -l 1 -r %s:HEAD \"%s\"" .svn.com &add 1 &set #l4 &rig #l2 9 #l9 "*svn-tmp*"
    !force search-buffer "me" &spr "^r\\(\\d+\\) | "
    !if &not $status
      goto-position "\x82"
      !force delete-buffer "*svn-tmp*"
      ml-write &spr "[ERROR: Cannot diff next - r%s is the latest (committed) version of %s]" #l4 #l9
      !abort
    !endif
    set-variable #l5 &spr "%s:%s" #l4 @s1
    goto-position "\x82"
    !force delete-buffer "*svn-tmp*"
  !endif
  svn-diff-file #l9 #l5
!emacro
define-macro svn-diff
  7 svn-osd
  !if &les &lget .svn.cvalues7 7 6
    set-variable #l3 &lget "|BASE:HEAD|HEAD|BASE|COMMITTED|PREV|" &lget .svn.cvalues7 7
  !elif &les &set #l3 .svn.cfver 1
    osd-dialog "Subversion - Diff" "    The specific revision number must be given!    " "  \HAbort  "
    !abort
  !endif
  set-variable #l2 &spr "%s diff -r %s %s" .svn.com #l3 &con &lget .svn.cvalues7 2 "" "--depth=immediates "
  !if &lget .svn.cvalues7 3
    !if &xse &set #l0 %diff-com "\\( *\\(\"[^\"]+\"\\|[^\" ][^ ]*\\)\\) .*"
      set-variable #l1 &len @s1
      set-variable #l3 &rig #l0 #l1
      set-variable #l0 &lef #l0 #l1
      !if &lget .svn.cvalues7 4
      !elif &xse #l3 ".* -w\\>.*"
        ; this assumes that -w is a standard option and is set in %diff-com
        set-variable #l3 &rep #l3 " -w" ""
      !endif
      set-variable #l3 &trb #l3
    !endif
    set-variable #l2 &spr "%s--diff-cmd %s " #l2 &trb #l0
    !iif &len #l3  set-variable #l2 &spr "%s-x \"%s\" " #l2 #l3
  !elif &lget .svn.cvalues7 4
    set-variable #l2 &cat #l2 "-x -w "
  !endif
  7 svn-get-file-list 0 &lget .svn.cvalues7 1 #l2
  7 svn-exec 1 $buffer-fname
  popup-window "*svn-diff*"
  beginning-of-buffer
!emacro

;; SVN Ignore ;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;

define-macro svn-ignore
  15 svn-osd
  !if &seq "" &set #l3 &rep &rep &rep &trb .svn.cignrmsk "\n\n" "\n" "\\" "\\\\" "\"" "\\\""
    osd-dialog "Subversion - Ignore" "    A file mask list must be given!    " "  \HAbort  "
    !abort
  !endif
  !iif &seq &rig $SHELL -3 "csh"  set-variable #l3 &rep #l3 "\n" "\\\n"
  4 pipe-shell-command &spr "%s propset svn:ignore \"%s\" \"%s\"" .svn.com #l3 .find-file.file-dir "*svn-ignore*"
!emacro

;; SVN Info ;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;

define-macro svn-info
  16 svn-osd
  set-variable #l2 &spr "%s proplist -v " .svn.com
  7 svn-get-file-list 0 0 #l2
  !force 16 svn-exec 0 .find-file.file-dir
  !force unset-variable .svn.lock
  !if $status
    find-buffer "*svn-cmdline*"
    0x404 pipe-shell-command &rep @wl " proplist -v " " info " #l1
    popup-window "*svn-info*"
    set-variable $window-line 2
    insert-newline
    set-variable @wl &spr "Info on '%s':" .find-file.file1
    forward-line
    insert-string "  "
    insert-string &xrep &trb #l1 "\r?\n" "\n  "
    insert-newline
    set-variable $window-line 1
    -1 buffer-mode "edit"
    1 buffer-mode "view"
  !endif
  !force 0 delete-buffer "*svn-cmdline*"
  1 goto-position "\x82"
!emacro

;; SVN Log ;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;

0 define-macro svn-exec-complete8
  set-variable $buffer-hilight &con .svn-exec.exit 0 .hilight.svn-log
  !iif &lget .svn.cvalues8 3  svn-log-process
  beginning-of-buffer
!emacro
define-macro svn-log
  8 svn-osd
  !if &len &set #l1 &trb .svn.logfs
    !if &sin "\"" #l1
      set-variable #l1 &spr "--search %s " &rep #l1 "\" \"" "\" --search-and \""
    !else
      set-variable #l1 &spr "--search %s " &rep #l1 " " " --search-and "
    !endif
  !endif
  set-variable #l2 &spr "%s log %s%s" .svn.com &con &lget .svn.cvalues8 2 "-v " "" #l1
  7 svn-get-file-list 0 &lget .svn.cvalues8 1 #l2
  8 svn-exec 1 $buffer-fname
  popup-window "*svn-log*"
  beginning-of-buffer
!emacro

;; SVN Move ;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;

define-macro svn-move
  9 svn-osd
  !if &seq "" &trr .svn.cname
    osd-dialog "Subversion - Move" "    A valid destination name must be given!    " "  \HAbort  "
    !abort
  !endif
  set-variable #l2 &spr "%s move %s" .svn.com &con &lget .svn.cvalues9 2 "--parents " ""
  7 svn-get-file-list 0 0 #l2
  find-buffer "*svn-cmdline*"
  end-of-line
  !if &sin " " .svn.cname
    -1 insert-string &cat &cat " \"" &rep &rep .svn.cname "\\" "\\\\" "\"" "\\\"" "\""
  !else
    -1 insert-string &cat " " .svn.cname
  !endif
  goto-position "\x82"
  9 svn-exec 3 $buffer-fname
  !if &seq $buffer-fname ""
  !elif &not &band $buffer-fmod 0x10000
  !elif &lget .svn.cvalues9 3
    dirlst-refresh
  !endif
!emacro

;; SVN Resolve ;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;

define-macro svn-resolve
  14 svn-osd
  set-variable #l2 &spr "%s resolve --accept %s %s" .svn.com &lget .av &lget .svn.cvalues14 3 &con &lget .svn.cvalues14 2 "--depth=infinity " "--depth=immediates "
  7 svn-get-file-list 0 &lget .svn.cvalues14 1 #l2
  14 svn-exec 3 $buffer-fname
!emacro
set-variable .svn-resolve.al "|\HBase|\HWorking|\HMine full|\HTheirs full|"
set-variable .svn-resolve.av "|base|working|mine-full|theirs-full|"

;; SVN Status ;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;

0 define-macro svn-exec-ipipe10
  !if &band @# 1
    set-variable $window-line 3
    !if &lget .svn.cvalues10 3
      insert-string "\n Flags     Work Rev Comt Rev Comt Author  Name  <Order by Extension>"
      set-variable :fmpf 42
    !elif &lget .svn.cvalues10 5
      insert-string "\n Flags     Work Rev   Name  <Order by Extension>"
      set-variable :fmpf 22
    !else
      insert-string "\n Flags   Name  <Order by Extension>"
      set-variable :fmpf 9
    !endif
    -1 buffer-mode "edit"
    1 buffer-mode "view"
    @# buffer-init "svn-status"
    set-variable :sc &lfind .fhook-dirlst.sc-detect "svn-dirlst-detect"
    set-variable :tag-count 0
    set-variable :mouse-pick-1 dirlst-mouse-pick-1
    set-variable :mouse-drop-1 dirlst-mouse-drop-1
    set-variable :mouse-pick-3 dirlst-mouse-pick-3
    set-variable :mouse-drop-3 dirlst-mouse-drop-3
    set-variable :dirlst-mode .fhook-dirlst.mode
    set-variable $window-line 5
    buffer-init-hooks
    set-variable :svn-last 5
  !endif
  goto-alpha-mark "I"
  !iif &equ $window-line :svn-last  !return
  -1 buffer-mode "view"
  1 buffer-mode "edit" "y"
  set-variable #l0 $window-line
  goto-line :svn-last
  !while &gre #l0 $window-line
    !if &seq &lef @wl 24 "Status against revision:"
      set-variable :svn-srev &trb &rig @wl 24
      set-mark
      forward-line
      -1 kill-region
      set-variable #l0 &sub #l0 1
      backward-line
    !elif &xse @wl "\\( +> moved \\(from\\|to\\) +\\).*"
      &len @s1 forward-delete-char
      -1 yank
      backward-delete-char
      insert-string &con &seq @s2 "to" " -> " " <- "
      beginning-of-line
      set-variable #l0 &sub #l0 1
    !elif &sin @wc " ACDIMRX?!~"
      insert-string " "
      beginning-of-line
    !endif
    forward-line
  !done
  goto-line :svn-last
  replace-string &con &bmo "magic" "\\\\" "\\" "/"
  goto-alpha-mark  "I"
  1 buffer-mode "view"
  -1 buffer-mode "edit"
  set-variable :svn-last $window-line
  !iif &band @# 2  dirlst-sort-list
!emacro

0 define-macro svn-exec-complete10
  !if &exi :svn-srev
    end-of-buffer
    backward-line
    !if &not &seq &lef @wl 9 "[**** SVN"
    !elif &seq &rig @wl -6 " ****]"
      end-of-line
      6 backward-char
      insert-string &cat "; Server revision: " :svn-srev
    !endif
  !endif
  beginning-of-buffer
  1 buffer-mode "view"
  -1 buffer-mode "edit"
  set-variable #l4 $window-id
  !repeat
    !iif &seq $buffer-bname "*svn-status*"  beginning-of-buffer
    3 next-window
  !until &equ $window-id #l4
  ml-write "[SVN Status: Complete]"
!emacro

0 define-macro svn-context-menu
  !if @#
    set-variable $mouse-x $cursor-x
    set-variable $mouse-y $cursor-y
    0x21 set-position "\x82"
  !endif
  &neg .osd.tmp osd-svn-file-menu
  goto-position "\x82"
  !force !force .osd.tmp osd
!emacro

0 define-macro svn-status-refresh
  !if .find-file.file-count
    set-alpha-mark "\x81"
    -1 buffer-mode "view"
    set-variable $window-line 5
    !while &not &seq @wc ""
      !if &seq @wc "*"
        set-variable @wc " "
        set-variable :tag-count &sub :tag-count 1
        !if &seq "X" &stat "t" &cat .find-file.file-dir &rig @wl :fmpf
          forward-char
          !if &seq @wc "?"
            backward-char
            set-mark
            forward-line
            -1 kill-region
            backward-line
          !else
            set-variable @wc "!"
            backward-char
          !endif
        !endif
      !endif
      forward-line
    !done
    -1 buffer-mode "edit"
    1 buffer-mode "view"
    goto-alpha-mark "\x81"
  !endif
!emacro

define-macro svn-status
  10 svn-osd
  set-variable #l2 &spr "%s status %s%s%s%s" .svn.com &con &lget .svn.cvalues10 2 "" "--depth=immediates " &con &lget .svn.cvalues10 3 "-v " "" &con &lget .svn.cvalues10 4 "--no-ignore " "" &con &lget .svn.cvalues10 5 "-u " ""
  7 svn-get-file-list 0 &lget .svn.cvalues10 1 #l2
  10 svn-exec 1 $buffer-fname
!emacro

;; SVN Update ;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;

define-macro svn-update
  11 svn-osd
  set-variable #l2 &spr "%s update %s" .svn.com &con &lget .svn.cvalues11 3 &con &lget .svn.cvalues11 2 "--set-depth=infinity " "--set-depth=immediates " &con &lget .svn.cvalues11 2 "" "--depth=empty "
  7 svn-get-file-list 0 &lget .svn.cvalues11 1 #l2
  11 svn-exec 3 $buffer-fname
!emacro

;; SVN Server Copy ;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;

define-macro svn-server-copy
  12 svn-osd
  set-variable #l3 .svn.crep-surl
  !if &seq "" &set #l4 &trr .svn.crep-url
    osd-dialog "Subversion - Server Copy" "    A valid destination URL must be given!    " "  \HAbort  "
    !abort
  !endif
  1 svn-get-file-list 0 0
  find-buffer "*svn-cmdline*"
  end-of-line
  !if &equ .find-file.file-count 1
    set-mark
    beginning-of-line
    -1 kill-region
    !if &sin " " #l3
      -1 insert-string &spr " \"%s\"" &rep &rep #l3 "\\" "\\\\" "\"" "\\\""
    !else
      -1 insert-string &cat " " #l3
    !endif
  !elif &sin " " #l3
    osd-dialog "Subversion - Server Copy" "    The source URL has a space - not currently supported for multiple files!    " "  \HAbort  "
    !abort
  !else
    backward-delete-char
    beginning-of-line
    -1 replace-string "\b\"" &cat " \"" #l3
    beginning-of-line
    -1 replace-string "\b" &cat " " #l3
  !endif
  beginning-of-line
  insert-string &spr "%s copy -m \"%s\" %s" .svn.com .svn.ccomquot &con &lget .svn.cvalues12 3 "--parents " ""
  set-variable .svn.ccomment12 .svn.ccomment
  end-of-line
  !if &sin " " #l4
    -1 insert-string &spr " \"%s\"" &rep &rep #l4 "\\" "\\\\" "\"" "\\\""
  !else
    -1 insert-string &cat " " #l4
  !endif
  goto-position "\x82"
  12 svn-exec 3 $buffer-fname
!emacro

;; SVN Server Move ;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;

define-macro svn-server-move
  13 svn-osd
  set-variable #l3 .svn.crep-surl
  !if &seq "" &set #l4 &trr .svn.crep-url
    osd-dialog "Subversion - Server Move" "    A valid destination URL must be given!    " "  \HAbort  "
    !abort
  !endif
  1 svn-get-file-list 0 0
  find-buffer "*svn-cmdline*"
  end-of-line
  !if &equ .find-file.file-count 1
    set-mark
    beginning-of-line
    -1 kill-region
    !if &sin " " #l3
      -1 insert-string &spr " \"%s\"" &rep &rep #l3 "\\" "\\\\" "\"" "\\\""
    !else
      -1 insert-string &cat " " #l3
    !endif
  !elif &sin " " #l3
    osd-dialog "Subversion - Server Move" "    The source URL has a space - not currently supported for multiple files!    " "  \HAbort  "
    !abort
  !else
    backward-delete-char
    beginning-of-line
    -1 replace-string "\b\"" &cat " \"" #l3
    beginning-of-line
    -1 replace-string "\b" &cat " " #l3
  !endif
  beginning-of-line
  insert-string &spr "%s move -m \"%s\" %s" .svn.com .svn.ccomquot &con &lget .svn.cvalues13 3 "--parents " ""
  set-variable .svn.ccomment13 .svn.ccomment
  end-of-line
  !if &sin " " #l4
    -1 insert-string &spr " \"%s\"" &rep &rep #l4 "\\" "\\\\" "\"" "\\\""
  !else
    -1 insert-string &cat " " #l4
  !endif
  goto-position "\x82"
  13 svn-exec 3 $buffer-fname
!emacro

;; SVN XDiff ;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;

define-macro svn-xdiff
  !if &not &exi %svn-xdiff-com
    ml-write "[ERROR: svn-xdiff not configured - set %svn-xdiff-com first]"
    !abort
  !endif
  17 svn-osd
  0 svn-get-file-list 0 &lget .svn.cvalues17 1
  set-variable #l0 $buffer-bname
  !iif &not &seq &rig &set #l1 $buffer-fname -1 "/"  set-variable #l1 &lef #l1 &rsin "/" #l1
  find-buffer "*svn-cmdline*"
  beginning-of-buffer
  forward-delete-char
  end-of-line
  backward-delete-char
  beginning-of-line
  replace-string "\b" "\n"
  beginning-of-buffer
  set-variable #l3 0
  !while &not &seq @wc ""
    !if &not &seq &set #l2 @wl ""
      find-buffer #l0
      !iif &sin " " &set #l4 &cat #l1 #l2  set-variable #l4 &cat &cat "\"" &rep &rep #l4 "\\" "\\\\" "\"" "\\\"" "\""
      !if &not &exi %svn-xdiff-nto
        set-variable #l5 %svn-xdiff-com
      !elif &not #l3
        set-variable #l5 &rep %svn-xdiff-com %svn-xdiff-nto ""
      !elif &equ #l3 1
        set-variable #l5 %svn-xdiff-com
        ; give the xdiff tool a chance to get running
        1000 command-wait
      !endif
      ml-write &cat "Exec: " &rep #l5 "%f" #l4
      0 shell-command &rep #l5 "%f" #l4
      set-variable #l3 &add #l3 1
      find-buffer "*svn-cmdline*"
    !endif
    forward-line
  !done
  find-buffer #l0
!emacro

;; SVN Setup dialog ;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;

0 define-macro svn-setup-entry
  set-variable #l2 &lget "|com|garg|def-rep-url|" &abs @#
  !if &equ &abs @# 1
    set-variable #l2 &spr "/history/%s/svn/%s" $platform #l2
  !else
    set-variable #l2 &cat "/history/svn/" #l2
  !endif
  set-variable #l0 &reg #l2 &set #l3 &lget "|svn|--non-interactive||" &abs @#
  !if &les @# 0
    set-variable #l1 @ml2 "" #l0
    !if &seq #l0 #l1
    !elif &seq #l1 #l3
      !force delete-registry #l2
    !else
      set-registry #l2 #l1
    !endif
  !else
    set-variable $result #l0
  !endif
!emacro

0 define-macro svn-setup-ctf
  set-variable #l2 &spr "/history/svn/%d_C" &abs @#
  set-variable #l0 &reg #l2 ""
  !if &les @# 0
    set-variable #l1 @ml24 "" #l0
    !if &seq #l0 #l1
    !elif &seq #l1 ""
      !force delete-registry #l2
    !else
      set-registry #l2 #l1
    !endif
  !else
    set-variable $result #l0
  !endif
!emacro
0 define-macro svn-setup-cbc
  set-variable #l0 &band &abs @# 0x0fff
  set-variable #l1 &div  &abs @# 0x1000
  set-variable #l2 &cat ".svn.cdefs" #l1
  set-variable #l3 &ind #l2
  !if &les @# 0
    set-variable &ind #l2 &lset #l3 #l0 &set #l4 &bxor 1 &lget #l3 #l0
    set-registry &spr "/history/svn/%d_%s" #l1 &mid &lget .svn.cflags #l1 &sub #l0 1 1 #l4
  !elif &not &lget #l3 #l0
    !abort
  !endif
!emacro
0 define-macro svn-setup-gcbc
  set-variable #l0 &cat "/history/svn/" &lget "|en-strt|browse-mode|" &abs @#
  set-variable #l1 &reg #l0 "0"
  !if &les @# 0
    set-registry #l0 &bxor #l1 1
  !elif &not #l1
    !abort
  !endif
!emacro

0 define-macro svn-setup-page
  osd .osd.svn-tlc &lget .order .cur "RxBd" &lget .svn.cnames .cur .cur svn-setup-page
  set-variable .cur @#
  osd .osd.svn-tlc &lget .order @# "RxBdH" .scheme.osd-entry &lget .svn.cnames @# @# svn-setup-page
  set-variable #l8 .osd.svn-tlo
  set-variable #l9 &lget .svn.cflags @#
  -1 osd #l8
  osd #l8 0 "s" 49 15 -1 -1
  osd #l8 1 ""
  osd #l8 2 "" &spr "  %s Default Settings:" &lget .svn.cnames @#
  set-variable #l0 0
  set-variable #l4 &mul @# 0x1000
  !while &not &seq "" &set #l1 &mid #l9 &pinc #l0 1 1
    !if &not &sin #l1 "aDiNnSsUv^*!$"
      set-variable #l2 &mul #l0 100
      osd #l8 &inc #l2 1 ""
      !if &seq #l1 "A"
        osd #l8 &inc #l2 1 "fh" "    "
        osd #l8 &inc #l2 1 "Cptfx" &cat .osd.checkbox-chars "\} List \Hall items with revision info" &bor #l4 #l0 svn-setup-cbc
      !elif &seq #l1 "C"
        osd #l8 &inc #l2 1 "Sf" "    \HComment template file:" &add #l2 2
        osd #l8 &inc #l2 1 "fh" "        "
        osd #l8 &inc #l2 1 "EtxHfz" .scheme.osd-entry 36 1 "" @# svn-setup-ctf
      !elif &seq #l1 "h"
        osd #l8 &inc #l2 1 "fh" "    "
        osd #l8 &inc #l2 1 "Cptfx" &cat .osd.checkbox-chars "\} R\Haw out, remove header etc" &bor #l4 #l0 svn-setup-cbc
      !elif &seq #l1 "I"
        osd #l8 &inc #l2 1 "fh" "    "
        osd #l8 &inc #l2 1 "Cptfx" &cat .osd.checkbox-chars "\} \HInclude ignored files" &bor #l4 #l0 svn-setup-cbc
      !elif &seq #l1 "K"
        osd #l8 &inc #l2 1 "fh" "    "
        osd #l8 &inc #l2 1 "Cptfx" &cat .osd.checkbox-chars "\} \HKeep local file(s)" &bor #l4 #l0 svn-setup-cbc
      !elif &seq #l1 "M"
        osd #l8 &inc #l2 1 "fh" "    "
        osd #l8 &inc #l2 1 "Cptfx" &cat .osd.checkbox-chars "\} Checkout \Hmissing items" &bor #l4 #l0 svn-setup-cbc
      !elif &seq #l1 "P"
        osd #l8 &inc #l2 1 "fh" "    "
        osd #l8 &inc #l2 1 "Cptfx" &cat .osd.checkbox-chars "\} Create \Hintermediate directories" &bor #l4 #l0 svn-setup-cbc
      !elif &seq #l1 "p"
        osd #l8 &inc #l2 1 "fh" "    "
        osd #l8 &inc #l2 1 "Cptfx" &cat .osd.checkbox-chars "\} \HProcess output - add buttons etc" &bor #l4 #l0 svn-setup-cbc
      !elif &seq #l1 "R"
        osd #l8 &inc #l2 1 "fh" "    "
        osd #l8 &inc #l2 1 "Cptfx" &cat .osd.checkbox-chars "\} \HRecurse into sub-directories" &bor #l4 #l0 svn-setup-cbc
      !elif &seq #l1 "r"
        osd #l8 &inc #l2 1 "fh" "    "
        osd #l8 &inc #l2 1 "Cptfx" &cat .osd.checkbox-chars "\} Refresh directory \Hlisting" &bor #l4 #l0 svn-setup-cbc
      !elif &seq #l1 "t"
        osd #l8 &inc #l2 1 "fh" "    "
        osd #l8 &inc #l2 1 "Cptfx" &cat .osd.checkbox-chars "\} Use new \Htab option for multiple files" &bor #l4 #l0 svn-setup-cbc
      !elif &seq #l1 "u"
        osd #l8 &inc #l2 1 "fh" "    "
        osd #l8 &inc #l2 1 "Cptfx" &cat .osd.checkbox-chars "\} Get o\Hut-of-date info from server" &bor #l4 #l0 svn-setup-cbc
      !elif &seq #l1 "V"
        osd #l8 &inc #l2 1 "fh" "    "
        osd #l8 &inc #l2 1 "Cptfx" &cat .osd.checkbox-chars "\} \HVerbose output" &bor #l4 #l0 svn-setup-cbc
      !elif &seq #l1 "W"
        osd #l8 &inc #l2 1 "fh" "    "
        osd #l8 &inc #l2 1 "Cptfx" &cat .osd.checkbox-chars "\} Ignore \Hwhite spaces" &bor #l4 #l0 svn-setup-cbc
      !elif &seq #l1 "w"
        osd #l8 &inc #l2 1 "fh" "    "
        osd #l8 &inc #l2 1 "Cptfx" &cat .osd.checkbox-chars "\} Use \Hsingle window" &bor #l4 #l0 svn-setup-cbc
      !elif &seq #l1 "x"
        osd #l8 &inc #l2 1 "fh" "    "
        osd #l8 &inc #l2 1 "Cptfx" &cat .osd.checkbox-chars "\} Use \HME diff tool" &bor #l4 #l0 svn-setup-cbc
      !elif &seq #l1 "Z"
        osd #l8 &inc #l2 1 "fh" "    "
        osd #l8 &inc #l2 1 "Cptfx" &cat .osd.checkbox-chars "\} Operate on \Hdirectory (ignore selection)" &bor #l4 #l0 svn-setup-cbc
      !else
        osd #l8 &inc #l2 1 "" &cat "flag " #l1
      !endif
    !endif
  !done
!emacro
set-variable .svn-setup-page.cur 1
set-variable .svn-setup-page.order "|1|2|3|4|5|6|7|10|11|13|14|16|17|12|8|9|15|"

-1 osd .osd.svn-tlc
osd .osd.svn-tlc 0 "sS" .scheme.osd-child 12 12 -1 -1
set-variable #l9 .svn.ccount
!repeat
  !iif &not &seq "" &set #l8 &lget .svn-setup-page.order #l9  osd .osd.svn-tlc #l8 "RxBd" &lget .svn.cnames #l9 #l9 svn-setup-page
!until &not &dec #l9 1

osd .osd.svn-tll 0 "s" 17 16 -1 -1
osd .osd.svn-tll 10 "" ""
osd .osd.svn-tll 20 "S" "  Select Tool:" 50
osd .osd.svn-tll 40 "fh" " "
osd .osd.svn-tll 50 "IbHt" .scheme.osd-sbar 12 12 .osd.svn-tlc

1 svn-setup-page

osd .osd.svn-tl 0 "s" 66 16 -1 -1
osd .osd.svn-tl 20 "Ith" .osd.svn-tll
osd .osd.svn-tl 40 "It" .osd.svn-tlo

osd .osd.svn-gen 0 "s" 66 16 -1 -1
osd .osd.svn-gen 10  "" ""
osd .osd.svn-gen 20  "Sfh" "  \HSVN Command:   " 30
osd .osd.svn-gen 30  "EtxHfz" .scheme.osd-entry 42 1 "" 1 svn-setup-entry
osd .osd.svn-gen 35  "" ""
osd .osd.svn-gen 40  "Sfh" "    \HGlobal opts: " 50
osd .osd.svn-gen 50  "EtxHfz" .scheme.osd-entry 42 1 "" 2 svn-setup-entry
osd .osd.svn-gen 60  "" ""
osd .osd.svn-gen 70  "Sfh" "  Def \HRep URL:   " 80
osd .osd.svn-gen 80  "EtxHfz" .scheme.osd-entry 42 1 "" 3 svn-setup-entry
osd .osd.svn-gen 90  "" ""
osd .osd.svn-gen 100 "fh" "    "
osd .osd.svn-gen 110 "Cptfx" &cat .osd.checkbox-chars "\} \HEnable Support on Start-Up" 1 svn-setup-gcbc
osd .osd.svn-gen 120 "" ""
osd .osd.svn-gen 130 "fh" "    "
osd .osd.svn-gen 140 "Cptfx" &cat .osd.checkbox-chars "\} \HQuick browser mode (no out-of-date check)" 2 svn-setup-gcbc
osd .osd.svn-gen 150 "" ""

osd .osd.svn-nb 0 "NsI" 68 20 -1 -1 1
osd .osd.svn-nb 1 "Pft" "General" .osd.svn-gen
osd .osd.svn-nb 2 "Pft" "Tools" .osd.svn-tl
osd .osd.svn-nb 100 "It" .osd.svn-gen

osd .osd.svn-stp 0 "batcDIHs" 5 4 70 24 -1 -1 2010 100 .scheme.osd-title "Subversion Setup"
osd .osd.svn-stp 100 "It" .osd.svn-nb
osd .osd.svn-stp 2000 ""
osd .osd.svn-stp 2010 "BtcfHh" .scheme.osd-ebtt " \HOkay " f void

define-macro svn-setup
  ; reread the setup if not run from user-setup
  !iif &band @# 1  reread-settings
  !force !force .osd.svn-stp osd
  !if &band @# 1
    !if $status
      save-registry "/history" ""
    !else
      0 reread-settings
    !endif
    execute-file "svn"
  !endif
!emacro

define-macro svn
  !if .toolbar.open
    !force toolbar-make-tool-visible "*svn-console*"
    !if $status
      &bor @# 2 file-browser
      !return
    !endif
  !endif
  1 set-position "\x81"
  !force 0 popup-window "*svn-console*"
  !if &not $status
    delete-other-windows
    2 split-window-vertically
    change-window-depth 15
    find-buffer "*svn-console*"
    buffer-bind-create "bio" "f10" "" file-browser-close
    3 previous-window
  !else
    goto-position "\x81"
  !endif
  &bor @# 2 file-browser
!emacro

;; SVN directory list/status context menu ;;;;;;;;;;;;;;;;;;

0 define-macro svn-clear-console
  set-position "\x82"
  find-buffer "*svn-console*"
  beginning-of-buffer
  set-mark
  end-of-buffer
  -1 kill-region
  -1 buffer-mode "edit"
  goto-position "\x82"
!emacro

0 define-macro osd-svn-file-menu
  !if &les @# 0
    -1 osd &set @# &neg @#
    osd @# 0 "bBo" -1 1
  !else
    -1 osd @#
    osd @# 0 "bB"
  !endif
  osd @# 1  "" "Current \HStatus"    f svn-status
  osd @# 5  "" "Check\Hout files"    f svn-checkout
  osd @# 10 "" "\HUpdate files"      f svn-update
  osd @# 30 "" "\HDiff files"        f svn-diff
  osd @# 20 "" "\HCommit files"      f svn-commit
  osd @# 100 "-"
  osd @# 160 "" "Clear S\HVN console" f svn-clear-console
  osd @# 170 "" "SVN Setup"           f svn-setup
  !if :tag-count
    osd @# 55 "" "\HCopy items"      f svn-copy
    osd @# 60 "" "\HMove items"      f svn-move
    osd @# 70 "" "\HAdd items"       f svn-add
    osd @# 80 "" "Delete items"      f svn-delete
    osd @# 120 "" "\HResolve conflicts" f svn-resolve
  !else
    osd @# 55 "" "Copy items"
    osd @# 60 "" "Move items"
    osd @# 70 "" "Add items"
    osd @# 80 "" "Delete items"
    osd @# 120 "" "Resolve conflicts"
  !endif
  !if &equ :tag-count 1
    osd @# 40 "" "Item \HLog"          f svn-log
    osd @# 50 "" "Item \HBlame"        f svn-blame
  !else
    osd @# 40 "" "Item Log"
    osd @# 50 "" "Item Blame"
  !endif
!emacro

;; SVN status help dialog ;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;

osd .osd.svn-hlp 0 "batcDH" 9 3 99 .scheme.osd-title "SVN Status Help"
osd .osd.svn-hlp 1  ""
osd .osd.svn-hlp 23 ""
osd .osd.svn-hlp 24 ""  "  A  Add      - item flagged to be added"
osd .osd.svn-hlp 25 ""  "  C  Conflict - local and repository changed"
osd .osd.svn-hlp 29 ""  "  D  Deleted  - item flagged to be removed"
osd .osd.svn-hlp 29 ""  "  I  Ignored  - SVN is configured to ignore item"
osd .osd.svn-hlp 27 ""  "  M  Modified - local item has been changed"
osd .osd.svn-hlp 29 ""  "  R  Replaced - item has been replaced"
osd .osd.svn-hlp 30 ""  "  X  eXternal - unversioned dir created via external def  "
osd .osd.svn-hlp 30 ""  "  ?  Unknown  - item is not under version control"
osd .osd.svn-hlp 26 ""  "  !  Lost     - local item has been deleted"
osd .osd.svn-hlp 26 ""  "  ~  obstruct - obstructed versioned item"
osd .osd.svn-hlp 91 ""
osd .osd.svn-hlp 92 ""
osd .osd.svn-hlp 99 "BcfH" .scheme.osd-ebtt "  \HOK  " f void
