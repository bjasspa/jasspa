; -!- emf -!-
; This is part of the JASSPA MicroEmacs macro files
; Copyright (C) 1998-2024 JASSPA (www.jasspa.com)
; See the file me.emf for copying and conditions.
;
; Created:     2024-06-04
; Synopsis:    MicroEmacs package installer 
; Authors:     Steven Phillips
;
set-variable .help.purl "http://d1j8pqz26umfbo.cloudfront.net/packages/"

0 define-macro me-pinstall
  set-variable #l1 @1
  !if &band @# 1
    set-variable #l2 "_"
  !else
    set-variable #l2 @2
  !endif
  find-buffer "*jmplst*"
  !if &equ $window-eline 1
    ml-write "Downloading JASSPA MicroEmacs Package list ... "
    1 buffer-mode "hide"
    !force insert-file &cat .help.purl "jmp.idx"
    !if &equ $window-eline 1
      ml-write "Failed to download MicroEmacs package list - computer or server may be offline"
      !abort
    !endif
    beginning-of-buffer
    !if &not &seq @wl "JMP1"
      !force delete-buffer "*jmplst*"
      ml-write "This version of MicroEmacs is not compatible with the current package list, try upgrading"
      !abort
    !endif
  !endif
  !if &band @# 2
    set-variable #l3 &spr "^%s-%s-%s-%s-%s-\\(%s\\)?-\\(%s\\)?\t\\(\\d+\\)\t\\([_\\d]+\\)\t\\(.*\\)" #l1 &lget $build 1 &lget $build 2 &lget $build 3 &lget $build 4 &lget $build 5 &lget $build 6
  !else
    set-variable #l3 &spr "^\\(%s\\)\\(\t\\)\\(\\d+\\)\t\\([_\\d]+\\)\t\\(.*\\)" #l1
  !endif
  beginning-of-buffer
  !repeat
    search-buffer "me" #l3 
    !if &not $status
      !if &gre $window-line 1
        ml-write &spr "Error: Failed to find a version of '%s' compatible with this version of MicroEmacs, try upgrading" #l1
      !else
        ml-write &spr "Error: Failed to find package '%s', please report" #l1
      !endif
      !abort
    !endif
    !if &sgre &set #l8 @s3 $version
    !elif &sgre @s4 $version
      !break
    !endif
  !done
  set-variable #l3 @s5
  !if &not &gre @s3 #l2
    ml-write &spr "MicroEmacs Package %s: Up-to-date" #l1
    !return
  !endif
  ml-write &spr "MicroEmacs Package %s: Downloading ... " #l1
  set-variable #l4 $temp-name
  !force 0x80 file-op &cat .help.purl #l3 #l4
  !if &not $status
    ml-write &spr "[Error: Failed to download package file %s]" #l3
    !abort
  !endif
  set-variable #l3 &set #l5 &cat "file:" &stat a $user-path
  ml-write &spr "MicroEmacs Package %s: %s ... " #l1 &con &band @# 1 "Installing" "Updating"
  !if &xse $search-path &spr "\\(.*?%s\\)??\\([^%s]*/macros\\)/?\\(%s.*\\)?" &set #l2 &con &band $system 0x200 ";" ":" #l2 #l2
    set-variable #l5 &cat "file:" &stat a &spr "file:%s/" @s2
  !endif
  set-variable #l6 &set #l7 0
*try-mcs-agn
  !force 0xf7 copy-file &spr "tfs://%s?/macros/.*" #l4 #l5
  !if &not $status
    set-variable #l7 &add #l7 1
  !elif &and &add &lget $result 2 &lget $result 4 &not &seq &lef #l5 &len #l3 #l3
    ; had a copy fail to search-path location that is not the user-path, user may not have write permission to central dir like /usr/share. try the user-path instead
    set-variable #l5 #l3
    !goto try-mcs-agn
  !else
    set-variable #l6 &add &add #l6 &lget $result 1 &lget $result 3
    set-variable #l7 &add &add #l7 &lget $result 2 &lget $result 4
  !endif
  !if &not &seq &rig #l5 -8 "/macros/"
    set-variable #l9 #l3
  !elif &seq &stat a &cat #l5 "./../" &set #l9 &stat a &cat $user-path "./../"
    set-variable #l9 &cat "file:" #l9
  !else
*try-bin-agn
    set-variable #l9 #l3
  !endif
  !force 0xf7 copy-file &spr "tfs://%s?/bin/.*" #l4 &cat #l9 "bin/"
  !if &not $status
    set-variable #l7 &add #l7 1
  !elif &and &add &lget $result 2 &lget $result 4 &not &seq &lef #l9 &len #l3 #l3
    !goto try-bin-agn
  !else
    set-variable #l6 &add &add #l6 &lget $result 1 &lget $result 3
    set-variable #l7 &add &add #l7 &lget $result 2 &lget $result 4
  !endif
  !iif &xse $search-path &spr "\\(.*?%s\\)??\\([^%s]*/spelling\\)/?\\(%s.*\\)?" #l2 #l2 #l2  set-variable #l5 &spr "file:%s/" @s2
*try-spl-agn
  !force 0xf7 copy-file &spr "tfs://%s?/spelling/.*" #l4 #l5
  !if &not $status
    set-variable #l7 &add #l7 1
  !elif &and &add &lget $result 2 &lget $result 4 &not &seq &lef #l5 &len #l3 #l3
    ; had a copy fail to search-path location that is not the user-path, user may not have write permission to central dir like /usr/share. try the user-path instead
    set-variable #l5 #l3
    !goto try-spl-agn
  !else
    set-variable #l6 &add &add #l6 &lget $result 1 &lget $result 3
    set-variable #l7 &add &add #l7 &lget $result 2 &lget $result 4
  !endif
  !force 0xf7 copy-file &spr "tfs://%s?/.*[^/]" #l4 #l3
  !if &not $status
    set-variable #l7 &add #l7 1
  !else
    set-variable #l6 &add &add #l6 &lget $result 1 &lget $result 3
    set-variable #l7 &add &add #l7 &lget $result 2 &lget $result 4
  !endif
  0x20 file-op #l4
  !if #l7
    1000 ml-write &spr "Error: Failed to %s MicroEmacs Package %s - %d,%d" &con &band @# 1 "install" "update" #l1 #l6 #l7
  !elif &not #l6
    1000 ml-write &spr "Error: Failed to find any content in MicroEmacs Package %s" #l1
  !else
    find-file &cat $user-path "jmp.lst"
    beginning-of-buffer
    insert-string &spr "%s\t%s\n" #l1 #l8
    !force search-buffer "me" &spr "^%s\t" #l1
    !if $status
      beginning-of-line
      set-mark
      forward-line
      -1 kill-region
    !endif
    save-buffer
    delete-buffer $buffer-bname
    ml-write &spr "MicroEmacs Package %s: %s complete" #l1 &con &band @# 1 "Install" "Update"
  !endif
!emacro
