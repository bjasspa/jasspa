; -!- emf -!-
;
; Copyright (C) 2024-2025 JASSPA (www.jasspa.com)
;
; This is part of JASSPA's MicroEmacs, see the LICENSE file for licensing and
; copying information.
;
; Synopsis:    MicroEmacs package installer 
; Authors:     Steven Phillips
;
0 define-macro me-pinstall-comp
  set-variable $file-names &spr "tfs://%s?%s" &set #l1 @1 &set #l2 @2
  !iif &seq $file-names ""  !return
  !if &len &set #l3 @3
    set-variable #l0 &spr "%s%s%s" &set #l9 &con &band $system 0x200 ";" ":" $search-path #l9
    set-variable #l4 0
    !while &len &set #l5 &lget #l0 &inc #l4 1
      !iif &not &seq &rig #l5 -1 "/"  set-variable #l5 &cat #l5 "/" 
      !iif &xseq #l5 &spr ".*/%s/" #l3  !break
    !done
    !if &len #l5
      ; found a directory of the right type, attempt to extract
      !force 0xf7 copy-file &spr "tfs://%s?%s" #l1 #l2 &stat a &cat "file:" #l5
      !if &not $status
      !elif &not &add &lget $result 2 &lget $result 4
        set-variable #p6 &add &add #p6 &lget $result 1 &lget $result 3
        !return
      !endif
      ; extraction failed, but can't add another path of same type to search-path except to user-path area
      !iif &gre #l4 2  set-variable #l4 2
    !endif
    !if &or &not &exi $MEPATH &seq #l3 "bin"
      ; Useable path of right type not found, but as $MEPATH not being used we can create one at the same level and it should be found next time
      ; Exception is a bin component, such as openssl, which does not need to be on the search-path
      !while &dec #l4 1
        !iif &not &seq &rig &set #l5 &lget #l0 #l4 -1 "/"  set-variable #l5 &cat #l5 "/" 
        set-variable #l5 &stat a &spr "file:%s../%s/" #l5 #l3 
        !force 0xf7 copy-file &spr "tfs://%s?%s" #l1 #l2 #l5
        !if &not $status
        !elif &not &set #l7 &add &lget $result 2 &lget $result 4
          set-variable $search-path &spr "%s%s%s" $search-path #l9 #l5
          set-variable #p6 &add &add #p6 &lget $result 1 &lget $result 3
          !return
        !endif
      !done
    !endif
  !endif
  !force 0xf7 copy-file &spr "tfs://%s?%s" #l1 #l2 &stat a &cat "file:" $user-path
  !if $status
    set-variable #p6 &add #p6 &add &lget $result 1 &lget $result 3
    set-variable #p7 &add #p7 &add &lget $result 2 &lget $result 4
  !else
    set-variable #p7 &add #p7 1
  !endif
!emacro

0 define-macro me-pinstall
  set-variable #l9 $buffer-bname
  set-variable #l1 @1
  !if &band @# 1
    set-variable #l2 "_"
  !else
    set-variable #l2 @2
  !endif
  find-buffer "*jmplst*"
  !if &not &exi .jmpver
    ml-write "Downloading JASSPA MicroEmacs Package list ... "
    !force insert-file &cat .purl "jmp.idx"
    beginning-of-buffer
    !if &les $window-eline 3
      set-variable #p9 "  Failed to download MicroEmacs package list,  \n  computer or server may be offline.  "
    !elif &not &seq @wl "JMP2"
      set-variable #p9 "  This version of MicroEmacs is not compatible  \n  with the current package server, try upgrading.  "
    !else
      forward-line
      !if &les $version &set #l3 @wl
        set-variable #p9 &spr "  This version of MicroEmacs is not compatible  \n  with the current package server. \n\n  Upgrade to version %s or newer.  " #l3
      !else
        forward-line
        set-variable .jmpver @wl
      !endif
    !endif
    !if &not &exi .jmpver
      !force delete-buffer "*jmplst*"
      !abort
    !endif
  !endif
  !if &not &gre .jmpver #l2
    !force delete-buffer "*jmplst*"
    ml-write &spr "MicroEmacs Package %s: Up-to-date" #l1
    !return
  !endif
  !if &band @# 2
    set-variable #l3 &spr "Jasspa_MicroEmacs_%s_%s_%s%s_%s%s.tfs" .jmpver #l1 &lget $build 1 &lget $build 2 &lget $build 3 &lget $build 4
  !else
    set-variable #l3 &spr "Jasspa_MicroEmacs_%s_%s.tfs" .jmpver #l1
  !endif
  ml-write &spr "MicroEmacs Package %s: Downloading ... " #l1
  set-variable #l4 $temp-name
  !force 0x80 file-op &cat .purl #l3 #l4
  !if &not $status
    !force delete-buffer "*jmplst*"
    set-variable #p9 &spr "  Failed to download package file:  \n\n    %s  " #l3
    !abort
  !endif
  set-variable #l3 &set #l5 &cat "file:" &stat a $user-path
  ml-write &spr "MicroEmacs Package %s: %s ... " #l1 &con &band @# 1 "Installing" "Updating"
  set-variable #l6 &set #l7 0
  me-pinstall-comp #l4 "/macros/.*" "macros"
  me-pinstall-comp #l4 "/spelling/.*" "spelling"
  me-pinstall-comp #l4 "/bin/.*" "bin"
  me-pinstall-comp #l4 "/.*[^/]" ""
  0x20 file-op #l4
  !if &set #l2 #l7
    set-variable #p9 &spr "  Failed to %s MicroEmacs Package %s (%d,%d)  " &con &band @# 1 "install" "update" #l1 #l6 #l7
  !elif &set #l2 &not #l6
    set-variable #p9 &spr "  Failed to find any content in MicroEmacs Package %s  " #l1
  !else
    find-file &cat $user-path "jmp.lst"
    beginning-of-buffer
    insert-string &spr "%s\t%s\n" #l1 #l8
    !force search-buffer "me" &spr "^%s\t" #l1
    !if $status
      beginning-of-line
      set-mark
      forward-line
      -1 kill-region
    !endif
    save-buffer
    delete-buffer $buffer-bname
    ml-write &spr "MicroEmacs Package %s: %s complete" #l1 &con &band @# 1 "Install" "Update"
  !endif
  find-buffer #l9
  !force delete-buffer "*jmplst*"
  !return &not #l2
!emacro
set-variable .me-pinstall.purl "http://docs.jasspa.com/packages/"
