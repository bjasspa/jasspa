; -!- emf -!-
; This is part of the JASSPA MicroEmacs macro files
; Copyright (C) 2001-2004 JASSPA (www.jasspa.com)
; See the file me.emf for copying and conditions.
;
; Created:     Tue Jan 23 2001
; Synopsis:    CVS support macros for MicroEmacs
; Authors:     Steven Phillips
;
!if &not &exi dirlst-sort-list
    0 exec-file "hkdirlst"
!endif

!if &not &exi %cvs-com
    set-variable %cvs-com "cvs "
!endif
!if &not &exi .osd.cvsosd
    set-variable .osd.cvsosd &pinc .osd.next 1
    define-macro cvs
    !emacro
!endif

set-variable .cvs.filter &cond &exi .cvs.filter .cvs.filter 1
set-variable .cvs.message ""

!if &band .hilight.flags 0x02
    ; fix the *files* buffer hilighting for cvs additions
    hilight .hilight.dirlst 0x012 "[0-9][0-9]:[0-9][0-9] [0-9][0-9][0-9][0-9]/[0-9][0-9]/[0-9][0-9] [0-9][0-9]:[0-9][0-9] \\{" .scheme.file
    hilight .hilight.dirlst 0x112 " d\\?"     .scheme.keyword
    hilight .hilight.dirlst 0x112 " dA"       .scheme.type
    hilight .hilight.dirlst 0x112 " dL"       .scheme.error
    hilight .hilight.dirlst 0x112 " -\\?"     .scheme.keyword
    hilight .hilight.dirlst 0x112 " -A"       .scheme.type
    hilight .hilight.dirlst 0x112 " -M"       .scheme.operator
    hilight .hilight.dirlst 0x112 " -C"       .scheme.error
    hilight .hilight.dirlst 0x112 " -R"       .scheme.error
    hilight .hilight.dirlst 0x112 " -L"       .scheme.error
    hilight .hilight.dirlst 0x112 " -U"       .scheme.variable
    !if &not &exi .hilight.diff
        !force execute-file "hkdiff"
    !endif
!endif

0 define-macro cvs-command-init
    !if &band @# 1
        !if &seq $buffer-fname ""
            ml-write "[Current buffer has no file name]"
            !abort
        !endif
        !if &band @# 2
            !if &bmod "edit"
                !if &iseq @mc1 "Save buffer first [y/n]? " "nNyY" "y"
                    save-buffer
                !endif
            !endif
        !endif
    !endif
!emacro

0 define-macro cvs-log-list-gen-prep-single
    set-mark
    search-forward "^RCS file:"
    beginning-of-line
    forward-line
    set-variable #l0 @wl
    search-forward "^\\(----\\|====\\)"
    beginning-of-line
    kill-region
    -1 yank
    !while &seq @wc "-"
        forward-line
        set-variable #l1 @wl
        set-mark
        forward-line
        kill-region
        -1 yank
        search-forward ";"
        backward-delete-char
        forward-delete-char
        forward-delete-char
        insert-newline
        search-forward ";"
        backward-char
        kill-line
        -1 yank
        search-forward "^\\(----\\|====\\)"
        beginning-of-line
        insert-string &spr "%s; %s\n" #l0 #l1
    !done
    set-mark
    forward-line
    kill-region
    -1 yank
!emacro

0 define-macro cvs-log-list-gen-rmdup-single
    forward-line
    !if &not &seq &lef @wl 5 "date:"
        !abort
    !endif
    set-variable #l0 &lef @wl 17
    forward-line
    set-mark
    forward-line
    !tjump &not &seq &lef @wl 13 "Working file:" -1
    copy-region
    set-variable #l1 @y
    -1 yank
    forward-line
    set-alpha-mark "\x81"
    str-to-regex #l1
    set-variable #l0 &spr "-\n%s.*\n%s" #l0 #l1 
    !force search-forward #l0
    !while $status
        -2 show-region
        beginning-of-line
        set-mark
        forward-line
        search-forward "^----"
        beginning-of-line
        backward-line
        set-variable #l1 @wl
        forward-line
        kill-region
        -1 yank
        set-alpha-mark "\x82"
        goto-alpha-mark "\x81"
        insert-string &cat #l1 "\n"
        set-alpha-mark "\x81"
        goto-alpha-mark "\x82"
        !force search-forward #l0
    !done
    goto-alpha-mark "\x81"
!emacro

0 define-macro cvs-log-list-gen
    set-variable #l2 $buffer-bname
    beginning-of-buffer
    -1 buffer-mode "view"
    1 buffer-mode "magic"
    !repeat
        !force cvs-log-list-gen-prep-single
    !until &not $status
    insert-string &spr "%n" 28 "-"
    beginning-of-buffer
    !repeat
        !force cvs-log-list-gen-rmdup-single
    !until &not $status
    beginning-of-buffer
    !force 0 delete-buffer "*cvs-log-list*"
    find-buffer "*cvs-log-list*"
    -1 buffer-mode "undo"
    find-buffer #l2
    !force search-forward "--\ndate:"
    !while $status
        set-variable #l0 $window-line
        set-variable #l1 @wl
        find-buffer "*cvs-log-list*"
        insert-string &spr "%9d %s\n" #l0 #l1
        find-buffer #l2
        !force search-forward "--\ndate:"
    !done
    find-buffer "*cvs-log-list*"
    beginning-of-buffer
    set-mark
    end-of-buffer
    backward-line
    -10 sort-lines
    beginning-of-buffer
    !while &not &seq @wl ""
        set-variable #l0 &add 0 @wl
        find-buffer #l2
        #l0 goto-line
        set-mark
        search-forward "^----"
        beginning-of-line
        forward-line
        copy-region
        find-buffer "*cvs-log-list*"
        yank
        -1 yank
        set-mark
        forward-line
        kill-region
        -1 yank
    !done
    beginning-of-buffer
    0 delete-buffer #l2
!emacro

define-macro cvs-log-list
    1 cvs-command-init
    set-variable #l3 &rig $buffer-fname &rsin "/" $buffer-fname
    !if &sin " " #l3
        set-variable #l3 &spr "\"%s\"" #l3
    !endif
    set-variable #l2 &spr "%s log %s" %cvs-com #l3
    4 pipe-shell-command #l2 "*cvs-log-list-tmp*"
    cvs-log-list-gen
!emacro

0 define-macro cvs-resolve-ver2
    beginning-of-buffer
    !force search-forward "^<<<<<<< "
    !while $status
        beginning-of-line
        set-mark
        forward-line
        !if &not @#
            kill-region
        !endif
        search-forward "^=======$"
        beginning-of-line
        !if @#
            forward-line
            kill-region
        !else
            set-mark
        !endif
        -1 yank
        !force search-forward "^>>>>>>> "
        beginning-of-line
        !if @#
            set-mark
        !endif
        forward-line
        -1 kill-region
        !force search-forward "^<<<<<<< "
    !done
!emacro

0 define-macro cvs-resolve-ver
    set-variable #p9 $temp-name
    !force 4 append-buffer #p9
    !if $status
        !force find-file #p9
        -1 buffer-mode "backup"
        -1 buffer-mode "time"
        1 buffer-mode "magic"
        !if $status
            !force @# cvs-resolve-ver2
            !if $status
                !force save-buffer
                !if $status
                    !force 0 delete-buffer $buffer-bname
                    !return
                !endif
            !endif
            !force 0 delete-buffer $buffer-bname
        !endif
        !force 0x20 file-op #p9
    !endif
    ml-write &spr "[Failed to generate %s version of file]" &cond @# "repository" "local"
    !abort
!emacro

0 define-macro cvs-resolve-conflictsi
    beginning-of-buffer
    1 buffer-mode "magic"
    !force search-forward "^=======$"
    !if &not $status
        goto-position "\x82"
        ml-write "[No conflicts found]"
        !return
    !endif
    !if &bmod "edit"
        ml-write "[Must save changes first]"
        !abort
    !endif
    set-variable #l0 $buffer-fname
    !force 1 cvs-resolve-ver
    !if &not $status
        !abort
    !endif
    set-variable #l1 #l9
    !force 0 cvs-resolve-ver
    !if &not $status
        !abort
    !endif
    set-variable #l2 #l9
    !force !force gdiff #l1 #l2 "*CVS-REP*" #l0
    set-variable #l3 $status
    ; Remove the temp files
    !force 0x20 file-op #l1
    !force 0x20 file-op #l2
    goto-position "\x82"
    !if #l3
        reread-file
    !endif
    !return #l3
!emacro

0 define-macro cvs-get-checkout-info
    set-variable #l0 ""
    !force 0 find-file "./CVS/Repository"
    !if $status
        beginning-of-buffer
        set-variable #l1 @wl
        0 delete-buffer $buffer-bname
        set-variable #l2 .cvs-osd.cwd
        ; .cwd has a trailing '/', #l1 does not
*rm_dir_test
        !if &set #l3 &rsin "/" #l1
            set-variable #l4 &sub &len #l2 &sub &len #l1 &dec #l3 1 
            !if &seq &cat &rig #l1 #l3 "/" &rig #l2 &sub #l4 1
                set-variable #l0 &cat &rig #l2 #l4 #l0
                set-variable #l1 &lef #l1 #l3
                set-variable #l2 &lef #l2 #l4
                !goto rm_dir_test
            !endif
        !endif
        !if &not &sin &lef #l1 1 "/"
            set-variable #l4 &sub &sub &len #l2 &len #l1 1
            !if &seq &cat &cat "/" #l1 "/" &rig #l2 &sub #l4 1
                set-variable #l0 &cat &rig #l2 #l4 #l0
                set-variable #l1 ""
                set-variable #l2 &lef #l2 #l4
            !endif
        !endif
        set-variable .cvs-osd.cwd #l2
    !endif
    set-variable .find-file.file1 #l0
    set-variable .find-file.file-count 1
    0x10 goto-position "\x82"
!emacro
    
0 define-macro cvs-get-message-template
    !force 0 find-file "./CVS/Template"
    !if $status
        beginning-of-buffer
        set-mark
        end-of-buffer
        backward-char
        copy-region
        set-variable .cvs.message &cat "\n" @y
        -1 yank
        0 delete-buffer $buffer-bname
    !endif
    0x10 goto-position "\x82"
!emacro
    
0 define-macro cvs-exec-gdiff
    set-variable #l0 @1
    set-variable #l1 .find-file.file1
    set-variable #l8 &cat %cvs-com " -n update -p"
    set-variable #l2 &sub &sin "O" .cvs-osd.options 1
    set-variable #l2 &mid .cvs-osd.values #l2 1
    !if &gre #l2 4
        osd-dialog "CVS - GDiff" "    GDiff of 2 different revisions not yet supported!    " "  \HAbort  "
        !abort
    !elif &gre #l2 2
        set-variable #l3 &bxor 1 &band #l2 1
        set-variable #l4 &cond #l3 "D" "r"
        set-variable #l9 &lget .cvs-osd.revs &add 1 #l3
        set-variable #l8 &spr "%s -%s \"%s\"" #l8 #l4 #l9
    !elif &equ #l2 2
        set-variable #l9 "HEAD"
        set-variable #l8 &cat #l8 " -r HEAD"
    !else
        set-variable #l9 "BASE"
        set-variable #l8 &cat #l8 " -r BASE"
    !endif
    set-variable #l9 &cat "CVS: " #l9
    set-variable #l2 $temp-name
    !if &seq $platform "win32"
        ; system does not maintain the correct starting dir so use whole file name
        set-variable #l3 &cat #l0 #l1
    !else
        set-variable #l3 #l1
    !endif
    !if &sin " " #l3
        set-variable #l3 &rep &rep #l3 "\\" "\\\\" "\"" "\\\""
        set-variable #l3 &cat &cat "\"" #l3 "\""
    !endif
    set-variable #l4 #l2
    !if &sin " " #l4
        set-variable #l4 &rep &rep #l4 "\\" "\\\\" "\"" "\\\""
        set-variable #l4 &cat &cat "\"" #l4 "\""
    !endif
    set-variable #l8 &spr "%s %s > %s" #l8 #l3 #l4
    !if &band $system 0x080
        set-variable #l8 &cat #l8 " 2> /dev/null"
    !endif
    shell-command #l8
    set-variable #l3 $temp-name
    0x80 file-op &cat #l0 #l1 #l3
    !force !force gdiff #l2 #l3 #l9 &cat #l0 #l1
    set-variable #l4 $status
    ; Remove the temp files
    !force 0x20 file-op #l2
    !force 0x20 file-op #l3
    !return #l4
!emacro

0 define-macro cvs-exec-add-recurse
    ; !force !force cvs-exec-add-recurse #l8 #l4 #l6 .cwd
    set-variable #l1 @2
    set-variable #l2 @3
    set-variable #l3 @4
    ml-write &spr "Add file [%s]" #l1
    set-variable #l4 #l1
    !if &seq "/" &rig #l4 &sub &len #l4 1
        set-variable #l4 &lef #l4 &sub &len #l4 1
    !endif
    !if &sin " " #l4
        set-variable #l4 &rep &rep #l4 "\\" "\\\\" "\"" "\\\""
        set-variable #l4 &cat &cat "\"" #l4 "\""
    !endif
    set-variable #l0 &cat @1 &cat " "  #l4
    set-variable #l5 $buffer-fname
    set-variable $buffer-fname #l3
    !force !force !force 6 pipe-shell-command #l0 "*cvs-temp*"
    set-variable #l0 $status
    !if $result
        set-variable #p7 $result
    !endif
    set-variable $buffer-fname #l5
    find-buffer "*cvs-temp*"
    beginning-of-buffer
    !force forward-line
    set-variable #l5 @wl
    !force 2 forward-line
    set-mark
    end-of-buffer
    copy-region
    find-buffer #l2
    set-variable $buffer-fname #l3
    end-of-buffer
    !if &equ &band @# 3 1
        !if &gre $window-line 1
            insert-newline
        !endif
    !endif
    !if &not &band @# 2
        insert-string &spr "cd %s\n" #l3
    !endif
    insert-string &spr "\n%s\n" #l5
    yank
    -1 yank
    -1 buffer-mode "edit"
    !if #l0
        !if &seq &stat "t" &cat #l3 #l1 "D"
            find-buffer "*cvs-temp*"
            !force !force !force 6 pipe-shell-command &spr "%s -nq update %s" %cvs-com #l4 "*cvs-temp*"
            !force find-buffer "*cvs-temp*"
            beginning-of-buffer
            !while &not &seq @wc ""
                !if &xseq @wl "\\? \\(.*\\)"
                    set-variable #l5 @s1
                    set-variable .find-file.file-count &add .find-file.file-count 1
                    set-variable &ind &cat ".find-file.file" .find-file.file-count #l5
                !endif
                forward-line
            !done
            find-buffer #l2
        !endif
    !endif
    !force 0 delete-buffer "*cvs-temp*"
    0x10 goto-position "\x82"
    !return #l0
!emacro
    
0 define-macro cvs-exec-cmd
    set-variable #l0 $buffer-fname
    set-variable $buffer-fname @3
    !if &equ @# 4
        ; if this is the only exec line and its not to the *cvs-console* then use ipipe straight to the buffer
        !force !force !force 6 ipipe-shell-command @1 @2
        set-variable #l1 $status
    !else
        !force !force !force 6 pipe-shell-command @1 "*cvs-temp*"
        set-variable #l1 $status
        !if $result
            set-variable #p7 $result
        !endif
    !endif
    set-variable $buffer-fname #l0
    !if &not &equ @# 4
        !force find-buffer "*cvs-temp*"
        beginning-of-buffer
        !if &band @# 2
            forward-line
        !endif
        set-mark
        end-of-buffer
        copy-region
        -1 find-buffer "*cvs-temp*"
        find-buffer @2
        set-variable $buffer-fname @3
        end-of-buffer
        !if &equ &band @# 3 1
            !if &gre $window-line 1
                insert-newline
            !endif
        !endif
        yank
        -1 yank
        exchange-point-and-mark
        beginning-of-line
        !repeat
            !while &gre &len @wl $fill-col
                set-variable $window-col $fill-col
                !force search-backward " "
                forward-char
                insert-newline
            !done
            forward-line
        !until &seq @wl ""
        -1 buffer-mode "edit"
    !endif
    0x10 goto-position "\x82"
    !force 0 delete-buffer "*cvs-temp*"
    !return #l1
!emacro

0 define-macro cvs-osd-mec
    !if &les @# 0
        set-variable .cvs.message @ml2 "" .cvs.message
    !else
        set-variable $result .cvs.message
    !endif
!emacro
0 define-macro cvs-osd-fec
    !if &les @# 0
        set-variable .find-file.file1 @ml2 "" .find-file.file1
    !else
        set-variable $result .find-file.file1
    !endif
!emacro
0 define-macro cvs-osd-tec
    !if &les @# 0
        set-variable .cvs-osd.tag @ml2 "" .cvs-osd.tag
    !else
        set-variable $result .cvs-osd.tag
    !endif
!emacro
0 define-macro cvs-osd-Dec
    !if &les @# 0
        set-variable .cvs-osd.cwd @ml21 "" .cvs-osd.cwd
    !else
        set-variable $result .cvs-osd.cwd
    !endif
!emacro
0 define-macro cvs-osd-cbc
    !if &les @# 0
        set-variable #l0 &abs @#
        set-variable .cvs-osd.values &cat &cat &lef .cvs-osd.values #l0 &bxor &mid .cvs-osd.values #l0 1 1 &rig .cvs-osd.values &add #l0 1
    !elif &not &mid .cvs-osd.values @# 1
        !abort
    !endif
!emacro
0 define-macro cvs-osd-rec
    !if &les @# 0
        set-variable .cvs-osd.revs &lset .cvs-osd.revs &abs @# @ml2 "" &lget .cvs-osd.revs &abs @#
    !else
        set-variable $result &lget .cvs-osd.revs @#
    !endif
!emacro

0 define-macro cvs-osd-crev-setup
    set-variable #l0 &sub &sin "O" .cvs-osd.options 1
    set-variable #l1 &mid .cvs-osd.values #l0 1
    set-variable #l2 &add &mul #l0 100 110
    set-variable #l3 &bxor 1 &band #l1 1
    osd .osd.cvsosd &inc #l2 1 "" ""
    !if &gre #l1 2
        osd .osd.cvsosd &inc #l2 1 "Sfh" &spr "        %s %s" &cond &gre #l1 4 "\H1st" "CVS" &cond #l3 "Date: " "Revision/Tag: " &add #l2 1
        osd .osd.cvsosd &inc #l2 1 "EtxHf" #l2 .scheme.osd-entry "################" &add 1 #l3 cvs-osd-rec
    !else
        osd .osd.cvsosd &inc #l2 1 "" ""
        osd .osd.cvsosd &inc #l2 1 "D"
    !endif
    !if &gre #l1 4
        osd .osd.cvsosd &inc #l2 1 "fh" &cat "        \H2nd " &cond #l3 "Date: " "Revision/Tag: "
        osd .osd.cvsosd &inc #l2 1 "EtxHf" #l2 .scheme.osd-entry "################" &add 3 #l3 cvs-osd-rec
    !else
        osd .osd.cvsosd &inc #l2 1 "" ""
        osd .osd.cvsosd &inc #l2 1 "D"
    !endif
    osd .osd.cvsosd &inc #l2 1 "" ""
!emacro
0 define-macro cvs-osd-compare-set
    set-variable #l0 &sin "O" .cvs-osd.options
    set-variable .cvs-osd.values &cat &cat &lef .cvs-osd.values &sub #l0 1 @# &rig .cvs-osd.values #l0
    cvs-osd-crev-setup
!emacro
0 define-macro cvs-osd-compare
    !if &les @# 0
        -1 osd .osd.tmp
        osd .osd.tmp 0 "bs" 30 0 0 0
        set-variable #l0 0
        !while &not &seq "" &set #l1 &lget .values &inc #l0 1
            osd .osd.tmp #l0 "" #l1 #l0 cvs-osd-compare-set
        !done
    !else
        set-variable #l0 &sin "O" .cvs-osd.options
        set-variable $result &lget .values &mid .cvs-osd.values &sub #l0 1 1
    !endif
!emacro
set-variable .cvs-osd-compare.values "|Local with Same CVS rev|Local with CVS HEAD|Local with another CVS rev|Local with another CVS date|Two different CVS revs|Two different CVS dates|"

0 define-macro cvs-osd-version-setup
    set-variable #l0 &sub &sin "v" .cvs-osd.options 1
    set-variable #l1 &mid .cvs-osd.values #l0 1
    set-variable #l2 &add &mul #l0 100 110
    !if &equ #l1 3
        osd .osd.cvsosd &inc #l2 1 "Shf" "    D\Hate (yyyy-mm-dd): " &add #l2 1
        osd .osd.cvsosd &inc #l2 1 "EtxHf" #l2 .scheme.osd-entry "yyyy-mm-dd   " 2 cvs-osd-rec
    !elif &equ #l1 2
        osd .osd.cvsosd &inc #l2 1 "Shf" "    Revision/T\Hag: " &add #l2 1
        osd .osd.cvsosd &inc #l2 1 "EtxHf" #l2 .scheme.osd-entry "##################" 1 cvs-osd-rec
    !else
        osd .osd.cvsosd &inc #l2 1 "" ""
        osd .osd.cvsosd &inc #l2 1 "D"
    !endif
!emacro
0 define-macro cvs-osd-version-set
    set-variable #l0 &sin "v" .cvs-osd.options
    set-variable .cvs-osd.values &cat &cat &lef .cvs-osd.values &sub #l0 1 @# &rig .cvs-osd.values #l0
    cvs-osd-version-setup
!emacro
0 define-macro cvs-osd-version
    !if &les @# 0
        -1 osd .osd.tmp
        osd .osd.tmp 0 "bs" 15 0 0 0
        set-variable #l0 0
        !while &not &seq "" &set #l1 &lget .values &inc #l0 1
            osd .osd.tmp #l0 "" #l1 #l0 cvs-osd-version-set
        !done
    !else
        set-variable #l0 &sin "v" .cvs-osd.options
        set-variable $result &lget .values &mid .cvs-osd.values &sub #l0 1 1
    !endif
!emacro
set-variable .cvs-osd-version.values "|HEAD|Revision/Tag|Date|"

0 define-macro cvs-osd
    set-position "\x82"
    !if &seq $buffer-fname ""
        osd-dialog &cat "CVS - " &lget .names #l9 "    Current buffer has no file name!    " "  \HAbort  "
        !abort
    !endif
    set-variable .cwd &lef $buffer-fname &rsin "/" $buffer-fname
    !if &not &seq &stat "t" .cwd "D"
        osd-dialog &cat "CVS - " &lget .names @# "    Current buffer is not located in a directory!    " "  \HAbort  "
        !abort
    !endif
    !if &set #l9 &seq $buffer-fhook "fhook-dirlst"
        dirlst-tag-list
        goto-position "\x82"
    !else
        !if &sin "S" &lget .flags @#
            !if &bmod "edit"
                set-variable #l0 @mc1 "Save buffer first [y/n]? " "nNyY"
                !if &iseq #l0 "y"
                    save-buffer @mna
                !endif
            !endif
        !endif
        set-variable .find-file.file-count 1
        set-variable .find-file.file1 &rig $buffer-fname &rsin "/" $buffer-fname
    !endif
    !if &sin "s" &lget .flags @#
        !if &not &equ .find-file.file-count 1
            osd-dialog &cat "CVS - " &lget .names @# "    This tool can only opperate on one specific file at a time!    " "  \HAbort  "
        !endif
        !if &seq &lget .cmds @# "res-conf"
            cvs-resolve-conflictsi            
        !endif
    !endif
    !if &seq &lget .cmds @# "remove"
        !if &not #l9
            set-variable #l0 &spr "  cvs-remove will first delete the file \"%s\"  \n     and then remove it from the repository.  " .find-file.file1
        !elif &equ .find-file.file-count 0
            osd-dialog "CVS - Remove" "    Removing a directory is not currently supported!    " "  \HAbort  "
            !abort
        !else
            ; check that all the selected files have been 'lost' (deleted locally) first
            set-variable $window-line 5
            !force regex-forward "^\\*.[^L]"
            !if $status
                beginning-of-line
                osd-dialog "CVS - Remove" "     All files to be removed must be deleted first!  " "  \HAbort  "
                !abort
            !endif
            set-variable #l0 &spr "  CVS Remove will remove %d file%s from the repository.  " .find-file.file-count &cond &equ .find-file.file-count 1 "" "s"
        !endif
        set-variable #l0 &cat #l0 "\n  Use commit in cvs browser to make removal permanent.  \n\n      Are you sure you wish to Continue?"
        osd-xdialog &cat "CVS - " &lget .names @# #l0 1 "  \HYes  " "  \HNo  "
        !if &equ $result 2
            !abort
        !endif
        !if &not #l9
            ; delete the buffer and the file
            delete-buffer $buffer-bname @mna
            0x20 file-op &cat .cwd .find-file.file1
            set-position "\x82"
        !endif
        !goto no-gui
    !endif
    -1 osd .osd.cvsosd
    osd .osd.cvsosd 0 "batcdHs" 9 3 47 4 -1 -1 2020 .scheme.osd-title &cat "CVS " &lget .names @#
    set-variable #l0 0
    !repeat
        set-variable #l1 &mid .options #l0 1
        !if &sin #l1 &lget .flags @#
            set-variable #l2 &add &mul #l0 100 100
            osd .osd.cvsosd  &inc #l2 1 ""
            !if &seq #l1 "D"
                osd .osd.cvsosd &inc #l2 1 "Sf" "    Root Directory:" &add #l2 2
                osd .osd.cvsosd &inc #l2 1 "fh" "    "
                osd .osd.cvsosd &inc #l2 1 "EtxHfhz" #l2 .scheme.osd-entry 60 1 "" f cvs-osd-Dec
                osd .osd.cvsosd &inc #l2 1 "f"  "    "
            !elif &seq #l1 "f"
                cvs-get-checkout-info
                osd .osd.cvsosd &inc #l2 1 "Sf" "    \HFile, Directory or Module:" &add #l2 2
                osd .osd.cvsosd &inc #l2 1 "fh" "    "
                osd .osd.cvsosd &inc #l2 1 "EtxHfhz" #l2 .scheme.osd-entry 60 1 "" f cvs-osd-fec
                osd .osd.cvsosd &inc #l2 1 "f"  "    "
            !elif &seq #l1 "t"
                osd .osd.cvsosd &inc #l2 1 "Sfh" "    \HTag Name: " &add #l2 1
                osd .osd.cvsosd &inc #l2 1 "EtxHfz" #l2 .scheme.osd-entry 20 1 "" f cvs-osd-tec
            !elif &seq #l1 "m"
                cvs-get-message-template
                osd .osd.cvsosd &inc #l2 1 "Sf" "    \HLog message:" &add #l2 2
                osd .osd.cvsosd &inc #l2 1 "fh" "    "
                osd .osd.cvsosd &inc #l2 1 "EtNxHfhz" #l2 .scheme.osd-entry 60 10 "" f cvs-osd-mec
                osd .osd.cvsosd &inc #l2 1 "f"  "    "
            !elif &seq #l1 "v"
                osd .osd.cvsosd &inc #l2 1 "Sfh" "    \HVersion : " &add #l2 1
                osd .osd.cvsosd &inc #l2 1 "OtxmsfhHzR" #l2 .scheme.osd-entry 13 1 "" .osd.tmp cvs-osd-version
                osd .osd.cvsosd &inc #l2 1 "BdxfhHR" .scheme.osd-ebtt &mid $window-chars 10 1 &sub #l2 1
                cvs-osd-version-setup
            !elif &seq #l1 "O"
                osd .osd.cvsosd &inc #l2 1 "Sfh" "    Compare : " &add #l2 1
                osd .osd.cvsosd &inc #l2 1 "OtxmsfhHzR" #l2 .scheme.osd-entry 28 1 "" .osd.tmp cvs-osd-compare
                osd .osd.cvsosd &inc #l2 1 "BdxfHR" .scheme.osd-ebtt &mid $window-chars 10 1 &sub #l2 1
                cvs-osd-crev-setup
            !elif &seq #l1 "Z"
                !if .find-file.file-count
                    osd .osd.cvsosd &inc #l2 1 "fh" "    "
                    osd .osd.cvsosd &inc #l2 1 "Cptfx" #l2 &cat .osd.checkbox-chars "\} Operate on current \Hdirectory" #l0 cvs-osd-cbc
                    set-variable .values &cat &cat &lef .values #l0 0 &rig .values &add #l0 1
                !endif
            !elif &seq #l1 "k"
                osd .osd.cvsosd &inc #l2 1 "fh" "    "
                osd .osd.cvsosd &inc #l2 1 "Cptfx" #l2 &cat .osd.checkbox-chars "\} Add as \HBinary Files" #l0 cvs-osd-cbc
            !elif &seq #l1 "d"
                osd .osd.cvsosd &inc #l2 1 "fh" "    "
                osd .osd.cvsosd &inc #l2 1 "Cptfx" #l2 &cat .osd.checkbox-chars "\} Check-out \Hmissing directories" #l0 cvs-osd-cbc
            !elif &seq #l1 "R"
                osd .osd.cvsosd &inc #l2 1 "fh" "    "
                osd .osd.cvsosd &inc #l2 1 "Cptfx" #l2 &cat .osd.checkbox-chars "\} \HRecurse into sub-directories" #l0 cvs-osd-cbc
            !elif &seq #l1 "P"
                osd .osd.cvsosd &inc #l2 1 "fh" "    "
                osd .osd.cvsosd &inc #l2 1 "Cptfx" #l2 &cat .osd.checkbox-chars "\} \HPrune empty directories" #l0 cvs-osd-cbc
            !elif &seq #l1 "C"
                osd .osd.cvsosd &inc #l2 1 "fh" "    "
                osd .osd.cvsosd &inc #l2 1 "Cptfx" #l2 &cat .osd.checkbox-chars "\} Overwrite locally modified files" #l0 cvs-osd-cbc
            !elif &seq #l1 "c"
                osd .osd.cvsosd &inc #l2 1 "fh" "    "
                osd .osd.cvsosd &inc #l2 1 "Cptfx" #l2 &cat .osd.checkbox-chars "\} Conte\Hxt diffs" #l0 cvs-osd-cbc
            !elif &seq #l1 "w"
                osd .osd.cvsosd &inc #l2 1 "fh" "    "
                osd .osd.cvsosd &inc #l2 1 "Cptfx" #l2 &cat .osd.checkbox-chars "\} Ignore \Hwhite spaces" #l0 cvs-osd-cbc
            !elif &seq #l1 "b"
                osd .osd.cvsosd &inc #l2 1 "fh" "    "
                osd .osd.cvsosd &inc #l2 1 "Cptfx" #l2 &cat .osd.checkbox-chars "\} Create a \HBranch tag" #l0 cvs-osd-cbc
            !elif &seq #l1 "r"
                osd .osd.cvsosd &inc #l2 1 "fh" "    "
                osd .osd.cvsosd &inc #l2 1 "Cptfx" #l2 &cat .osd.checkbox-chars "\} Delete the tag" #l0 cvs-osd-cbc
            !elif &seq #l1 "F"
                osd .osd.cvsosd &inc #l2 1 "fh" "    "
                osd .osd.cvsosd &inc #l2 1 "Cptfx" #l2 &cat .osd.checkbox-chars "\} \HMove the tag if it exists" #l0 cvs-osd-cbc
            !else
                osd .osd.cvsosd &inc #l2 1 "" &cat "flag " #l1
            !endif
        !endif
    !until &seq &mid .options &inc #l0 1 1 ""
    osd .osd.cvsosd 2000 ""
    osd .osd.cvsosd 2010 ""
    osd .osd.cvsosd 2020 "BtcfHh" 2020 .scheme.osd-ebtt " \HOkay " f void
    osd .osd.cvsosd 2030 "BtcfH"  2030 .scheme.osd-ebtt " \HCancel " 0 void
    !force !force .osd.cvsosd osd
    !if &not $status
        !abort
    !endif
*no-gui
    ; gdiffs are a little different
    !if &seq &lget .cmds @# "gdiff"
        cvs-exec-gdiff .cwd
        !return
    !endif
    ; construct the base command-line
    set-variable #l8 ""
    set-variable #l7 ""
    set-variable #l0 0
    !repeat
        set-variable #l1 &mid .options #l0 1
        !if &sin #l1 &lget .flags @#
            set-variable #l2 &mid .values #l0 1
            !if &seq #l1 "D"
            !elif &seq #l1 "f"
                !if &seq .find-file.file1 ""
                    osd-dialog &cat "CVS - " &lget .names @# "    A file or module must be given!    " "  \HAbort  "
                    !abort
                !endif
            !elif &seq #l1 "t"
                !if &seq .tag ""
                    osd-dialog &cat "CVS - " &lget .names @# "    A tag name must be given!    " "  \HAbort  "
                    !abort
                !endif
                set-variable #l7 &spr "%s \"%s\"" #l7 &rep &rep .tag "\\" "\\\\" "\"" "\\\""
            !elif &seq #l1 "m"
                set-variable #l8 &spr "%s -m \"%s\"" #l8 &rep &rep &trb .cvs.message "\\" "\\\\" "\"" "\\\""
            !elif &seq #l1 "k"
                !if #l2
                    set-variable #l8 &cat #l8 " -kb"
                !endif
            !elif &seq #l1 "v"
                !if &equ #l2 3
                    set-variable #l8 &spr "%s -D \"%s\"" #l8 &lget .revs 2
                !elif &equ #l2 2
                    set-variable #l8 &spr "%s -r \"%s\"" #l8 &lget .revs 1
                !endif
            !elif &seq #l1 "O"
                !if &gre #l2 2
                    set-variable #l3 &bxor 1 &band #l2 1
                    set-variable #l4 &cond #l3 "D" "r"
                    set-variable #l8 &spr "%s -%s \"%s\"" #l8 #l4 &lget .revs &add 1 #l3
                    !if &gre #l2 4
                        set-variable #l8 &spr "%s -%s \"%s\"" #l8 #l4 &lget .revs &add 3 #l3
                    !endif
                !elif &equ #l2 2
                    set-variable #l8 &cat #l8 " -r HEAD"
                !endif
            !elif &seq #l1 "Z"
                !if #l2
                    set-variable .find-file.file-count 0
                !endif
            !elif &seq #l1 "R"
                !if &not #l2
                    set-variable #l8 &cat #l8 " -l"
                !endif
            !elif #l2
                set-variable #l8 &cat &cat #l8 " -" #l1
            !endif
        !endif
    !until &seq &mid .options &inc #l0 1 1 ""
    set-variable #l8 &spr "%s %s%s%s" %cvs-com &lget .cmds @# #l8 #l7
    ; get the output buffer name
    set-variable #l2 0
    !if &and #l9 &sin "*" &lget .flags @#
        set-variable #l2 1
        set-variable #l6 "*cvs-console*"
    !else
        set-variable #l6 &spr "*cvs-%s*" &slow &lget .names @#
        !force 0 delete-buffer #l6
    !endif
    set-variable #l5 $mouse
    set-variable $mouse &bor 0x50000 &band 0xffff #l5
    !if &seq &lget .names @# "Add"
        ; if adding the current buffer which is a directory the file-count
        ; will be 0 and .cwd has the dir we want to add - fix this
        !if &not .find-file.file-count
            set-variable .cwd &lef .cwd &sub &len .cwd 1
            set-variable .find-file.file1 &rig .cwd &rsin "/" .cwd
            set-variable .cwd &lef .cwd &rsin "/" .cwd
            set-variable .find-file.file-count 1
        !endif
        ; if adding recursively we must do this ourselves
        !if &not &sin " -l" #l8
            set-variable #l0 0
            set-variable #l7 0
            !while &les &pinc #l0 1 .find-file.file-count
                set-variable #l4 &ind &cat ".find-file.file" #l0
                !force !force #l2 cvs-exec-add-recurse #l8 #l4 #l6 .cwd
                !if &not $status
                    set-variable #l0 .find-file.file-count
                !endif
                set-variable #l2 &bor #l2 2
            !done
            !goto recurse-add-complete
        !endif
        set-variable #l8 &rep #l8 " -l" ""
    !endif
    set-variable #l0 0
    set-variable #l7 0
    set-variable #l1 #l8
    set-variable #l3 &len #l1
    !while &les &pinc #l0 1 .find-file.file-count
        ; set-variable #l1 &spr "%s %s" #l1 &ind &cat ".find-file.file" #l0
        ; !if &or &not &band #l0 0x07 &equ #l0 .find-file.file-count
        set-variable #l4 &ind &cat ".find-file.file" #l0
        !if &seq "/" &rig #l4 &sub &len #l4 1
            set-variable #l4 &lef #l4 &sub &len #l4 1
        !endif
        !if &sin " " #l4
            set-variable #l4 &rep &rep #l4 "\\" "\\\\" "\"" "\\\""
            set-variable #l4 &cat &cat "\"" #l4 "\""
        !endif
        !if &gre &add #l3 &len #l4 1020
            !force !force #l2 cvs-exec-cmd #l1 #l6 .cwd
            !if &not $status
                set-variable #l0 .find-file.file-count
            !endif
            set-variable #l2 &bor #l2 2
            set-variable #l1 #l8
            set-variable #l3 &len #l1
        !endif
        set-variable #l1 &cat #l1 &cat " " #l4
        set-variable #l3 &add #l3 &add &len #l4 1
    !done
    set-variable #l2 &bor #l2 4
    !force !force #l2 cvs-exec-cmd #l1 #l6 .cwd
*recurse-add-complete
    !if .toolbar.open
        !if &seq #l6 "*cvs-console*"
            !force toolbar-make-tool-visible "*cvs-console*"
        !endif
    !endif
    !force popup-window #l6
    !if &not &equ #l2 4
        end-of-buffer
        insert-string &spr "\n*****CVS exited with code %d*****\n" #l7
        -1 recenter
    !endif
    !if &not &seq #l6 "*cvs-console*"
        beginning-of-buffer
        !if &seq &lget .names @# "Diff"
            set-variable $buffer-hilight .hilight.diff
        !endif
    !endif
    !force goto-position "\x82"
    !if &and #l9 &lfind "|11|3|1|7|2|" @#
        ; if update, commit remove or add then refresh or drop tags
        !if &band .find-file.flags 1
            dirlst-refresh
        !else
            dirlst-tag-remove
        !endif
    !endif
    set-variable $mouse #l5
!emacro

; tag command version
; resolve conflicts
; remove command - to prompt & del the file or prompt and cvs remove

set-variable .cvs-osd.names "|Add|Checkout|Commit|Diff|Graphical Diff|Log|Remove|State|Status|Tag|Update|Annotate|Resolve Conflicts|"
set-variable .cvs-osd.cmds  "|add|checkout|commit|diff|gdiff|log|remove|-nq update|status|tag|update|annotate|res-conf|"
set-variable .cvs-osd.flags "|kRS*|DfvRP*|mZRS*|OZRScw|OSs|ZR|*|ZSR|ZR|tZRbrF*|ZSdRPC*|vZR|Ss|"
set-variable .cvs-osd.options "mDftvOZkdRPCcwbrF"
set-variable .cvs-osd.values  " 0  1110010011000"
set-variable .cvs-osd.revs "|||||"
set-variable .cvs-osd.tag ""

define-macro cvs-add
    1 cvs-osd
!emacro
define-macro cvs-checkout
    2 cvs-osd
!emacro
define-macro cvs-commit
    3 cvs-osd
!emacro
define-macro cvs-diff
    4 cvs-osd
!emacro
define-macro cvs-gdiff
    5 cvs-osd
!emacro
define-macro cvs-log
    6 cvs-osd
!emacro
define-macro cvs-remove
    7 cvs-osd
!emacro
define-macro cvs-state
    8 cvs-osd
!emacro
define-macro cvs-status
    9 cvs-osd
!emacro
define-macro cvs-tag
    10 cvs-osd
!emacro
define-macro cvs-update
    11 cvs-osd
!emacro
define-macro cvs-annotate
    12 cvs-osd
!emacro
define-macro cvs-resolve-conflicts
    13 cvs-osd
!emacro

0 define-macro cvs-clear-console
    set-position "\x82"
    find-buffer "*cvs-console*"
    beginning-of-buffer
    set-mark
    end-of-buffer
    -1 kill-region
    -1 buffer-mode "edit"
    goto-position "\x82"
!emacro

0 define-macro osd-cvs-file-menu
    -1 osd .osd.tmp
    osd .osd.tmp 0 "b"
    osd .osd.tmp 1  "" "Current \HState"     8 cvs-osd
    osd .osd.tmp 5  "" "Check\Hout files"    2 cvs-osd
    osd .osd.tmp 10 "" "\HUpdate files"     11 cvs-osd
    osd .osd.tmp 30 "" "\HDiff files"        4 cvs-osd
    osd .osd.tmp 20 "" "\HCommit files"      3 cvs-osd
    osd .osd.tmp 40 "" "\HLog files"         6 cvs-osd
    osd .osd.tmp 50 "" "Status \Hfiles"      9 cvs-osd
    osd .osd.tmp 55 "" "A\Hnnotate files"   12 cvs-osd
    osd .osd.tmp 60 "" "\HTag files"        10 cvs-osd
    !if :tag-count
        osd .osd.tmp 70 "" "\HAdd files"     1 cvs-osd
        osd .osd.tmp 80 "" "Re\Hmove files"    7 cvs-osd
    !else
        osd .osd.tmp 70 "" "Add files"
        osd .osd.tmp 80 "" "Remove files"
    !endif
    osd .osd.tmp 100 "-"
    !if &equ :tag-count 1
        osd .osd.tmp 110 "" "\HGraphical diff"      5 cvs-osd
        osd .osd.tmp 120 "" "\HResolve conflicts"  13 cvs-osd
    !else
        osd .osd.tmp 110 "" "Graphical diff"
        osd .osd.tmp 120 "" "Resolve conflicts"
    !endif
    osd .osd.tmp 150 "-"
    osd .osd.tmp 155 "" "Generate Log List"         f cvs-log-list
    osd .osd.tmp 160 "" "Clear c\Hvs console"       f cvs-clear-console
!emacro

0 define-macro cvs-read-entries
    set-variable #l1 @1
    set-variable #l2 @2
    !force 0 delete-buffer #l2
    find-buffer #l2
    !force insert-file &cat #l1 "CVS/Entries"
    !if &not $status
        ; not a cvs directory
        !force 0 delete-buffer #l2
        !abort
    !endif
    1 buffer-mode "magic"
    set-variable #l3 $window-line
    !force insert-file &cat #l1 "CVS/Entries.Log"
    !if $status
        ; merge in the Enrties.Log
        set-variable $window-line #l3
        !while &not &seq @wc ""
            !if &xseq @wl "[AR] D/\\([^/\n]*\\)/.*"
                set-variable #l5 @wc
                set-variable #l4 @s1
                str-to-regex #l4
                set-variable #l3 $window-line
                backward-char
                !force search-backward &spr "^D/%s/" #l4
                !if &seq #l5 "A"
                    !if $status
                        set-variable $window-line #l3
                        1000 ml-write &spr "[Warning: found unexpected A Entries.Log line \"%s\"]" @wl
                        !goto rm-line
                    !endif
                    set-variable $window-line #l3
                    forward-delete-char
                    forward-delete-char
                    forward-line
                !else
                    !if $status
                        beginning-of-line
                        set-mark
                        forward-line
                        -1 kill-region
                        set-variable $window-line &sub #l3 1
                    !else
                        set-variable $window-line #l3
                        1000 ml-write &spr "[Warning: found unexpected R Entries.Log line \"%s\"]" @wl
                    !endif
                    !goto rm-line
                !endif
            !else
                !if &sin @wc "AR"
                    1000 ml-write &spr "[Warning: found unexpected Entries.Log line \"%s\"]" @wl
                !endif
*rm-line
                set-mark
                forward-line
                -1 kill-region
            !endif
        !done
    !endif
!emacro
0 define-macro cvs-get-dir-entries-info
    find-buffer @1
    set-variable #l2 @2
    beginning-of-buffer
    search-forward &spr "^D/%s////" #l2
    set-variable #p8 "        "
    set-variable #p9 "                "
    beginning-of-line
    set-mark
    forward-line
    -1 kill-region
!emacro

0 define-macro cvs-get-file-entries-info
    find-buffer @1
    set-variable #l2 @2
    beginning-of-buffer
    search-forward &spr "^/%s/\\([^/]*\\)/\\(\\(Result of merge\\+\\)?\\w+ +\\(\\w+\\) +\\([0-9]+\\) +\\([0-9]+:[0-9]+\\):[0-9]+ +\\([0-9]+\\)\\|Initial +%s\\|dummy timestamp\\)/\\([^/\n]*/\\)" #l2 #l2
    ; get the version
    !if &gre &len @s1 7
        set-variable #p8 &cat "<" &rig @s1 &sub &len @s1 6
    !else
        set-variable #p8 @s1
    !endif
    !if &seq @s4 ""
        ; initial version or dummy timestamp etc
        set-variable #p9 @s2
        set-variable #p9 &spr "%s%n " &lef #p9 15 &sub 15 &len #p9 " "
    !else
        ; get the date in YYYY/MM/DD HH:MM form
        set-variable #p9 &spr "%s/%02d/%02d %s" @s7 &lfind "|Jan|Feb|Mar|Apr|May|Jun|Jul|Aug|Sep|Oct|Nov|Dec|" @s4 @s5 @s6
    !endif
    !if &set #l3 &sin "-k" @s8
        set-variable #p7 &cat &lef #p7 1 &mid @s8 &add #l3 1 1
    !endif
    beginning-of-line
    set-mark
    forward-line
    -1 kill-region
!emacro

0 define-macro cvs-get-status-info
    find-buffer @1
    1 buffer-mode "magic"
    beginning-of-buffer
    !force search-forward &spr "^\\([^ \t\n]\\) %s$" @2
    !if $status
        ; store the status
        set-variable #p7 &cat @s1 &rig #p7 1
    !endif
!emacro

0 define-macro cvs-dir-conv
    !if &not &sin "Revis'n" .fhook-dirlst.col-name
        set-variable #l0 &lef .fhook-dirlst.col-name 1
        set-variable .fhook-dirlst.col-name &spr "%sRevis'n%sRepository Date %s" .fhook-dirlst.col-name #l0 #l0
        set-variable .fhook-dirlst.col-flag &spr "%s1%1%s" .fhook-dirlst.col-flag #l0 #l0
    !endif
    !if &seq &lef $buffer-fname 6 "ftp://"
        ; ftp directories not supported
        !return
    !endif
    set-position "\x83"
    set-variable #l1 $buffer-bname
    set-variable #l2 $buffer-fname
    !force cvs-read-entries #l2 "*cvs-temp2*"
    !if &not $status
        ; not a cvs directory
        goto-position "\x83"
        !return
    !endif
    ; make sure that the main window is only displayed once
    -1 find-buffer #l1
    0x10 goto-position "\x83"
    6 pipe-shell-command &cat %cvs-com " -nq update -l" "*cvs-temp*"
    -1 buffer-mode "view"
    !if .cvs.filter
        2 goto-line
        end-of-line
        insert-string " (Filtered)"
    !endif
    4 goto-line
    set-variable $window-col 15
    insert-string "Repository Date  "
    set-variable $window-col 6
    insert-string "  Revis'n "
    beginning-of-line
    forward-line
    !while &not &seq @wl ""
        set-variable #l7 "--"
        set-variable #l3 &rig @wl 32
        !if &sin " -> " #l3
            !goto pad-line
        !elif &set #l4 &seq &rig #l3 &sub &len #l3 1 "/"
            !if &seq #l3 "CVS/"
                !goto rmv-line
            !elif &seq #l3 "./"
                !goto rmv-line
            !elif &seq #l3 "../"
                !goto pad-line
            !endif
            set-variable #l3 &lef #l3 &sub &len #l3 1
        !endif
        filemask-to-regex #l3
        !force cvs-get-status-info "*cvs-temp*" #l3
        !if #l4
            !force cvs-get-dir-entries-info "*cvs-temp2*" #l3
        !else
            !force cvs-get-file-entries-info "*cvs-temp2*" #l3
        !endif
        set-variable #l5 $status
        find-buffer #l1
        !if #l5
            2 forward-char
            insert-string #l7
            set-variable $window-col 17
            insert-string &cat #l9 " "
            set-variable $window-col 8
            insert-string &spr "%s%n" #l8 &sub 8 &len #l8 " " 
            beginning-of-line
            forward-line
        !elif &seq #l7 "--"
*rmv-line
            !if &not .cvs.filter
                !gote pad-line
            !endif
            beginning-of-line
            set-mark
            forward-line
            -1 kill-region
        !else
*pad-line
            2 forward-char
            insert-string #l7
            set-variable $window-col 17
            insert-string "                 "
            set-variable $window-col 8
            insert-string "        "
            beginning-of-line
            forward-line
        !endif
    !done
    ; find any cvs or locally deleted files and add these to the end of the list
    find-buffer "*cvs-temp2*"
    beginning-of-buffer
    !force search-forward "^/\\([^/\n]+\\)/"
    !while $status
        set-variable #l3 @s1
        set-variable #l4 #l3
        filemask-to-regex #l3
        find-buffer "*cvs-temp*"
        beginning-of-buffer
        !force search-forward &spr "^\\(.\\) %s$" #l3
        !if &not $status
            set-variable #l7 "?"
        !elif &seq @s1 "U"
            set-variable #l7 "L"
        !else
            set-variable #l7 @s1
        !endif
        set-variable #l7 &cat #l7 "-"
        ; this really should work, if not then we are likely to spin!
        !force cvs-get-file-entries-info "*cvs-temp2*" #l3
        set-variable #l5 $status
        find-buffer #l1
        !if #l5
            insert-string &spr " -%s--- %s%n      -  %s                  %s" #l7 #l8 &sub 8 &len #l8 " " #l9 #l4
        !else
            insert-string &spr " -%s--- CVS Error - status failure on this file:           %s" #l7 #l4
            !goto del-break-out
        !endif
        beginning-of-line
        forward-line
        find-buffer "*cvs-temp2*"
        beginning-of-buffer
        !force search-forward "^/\\([^/\n]+\\)/"
    !done
    ; find any cvs or locally deleted dirs
    find-buffer #l1
    5 goto-line
    find-buffer "*cvs-temp2*"
    beginning-of-buffer
    !force search-forward "^D/\\([^/\n]+\\)/"
    !while $status
        set-variable #l3 @s1
        set-variable #l4 #l3
        filemask-to-regex #l3
        find-buffer "*cvs-temp*"
        beginning-of-buffer
        !force search-forward &spr "^\\(.\\) %s$" #l3
        !if &not $status
            set-variable #l7 "L"
        !elif &seq @s1 "U"
            set-variable #l7 "L"
        !else
            set-variable #l7 @s1
        !endif
        set-variable #l7 &cat #l7 "-"
        ; this really should work, if not then we are likely to spin!
        !force cvs-get-dir-entries-info "*cvs-temp2*" #l3
        set-variable #l5 $status
        find-buffer #l1
        !if #l5
            insert-string &spr " d%s--- %s%n      -  %s                  %s/\n" #l7 #l8 &sub 8 &len #l8 " " #l9 #l4
        !else
            insert-string &spr " d%s--- CVS Error - status failure on this dir:            %s/\n" #l7 #l4
            !goto del-break-out
        !endif
        beginning-of-line
        forward-line
        find-buffer "*cvs-temp2*"
        beginning-of-buffer
        !force search-forward "^D/\\([^/\n]+\\)/"
    !done
*del-break-out
    !force 0 delete-buffer "*cvs-temp*"
    !force 0 delete-buffer "*cvs-temp2*"
    goto-position "\x83"
    -1 buffer-mode "edit"
    set-variable :fmpf 59
!emacro

define-macro cvs
    set-variable .fhook-dirlst.cvs-enable 1
    set-variable .fhook-dirlst.file-process "cvs-dir-conv"
    !if &band $window-flags 0x1000
        ; if this is a toolbar window move to the next normal window
        !force next-window
    !endif
    !if .toolbar.open
        !force toolbar-make-tool-visible "*cvs-console*"
        !tjump &not $status 2
    !else
        1 set-position "\x81"
        !force 0 popup-window "*cvs-console*"
        !if &not $status
            delete-other-windows
            2 split-window-vertically
            change-window-depth 15
            find-buffer "*cvs-console*"
            buffer-bind-create "bio" "delete" "" file-browser-close
            3 previous-window
        !else
            goto-position "\x81"
        !endif
    !endif
    file-browser-init
!emacro
