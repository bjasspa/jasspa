; -!- emf -!-
; This is part of the JASSPA MicroEmacs macro files
; Copyright (C) 2001-2002 JASSPA (www.jasspa.com)
; See the file me.emf for copying and conditions.
;
; Created:     Tue Jan 23 2001
; Synopsis:    CVS support macros for MicroEmacs
; Authors:     Steven Phillips
;
!if &not &exi dirlst-sort-list
    0 exec-file "hkdirlst"
!endif

define-macro cvs
!emacro
set-variable .cvs.filter &cond &exi .cvs.filter .cvs.filter 1
set-variable .cvs.diff-c &cond &exi .cvs.diff-c .cvs.diff-c 1
set-variable .cvs.diff-w &cond &exi .cvs.diff-w .cvs.diff-w 1
set-variable .cvs.console-hilight "|||||||"
set-variable .cvs.message ""

!if &band .hilight.flags 0x02
    ; fix the *files* buffer hilighting for cvs additions
    hilight .hilight.dirlst 0x012 "[0-9][0-9]:[0-9][0-9] [0-9][0-9][0-9][0-9]/[0-9][0-9]/[0-9][0-9] [0-9][0-9]:[0-9][0-9] \\{" ..scheme.file
    hilight .hilight.dirlst 0x112 "*[?AMCR]" .scheme.select
    hilight .hilight.dirlst 0x112 " \\?"     .scheme.keyword
    hilight .hilight.dirlst 0x112 " A"       .scheme.type
    hilight .hilight.dirlst 0x112 " M"       .scheme.operator
    hilight .hilight.dirlst 0x112 " C"       .scheme.error
    hilight .hilight.dirlst 0x112 " R"       .scheme.error
    !if &not &exist .hilight.cvs
        set-variable .hilight.cvs &pinc .hilight.next 1
    !endif
    0 hilight .hilight.cvs 2 100           $global-scheme
    hilight .hilight.cvs 0x40 "^>cvs-" ""  $global-scheme
    !if &not &exi .hilight.diff
        !force execute-file "hkdiff"
    !endif
    ; If the hilighting mode is loaded then modify it.
    !if &and &sin "h" .fhook-emf.setup &band .hilight.flags 0x02 
        hilight .hilight.cvs  0xc0 "^>diff-" "" .hilight.diff $global-scheme
        hilight .hilight.diff 0x40 "^>diff-" ""               $global-scheme
        hilight .hilight.diff 0xc0 "^>cvs-"  "" .hilight.cvs  $global-scheme
        set-variable .cvs.console-hilight &lset .cvs.console-hilight 3 ">diff-"
    !endif
    
!endif

0 define-macro cvs-command-init
    !if &band @# 1
        !if &seq $buffer-fname ""
            ml-write "[Current buffer has no file name]"
            !abort
        !endif
        !if &band @# 2
            !if &bmod "edit"
                !if &iseq @mc1 "Save buffer first [y/n]? " "nNyY" "y"
                    save-buffer
                !endif
            !endif
        !endif
    !endif
!emacro

define-macro cvs-state
    4 ipipe-shell-command "cvs -nq update" "*cvs-state*"
!emacro

define-macro cvs-update
    4 ipipe-shell-command "cvs update" "*cvs-update*"
!emacro

0 define-macro cvs-commit-get-log
    !force 0 find-file "./CVS/Template"
    !if $status
        beginning-of-buffer
        set-mark
        end-of-buffer
        backward-char
        copy-region
        set-variable .cvs.message &cat "\n" @y
        -1 yank
        0 delete-buffer $buffer-bname
    !endif
    osd-entry "CVS - Commit" "&Log message:" .cvs.message "70x10"
    set-variable @1 &rep &rep &trb .cvs.message "\\" "\\\\" "\"" "\\\""
!emacro
    
define-macro cvs-commit
    3 cvs-command-init
    cvs-commit-get-log #l2
    set-variable #l3 &rig $buffer-fname &rsin "/" $buffer-fname
    !if &sin " " #l3
        set-variable #l3 &spr "\"%s\"" #l3
    !endif
    set-variable #l2 &spr "cvs commit -m \"%s\" %s" #l2  #l3
    4 ipipe-shell-command #l2 "*cvs-commit*"
!emacro

define-macro cvs-log
    1 cvs-command-init
    set-variable #l3 &rig $buffer-fname &rsin "/" $buffer-fname
    !if &sin " " #l3
        set-variable #l3 &spr "\"%s\"" #l3
    !endif
    set-variable #l2 &cat "cvs log " #l3
    4 ipipe-shell-command #l2 "*cvs-log*"
!emacro

define-macro cvs-add
    3 cvs-command-init
    set-variable #l3 &rig $buffer-fname &rsin "/" $buffer-fname
    !if &seq "" #l3
        set-variable #l0 $buffer-fname
        set-variable #l1 &lef $buffer-fname &sub &len $buffer-fname 1
        set-variable $buffer-fname &lef #l1 &rsin "/" #l1
        set-variable #l3 &rig #l1 &rsin "/" #l1
    !else
        set-variable #l0 ""
    !endif
    !if &sin " " #l3
        set-variable #l3 &spr "\"%s\"" #l3
    !endif
    set-variable #l2 &cat "cvs add " #l3
    !force !force 4 ipipe-shell-command #l2 "*cvs-add*"
    !if &not &seq #l0 ""
        set-variable $buffer-fname #l0
    !endif
!emacro

define-macro cvs-checkout
    3 cvs-command-init
    set-variable #l3 @ml "Enter file/dir"
    !if &sin " " #l3
        set-variable #l3 &spr "\"%s\"" #l3
    !endif
    set-variable #l2 &cat "cvs checkout " #l3
    4 ipipe-shell-command #l2 "*cvs-checkout*"
!emacro

define-macro cvs-remove
    1 cvs-command-init
    set-variable #l1 $buffer-fname
    set-variable #l3 &rig #l1 &rsin "/" $buffer-fname
    !if &seq "" #l3
        ml-write "[Removing a directory is not currently supported]"
        !abort 1
    !elif &sin " " #l3
        set-variable #l3 &spr "\"%s\"" #l3
    !endif
    set-variable #l0 &spr "  cvs-remove will first delete the file \"%s\"  \n     and then remove it from the repository.\n\n  Use commit in cvs browser to make removal permanent  \n\n                      Continue?" #l3
    osd-xdialog "CVS - Remove" #l0 2 "  &Yes  " "  &No  "
    !if &equ $result 2
        !abort
    !endif
    delete-buffer $buffer-bname @mna
    0x20 file-op #l1
    find-buffer "*cvs-temp*"
    set-variable $buffer-fname #l1 
    set-variable #l2 &cat "cvs remove " #l3
    4 ipipe-shell-command #l2 "*cvs-remove*"
    !force 0 delete-buffer "*cvs-temp*"
!emacro

define-macro cvs-diff
    1 cvs-command-init
    set-variable #l3 &rig $buffer-fname &rsin "/" $buffer-fname
    !if &sin " " #l3
        set-variable #l3 &spr "\"%s\"" #l3
    !endif
    set-variable #l2 &spr "cvs diff %s%s%s" &cond .cvs.diff-c "-c " "" &cond .cvs.diff-w "-w " "" #l3
    4 ipipe-shell-command #l2 "*cvs-diff*"
    !if &not &seq &lget .cvs.console-hilight 3 ""
        set-position "\x82"
        find-buffer "*cvs-diff*"
        set-variable $buffer-hilight .hilight.diff
        goto-position "\x82"
    !endif
!emacro

define-macro cvs-gdiff
    3 cvs-command-init
    set-variable #l0 $buffer-fname
    set-variable #l3 &rig #l0 &rsin "/" $buffer-fname
    !if &seq "" #l3
        ml-write "[cvs-gdiff does not support directories]"
        !abort 1
    !elif &sin " " #l3
        set-variable #l3 &spr "\"%s\"" #l3
    !endif
    set-variable #l1 $temp-name
    !if &seq $platform "win32"
	shell-command &spr "cvs -n update -p %s > %s" #l3 #l1
    !else
	shell-command &spr "cvs -n update -p %s > %s 2> /dev/null" #l3 #l1
    !endif
    set-variable #l2 $temp-name
    0x80 file-op #l0 #l2
    !force !force gdiff #l1 #l2 "*CVS-REP*" #l0
    set-variable #l4 $status
    ; Remove the temp files
    !force 0x20 file-op #l1
    !force 0x20 file-op #l2
    !return #l4
!emacro

0 define-macro cvs-resolve-ver2
    beginning-of-buffer
    !force search-forward "^<<<<<<< "
    !while $status
        beginning-of-line
        set-mark
        forward-line
        !if &not @#
            kill-region
        !endif
        search-forward "^=======$"
        beginning-of-line
        !if @#
            forward-line
            kill-region
        !else
            set-mark
        !endif
        -1 yank
        !force search-forward "^>>>>>>> "
        beginning-of-line
        !if @#
            set-mark
        !endif
        forward-line
        -1 kill-region
        !force search-forward "^<<<<<<< "
    !done
!emacro

0 define-macro cvs-resolve-ver
    set-variable #p9 $temp-name
    !force 4 append-buffer #p9
    !if $status
        !force find-file #p9
        -1 buffer-mode "backup"
        -1 buffer-mode "time"
        1 buffer-mode "magic"
        !if $status
            !force @# cvs-resolve-ver2
            !if $status
                !force save-buffer
                !if $status
                    !force 0 delete-buffer $buffer-bname
                    !return
                !endif
            !endif
            !force 0 delete-buffer $buffer-bname
        !endif
        !force 0x20 file-op #p9
    !endif
    ml-write &spr "[Failed to generate %s version of file]" &cond @# "repository" "local"
    !abort
!emacro

define-macro cvs-resolve-conflicts
    !if &seq $buffer-fname ""
        ml-write "[Current buffer has no file name]"
        !abort
    !endif
    set-position "\x82"
    beginning-of-buffer
    1 buffer-mode "magic"
    !force search-forward "^=======$"
    !if &not $status
        goto-position "\x82"
        ml-write "[No conflicts found]"
        !return
    !endif
    !if &bmod "edit"
        goto-position "\x82"
        !if &iseq @mc1 "Save buffer first [y/n]? " "nNyY" "y"
            save-buffer
        !else
            ml-write "[Must save changes first]"
            !abort
        !endif
    !endif
    set-variable #l0 $buffer-fname
    !force 1 cvs-resolve-ver
    !if &not $status
        !abort
    !endif
    set-variable #l1 #l9
    !force 0 cvs-resolve-ver
    !if &not $status
        !abort
    !endif
    set-variable #l2 #l9
    !force !force gdiff #l1 #l2 "*CVS-REP*" #l0
    set-variable #l3 $status
    ; Remove the temp files
    !force 0x20 file-op #l1
    !force 0x20 file-op #l2
    goto-position "\x82"
    !if #l3
        reread-file
    !endif
    !return #l3
!emacro

0 define-macro cvs-get-entries-info
    find-buffer @1
    beginning-of-buffer
    search-forward &spr "^/%s/\\([^/]*\\)/\\(Result of merge\\+\\)?\\w+ +\\(\\w+\\) +\\([0-9]+\\) +\\([0-9]+:[0-9]+\\):[0-9]+ +\\([0-9]+\\)/" @2
    ; get the version
    !if &gre &len @s1 7
        set-variable #p8 &cat "<" &rig @s1 &sub &len @s1 6
    !else
        set-variable #p8 @s1
    !endif
    ; get the date in YYYY/MM/DD HH:MM form
    set-variable #p9 &spr "%s/%02d/%02d %s" @s6 &lfind "|Jan|Feb|Mar|Apr|May|Jun|Jul|Aug|Sep|Oct|Nov|Dec|" @s3 @s4 @s5
!emacro

0 define-macro cvs-get-status-info
    find-buffer @1
    beginning-of-buffer
    search-forward &spr "^\\([^ \t\n]\\) %s$" @2
    ; get the status
    set-variable #p9 @s1
!emacro

0 define-macro cvs-dir-conv
    !if &not &sin "Revis'n" .fhook-dirlst.col-name
        set-variable #l0 &lef .fhook-dirlst.col-name 1
        set-variable .fhook-dirlst.col-name &spr "%sRevis'n%sRepository Date %s" .fhook-dirlst.col-name #l0 #l0
        set-variable .fhook-dirlst.col-flag &spr "%s1%1%s" ..fhook-dirlst.col-flag #l0 #l0
    !endif
    !if &seq &lef $buffer-fname 6 "ftp://"
        ; ftp directories not supported
        !return
    !endif
    set-position "\x83"
    set-variable #l1 $buffer-bname
    !force 0 find-file "CVS/Entries"
    !if &not $status
        ; not a cvs directory
        !return
    !endif
    set-variable #l2 $buffer-bname
    ; make sure that the main window is only displayed once
    -1 find-buffer #l1
    0x10 goto-position "\x83"
    6 pipe-shell-command "cvs -nq update -l" "*cvs-temp*"
    -1 buffer-mode "view"
    !if .cvs.filter
        2 goto-line
        end-of-line
        insert-string " (Filtered)"
    !endif
    4 goto-line
    set-variable $window-col 15
    insert-string "Repository Date  "
    set-variable $window-col 6
    insert-string "Revis'n "
    beginning-of-line
    forward-line
    !while &not &seq @wl ""
        !if &seq &mid @wl 1 1 "-"
            set-variable #l3 &rig @wl 32
            !if &sin " -> " #l3
                !goto pad-line
            !else
                filemask-to-regex #l3
                !force cvs-get-status-info "*cvs-temp*" #l3
                set-variable #l4 $status
                !if #l4
                    ; save the status
                    set-variable #l6 #l9
                !endif
                !force cvs-get-entries-info #l2 #l3
                set-variable #l5 $status
                find-buffer #l1
                !if #l4
                    forward-char
                    forward-delete-char
                    insert-string #l6
                !endif
                !if #l5
                    set-variable $window-col 15
                    insert-string &cat #l9 " "
                    set-variable $window-col 6
                    insert-string &spr "%s%n" #l8 &sub 8 &len #l8 " " 
                !elif #l4
                    !goto pad-line
                !else
                    !goto rmv-line
                !endif
                beginning-of-line
                forward-line
            !endif
        !elif &seq &mid @wl 1 1 "d"
            !if &not &seq &rig @wl 32 "CVS/"
                !goto pad-line
            !endif
*rmv-line
            !if &not .cvs.filter
                !gote pad-line
            !endif
            beginning-of-line
            set-mark
            forward-line
            -1 kill-region
        !else
*pad-line
            set-variable $window-col 15
            insert-string "                 "
            set-variable $window-col 6
            insert-string "        "
            beginning-of-line
            forward-line
        !endif
    !done
    ; find any deleted files and add these to the end of the list
    find-buffer "*cvs-temp*"
    beginning-of-buffer
    !force search-forward "^R \\(.*\\)$"
    !while $status
        set-variable #l3 @s1
        filemask-to-regex #l3
        set-variable #l4 @s1
        !force cvs-get-entries-info #l2 #l3
        set-variable #l5 $status
        find-buffer #l1
        !if #l5
            insert-string &spr " R--- %s%n      -  %s                  %s" #l8 &sub 8 &len #l8 " " #l9 #l4
        !else
            insert-string &spr " R---               -                                    %s" #l4
        !endif
        beginning-of-line
        forward-line
        find-buffer "*cvs-temp*"
        !force search-forward "^R \\(.*\\)$"
    !done
    !force 0 delete-buffer #l2
    !force 0 delete-buffer "*cvs-temp*"
    -1 buffer-mode "edit"
    goto-position "\x83"
    set-variable :fmpf 57
!emacro

0 define-macro cvs-exec-file-menu-cmd
    !force 6 pipe-shell-command @1 "*cvs-temp*"
    !force find-buffer "*cvs-temp*"
    beginning-of-buffer
    forward-line
    set-mark
    end-of-buffer
    copy-region
    -1 find-buffer "*cvs-temp*"
    find-buffer "*cvs-console*"
    end-of-buffer
    yank
    -1 yank
    exchange-point-and-mark
    beginning-of-line
    !repeat
        !while &gre &len @wl $fill-col
            set-variable $window-col $fill-col
            !force search-backward " "
            forward-char
            insert-newline
        !done
        forward-line
    !until &seq @wl ""
    !if &not &seq &set #l0 &lget .cvs.console-hilight @2 ""
        !repeat
            insert-string #l0
            beginning-of-line
            !force 100 forward-line
        !until &not $status
        set-variable $buffer-hilight .hilight.cvs
    !endif
    -1 buffer-mode "edit"
    0x10 goto-position "\x82"
!emacro

0 define-macro osd-cvs-file-menu-cmd
    set-position "\x82"
    !if &gre @# 99
        !if &equ @# 100
            find-buffer "*cvs-console*"
            beginning-of-buffer
            set-mark
            end-of-buffer
            -1 kill-region
            -1 buffer-mode "edit"
            goto-position "\x82"
        !elif &equ @# 103
            
        !else
            ; load the file in
            dirlst-tag-list
            find-file .find-file.file1
            !if &equ @# 101
                cvs-gdiff
            !else
                cvs-resolve-conflicts
            !endif
            cvs
        !endif
        !return
    !endif
    dirlst-tag-list
    !if &seq @# 2
        cvs-commit-get-log #l2
        set-variable #l2 &spr "cvs commit -m \"%s\"" #l2
    !elif &seq @# 7
        osd-dialog "CVS - Remove" "     Yikes! Too dangerous.\n\n  Use cvs-remove on each file.  " "  &Abort  "
        !abort
    !elif &seq @# 8
        set-variable #l2 ""
        osd-entry "CVS - Checkout" "File/Dir &Name : " #l2 20
        set-variable .find-file.file-count 1
        set-variable .find-file.file1 #l2
        set-variable #l2 "cvs checkout"
    !elif &seq @# 3
        set-variable #l2 &spr "cvs diff %s%s" &cond .cvs.diff-c "-c " "" &cond .cvs.diff-w "-w " ""
    !else
        set-variable #l2 &spr "cvs %s" &lget "|update|commit|diff|log|status -v|add|||add -kb|" @#
    !endif
    set-variable #l5 $mouse
    set-variable $mouse &bor 0x50000 &band 0xffff #l5
    set-variable #l0 0
    set-variable #l1 #l2
    set-variable #l3 &len #l2
    !while &les &pinc #l0 1 .find-file.file-count
        ; set-variable #l1 &spr "%s %s" #l1 &ind &cat ".find-file.file" #l0
        ; !if &or &not &band #l0 0x07 &equ #l0 .find-file.file-count
        set-variable #l4 &ind &cat ".find-file.file" #l0
        !if &seq "/" &rig #l4 &sub &len #l4 1
            set-variable #l4 &lef #l4 &sub &len #l4 1
        !endif
        !if &sin " " #l4
            set-variable #l4 &rep &rep #l4 "\\" "\\\\" "\"" "\\\""
            set-variable #l4 &cat &cat "\"" #l4 "\""
        !endif
        !if &gre &add #l3 &len #l4 1020
            cvs-exec-file-menu-cmd #l1 @# 
            set-variable #l1 #l2
            set-variable #l3 &len #l2
        !endif
        set-variable #l1 &cat #l1 &cat " " #l4
        set-variable #l3 &add #l3 &add &len #l4 1
    !done
    cvs-exec-file-menu-cmd #l1 @# 
    !force 0 popup-window "*cvs-console*"
    !if $status
        end-of-buffer
    !endif
    goto-position "\x82"
    set-variable $mouse #l5
    !if &lfind "|1|2|6|" @#
        ; if update, commit or add then refresh or drop tags
        !if &band .find-file.flags 1
            dirlst-refresh
        !else
            dirlst-tag-remove
        !endif
    !endif
!emacro

0 define-macro osd-cvs-file-menu
    -1 osd .osd.tmp
    osd .osd.tmp 0 "b"
    !if :tag-count
        osd .osd.tmp 10 "" "&Update files"      1 osd-cvs-file-menu-cmd
        osd .osd.tmp 20 "" "&Commit files"      2 osd-cvs-file-menu-cmd
        osd .osd.tmp 40 "" "&Log files"         4 osd-cvs-file-menu-cmd
        osd .osd.tmp 50 "" "&Status files"      5 osd-cvs-file-menu-cmd
        osd .osd.tmp 54 "" "&Add files"         6 osd-cvs-file-menu-cmd
        osd .osd.tmp 54 "" "&Add binary files"  9 osd-cvs-file-menu-cmd
        osd .osd.tmp 55 "" "Remove files"       7 osd-cvs-file-menu-cmd
    !else
        osd .osd.tmp 10 "" "Update files"
        osd .osd.tmp 20 "" "Commit files"
        osd .osd.tmp 30 "" "Diff files"
        osd .osd.tmp 40 "" "Log files"
        osd .osd.tmp 50 "" "Status files"
        osd .osd.tmp 54 "" "Add files"
        osd .osd.tmp 55 "" "Remove files"
    !endif
    osd .osd.tmp 5  "" "Check&out files"        8 osd-cvs-file-menu-cmd
    osd .osd.tmp 30 "" "&Diff files"            3 osd-cvs-file-menu-cmd
    osd .osd.tmp 60 "-"
    !if &equ :tag-count 1
        osd .osd.tmp 70 "" "&Graphical diff"    101 osd-cvs-file-menu-cmd
        osd .osd.tmp 80 "" "&Resolve conflicts" 102 osd-cvs-file-menu-cmd
    !else
        osd .osd.tmp 70 "" "Graphical diff"
        osd .osd.tmp 80 "" "Resolve conflicts"
    !endif
    osd .osd.tmp  90 "-"
    osd .osd.tmp 100 "" "Clear c&vs console"    100 osd-cvs-file-menu-cmd
!emacro

define-macro cvs
    set-variable .fhook-dirlst.file-process "cvs-dir-conv"
    osd .osd.dirlst  11 "Md" "C&VS" .osd.tmp osd-cvs-file-menu
    delete-other-windows
    2 split-window-vertically
    15 resize-window-vertically
    find-buffer "*cvs-console*"
    buffer-bind-create "bio" "delete" "" file-browser-close
    previous-window
    file-browser-init
!emacro

