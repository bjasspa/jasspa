;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;
; -!- emf -!-
; This is part of the JASSPA MicroEmacs macro files
; Copyright (C) 1999-2009 JASSPA (www.jasspa.com)
; See the file me.emf for copying and conditions.
;
; Created:    Tue Feb 22 2005
; Author:     Dr. Detlef Groth
; Synopsis:   R/S-Plus file hook (http://www.r-project.org)
;
set-char-mask "3" "-$."

define-macro fhook-r
  set-variable $buffer-mask "luh13"
  @# buffer-init "r"
  buffer-init-hooks
!emacro

; buffer-init variables
set-variable .fhook-r.name "R/S-Plus"
set-variable .fhook-r.setup &reg "/history/fhook/r" "dfghopx"
set-variable .fhook-r.setup-mask "adefghikmoptux"
set-variable .fhook-r.comment "|#||#|# | #|r|"

set-variable .fhook-r.collapse-open  "^['\\w]+[ \t]+<-[ \t]+function[ \t]*("
set-variable .fhook-r.collapse-close "^}"
set-variable .fhook-r.collapse-mclose "1"
set-variable .fhook-r.collapse-mnext "-1"

; setup item-list
set-variable .fhook-r.item-list-s1 "^[ \t]*\\(\\w+\\)[ \t]+<-[ \t]+function[ \t]*("
set-variable .fhook-r.item-list-r1 "Func \ecB\\1\ecA"
set-variable .fhook-r.item-list-s2 "^#'\\s+##\\s+\\([a-zA-Z0-9.\\s\\$]*\\) *\$"
set-variable .fhook-r.item-list-r2 "MDHE \ecB\\1\ecA"
set-variable .fhook-r.item-list-s3 "^\\s*\\('[%_>a-zA-Z0-9.\\$]+'\\) [-=<]+ function\\s *("
set-variable .fhook-r.item-list-r3 "Oper \ecB\\1\ecA"

!iif &not &exist .hilight.r  set-variable .hilight.r &pinc .hilight.next 1
!if &and &sin "h" .fhook-r.setup &band .hilight.flags 0x02
  ; High-light R Mode
  0 hilight .hilight.r 0              $global-scheme
  hilight .hilight.r 0 "^\\s *\\{#.*" .scheme.comment
  hilight .hilight.r 0 "^#.*"         .scheme.comment   
  hilight .hilight.r 2 "\\s\\{#"      .scheme.comment
  hilight .hilight.r 2 "^#!"          .scheme.header
  hilight .hilight.r 4 "\"" "\"" "\\" .scheme.string
  hilight .hilight.r 4 "'" "'" "\\"   .scheme.string    
  !if &band .hilight.flags 0x04
    hilight .hilight.r 1 "\\w+\\s+<-\\s+function"   .scheme.function
  !endif
  hilight .hilight.r 2 "^library"     .scheme.prepro
  hilight .hilight.r 1 "abline"       .scheme.keyword
  hilight .hilight.r 1 "abs"          .scheme.keyword    
  hilight .hilight.r 1 "all"          .scheme.keyword
  hilight .hilight.r 1 "any"          .scheme.keyword    
  hilight .hilight.r 1 "apply"        .scheme.keyword
  hilight .hilight.r 1 "array"        .scheme.keyword
  hilight .hilight.r 1 "as.data.frame" .scheme.keyword    
  hilight .hilight.r 1 "as.matrix"    .scheme.keyword        
  hilight .hilight.r 1 "attach"       .scheme.keyword
  hilight .hilight.r 1 "axis"         .scheme.keyword
  hilight .hilight.r 1 "barplot"      .scheme.keyword
  hilight .hilight.r 1 "boxplot"      .scheme.keyword
  hilight .hilight.r 1 "break"        .scheme.keyword
  hilight .hilight.r 1 "cat"          .scheme.keyword
  hilight .hilight.r 1 "cbind"        .scheme.keyword
  hilight .hilight.r 1 "coef"         .scheme.keyword
  hilight .hilight.r 1 "colnames"     .scheme.keyword
  hilight .hilight.r 1 "contour"      .scheme.keyword
  hilight .hilight.r 1 "coplot"       .scheme.keyword
  hilight .hilight.r 1 "cor"          .scheme.keyword
  hilight .hilight.r 1 "commandArgs"  .scheme.keyword
  hilight .hilight.r 1 "crossprod"    .scheme.keyword
  hilight .hilight.r 1 "curve"        .scheme.keyword
  hilight .hilight.r 1 "data"         .scheme.keyword
  hilight .hilight.r 1 "data.frame"   .scheme.keyword
  hilight .hilight.r 1 "density"      .scheme.keyword
  hilight .hilight.r 1 "detach"       .scheme.keyword
  hilight .hilight.r 1 "dim"          .scheme.keyword
  hilight .hilight.r 1 "dirname"      .scheme.keyword
  hilight .hilight.r 1 "dotchart"     .scheme.keyword
  hilight .hilight.r 1 "edit"         .scheme.keyword
  hilight .hilight.r 1 "eigen"        .scheme.keyword
  hilight .hilight.r 1 "eval"         .scheme.keyword
  hilight .hilight.r 1 "else"         .scheme.keyword
  hilight .hilight.r 1 "file.path"    .scheme.keyword
  hilight .hilight.r 1 "file.exists"  .scheme.keyword
  hilight .hilight.r 1 "for"          .scheme.keyword
  hilight .hilight.r 1 "function"     .scheme.function
  hilight .hilight.r 1 "grep"         .scheme.keyword
  hilight .hilight.r 1 "grepl"        .scheme.keyword
  hilight .hilight.r 1 "grid"         .scheme.keyword
  hilight .hilight.r 1 "gsub"         .scheme.keyword
  hilight .hilight.r 1 "hist"         .scheme.keyword
  hilight .hilight.r 1 "if"           .scheme.keyword
  hilight .hilight.r 1 "image"        .scheme.keyword
  hilight .hilight.r 1 "intersect"    .scheme.keyword
  hilight .hilight.r 1 "lapply"       .scheme.keyword
  hilight .hilight.r 1 "legend"       .scheme.keyword
  hilight .hilight.r 1 "length"       .scheme.keyword
  hilight .hilight.r 1 "levels"       .scheme.keyword
  hilight .hilight.r 1 "lines"        .scheme.keyword
  hilight .hilight.r 1 "list"         .scheme.keyword
  hilight .hilight.r 1 "lm"           .scheme.keyword
  hilight .hilight.r 1 "load"         .scheme.keyword
  hilight .hilight.r 1 "log"          .scheme.keyword
  hilight .hilight.r 1 "log2"         .scheme.keyword
  hilight .hilight.r 1 "log10"        .scheme.keyword    
  hilight .hilight.r 1 "loadhistory"  .scheme.keyword
  hilight .hilight.r 1 "lsfit"        .scheme.keyword
  hilight .hilight.r 1 "matrix"       .scheme.keyword
  hilight .hilight.r 1 "max"          .scheme.keyword
  hilight .hilight.r 1 "median"       .scheme.keyword
  hilight .hilight.r 1 "min"          .scheme.keyword
  hilight .hilight.r 1 "mtext"        .scheme.keyword
  hilight .hilight.r 1 "ncol"         .scheme.keyword    
  hilight .hilight.r 1 "next"         .scheme.keyword
  hilight .hilight.r 1 "new.env"      .scheme.keyword
  hilight .hilight.r 1 "nrow"         .scheme.keyword
  hilight .hilight.r 1 "options"      .scheme.keyword    
  hilight .hilight.r 1 "outer"        .scheme.keyword
  hilight .hilight.r 1 "pairs"        .scheme.keyword
  hilight .hilight.r 1 "panel"        .scheme.keyword
  hilight .hilight.r 1 "par"          .scheme.keyword
  hilight .hilight.r 1 "parse"        .scheme.keyword
  hilight .hilight.r 1 "paste"        .scheme.keyword
  hilight .hilight.r 1 "persp"        .scheme.keyword
  hilight .hilight.r 1 "plot"         .scheme.keyword
  hilight .hilight.r 1 "points"       .scheme.keyword
  hilight .hilight.r 1 "predict"      .scheme.keyword
  hilight .hilight.r 1 "prcomp"       .scheme.keyword
  hilight .hilight.r 1 "prod"         .scheme.keyword
  hilight .hilight.r 1 "print"        .scheme.keyword
  hilight .hilight.r 1 "qqnorm"       .scheme.keyword
  hilight .hilight.r 1 "qqline"       .scheme.keyword
  hilight .hilight.r 1 "qqplot"       .scheme.keyword
  hilight .hilight.r 1 "qr"           .scheme.keyword
  hilight .hilight.r 1 "read.table"   .scheme.keyword
  hilight .hilight.r 1 "readRDS"      .scheme.keyword
  hilight .hilight.r 1 "repeat"       .scheme.keyword
  hilight .hilight.r 1 "require"      .scheme.keyword
  hilight .hilight.r 1 "rm"           .scheme.keyword
  hilight .hilight.r 1 "return"       .scheme.keyword
  hilight .hilight.r 1 "rbind"        .scheme.keyword
  hilight .hilight.r 1 "rep"          .scheme.keyword
  hilight .hilight.r 1 "round"        .scheme.keyword
  hilight .hilight.r 1 "rownames"     .scheme.keyword
  hilight .hilight.r 1 "save"         .scheme.keyword
  hilight .hilight.r 1 "save.image"   .scheme.keyword
  hilight .hilight.r 1 "savehistory"  .scheme.keyword
  hilight .hilight.r 1 "saveRDS"      .scheme.keyword
  hilight .hilight.r 1 "scan"         .scheme.keyword
  hilight .hilight.r 1 "scale"        .scheme.keyword
  hilight .hilight.r 1 "seq"          .scheme.keyword
  hilight .hilight.r 1 "setdiff"      .scheme.keyword
  hilight .hilight.r 1 "solve"        .scheme.keyword
  hilight .hilight.r 1 "source"       .scheme.keyword
  hilight .hilight.r 1 "sub"          .scheme.keyword    
  hilight .hilight.r 1 "substr"       .scheme.keyword    
  hilight .hilight.r 1 "summary"      .scheme.keyword
  hilight .hilight.r 1 "table"        .scheme.keyword
  hilight .hilight.r 1 "tapply"       .scheme.keyword
  hilight .hilight.r 1 "title"        .scheme.keyword
  hilight .hilight.r 1 "unlist"       .scheme.keyword
  hilight .hilight.r 1 "union"        .scheme.keyword
  hilight .hilight.r 1 "unique"       .scheme.keyword    
  hilight .hilight.r 1 "var"          .scheme.keyword
  hilight .hilight.r 1 "which"        .scheme.keyword
  hilight .hilight.r 1 "while"        .scheme.keyword
  
  ; tests
  hilight .hilight.r 1 "aov"          .scheme.keyword
  hilight .hilight.r 1 "chisq.test"   .scheme.keyword
  hilight .hilight.r 1 "cor.test"     .scheme.keyword
  hilight .hilight.r 1 "fisher.test"  .scheme.keyword
  hilight .hilight.r 1 "ks.test"      .scheme.keyword
  hilight .hilight.r 1 "pairwise.prop.test" .scheme.keyword
  hilight .hilight.r 1 "pairwise.t.test" .scheme.keyword
  hilight .hilight.r 1 "pairwise.wilcox.test" .scheme.keyword
  hilight .hilight.r 1 "prop.test"    .scheme.keyword
  hilight .hilight.r 1 "t.test"       .scheme.keyword
  hilight .hilight.r 1 "var.test"     .scheme.keyword
  hilight .hilight.r 1 "wilcox.test"  .scheme.keyword
  ; operators
  hilight .hilight.r 0 "&"            .scheme.operator
  hilight .hilight.r 0 "|"            .scheme.operator
  hilight .hilight.r 0 "|>"           .scheme.operator  
  hilight .hilight.r 0 "%in%"         .scheme.operator    
  hilight .hilight.r 0 "%ni%"         .scheme.operator        
  hilight .hilight.r 1 "FALSE"        .scheme.prepro
  hilight .hilight.r 1 "NA"           .scheme.prepro
  hilight .hilight.r 1 "TRUE"         .scheme.prepro
  hilight .hilight.r 1 "NULL"         .scheme.prepro  
  ; sql stuff
  hilight .hilight.r 1 "dbSendQuery"  .scheme.keyword
  hilight .hilight.r 1 "dbDriver"     .scheme.keyword
  hilight .hilight.r 1 "dbConnect"    .scheme.keyword
  hilight .hilight.r 1 "fetch"        .scheme.keyword
!endif
!if &sin "d" .fhook-r.setup
  0 indent  .hilight.r 0 10
  indent .hilight.r n "{"  t
  indent .hilight.r o "}" -t
  indent .hilight.r e "\"" "\"" "\\"  
  indent .hilight.r b "(" ")"
  indent .hilight.r c "\\\\\\s " 3/2t
  indent .hilight.r i "#"
!endif

define-macro fhook-r-doc
  buffer-init-hooks
!emacro

set-variable .fhook-r-doc.name "R Doc"
set-variable .fhook-r-doc.setup &reg "/history/fhook/r-doc" "acfhmw"
set-variable .fhook-r-doc.setup-mask "acfhmw"

!if &and &sin "h" .fhook-r-doc.setup &band .hilight.flags 0x02
  !iif &not &exi .hilight.r-doc  set-variable .hilight.r-doc &pinc .hilight.next 1
  !iif &not &exi .hilight.r-docL  set-variable .hilight.r-docL &pinc .hilight.next 1  
  0 hilight .hilight.r-doc 2 200               $global-scheme
  0 hilight .hilight.r-docL  0                 $global-scheme
  hilight .hilight.r-doc     2 "^_"            .scheme.header
  hilight .hilight.r-doc  0x42 "\ecE" ""       .scheme.header
  hilight .hilight.r-doc  0x42 "\ecL" ""       .scheme.link
  hilight .hilight.r-doc  0x40 "\esA" ""       $global-scheme
  hilight .hilight.r-doc  0xc0 "\esR" ""       .hilight.r .scheme.header
  hilight .hilight.r-doc  0xc0 "\esL" ""       .hilight.r-docL .scheme.header  
  hilight .hilight.r-doc     0 "^\\a[-A-Za-z0-9.$_]*\\}[\t ][\t ]" .scheme.link  
  hilight .hilight.r-doc     0 "^\\a[-A-Za-z0-9.$_]*::[-A-Za-z0-9.$_]+" .scheme.link    
  hilight .hilight.r-doc     0 "package:\\a[-A-Za-z0-9.$_]*" .scheme.link    
  hilight .hilight.r-docL    4 "'\\{\\a" "\\}'" "\\"          .scheme.link
  hilight .hilight.r-docL 0x80 "^[A-Z].+"      .hilight.r-doc .scheme.header
  hilight .hilight.r-docL 0xc0 "\esA" ""       .hilight.r-doc .scheme.header
  hilight .hilight.r      0xc0 "\esA" ""       .hilight.r-doc .scheme.header
!endif

!if &sin "f" .fhook-r-doc.setup
  ; setup emf collapsing
  set-variable .fhook-r-doc.collapse-open  "^[A-Z]"
  set-variable .fhook-r-doc.collapse-close "^\\([A-Z]\\|\\'\\)"
  set-variable .fhook-r-doc.collapse-mnext "-1"
!endif

define-macro r-doc
  !force set-variable #l0 @1
  !if &not $status
    set-variable #l0 @ml00 "R help on"
  !endif
  set-variable #l0 &rep #l0 "::" ":"
  set-variable #l5 &rep #l0 "$" "_"  
  ml-write &cat "fetching ... " #l5
  ; sort out back history
  !if &set #l2 &exi ":*r-doc*:"
    set-position "\x81"
    !force 0 delete-buffer "*r-doc*9"
    set-variable #l1 8
    !repeat
      !if &exi &spr ":*r-doc*%d:" #l1
        find-buffer &spr "*r-doc*%d" #l1
        change-buffer-name &spr "*r-doc*%d" &add #l1 1
      !endif
    !until &not &dec #l1 1
    find-buffer "*r-doc*"
    1 buffer-mode "hide"
    change-buffer-name "*r-doc*1"
    goto-position "\x81"
  !endif
  !if &not &set #l3 &xse #l5 "\\(.+\\):\\(.+\\)" 
    set-variable #l0 &spr "help('%s')" #l5
    set-variable #l4 ""
  !elif &set #l3 &lfin "|library|package|" @s1
    set-variable #l0 &spr "library(help='%s')" &set #l5 &set #l4 @s2
  !else
    set-variable #l0 &spr "help('%s',package='%s')" &set #l5 @s2 &set #l4 @s1
  !endif
*try_again
  &con &seq $buffer-bname "*r-doc*1" 2 0 pipe-shell-command &spr "Rscript -e \"options(useFancyQuotes=FALSE);%s\"" #l0 "*r-doc*"
  !iif &not &seq $buffer-bname "*r-doc*" find-buffer "*r-doc*"
  -1 buffer-mode "view"
  1 buffer-mode "magic"
  beginning-of-buffer
  set-mark
  3 forward-line
  !if &seq @wl "During startup - Warning messages:"
    forward-line
    !while &xseq @wl "\\d+: Setting LC_\\S+ failed.*"
      forward-line
    !done
  !endif
  ; if 3 or less lines then most likely a look-up failure - try removing the package as it can be wrong (e.g. ref to 'cutree' in cluster:diana) or do a search
  !if &gre $window-eline &add $window-line 3
  !elif &and &not #l3 &len #l4
    set-variable #l0 &spr "help('%s')" #l5
    set-variable #l4 ""
    !goto try_again
  !elif &not &seq &lef #l0 11 "help.search"
    set-variable #l0 &spr "help.search('%s')" #l5
    set-variable #l3 1
    set-variable #l4 ""
    !goto try_again
  !endif
  -1 kill-region
  set-variable :cl #l0
  !if &set :package-overview #l3
    !iif &seq @wl ""  62 insert-space
  !elif &len #l4
  !elif &xse @wl ".*?\\<package:\\(\\w+\\).*"
    set-variable #l4 @s1
  !endif
  set-variable :package #l4
  set-variable @wl &cat &rep @wl " R Documentation" " R Doc  " "    \ecLBACK"
  buffer-is-utf8 #l1
  !iif #l1  4 change-buffer-charset "utf8" "display"
  replace-string "^_\b\\(\\w.*:\\)$" "sAcE\\1"
  set-variable #l1 &lget $result 1
  beginning-of-buffer
  replace-string "_\b" "" 
  !if &not #l1
    beginning-of-buffer
    !force replace-string "^\n\\([ \t]+['\\w()]+\\( ['\\w()]+\\)*\n\\)$" "\nsAcE\\1"
    beginning-of-buffer
    !force replace-string "^\n\\(\\w['\\w()]+\\( ['\\w()]+\\)*:\n\\)$" "\nsAcE\\1"
  !endif
  set-variable #l1 &cond &bmod "exact" 1 -1
  -1 buffer-mode "exact"
  beginning-of-buffer
  replace-string "^sAcE\\(Usage\\|Examples\\):.*\n" "\\0sR"
  beginning-of-buffer
  replace-string "^sAcE\\(See Also\\|Methods\\):.*\n" "\\0sL"
  beginning-of-buffer
  #l1 buffer-mode "exact"
  -1 buffer-mode "edit"
  1 buffer-mode "view"
  set-variable :mouse-word-select r-doc-process-link
  buffer-bind-create "bio" "return" "0" r-doc-process-link
  buffer-bind-create "bio" "backspace" "0" r-doc-back
  buffer-bind-key  "scroll-down" "space"
  buffer-init "r-doc"
  ; some terminals might need this
  !iif &band $system 0x01  screen-update
!emacro    

0 define-macro r-doc-back
  !iif &not &exi ":*r-doc*1:"  -8 ml-write "[Error: No more history!]"
  !force 0 delete-buffer "*r-doc*"
  find-buffer "*r-doc*1"
  -1 buffer-mode "hide"
  change-buffer-name "*r-doc*"
  set-variable #l1 1
  !while &exi &spr ":*r-doc*%d:" &inc #l1 1
    find-buffer &spr "*r-doc*%d" #l1
    change-buffer-name &spr "*r-doc*%d" &sub #l1 1
  !done
  find-buffer "*r-doc*"
!emacro

0 define-macro r-doc-process-link
  set-position "\x80"
  !if &equ $window-line 1
    r-doc-process-header-link
    !return
  !elif :package-overview
    r-doc-process-package-link
    !return
  !endif
  ; check first for a beginning of line link in package index
  !force search-buffer "meb" "^s\\(.\\)"
  !iif &not &and $status &seq @s1 "L"  -8 ml-write "Error: Not in the Method or See Also section."
  goto-position "\x80"
  set-variable #l1 $window-col
  !force -1 search-buffer "meb" "'\\m"
  !if $status
    forward-char
    set-variable #l2 $window-col
    !force -1 search-buffer "Me" "'"
    !if &not $status
    !elif &gre $window-col #l1
      backward-char
      set-mark
      set-variable $window-col #l2 
      copy-region
      set-variable #l6 @y
      -1 yank
      goto-position "\x80"
      !if &seq :package ""
        r-doc #l6
      !else
        r-doc &spr "%s::%s" :package #l6
      !endif
      !return
    !endif
  !endif
  goto-position "\x80"
  -8 ml-write "Error: Not in a Method or See Also section link."
!emacro

0 define-macro r-doc-process-header-link
  ; click on header package:pkgname or BACK
  !force search-buffer "meb" "[ \t]"
  !if &not $status
  !elif &seq &mid @wl &set #l0 &add $window-col 1 7 "cLBACK"
    goto-position "\x80"
    r-doc-back
    !return
  !elif &seq &mid @wl #l0 8 "package:"
    goto-position "\x80"
    search-buffer "me" "\\s"
    backward-char
    set-variable #l6 &mid @wl #l0 &sub $window-col #l0
    goto-position "\x80"
    r-doc #l6
    !return
  !endif
  goto-position "\x80"
  -8 ml-write "Error: Not in a header link."
!emacro

0 define-macro r-doc-process-package-link
  ; fetch links on the index part at the beginning of a line
  set-variable #l0 $window-col
  beginning-of-line
  search-buffer "me" "\\s"
  backward-char
  !if &gre $window-col #l0
    set-variable #l6 &lef @wl $window-col
    goto-position "\x80"
    !if &seq :package ""
      r-doc #l6
    !else
      r-doc &spr "%s::%s" :package #l6
    !endif
    !return
  !endif
  goto-position "\x80"
  -8 ml-write "Error: Not in an index link."
!emacro    

buffer-init-fhook "r"
