; -!- emf -!-
; This is part of the JASSPA MicroEmacs macro files
; Copyright (C) 2001-2009 JASSPA (www.jasspa.com)
; See the file me.emf for copying and conditions.
;
; Created:     Sat Sep 15 2001
; Synopsis:    Language setup routines
; Authors:     Steven Phillips
;
; Notes:
;     Originally part of language.emf but broken out to keep language.emf
;     clean and speed up start-up
;
0 define-macro spell-rules-init
  !if &not &exi .spell.language
    !if &len &set #l0 &reg "/history/language" ""
      set-variable #l0 &spr "  %s language not yet supported.  " #l0
    !else
      set-variable #l0 "  Lanuage has not yet been set.\n\n  See Start-Up tab of user-setup.  "
    !endif
    osd-dialog "Spelling Checker" #l0 "  \HOk  "
    !abort
  !endif
  !force execute-file &cat "lsr" .spell.language
  !if &not $status
    !force osd-pinstall &cat "ls" .spell.language &cat &set #l1 &lget .spell.lang-name &lfin .spell.lang-id .spell.language " Language"
    !force execute-file &cat "lsr" .spell.language
    !if &not $status
      !if @#
        set-variable #l0 &spr "  Rules and dictionaries for %s language  \n     are not available, please download." #l1
        3 osd-dialog "Spelling Checker" #l0 8 5 "  \HOk  "
      !endif
      !abort
    !endif
  !endif
  ; Add the Main and Personal dictionaries
  add-dictionary &cat "lsm" .spell.language
  add-dictionary &cat "lsp" .spell.language
!emacro
