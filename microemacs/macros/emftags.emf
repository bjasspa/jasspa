; -!- emf -!-
;
; Copyright (C) 1998-2024 JASSPA (www.jasspa.com)
;
; This is part of JASSPA's MicroEmacs, see the LICENSE file for licensing and
; copying information.
;
; Synopsis:    Command-line macro for creating a macros (emf) tags file
; Authors:     Steven Phillips
;
; Notes:       See gentagsc.emf for usage & info
;
set-variable %tag-id "emf"
set-variable %tag-filemask ":*.emf:"
!iif &not &exi gentags  execute-file "gentagsc"
set-char-mask "2" "-$&#!%:@"

define-macro emftags-add-file
  set-variable $buffer-mask "luh12"
  set-variable #l9 $buffer-fname
  !iif &seq &lef #l9 %tag-plen %tag-path  set-variable #l9 &rig #l9 %tag-plen
  ml-write &spr "[Parsing file %s]" #l9
  1 buffer-mode "magic"
  beginning-of-buffer
  !repeat
    !force search-buffer "me" "^[ \t]*[01]?[ \t]*define-macro[ \t]\\(\\w+\\)"
    !iif &not $status !break
    gentags-add-tag @s1
  !done
  !if &sin "p" %tag-option
    beginning-of-buffer
    !repeat
      !force search-forward "^[ \t]*[01]?[ \t]*define-macro-file[ \t]"
      !if $status
        set-variable #l0 $window-line
        !force 2 forward-word
        !if $status
          !iif &not &equ #l0 $window-line  backward-word
        !endif
        !repeat
          backward-word
          set-mark
          forward-word
          copy-region
          set-variable #l1 @y
          -1 yank
          gentags-add-tag #l1
          !force forward-word
        !until &gre $window-line #l0
        beginning-of-line
      !endif
    !until &not $status
  !endif
!emacro

; load in user extensions if found
!force execute-file "myemftags"
