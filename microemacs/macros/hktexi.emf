;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;
;
;  Created By    : Jon Green
;  Created       : Mon Jul 12 21:29:44 1999
;  Last Modified : <000719.0012>
;
;  Description   : Quick hilight for texinfo files. 
;
;  Notes         : Could benifit with some tools.
; 
;  History
;
;  Copyright (c) 1999 JASSPA
;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;
; buffer-setup flags
set-variable #l0 &reg "/history" "fhook/texi" "bhix"
set-variable #l1 "abehikmtux"

!if &and &sin "h" #l0 &band .hilight.flags 0x02 
    !if &not &exist .hilight.texi
        set-variable .hilight.texi  &pinc .hilight.next 1
    !endif
    0 hilight .hilight.texi 0                  $global-scheme
    hilight .hilight.texi   0  "@c .*"         .scheme.comment
    hilight .hilight.texi   4  "@" "\\}\\W" "" .scheme.keyword
!endif

define-macro fhook-texi
    buffer-modes-init .fhook-texi.setup
    !if &and &sin "h" .fhook-texi.setup &band .hilight.flags 0x02 
        set-variable $buffer-hilight .hilight.texi
    !endif
    !if &sin "b" .fhook-texi.setup
        buffer-abbrev-file "texi"
    !endif
    ; execute user extensions if enabled
    !if &exist my-fhook-texi
        my-fhook-texi
    !endif
!emacro
set-variable .fhook-texi.setup #l0
set-variable .fhook-texi.setup-mask #l1

ml-write "[texinfo file hook loaded]"

; load in user extensions if found
!force execute-file "mytexi"

