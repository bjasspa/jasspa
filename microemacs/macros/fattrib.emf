; -!- emf -!-
; This is part of the JASSPA MicroEmacs macro files
; Copyright (C) 1997-2004 JASSPA (www.jasspa.com)
; See the file me.emf for copying and conditions.
;
; Synopsis:    OSD based file system file attribute setter.
; Authors:     Steven Phillips
;
!if &seq .osd.fattr "ERROR"
    set-variable .osd.fattr &pinc .osd.next 1
!endif

0 define-macro fattr-type
    !if &les @# 0
        ; set the mode
        !if &equ @# -3
            1 buffer-mode "crlf"
            1 buffer-mode "ctrlz"
        !elif &equ @# -2
            1 buffer-mode "crlf"
            -1 buffer-mode "ctrlz"
        !else
            -1 buffer-mode "crlf"
            -1 buffer-mode "ctrlz"
        !endif
    !else
        !if &bmod "crlf"
            !if &bmod "ctrlz"
                set-variable #l1 "3"
            !else
                set-variable #l1 "2"
            !endif
        !else
            !if &bmod "ctrlz"
                set-variable #l1 "0"
            !else
                set-variable #l1 "1"
            !endif
        !endif
        !if &not &equ @# #l1
            !abort
        !endif
    !endif
!emacro

-1 osd .osd.fattr
osd .osd.fattr 0  "batcdHs" 10 3 40 0 -1 -1 610 .scheme.osd-title "File attributes"
osd .osd.fattr 10  ""
osd .osd.fattr 40  ""
osd .osd.fattr 50  "" "  File Type : "
osd .osd.fattr 55  "fh" "      "
osd .osd.fattr 60  "CxfRpht" &cat .osd.checkbox-chars "\} Unix"       1 fattr-type
osd .osd.fattr 65  "fh" "  "
osd .osd.fattr 70  "CxfRpht" &cat .osd.checkbox-chars "\} Windows"       2 fattr-type
osd .osd.fattr 75  "fh" "  "
osd .osd.fattr 80  "CxfRpt" &cat .osd.checkbox-chars "\} Dos"           3 fattr-type
osd .osd.fattr 90  ""
osd .osd.fattr 100 ""

0 define-macro fattr-attr
    !if &les @# 0
        ; set the mode
        set-variable $buffer-fmod &bxor $buffer-fmod &abs @#
    !elif &not &band $buffer-fmod @#
        !abort
    !endif
!emacro

!if &seq $platform "dos"
    osd .osd.fattr 110 ""   "  Attributes:"
    osd .osd.fattr 115 "fh" "      "
    osd .osd.fattr 120 "CfxRpt" &cat .osd.checkbox-chars "\} Read-only"  1  fattr-attr
    osd .osd.fattr 125 "fh" "      "
    osd .osd.fattr 130 "CfxRpt" &cat .osd.checkbox-chars "\} Hidden   "  2  fattr-attr
    osd .osd.fattr 135 "fh" "      "
    osd .osd.fattr 140 "CfxRpt" &cat .osd.checkbox-chars "\} System   "  4  fattr-attr
    osd .osd.fattr 145 "fh" "      "
    osd .osd.fattr 150 "SCfxRp" &cat .osd.checkbox-chars "\} Directory"  16 fattr-attr
    osd .osd.fattr 155 "fh" "      "
    osd .osd.fattr 160 "CfxRpt" &cat .osd.checkbox-chars "\} Archive  "  32 fattr-attr
!elif &seq $platform "win32"
    osd .osd.fattr 110 "" "  Attributes:"
    osd .osd.fattr 115 "fh" "      "
    osd .osd.fattr 120 "CfxRpt" &cat .osd.checkbox-chars "\} Read-only "    1 fattr-attr
    osd .osd.fattr 125 "fh" "      "
    osd .osd.fattr 130 "CfxRpt" &cat .osd.checkbox-chars "\} Hidden    "    2 fattr-attr
    osd .osd.fattr 135 "fh" "      "
    osd .osd.fattr 140 "CfxRpt" &cat .osd.checkbox-chars "\} System    "    4 fattr-attr
    osd .osd.fattr 145 "fh" "      "
    osd .osd.fattr 150 "SCfxRp" &cat .osd.checkbox-chars "\} Directory "   16 fattr-attr
    osd .osd.fattr 155 "fh" "      "
    osd .osd.fattr 160 "CfxRpt" &cat .osd.checkbox-chars "\} Archive   "   32 fattr-attr
    osd .osd.fattr 165 "fh" "      "
    osd .osd.fattr 170 "CfxRpt" &cat .osd.checkbox-chars "\} Normal    "  128 fattr-attr
    osd .osd.fattr 175 "fh" "      "
    osd .osd.fattr 180 "CfxRpt" &cat .osd.checkbox-chars "\} Temporary "  256 fattr-attr
    osd .osd.fattr 185 "fh" "      "
    osd .osd.fattr 190 "CfxRpt" &cat .osd.checkbox-chars "\} Compressed" 2048 fattr-attr
!else
    osd .osd.fattr 110 "c" "File Permissions"
    osd .osd.fattr 120 ""
    osd .osd.fattr 130 ""  "  User:"
    osd .osd.fattr 135 "hf" "      "
    osd .osd.fattr 140 "CfxRpth" &cat .osd.checkbox-chars "\} Read  "  256 fattr-attr
    osd .osd.fattr 150 "CfxRpth" &cat .osd.checkbox-chars "\} Write  " 128 fattr-attr
    osd .osd.fattr 160 "CfxRpt"  &cat .osd.checkbox-chars "\} Exec"     64 fattr-attr
    osd .osd.fattr 170 ""
    osd .osd.fattr 180 "" "  Group:"
    osd .osd.fattr 185 "hf" "      "
    osd .osd.fattr 190 "CfxRpth" &cat .osd.checkbox-chars "\} Read  "  32 fattr-attr
    osd .osd.fattr 200 "CfxRpth" &cat .osd.checkbox-chars "\} Write  " 16 fattr-attr
    osd .osd.fattr 210 "CfxRpt"  &cat .osd.checkbox-chars "\} Exec"     8 fattr-attr
    osd .osd.fattr 220 ""
    osd .osd.fattr 230 "" "  Global:"
    osd .osd.fattr 235 "hf" "      "
    osd .osd.fattr 240 "CfxRpth" &cat .osd.checkbox-chars "\} Read  "  4 fattr-attr
    osd .osd.fattr 250 "CfxRpth" &cat .osd.checkbox-chars "\} Write  " 2 fattr-attr
    osd .osd.fattr 260 "CfxRpt"  &cat .osd.checkbox-chars "\} Exec"    1 fattr-attr
!endif

osd .osd.fattr 590 ""
osd .osd.fattr 595 ""
osd .osd.fattr 600 "BHcftxh" .scheme.osd-ebtt " \HSave changes " 0 save-buffer
osd .osd.fattr 610 "BHcft"   .scheme.osd-ebtt "  \HOK  "         1 void

define-macro file-attrib
    ; correct the main osd entry now we know the menu number
    !force osd 1 80 "" "Att\Hributes" .osd.fattr file-attrib
    osd .osd.fattr 20  "" &cat "  Buffer : " $buffer-bname 
    !if &gre &set #l1 &len &set #l0 $buffer-fname 70
        set-variable #l0 &cat "..." &rig #l0 &sub #l1 67
    !endif
    osd .osd.fattr 30  "" &spr "    %s  " #l0
    .osd.fattr osd
!emacro



