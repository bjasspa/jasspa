; -!- emf -!-
; This is part of the JASSPA MicroEmacs macro files
; Copyright (C) 2024 JASSPA (www.jasspa.com)
; See the file me.emf for copying and conditions.
;
; Synopsis:    Macros useful for Markdown
; Authors:     Detlef Groth & Steven Phillips
;

0 define-macro md-export-setup
  !if &exi %md-export-com
    !iif &or &sin "pandoc" %md-export-com &sin "lowdown" %md-export-com !return
    -24 ml-write &cat "Error: Cannot determine whether MD export tool is pandoc or lowdown:" %md-export-com
  !endif
  set-variable #l0 0
  !while &not &seq "" &set #l1 &lget "|pandoc|lowdown|" &inc #l0 1
    !if &not &seq "ERROR" &which #l1
      set-variable %md-export-com #l1
      ml-write &spr "MD %s executable found in path" #l1
      !return
    !endif
  !done
  -24 ml-write "Error: MD pandoc or lowdown export tool not found, please install or set %md-export-com"
!emacro

0 define-macro md-export
  set-variable #l0 @1
  set-variable #l1 @2
  !iif &not &seq $buffer-fhook "fhook-md"  -8 ml-write &spr "Error: '%s' is not a MD file!" $buffer-bname
  !iif &seq $buffer-fname ""  -8 ml-write &spr "Error: Buffer '%s' does not have a file name!" $buffer-bname
  !iif &not &exi %md-export-com  md-export-setup
  !if &not &bmod "edit"
  !elif &band @# 1
    set-variable #l2 @mc5 "Save buffer first (?/y/n) ? " "nNyY" "(Y)es, (N)o, (C-g)Abort ? "
    !tjump &iseq #l2 "y" 2 
  !else
    save-buffer @nma
  !endif
  !if &seq "ERROR" &set #l4 &ind &set #l5 &spr ":md-%s-export-opts" #l0
    set-variable #l4 &set &ind #l5 ""
    set-variable @# &bor @# 2
  !endif
  !iif &band @# 2  set-variable #l4 &set &ind #l5 @ml2 &spr "md-to-%s export options" #l0 #l4
  set-variable #l3 &rig $buffer-fname &rsin "/" $buffer-fname
  set-variable #l2 &con &iseq ".md" &rig #l3 -3 &lef #l3 -3 #l3
  !if &sin "lowdown" %md-export-com
    set-variable #l5 &spr "-t %s -o %s.%s" #l0 #l2 #l1
  !else
    set-variable #l5 &spr "pandoc-options-here" #l0 #l2 #l1
  !endif
  0x02 pipe-shell-command &spr "%s %s %s %s" %md-export-com #l5 #l4 #l3 "*md-export*" @nma
  !if &set #l4 $result
    popup-window "*md-export*"
    -8 ml-write &cat "Warning: MD export command exited with error code " #l4
  !elif &band @# 4
    popup-window "*md-export*"
    find-file &spr "./%s.%s" #l2 #l1 @mna 
  !endif
  ml-write &spr "MD export to %s.%s complete and successful" #l2 #l1
!emacro

define-macro md-to-html
  @# md-export "html" "html"
!emacro

define-macro md-to-htm
  @# md-export "html" "htm"
!emacro

define-macro md-to-latex
  @# md-export "latex" "tex"
!emacro
