; -!- emf -!-
; This is part of the JASSPA MicroEmacs macro files
; Copyright (C) 2000-2002 JASSPA (www.jasspa.com)
; See the file me.emf for copying and conditions.
;
; Created:     Tue Sep 19 2000
; Synopsis:    Generic tags generator
; Authors:     Steven Phillips
;
; Notes:
;      Launchs ME with a tags generation macro file and %tag-option variable
;      setting to generate the tags file, typical command-line is:
;               me "@ntags" "-v%tag-option=mrcsev"
;      Also provides a GUI front end for setting the flags and starting path,
;      the gui can be disabled by passing an argument of -1.
;
!if &not &exi .osd.gtf
    set-variable .osd.gtf &pinc .osd.next 1
!endif

0 define-macro generate-tags-file-entry
    !if &gre @# 0
        set-variable $result &cond &equ @# 1 .generate-tags-file.flags .generate-tags-file.path
    !elif &equ @# -1
        set-variable .generate-tags-file.flags @ml20 "" .generate-tags-file.flags
    !else
        set-variable .generate-tags-file.path @ml21 "" .generate-tags-file.path
    !endif
!emacro

0 define-macro generate-tags-file-flags
    set-variable #l0 &lget "|a|e|m|p|r|s|v|" @#
    !if &sin #l0 .generate-tags-file.flags
        set-variable .generate-tags-file.flags &rep .generate-tags-file.flags #l0 ""
    !else
        set-variable .generate-tags-file.flags &cat .generate-tags-file.flags #l0
    !endif
!emacro

0 define-macro generate-tags-file-exec
    set-position "\x81"
    find-buffer "*generate-tags-file tmp*"
    set-variable $buffer-fname .generate-tags-file.path
    !if &band $system 0x080
        !force exec-me &spr "\"@%s\" \"-v%%tag-option=%s\"" .generate-tags-file.type .generate-tags-file.flags
    !else
        !force exec-me &spr "\"@%s\" \"-v%%%%tag-option=%s\"" .generate-tags-file.type .generate-tags-file.flags
    !endif
    !force delete-buffer "*generate-tags-file tmp*"
    goto-position "\x81"
!emacro

-1 osd .osd.gtf
osd .osd.gtf 0   "batcdHs" 10 3 46 0 -1 -1 250 .scheme.osd-title "Generate Tags File"
osd .osd.gtf 10  ""
osd .osd.gtf 20  "Shf"  "     Tags &Flags : " 4
osd .osd.gtf 30  "ExfH" .scheme.osd-entry "###############" 1 generate-tags-file-entry
osd .osd.gtf 40  "hf"   "    ("
osd .osd.gtf 50  "Rhfx" "&Append"       1 generate-tags-file-flags
osd .osd.gtf 60  "hf"   ", "
osd .osd.gtf 70  "Rhfx" "&Enum-types"   2 generate-tags-file-flags
osd .osd.gtf 80  "hf"   ", "
osd .osd.gtf 90  "Rhfx" "&Multiple"     3 generate-tags-file-flags
osd .osd.gtf 100 "hf"   ", "
osd .osd.gtf 110 "Rhfx" "&Prototypes"   4 generate-tags-file-flags
osd .osd.gtf 120 "f"    ",  "
osd .osd.gtf 130 "hf"   "     "
osd .osd.gtf 140 "Rhfx" "&Recurse"      5 generate-tags-file-flags
osd .osd.gtf 150 "hf"   ", "
osd .osd.gtf 160 "Rhfx" "&Structure"    6 generate-tags-file-flags
osd .osd.gtf 170 "hf"   ", "
osd .osd.gtf 180 "Rhfx" "global &Vars"  7 generate-tags-file-flags
osd .osd.gtf 190 "f"    ")"
osd .osd.gtf 200 ""
osd .osd.gtf 210 "Sf"   "  S&tarting Path :" 230
osd .osd.gtf 220 "hf"   "  "
osd .osd.gtf 230 "ExfH" .scheme.osd-entry "########################################" 2 generate-tags-file-entry
osd .osd.gtf 240 ""
osd .osd.gtf 250 "BhcfHi" .scheme.osd-ebtt " &Generate Tags "    f generate-tags-file-exec
osd .osd.gtf 260 "BcfH"   .scheme.osd-ebtt " E&xit "    1 void

; generate-tags-file "<tagtype>" "[start-path]"
; runs ME @<tagtype> -v%tags-option=...
define-macro generate-tags-file
    !if &exi &ind &spr ".%s.tags" $buffer-fhook
        set-variable .type &ind &spr ".%s.tags" $buffer-fhook
    !else
        !force set-variable .type @1
        !if &not $status
            set-variable .type @ml "tag command"
        !endif
    !endif
    !if &seq &lef .type 1 "!"
        ; direct shell command - run it
        0 ipipe-shell-command &rig .type 1 "*generate-tags*"
        !return
    !endif
    set-variable .path &lef $buffer-fname &rsin "/" $buffer-fname
    !force set-variable #l2 @2
    !if $status
        set-variable .path &stat "a" &cat .path #l2
    !endif
    !if &band @# 1
        ; if bit 1 is set then just do it
        generate-tags-file-exec
    !else
        .osd.gtf osd
    !endif
!emacro
set-variable .generate-tags-file.flags %tag-option

