; -!- emf -!-
;
; Copyright (C) 2020-2025 JASSPA (www.jasspa.com)
;
; This is part of JASSPA's MicroEmacs, see the LICENSE file for licensing and
; copying information.
;
; Synopsis:   Markdown file hook (https://www.markdownguide.org/)
;             Quarkdown support (https://quarkdown.com/)
; Authors:    Detlef Groth, Steven Phillips
;
define-macro fhook-md
  ; quarkdown check
  !if &isin ".qd" $buffer-bname
    @# buffer-init "qd"
    hilight .hilight.md 2 "^\\.doc" .scheme.prepro
    hilight .hilight.md 2 "^\\.theme" .scheme.prepro
    hilight .hilight.md 2 "^\\.var" .scheme.prepro    
    hilight .hilight.md 2 "^\\.tableofcontents" .scheme.prepro        
    hilight .hilight.md 2 "^\\.function"  .scheme.keyword
  !else
    @# buffer-init &con &isin ".rmd" $buffer-bname "rmd" "md"
  !endif
  buffer-init-hooks
!emacro

; buffer-init variables
set-variable .fhook-md.setup &reg "/history/fhook/md" "bfghijnopsx"
set-variable .fhook-md.setup-mask "abefghijkmnopstuwx"
set-variable .fhook-md.comment "|<!---|-->|-|  -- | --||"
set-variable .fhook-md.command-flag  "|thbo|bo|th|th|"
set-variable .fhook-md.command-name  "|md-fill-paragraph|md-fill-paragraph|md-fill-paragraph|md-fill-paragraph|"
set-variable .fhook-md.command-nbind "|||999999|-999999|"
set-variable .fhook-md.command-kbind "|esc q|esc o|||"
set-variable .fhook-md.command-desc  "|\HFill Paragraph||Fill \HAll Paragraphs|All Paragraphs to Li\Hnes|"

; setup item-list
set-variable .fhook-md.item-list-s1 "^# \\(\\w\.+\\)"
set-variable .fhook-md.item-list-r1 "H1 \CCb\\1\CCA"
set-variable .fhook-md.item-list-s2 "^## <a \.+>\\(\.+\\)</a>"
set-variable .fhook-md.item-list-r2 "H2 \CCb\\1\CCA"
set-variable .fhook-md.item-list-s3 "^## \\(\\w\[^<\]\.+\\)"
set-variable .fhook-md.item-list-r3 "H2 \CCb\\1\CCA"

!if &sin "f" .fhook-md.setup
  ; setup emf collapsing
  set-variable .fhook-md.collapse-open  "^\\(# \\w\\|## \\w\\|---\\)"
  set-variable .fhook-md.collapse-close "^\\(# \\w\\|## \\w\\|---\\|\\'\\)"
  set-variable .fhook-md.collapse-mnext "-1"
!endif

!if &and &sin "h" .fhook-md.setup &band .hilight.flags 0x02
  !if &not &exi .hilight.md
    set-variable .hilight.md &pinc .hilight.next 1
    set-variable .hilight.mdE &pinc .hilight.next 1
    set-variable .hilight.mdB &pinc .hilight.next 1
    set-variable .hilight.mdI &pinc .hilight.next 1
    set-variable .hilight.mdBI &pinc .hilight.next 1
    set-variable .hilight.mdH &pinc .hilight.next 1
    set-variable .hilight.mdA &pinc .hilight.next 1
    set-variable .hilight.mdL &pinc .hilight.next 1
    set-variable .hilight.mdU &pinc .hilight.next 1
    set-variable .hilight.mdC &pinc .hilight.next 1
  !endif
  0 hilight .hilight.md 3 100           $global-scheme
  hilight .hilight.md 4 "<!--" "-->" "" .scheme.comment
  hilight .hilight.md 2 "^#"            .scheme.header
  hilight .hilight.md 4 "\"" "\"" "\\"  .scheme.string
  ;hilight .hilight.md 0 "'[^']+'"       .scheme.quote
  hilight .hilight.md 0x890 "\\\\\\}[-[-\\]`*_{}<>()#+.!|]" .hilight.mdE .scheme.hide

  hilight .hilight.md 0x890 "\\*\\}\\S" .hilight.mdI .scheme.hide
  hilight .hilight.md 0x890 "\\W\\{_\\}\\S" .hilight.mdI .scheme.hide
  hilight .hilight.md 0x890 "\\*\\*\\}\\S" .hilight.mdB .scheme.hide
  hilight .hilight.md 0x890 "\\W\\{__\\}\\S" .hilight.mdB .scheme.hide
  hilight .hilight.md 0 "\\*\\*\\*+"    $global-scheme
  hilight .hilight.md 0 "___+"          $global-scheme
  hilight .hilight.md 0x890 "!\\["      .hilight.mdA .scheme.hide
  hilight .hilight.md 0x890 "\\["       .hilight.mdL .scheme.hide

  hilight .hilight.md 0x890 "==\\}\\S"  .hilight.mdH .scheme.hide
  hilight .hilight.md 0 "===+"          $global-scheme

  ; HTTP links etc.
  hilight .hilight.md 0 "file:[-@~_a-zA-Z#%=\\\\.:\\/0-9]+"  .scheme.link
  hilight .hilight.md 0 "http://[-@~_a-zA-Z#%?&=.:\\/0-9]+"  .scheme.link
  hilight .hilight.md 0 "https://[-@~_a-zA-Z#%?&=.:\\/0-9]+" .scheme.link
  hilight .hilight.md 0 "ftp://[-@~_a-zA-Z#%.:\\/0-9]+"      .scheme.link
  hilight .hilight.md 0 "ftps://[-@~_a-zA-Z#%.:\\/0-9]+"     .scheme.link
  hilight .hilight.md 0 "mailto:[-@~_a-zA-Z#%.:\\/0-9]+"     .scheme.link
  hilight .hilight.md 0 "news:[-@~_a-zA-Z#%.:\\/0-9]+"       .scheme.link
  ; bullet and item lists
  hilight .hilight.md 0x20 "\\-\\} "                   .scheme.keyword
  hilight .hilight.md 0x20 "\\d+\\.\\} "               .scheme.keyword
  ; footnotes and task lists
  hilight .hilight.md 0 "\\[^\\{\\d+\\}]"              .scheme.link
  hilight .hilight.md 0x20 "\\[^\\d+]:"                .scheme.bold
  hilight .hilight.md 0x20 "- \\{\\[ ] "               .scheme.bold
  hilight .hilight.md 0x20 "- \\{\\[X] "               .scheme.bold
  
  0 hilight .hilight.mdE 0                             $global-scheme
  hilight   .hilight.mdE 0x80 "." .hilight.md          $global-scheme
  
  0 hilight .hilight.mdB 0                             .scheme.bold
  hilight   .hilight.mdB 0 "\\\\."                     .scheme.bold
  hilight   .hilight.mdB 0x80 "\\*\\*" .hilight.md     .scheme.hide
  hilight   .hilight.mdB 0x80 "__\\}\\W" .hilight.md   .scheme.hide
  hilight   .hilight.mdB 0x80 "\\*" .hilight.mdBI      .scheme.hide
  hilight   .hilight.mdB 0x80 "\\W\\{_\\}\\S" .hilight.mdBI .scheme.hide

  0 hilight .hilight.mdI 0                             .scheme.italic
  hilight   .hilight.mdI 0 "\\\\."                     .scheme.italic
  hilight   .hilight.mdI 0x80 "\\*" .hilight.md        .scheme.hide
  hilight   .hilight.mdI 0x80 "_\\}\\W" .hilight.md    .scheme.hide
  hilight   .hilight.mdI 0x80 "\\*\\*" .hilight.mdBI   .scheme.hide
  hilight   .hilight.mdI 0x80 "\\W\\{__\\}\\S" .hilight.mdBI .scheme.hide

  0 hilight .hilight.mdBI 0                            .scheme.bold-italic
  hilight   .hilight.mdBI 0 "\\\\."                    .scheme.bold-italic
  hilight   .hilight.mdBI 0x80 "\\*\\*" .hilight.mdI   .scheme.hide
  hilight   .hilight.mdBI 0x80 "__\\}\\W" .hilight.mdI .scheme.hide
  hilight   .hilight.mdBI 0x80 "\\*" .hilight.mdB      .scheme.hide
  hilight   .hilight.mdBI 0x80 "_\\}\\W" .hilight.mdB  .scheme.hide

  0 hilight .hilight.mdH 0                             .scheme.hlblue
  hilight   .hilight.mdH 0 "\\\\."                     .scheme.bold
  hilight   .hilight.mdH 0x80 "==" .hilight.md         .scheme.hide

  0 hilight .hilight.mdA 0                             $global-scheme
  hilight   .hilight.mdA 0 "\\\\."                     .scheme.link
  hilight   .hilight.mdA 0x80 "](" .hilight.mdU        .scheme.hide
  hilight   .hilight.mdA 0x80 "]" .hilight.md          .scheme.error

  0 hilight .hilight.mdL 0                             .scheme.link
  hilight   .hilight.mdL 0 "\\\\."                     .scheme.link
  hilight   .hilight.mdL 0x80 "](" .hilight.mdU        .scheme.hide
  hilight   .hilight.mdL 0x80 "]" .hilight.md          .scheme.error

  0 hilight .hilight.mdU 0                             .scheme.hide
  hilight   .hilight.mdU 0 "\\\\."                     .scheme.hide
  hilight   .hilight.mdU 0x80 ")" .hilight.md          .scheme.hide

  ; Code blocks
  hilight .hilight.md 1 "`[^`]+`"         .scheme.code
  hilight .hilight.md 1 "`` `[^`]+` ``"   .scheme.code  ; backticks in backticks
  hilight .hilight.md 0x220 "```"
  hilight .hilight.md 0x0a0 "```.*"   .hilight.mdC .scheme.hide

  0 hilight .hilight.mdC 0            .scheme.code
  hilight   .hilight.mdC 0x0a0 "```"  .hilight.md .scheme.hide

  ; Maths formulae blocks
  hilight .hilight.md 4 "\\\\(" "\\\\)" "\\"    .scheme.quote

  ; Allow inserts into the markdown mode. You may add to the inserts in your mymd.emf file
  0 define-macro md-add-file-support
    set-variable #l0 @1
    set-variable #l1 @2
    ; Force the hilighting mode to load if not already loaded.
    !if &not &exi &cat ".hilight." #l0
      !force execute-file &cat "hk" #l0
      ml-write &cat "executed " #l0
    !endif
    ; If the hilighting mode is loaded then modify it, else leave to the default code block hilighter
    !if &sin "h" &ind &spr ".fhook-%s.setup" #l0 &band .hilight.flags 0x02
      hilight .hilight.md 0x080 &spr "^```%s" #l1 &ind &cat ".hilight." #l0 .scheme.hide
      hilight .hilight.md 0x0a0 &spr "```{%s}" #l1 &ind &cat ".hilight." #l0 .scheme.hide
      hilight .hilight.md 0x0a0 &spr "```{\\.%s}" #l1 &ind &cat ".hilight." #l0 .scheme.hide
      hilight .hilight.md 0x0a0 &spr "```{%s[, ][^}]+}" #l1 &ind &cat ".hilight." #l0 .scheme.hide
      hilight .hilight.md 0x0a0 &spr "```{\\.%s[, ][^}]+}" #l1 &ind &cat ".hilight." #l0 .scheme.hide
      hilight &ind &cat ".hilight." #l0 0x080 "^```" .hilight.md .scheme.hide
    !endif
  !emacro
  md-add-file-support "tcl" "tcl"
  md-add-file-support "r" "r"
  md-add-file-support "dot" "dot"
  md-add-file-support "python" "python"
  md-add-file-support "python" "py"
  md-add-file-support "sql" "sql"
  md-add-file-support "emf" "emf"
  md-add-file-support "js" "javascript"
!endif

!if &sin "x" .fhook-md.setup
  ; only define the b & e hooks if time stamping is enabled
  0 define-macro bhook-md
    set-variable .timestamp $timestamp
    set-variable $timestamp "%Y-%M-%D %h:%m"
  !emacro
  0 define-macro ehook-md
    set-variable $timestamp .bhook-md.timestamp
  !emacro
!endif

; MD is a very poorly defined 'language', the only way to know if we are in normal text or
; a code block is to go right back to the start of the file and work through it.
0 define-macro md-para-move-to-start
  set-variable #l0 $window-line
  beginning-of-buffer
  !while &les $window-line #l0
    !force &sub $window-line #l0 search-buffer "me" "^\\(\\([`~]\\)\\2\\2+\\)"
    !iif &not $status  !break
    set-variable #l1 $window-line
    !force &sub $window-line #l0 search-buffer "me" &cat "^" @s1
    !if &not $status
      beginning-of-line
      !return
    !endif
  !done
  !while &les $window-line #l0
    !force &sub $window-line #l0 search-buffer "me" "^[ \t]*\n"
  !until &not $status
  !iif $window-col   forward-line
  beginning-of-line
!emacro
define-macro md-fill-paragraph
  !iif &not &set #l0 &abs @#  !return
  !iif &not @?  set-mark
  md-para-move-to-start
  ; Another nasty - the previous 'paragraph' might be an item list which means the initial indent limit could be 7
  ; not 3, but how can we know for sure? We would have to parse from the last fenced code block, add to
  ; md-para-move-to-start? ignore for now... 
  set-variable #l8 3
  !while &pdec #l0 1
    set-variable #l9 0
    !force search-buffer "me" "^\\([ \t]*\\)\\(\\S.\\{,15\\}\\)"
    !if &not $status
      end-of-buffer
      -8 ml-write "[end of buffer]"
    !endif
    set-variable $window-col &len &set #l2 @s1
    !if &gre &set #l1 $window-acol #l8
      !repeat
        beginning-of-line
        forward-line
        !force -1 search-buffer "me" "\\S"
        !iif &not $status  !break
      !until &les $window-acol &add #l8 1
      !goto end-p
    !endif
    !while &gre &sub #l8 4 #l1
      set-variable #l8 &sub #l8 4
    !done
    !if &seq &lef &set #l3 @s2 1 "#"
      ; Title should be at the start of the line and can only be one line
      beginning-of-line
      !if &len #l2
        &len #l2 backward-delete-char
        -1 yank
      !endif
      !if &not &seq " " &lef &rep #l3 "#" "" 1
        !repeat
          forward-char
        !until &not &seq @wc "#"
        insert-space
      !endif
      forward-line
    !elif &seq &lef #l3 1 "|"
      ; In MD's infinite stupidity tables don't have to have the initial '|'s so how does one realiably identiy a
      ; table? One might think on having 3 '-' on second line, but no, different renderers pick up on different
      ; things, so do we care? stick to strict definition to ensures all renderers will work... 
      forward-line
      !tgoto &not &xse @wl "^[ \t]*|[- \t:|]*?---[- \t:|]*$" std-line
      backward-line
      !jump 2
    !elif &seq &lef #l3 1 ">"
      ; Blockquote
      set-variable #l2 &cat #l2 &set #l1 &lef #l3 1
      !repeat
        beginning-of-line
        forward-line
        !if &not &seq &lef @wl &len #l2 #l2
          !force -1 search-buffer "me" "\\S"
          !iif &not $status  !break
          backward-char
          !iif &gre $window-acol #l8  !break
          !iif &not &seq @wc #l1  !break
          forward-char
          $window-col backward-delete-char
          -1 yank
          insert-string #l2
        !endif
      !done
    !elif &xse #l3 "\\(\\([`~]\\)\\2\\2+\\).*"
      ; fenced code block
      set-variable #l4 @s1
      end-of-line
      !force search-buffer "me" &cat "^\\([ \t]*\\)" #l4
      !iif &not $status  -8 ml-write "[Error: Open fenced code block]"
      !tjump &gre &len &rep @s1 "\t" "    " #l8 -2
      forward-line
    !elif &and &and &sin &lef #l3 1 "*-_" &gre &sub &len @wl &len &set #l4 &rep @wl &lef #l3 1 "" 2 &seq "" &trr #l4
      ; Horizontal rule
      forward-line
    !else
*std-line
      ; Check if this is a title line denoted by the second line...
      forward-line
      !if &and &and &len &set #l4 &trb @wl &sin &lef #l4 1 "=-" &seq "" &rep #l4 &lef #l4 1 ""
        set-variable #l4 &cat #l2 &lef #l4 1
        !iif &not &seq #l4 &lef @wl &len #l4  set-variable @wl &cat #l2 &trl @wl
        forward-line
      !else
        ; normal paragraph, glue all lines together, remove multiple spaces, then reformat...
        backward-line
        !repeat
          forward-line
          set-variable #l4 &lef &trl @wl 16
          backward-line
          end-of-line
          !if &seq #l4 ""
            !jump 4
          !elif &xse #l4 "\\([-*+>#]\\|\\d+\\.\\) .*"
            set-variable #l9 1
            set-variable #l0 &add #l0 1
            backward-char
            !while &seq @wc " "
              forward-delete-char
              backward-char
            !done
            !break
          !elif &gre $window-col 2
            backward-char
            set-variable #l4 @wc
            backward-char
            !if &seq &cat @wc #l4 "  "
              set-variable #l9 2
              set-variable #l0 &add #l0 1
              !break
            !else
              end-of-line
              insert-space
              forward-delete-char
            !endif
          !else
            insert-space
            forward-delete-char
          !endif
        !done
        set-variable $window-col &len #l2
        -1 replace-string "[ \t][ \t]+" " "
        !if &equ #l9 2
          insert-space
        !else
          end-of-line
          backward-char
          !iif &seq @wc " "  forward-delete-char
        !endif
        !if &les @# 0
          forward-line
        !elif &gre &len @wl $buffer-fill-col
          !iif &xse @wl "^[ \t]*\\(\\([-*+]\\|\\d+\\.\\)[ \t]+\\).*"  set-variable #l2 &spr "%s%n" #l2 &len @s1 " "
          !repeat
            !force set-variable $window-col $buffer-fill-col
            !iif &seq @wc "\n"  !break
            forward-char
            backward-word
            !iif &les $window-col &div $buffer-fill-col 2  forward-word
            !iif &seq "" &trr &rig @wl $window-col  !break
            !if &band 1 &sub &len &set #l4 &xre &lef @wl $window-col "\\\\." "XX" &len &rep #l4 "`" ""
              ; Note that a code block can be in a link label, bold & italic text etc.
              set-variable $window-col &rsin "`" #l4
              !if &les $window-col &div $buffer-fill-col 2
                !force -1 search-buffer "me" "[^\\\\]\\(\\\\.\\)*`[ \t]*"
                !iif &not $status  -8 ml-write "[Error: Open code block]"
                !iif &seq "\n" @wc  !break
              !else
                backward-char
              !endif
              set-variable #l4 &xre &lef @wl $window-col "\\\\." "XX"
            !endif
            !if &band 1 &sub &len #l4 &len &rep #l4 "**" "B"
              set-variable $window-col &rsin " " &lef #l4 &rsin "**" #l4
              !if &les $window-col &div $buffer-fill-col 2
                !force -1 search-buffer "me" "[^\\\\]\\(\\\\.\\)*\\*\\*.*?\\s"
                !iif &not $status  -8 ml-write "[Error: Open '**' bold]"
                backward-char
                !iif &seq "" &trr &rig @wl $window-col  !break
              !endif
              set-variable #l4 &xre &lef @wl $window-col "\\\\." "XX"
            !endif
            !if &band 1 &sub &len &set #l4 &rep #l4 "**" "BB" &len &rep #l4 "*" ""
              set-variable $window-col &rsin " " &lef #l4 &rsin "*" #l4
              !if &les $window-col &div $buffer-fill-col 2
                !force -1 search-buffer "me" "[^\\\\]\\(\\\\.\\)*\\*.*?\\s"
                !iif &not $status  -8 ml-write "[Error: Open '*' italic]"
                backward-char
                !iif &seq "" &trr &rig @wl $window-col  !break
              !endif
              set-variable #l4 &xre &lef @wl $window-col "\\\\." "XX"
            !endif
            ; MD is such a brain dead format that use of '_' is a total mess, rather than having to escape '_'s so their use is consistent
            ; and predictable, different tools apply different rules on whether to ignore them or not (consider: __A __ D__ or __A __C D__ or "__A__B__" vs "__A__"__")
            ; there's no good solution here because the best solution depends on the end tool being used
            ; So lets play safe and try not to break anything - this is only a line wrapper! so first ignore all w_w & w__w underscores as more interpreter do and then treat
            ; the rest like *s.
            !if &band 1 &sub &len #l4 &len &rep &set #l4 &xrep &xrep &xrep #l4 "\\(\\w\\)_\\(\\w\\)" "\\1U\\2" "\\(\\w\\)__\\(\\w\\)" "\\1UU\\2" "\\(\\w\\)___\\(\\w\\)" "\\1UUU\\2" "__" "B"
              set-variable $window-col &rsin " " &lef #l4 &rsin "__" #l4
              !if &les $window-col &div $buffer-fill-col 2
                !force -1 search-buffer "me" "[^\\\\]\\(\\\\.\\)*\\S__\\(\\s\\|\\W.*?\\s\\)"
                !iif &not $status  -8 ml-write "[Error: Open '__' bold]"
                backward-char
                !iif &seq "" &trr &rig @wl $window-col  !break
              !endif
              set-variable #l4 &xrep &xrep &xrep &xre &lef @wl $window-col "\\\\." "XX" "\\(\\w\\)_\\(\\w\\)" "\\1U\\2" "\\(\\w\\)__\\(\\w\\)" "\\1UU\\2" "\\(\\w\\)___\\(\\w\\)" "\\1UUU\\2"
            !endif
            !if &band 1 &sub &len &set #l4 &rep #l4 "__" "BB" &len &rep #l4 "_" ""
              set-variable $window-col &rsin " " &lef #l4 &rsin "_" #l4
              !if &les $window-col &div $buffer-fill-col 2
                !force -1 search-buffer "me" "[^\\\\]\\(\\\\.\\)*\\S_\\(\\s\\|\\W.*?\\s\\)"
                !iif &not $status  -8 ml-write "[Error: Open '_' italic]"
                backward-char
                !iif &seq "" &trr &rig @wl $window-col  !break
              !endif
              set-variable #l4 &xre &lef @wl $window-col "\\\\." "XX"
            !endif
            !if &sin "[" #l4
              ; there may be links on the line; the flimspy link format 'spec' of MD is so poor that great care is needed, its not an error to have a broken link, it then
              ; becomes normal text, its this lack of a strict format with errors that make this all such an ambiguous format. Can't think
              ; of an alternative to using regex to find each link starting before our wrap point to check the wrap is not within a link.
              set-variable #l5 $window-col
              beginning-of-line
              !repeat
                !force -1 search-buffer "me" "!?\\[\\([^]\\\\\n]\\|\\\\.\\)*](\\([^)\\\\\n]\\|\\\\.\\)*)"
                !iif &not $status  !break
                !if &gre $window-col #l5
                  !iif &not &les &set #l6 &sub $window-col &len @s0 #l5  !break
                  !iif &gre #l6 &div $buffer-fill-col 2  set-variable $window-col #l6
                  set-variable #l5 $window-col
                  set-variable #l4 &xre &lef @wl $window-col "\\\\." "XX"
                  !break
                !endif
              !done
              set-variable $window-col #l5
            !endif
            ; last thing to check, are we going to create a 'special' line if we insert a newline here, if so don't, move on...
            !while &xseq &mid @wl $window-col 16 "^\\([ \t]*\\([-*+#=>]\\|\\d+\\.\\)\\)[ \t].*"
              &len @s1 forward-char
            !done
            backward-char
            !iif &not &seq @wc " "  forward-char
            !iif &seq "" &trr &rig @wl $window-col  !break
            !iif &seq @wc " "  forward-delete-char
            insert-newline
            insert-string #l2
          !done
          forward-line
        !else
          forward-line
        !endif
        ; check if was an item list item, if so inc the allowed indent 
        !iif &xse #l3 "\\([-*+]\\|\\d+\\.\\) .*"  set-variable #l8 &add #l8 4
      !endif
    !endif
*end-p
    beginning-of-line
    !if #l9
    !elif &len &trr @wl
      insert-newline
      backward-line
    !endif
  !done
  ; If no arguments specified then restore the cursor position.
  !iif &not @?  exchange-point-and-mark
!emacro

!iif &not &exi md-export  execute-file "mdtools"

buffer-init-fhook "md"
