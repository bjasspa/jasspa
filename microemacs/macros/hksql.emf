; -!- emf -!-
; This is part of the JASSPA MicroEmacs macro files
; Copyright (C) 1999-2009 JASSPA (www.jasspa.com)
; See the file me.emf for copying and conditions.
;
; Created:     Sat Feb 6 1999
; Synopsis:    Simple hilighting of an SQL file.
; Authors:     Jon Green
;
define-macro fhook-sql
  set-variable $buffer-mask "luh1"
  @# buffer-init "sql"
  buffer-init-hooks
!emacro

; buffer-init variables
set-variable .fhook-sql.name "SQL"
set-variable .fhook-sql.setup &reg "/history/fhook/sql" "ghix"
set-variable .fhook-sql.setup-mask "aeghikmtux"
set-variable .fhook-sql.exact -1
set-variable .fhook-sql.comment "|/*| */|*| * | * |fr|"
set-variable .fhook-sql.comment-1 "|--||-|-- | --||"
set-variable .fhook-sql.comment-2 "|rem|||rem |||"

!if &and &sin "h" .fhook-sql.setup &band .hilight.flags 0x02 
  !if &not &exist .hilight.sql
    set-variable .hilight.sql &pinc .hilight.next 1
  !endif
  0 hilight .hilight.sql 3 50                $global-scheme
  ; What is this ?? Remove it does not appear to be standard
  ; hilight .hilight.sql 4 "<[[:alpha:]]" ">" "" .scheme.keyword
  hilight .hilight.sql 20 "/\\*" "*/" ""     .scheme.comment
  hilight .hilight.sql 2 "--"                .scheme.comment
  hilight .hilight.sql 0x22 "rem"            .scheme.comment
  hilight .hilight.sql 4 "'" "'" "\\"        .scheme.string
  hilight .hilight.sql 4 "\"" "\"" "\\"      .scheme.string
  
  hilight .hilight.sql 4 "@" "\\}\\W" ""     .scheme.variable
  hilight .hilight.sql 4 "@@" "\\}\\W" ""    .scheme.prepro
  hilight .hilight.sql 1 "absolute"          .scheme.keyword
  hilight .hilight.sql 1 "action"            .scheme.keyword
  hilight .hilight.sql 1 "actor"             .scheme.keyword
  hilight .hilight.sql 1 "add"               .scheme.keyword
  hilight .hilight.sql 1 "after"             .scheme.keyword
  hilight .hilight.sql 1 "alias"             .scheme.keyword
  hilight .hilight.sql 1 "all"               .scheme.operator
  hilight .hilight.sql 1 "allocate"          .scheme.keyword
  hilight .hilight.sql 1 "alter"             .scheme.keyword
  hilight .hilight.sql 1 "and"               .scheme.operator
  hilight .hilight.sql 1 "any"               .scheme.keyword
  hilight .hilight.sql 1 "are"               .scheme.keyword
  hilight .hilight.sql 1 "as"                .scheme.keyword
  hilight .hilight.sql 1 "asc"               .scheme.operator
  hilight .hilight.sql 1 "assertion"         .scheme.keyword
  hilight .hilight.sql 1 "async"             .scheme.keyword
  hilight .hilight.sql 1 "at"                .scheme.keyword
  hilight .hilight.sql 1 "attributes"        .scheme.keyword
  hilight .hilight.sql 1 "authorization"     .scheme.keyword
  hilight .hilight.sql 1 "avg"               .scheme.keyword
  hilight .hilight.sql 1 "before"            .scheme.keyword
  hilight .hilight.sql 1 "begin"             .scheme.keyword
  hilight .hilight.sql 1 "between"           .scheme.operator
  hilight .hilight.sql 1 "binary"            .scheme.type
  hilight .hilight.sql 1 "bit"               .scheme.type
  hilight .hilight.sql 1 "bit_length"        .scheme.keyword
  hilight .hilight.sql 1 "boolean"           .scheme.type
  hilight .hilight.sql 1 "both"              .scheme.keyword
  hilight .hilight.sql 1 "breadth"           .scheme.keyword
  hilight .hilight.sql 1 "by"                .scheme.operator
  hilight .hilight.sql 1 "cascade"           .scheme.keyword
  hilight .hilight.sql 1 "cascaded"          .scheme.keyword
  hilight .hilight.sql 1 "case"              .scheme.keyword
  hilight .hilight.sql 1 "cast"              .scheme.keyword
  hilight .hilight.sql 1 "catalog"           .scheme.keyword
  hilight .hilight.sql 1 "char"              .scheme.type
  hilight .hilight.sql 1 "character"         .scheme.type
  hilight .hilight.sql 1 "char_length"       .scheme.keyword
  hilight .hilight.sql 1 "character_length"  .scheme.keyword
  hilight .hilight.sql 1 "charindex"         .scheme.function
  hilight .hilight.sql 1 "check"             .scheme.keyword
  hilight .hilight.sql 1 "class"             .scheme.keyword
  hilight .hilight.sql 1 "close"             .scheme.keyword
  hilight .hilight.sql 1 "clustered"         .scheme.keyword
  hilight .hilight.sql 1 "coalesce"          .scheme.keyword
  hilight .hilight.sql 1 "collate"           .scheme.keyword
  hilight .hilight.sql 1 "collation"         .scheme.keyword
  hilight .hilight.sql 1 "column"            .scheme.keyword
  hilight .hilight.sql 1 "commit"            .scheme.keyword
  hilight .hilight.sql 1 "completion"        .scheme.keyword
  hilight .hilight.sql 1 "connect"           .scheme.keyword
  hilight .hilight.sql 1 "connection"        .scheme.keyword
  hilight .hilight.sql 1 "constraint"        .scheme.keyword
  hilight .hilight.sql 1 "constraints"       .scheme.keyword
  hilight .hilight.sql 1 "constructor"       .scheme.keyword
  hilight .hilight.sql 1 "continue"          .scheme.keyword
  hilight .hilight.sql 1 "convert"           .scheme.keyword
  hilight .hilight.sql 1 "corresponding"     .scheme.keyword
  hilight .hilight.sql 1 "count"             .scheme.function
  hilight .hilight.sql 1 "create"            .scheme.operator
  hilight .hilight.sql 1 "cross"             .scheme.keyword
  hilight .hilight.sql 1 "current"           .scheme.keyword
  hilight .hilight.sql 1 "current_date"      .scheme.keyword
  hilight .hilight.sql 1 "current_path"      .scheme.keyword
  hilight .hilight.sql 1 "current_time"      .scheme.keyword
  hilight .hilight.sql 1 "current_timestamp" .scheme.keyword
  hilight .hilight.sql 1 "current_user"      .scheme.keyword
  hilight .hilight.sql 1 "cursor"            .scheme.keyword
  hilight .hilight.sql 1 "cycle"             .scheme.keyword
  hilight .hilight.sql 1 "data"              .scheme.keyword
  hilight .hilight.sql 1 "date"              .scheme.keyword
  hilight .hilight.sql 1 "datepart"          .scheme.function
  hilight .hilight.sql 1 "datetime"          .scheme.type
  hilight .hilight.sql 1 "day"               .scheme.keyword
  hilight .hilight.sql 1 "deallocate"        .scheme.keyword
  hilight .hilight.sql 1 "dec"               .scheme.type
  hilight .hilight.sql 1 "decimal"           .scheme.type
  hilight .hilight.sql 1 "declare"           .scheme.operator
  hilight .hilight.sql 1 "default"           .scheme.keyword
  hilight .hilight.sql 1 "deferrable"        .scheme.keyword
  hilight .hilight.sql 1 "deferred"          .scheme.keyword
  hilight .hilight.sql 1 "delete"            .scheme.operator
  hilight .hilight.sql 1 "depth"             .scheme.keyword
  hilight .hilight.sql 1 "desc"              .scheme.operator
  hilight .hilight.sql 1 "describe"          .scheme.keyword
  hilight .hilight.sql 1 "descriptor"        .scheme.keyword
  hilight .hilight.sql 1 "designator"        .scheme.keyword
  hilight .hilight.sql 1 "destroy"           .scheme.keyword
  hilight .hilight.sql 1 "destructor"        .scheme.keyword
  hilight .hilight.sql 1 "dictionary"        .scheme.keyword
  hilight .hilight.sql 1 "diagnostics"       .scheme.keyword
  hilight .hilight.sql 1 "disconnect"        .scheme.keyword
  hilight .hilight.sql 1 "distinct"          .scheme.keyword
  hilight .hilight.sql 1 "domain"            .scheme.keyword
  hilight .hilight.sql 1 "double"            .scheme.type
  hilight .hilight.sql 1 "drop"              .scheme.operator
  hilight .hilight.sql 1 "each"              .scheme.keyword
  hilight .hilight.sql 1 "element"           .scheme.keyword
  hilight .hilight.sql 1 "else"              .scheme.keyword
  hilight .hilight.sql 1 "end"               .scheme.keyword
  hilight .hilight.sql 1 "end-exec"          .scheme.keyword
  hilight .hilight.sql 1 "equals"            .scheme.keyword
  hilight .hilight.sql 1 "escape"            .scheme.keyword
  hilight .hilight.sql 1 "except"            .scheme.keyword
  hilight .hilight.sql 1 "exec"              .scheme.operator
  hilight .hilight.sql 1 "execute"           .scheme.operator
  hilight .hilight.sql 1 "exists"            .scheme.function
  hilight .hilight.sql 1 "external"          .scheme.keyword
  hilight .hilight.sql 1 "extract"           .scheme.keyword
  hilight .hilight.sql 1 "factor"            .scheme.keyword
  hilight .hilight.sql 1 "false"             .scheme.constant
  hilight .hilight.sql 1 "fetch"             .scheme.keyword
  hilight .hilight.sql 1 "first"             .scheme.keyword
  hilight .hilight.sql 1 "float"             .scheme.type
  hilight .hilight.sql 1 "for"               .scheme.keyword
  hilight .hilight.sql 1 "foreign"           .scheme.keyword
  hilight .hilight.sql 1 "found"             .scheme.keyword
  hilight .hilight.sql 1 "from"              .scheme.operator
  hilight .hilight.sql 1 "full"              .scheme.keyword
  hilight .hilight.sql 1 "function"          .scheme.keyword
  hilight .hilight.sql 1 "general"           .scheme.keyword
  hilight .hilight.sql 1 "get"               .scheme.keyword
  hilight .hilight.sql 1 "global"            .scheme.keyword
  hilight .hilight.sql 1 "go"                .scheme.keyword
  hilight .hilight.sql 1 "goto"              .scheme.keyword
  hilight .hilight.sql 1 "grant"             .scheme.keyword
  hilight .hilight.sql 1 "group"             .scheme.keyword
  hilight .hilight.sql 1 "having"            .scheme.keyword
  hilight .hilight.sql 1 "hour"              .scheme.keyword
  hilight .hilight.sql 1 "identity"          .scheme.keyword
  hilight .hilight.sql 1 "ignore"            .scheme.keyword
  hilight .hilight.sql 1 "image"             .scheme.type
  hilight .hilight.sql 1 "immediate"         .scheme.keyword
  hilight .hilight.sql 1 "in"                .scheme.operator
  hilight .hilight.sql 1 "include"           .scheme.keyword
  hilight .hilight.sql 1 "index"             .scheme.type
  hilight .hilight.sql 1 "indicator"         .scheme.keyword
  hilight .hilight.sql 1 "initially"         .scheme.keyword
  hilight .hilight.sql 1 "inner"             .scheme.operator
  hilight .hilight.sql 1 "inout"             .scheme.keyword
  hilight .hilight.sql 1 "input"             .scheme.keyword
  hilight .hilight.sql 1 "insensitive"       .scheme.keyword
  hilight .hilight.sql 1 "insert"            .scheme.operator
  hilight .hilight.sql 1 "instead"           .scheme.keyword
  hilight .hilight.sql 1 "int"               .scheme.type
  hilight .hilight.sql 1 "integer"           .scheme.type
  hilight .hilight.sql 1 "intersect"         .scheme.operator
  hilight .hilight.sql 1 "interval"          .scheme.keyword
  hilight .hilight.sql 1 "into"              .scheme.operator
  hilight .hilight.sql 1 "if"                .scheme.keyword
  hilight .hilight.sql 1 "is"                .scheme.operator
  hilight .hilight.sql 1 "isnull"            .scheme.function
  hilight .hilight.sql 1 "isolation"         .scheme.keyword
  hilight .hilight.sql 1 "join"              .scheme.operator
  hilight .hilight.sql 1 "key"               .scheme.keyword
  hilight .hilight.sql 1 "language"          .scheme.keyword
  hilight .hilight.sql 1 "last"              .scheme.keyword
  hilight .hilight.sql 1 "len"               .scheme.function
  hilight .hilight.sql 1 "leading"           .scheme.keyword
  hilight .hilight.sql 1 "left"              .scheme.operator
  hilight .hilight.sql 1 "less"              .scheme.keyword
  hilight .hilight.sql 1 "level"             .scheme.keyword
  hilight .hilight.sql 1 "like"              .scheme.operator
  hilight .hilight.sql 1 "limit"             .scheme.keyword
  hilight .hilight.sql 1 "list"              .scheme.keyword
  hilight .hilight.sql 1 "local"             .scheme.keyword
  hilight .hilight.sql 1 "lower"             .scheme.function
  hilight .hilight.sql 1 "match"             .scheme.keyword
  hilight .hilight.sql 1 "max"               .scheme.function
  hilight .hilight.sql 1 "min"               .scheme.function
  hilight .hilight.sql 1 "minus"             .scheme.operator
  hilight .hilight.sql 1 "minute"            .scheme.keyword
  hilight .hilight.sql 1 "modif"             .scheme.keyword
  hilight .hilight.sql 1 "module"            .scheme.keyword
  hilight .hilight.sql 1 "money"             .scheme.type
  hilight .hilight.sql 1 "month"             .scheme.keyword
  hilight .hilight.sql 1 "move"              .scheme.keyword
  hilight .hilight.sql 1 "multiset"          .scheme.keyword
  hilight .hilight.sql 1 "names"             .scheme.keyword
  hilight .hilight.sql 1 "national"          .scheme.variable
  hilight .hilight.sql 1 "natural"           .scheme.keyword
  hilight .hilight.sql 1 "nchar"             .scheme.type
  hilight .hilight.sql 1 "new"               .scheme.keyword
  hilight .hilight.sql 1 "new_table"         .scheme.keyword
  hilight .hilight.sql 1 "next"              .scheme.keyword
  hilight .hilight.sql 1 "no"                .scheme.keyword
  hilight .hilight.sql 1 "none"              .scheme.keyword
  hilight .hilight.sql 1 "nonclustered"      .scheme.keyword
  hilight .hilight.sql 1 "not"               .scheme.operator
  hilight .hilight.sql 1 "ntext"             .scheme.type
  hilight .hilight.sql 1 "null"              .scheme.constant
  hilight .hilight.sql 1 "nullif"            .scheme.function
  hilight .hilight.sql 1 "number"            .scheme.type
  hilight .hilight.sql 1 "numeric"           .scheme.type
  hilight .hilight.sql 1 "nvarchar"          .scheme.type
  hilight .hilight.sql 1 "octet_length"      .scheme.keyword
  hilight .hilight.sql 1 "of"                .scheme.keyword
  hilight .hilight.sql 1 "off"               .scheme.keyword
  hilight .hilight.sql 1 "oid"               .scheme.keyword
  hilight .hilight.sql 1 "old"               .scheme.keyword
  hilight .hilight.sql 1 "old_table"         .scheme.keyword
  hilight .hilight.sql 1 "on"                .scheme.operator
  hilight .hilight.sql 1 "only"              .scheme.keyword
  hilight .hilight.sql 1 "open"              .scheme.keyword
  hilight .hilight.sql 1 "operation"         .scheme.keyword
  hilight .hilight.sql 1 "operator"          .scheme.keyword
  hilight .hilight.sql 1 "operators"         .scheme.keyword
  hilight .hilight.sql 1 "option"            .scheme.keyword
  hilight .hilight.sql 1 "or"                .scheme.operator
  hilight .hilight.sql 1 "order"             .scheme.operator
  hilight .hilight.sql 1 "out"               .scheme.keyword
  hilight .hilight.sql 1 "outer"             .scheme.operator
  hilight .hilight.sql 1 "output"            .scheme.keyword
  hilight .hilight.sql 1 "overlaps"          .scheme.keyword
  hilight .hilight.sql 1 "pad"               .scheme.keyword
  hilight .hilight.sql 1 "parameters"        .scheme.keyword
  hilight .hilight.sql 1 "partial"           .scheme.keyword
  hilight .hilight.sql 1 "path"              .scheme.keyword
  hilight .hilight.sql 1 "pendant"           .scheme.keyword
  hilight .hilight.sql 1 "position"          .scheme.keyword
  hilight .hilight.sql 1 "postfix"           .scheme.keyword
  hilight .hilight.sql 1 "precision"         .scheme.variable
  hilight .hilight.sql 1 "prefix"            .scheme.keyword
  hilight .hilight.sql 1 "preorder"          .scheme.keyword
  hilight .hilight.sql 1 "prepare"           .scheme.keyword
  hilight .hilight.sql 1 "preserve"          .scheme.keyword
  hilight .hilight.sql 1 "primary"           .scheme.keyword
  hilight .hilight.sql 1 "prior"             .scheme.keyword
  hilight .hilight.sql 1 "private"           .scheme.keyword
  hilight .hilight.sql 1 "privileges"        .scheme.keyword
  hilight .hilight.sql 1 "procedure"         .scheme.type
  hilight .hilight.sql 1 "protected"         .scheme.keyword
  hilight .hilight.sql 1 "public"            .scheme.keyword
  hilight .hilight.sql 1 "raiserror"         .scheme.error
  hilight .hilight.sql 1 "read"              .scheme.keyword
  hilight .hilight.sql 1 "real"              .scheme.type
  hilight .hilight.sql 1 "recursive"         .scheme.keyword
  hilight .hilight.sql 1 "references"        .scheme.keyword
  hilight .hilight.sql 1 "referencing"       .scheme.keyword
  hilight .hilight.sql 1 "relative"          .scheme.keyword
  hilight .hilight.sql 1 "representation"    .scheme.keyword
  hilight .hilight.sql 1 "restrict"          .scheme.keyword
  hilight .hilight.sql 1 "return"            .scheme.keyword
  hilight .hilight.sql 1 "returns"           .scheme.keyword
  hilight .hilight.sql 1 "revoke"            .scheme.keyword
  hilight .hilight.sql 1 "right"             .scheme.operator
  hilight .hilight.sql 1 "role"              .scheme.keyword
  hilight .hilight.sql 1 "rollback"          .scheme.keyword
  hilight .hilight.sql 1 "routine"           .scheme.keyword
  hilight .hilight.sql 1 "row"               .scheme.keyword
  hilight .hilight.sql 1 "rows"              .scheme.keyword
  hilight .hilight.sql 1 "savepoint"         .scheme.keyword
  hilight .hilight.sql 1 "schema"            .scheme.keyword
  hilight .hilight.sql 1 "scope_identity"    .scheme.function
  hilight .hilight.sql 1 "scroll"            .scheme.keyword
  hilight .hilight.sql 1 "search"            .scheme.keyword
  hilight .hilight.sql 1 "second"            .scheme.keyword
  hilight .hilight.sql 1 "section"           .scheme.keyword
  hilight .hilight.sql 1 "select"            .scheme.operator
  hilight .hilight.sql 1 "sensitive"         .scheme.keyword
  hilight .hilight.sql 1 "sequence"          .scheme.keyword
  hilight .hilight.sql 1 "session"           .scheme.keyword
  hilight .hilight.sql 1 "session_user"      .scheme.keyword
  hilight .hilight.sql 1 "set"               .scheme.operator
  hilight .hilight.sql 1 "unsigned"          .scheme.type
  hilight .hilight.sql 1 "similar"           .scheme.keyword
  hilight .hilight.sql 1 "size"              .scheme.keyword
  hilight .hilight.sql 1 "smallint"          .scheme.type
  hilight .hilight.sql 1 "smalldatetime"     .scheme.type
  hilight .hilight.sql 1 "smallmoney"        .scheme.type
  hilight .hilight.sql 1 "some"              .scheme.keyword
  hilight .hilight.sql 1 "space"             .scheme.keyword
  hilight .hilight.sql 1 "specific"          .scheme.keyword
  hilight .hilight.sql 1 "sql"               .scheme.keyword
  hilight .hilight.sql 1 "sqlcode"           .scheme.keyword
  hilight .hilight.sql 1 "sqlerror"          .scheme.keyword
  hilight .hilight.sql 1 "sqlexception"      .scheme.keyword
  hilight .hilight.sql 1 "sqlstate"          .scheme.keyword
  hilight .hilight.sql 1 "sqlwarning"        .scheme.keyword
  hilight .hilight.sql 1 "start"             .scheme.keyword
  hilight .hilight.sql 1 "state"             .scheme.keyword
  hilight .hilight.sql 1 "structure"         .scheme.keyword
  hilight .hilight.sql 1 "substring"         .scheme.function
  hilight .hilight.sql 1 "sum"               .scheme.function
  hilight .hilight.sql 1 "symbol"            .scheme.keyword
  hilight .hilight.sql 1 "system_user"       .scheme.keyword
  hilight .hilight.sql 1 "table"             .scheme.type
  hilight .hilight.sql 1 "template"          .scheme.keyword
  hilight .hilight.sql 1 "temporary"         .scheme.keyword
  hilight .hilight.sql 1 "term"              .scheme.keyword
  hilight .hilight.sql 1 "test"              .scheme.keyword
  hilight .hilight.sql 1 "text"              .scheme.type
  hilight .hilight.sql 1 "than"              .scheme.keyword
  hilight .hilight.sql 1 "then"              .scheme.keyword
  hilight .hilight.sql 1 "there"             .scheme.keyword
  hilight .hilight.sql 1 "time"              .scheme.keyword
  hilight .hilight.sql 1 "timestamp"         .scheme.keyword
  hilight .hilight.sql 1 "timezone_hour"     .scheme.keyword
  hilight .hilight.sql 1 "timezone_minute"   .scheme.keyword
  hilight .hilight.sql 1 "tinyint"           .scheme.type
  hilight .hilight.sql 1 "to"                .scheme.keyword
  hilight .hilight.sql 1 "top"               .scheme.operator
  hilight .hilight.sql 1 "trailing"          .scheme.keyword
  hilight .hilight.sql 1 "transaction"       .scheme.keyword
  hilight .hilight.sql 1 "translate"         .scheme.keyword
  hilight .hilight.sql 1 "translation"       .scheme.keyword
  hilight .hilight.sql 1 "trigger"           .scheme.keyword
  hilight .hilight.sql 1 "trim"              .scheme.keyword
  hilight .hilight.sql 1 "true"              .scheme.constant
  hilight .hilight.sql 1 "type"              .scheme.keyword
  hilight .hilight.sql 1 "under"             .scheme.keyword
  hilight .hilight.sql 1 "union"             .scheme.operator
  hilight .hilight.sql 1 "unique"            .scheme.keyword
  hilight .hilight.sql 1 "unknown"           .scheme.keyword
  hilight .hilight.sql 1 "update"            .scheme.operator
  hilight .hilight.sql 1 "upper"             .scheme.function
  hilight .hilight.sql 1 "usage"             .scheme.keyword
  hilight .hilight.sql 1 "user"              .scheme.keyword
  hilight .hilight.sql 1 "using"             .scheme.keyword
  hilight .hilight.sql 1 "value"             .scheme.keyword
  hilight .hilight.sql 1 "values"            .scheme.keyword
  hilight .hilight.sql 1 "varbinary"         .scheme.type
  hilight .hilight.sql 1 "varchar"           .scheme.type
  hilight .hilight.sql 1 "varchar2"          .scheme.type
  hilight .hilight.sql 1 "variable"          .scheme.keyword
  hilight .hilight.sql 1 "variant"           .scheme.keyword
  hilight .hilight.sql 1 "varying"           .scheme.variable
  hilight .hilight.sql 1 "view"              .scheme.keyword
  hilight .hilight.sql 1 "virtual"           .scheme.keyword
  hilight .hilight.sql 1 "visible"           .scheme.keyword
  hilight .hilight.sql 1 "wait"              .scheme.keyword
  hilight .hilight.sql 1 "when"              .scheme.keyword
  hilight .hilight.sql 1 "whenever"          .scheme.keyword
  hilight .hilight.sql 1 "where"             .scheme.operator
  hilight .hilight.sql 1 "with"              .scheme.keyword
  hilight .hilight.sql 1 "without"           .scheme.keyword
  hilight .hilight.sql 1 "work"              .scheme.keyword
  hilight .hilight.sql 1 "write"             .scheme.keyword
  hilight .hilight.sql 1 "year"              .scheme.keyword
  hilight .hilight.sql 1 "zone"              .scheme.keyword
  !if &band .hilight.flags 0x08
    ; hilight constants, e.g. numbers
    hilight .hilight.sql 1 "[[:digit:]]+"  .scheme.constant
  !endif
!endif

0 define-macro sql-format-line2
  set-variable #l9 $buffer-bname
  set-variable #l0 #p0
  set-variable #l1 &add #p1 4
  !repeat
    !while &gre &len @wl #l0
      set-variable #l2 0
      set-variable #l3 0
      set-variable #l4 0
      beginning-of-line
      !repeat
        !iif &seq @wc "\n"  !break
        !if &seq @wc " "
          !if &gre $window-col &add #l1 10
            !iif &gre $window-col &add 10 #l3  set-variable #l3 $window-col
            forward-char
            !if &isin @wc "DFGILORSUW"
              !iif &xise &mid @wl $window-col 10 "\\(DELETE\\|FROM\\|GROUP\\|INSERT\\|INNER\\|LEFT\\|RIGHT\\|FULL\\|SELECT\\|UPDATE\\|WHERE\\|ORDER\\) .*"  set-variable #l2 $window-col
            !else
              backward-char
            !endif
          !endif
        !elif &seq @wc ","
          set-variable #l3 $window-col
        !elif &seq @wc "("
          set-variable #l5 $window-col
          forward-char
          set-mark
          backward-char
          !force goto-matching-fence
          !if &not $status
          !elif &les &sub $window-col #l5 &sub #l0 &add #l1 4
            !iif &or &not #l4 &les $window-col #l0  set-variable #l4 $window-col
          !else
            kill-region
            !force 0 delete-buffer &cat "*sqltmp" #l1
            find-buffer &cat "*sqltmp" #l1
            yank
            -1 yank
            beginning-of-buffer
            !iif &not &xise &lef @wl 10 "\\(DELETE\\|INSERT\\|SELECT\\|UPDATE\\) .*"  insert-string "    "
            insert-string &spr "%n" #l1 " "
            0 sql-format-line2
            set-mark
            beginning-of-buffer
            kill-region
            !force 0 delete-buffer $buffer-bname
            find-buffer #l9
            insert-string "\n"
            yank
            -1 yank
            insert-string &spr "%n" #l1 " "
            backward-line
            set-variable #l4 &set #l3 &set #l2 0
            !break
          !endif
        !endif
        forward-char
      !until &and &add &add #l2 #l3 #l4 &gre $window-col #l0
      !if #l2
        set-variable $window-col #l2
        backward-delete-char
        insert-string &spr "\n%n" #p1 " "
      !elif #l3
        set-variable $window-col #l3
        !if &seq @wc ","
          forward-char
          insert-string &spr "\n%n" #l1 " "
        !elif &seq @wc " "
          insert-string &spr "\n%n" &sub #l1 1 " "
        !else
          1000 ml-write "[SQL ERROR: something went wrong (3)]"
          !abort
        !endif
      !elif #l4
        set-variable $window-col #l4
        !if &seq @wc ")"
          forward-char
          insert-string &spr "\n%n" &add #l1 4 " "
        !else
          1000 ml-write "[SQL ERROR: something went wrong (4)]"
          !abort
        !endif
      !else
        forward-line
      !endif
    !done
    !force forward-line
    !iif @#  !return
  !until &seq @wl ""
!emacro

0 define-macro sql-format-line1
  set-variable #l0 $window-line
  set-variable #l1 &sub &len @wl &len &trl @wl
  !repeat
    !force search-buffer "mE" "\\((\\|\\<\\(DELETE\\( FROM\\)?\\|FROM\\|GROUP\\|INSERT\\|INNER\\|LEFT\\|RIGHT\\|FULL\\|SELECT\\|UPDATE\\|WHERE\\|ORDER\\) \\)"
    !iif &not $status  !return
    !if &sub #l0 $window-line
      set-variable $window-line #l0
      !return
    !elif &seq &set #l2 @s1 "("
      backward-char
      !force goto-matching-fence
      !iif &not $status  !return
      !if &sub #l0 $window-line
        set-variable $window-line #l0
        !return
      !endif
    !else
      &len #l2 backward-char
      !if &not &seq &trr &lef @wl $window-col ""
        backward-char
        !if &seq @wc " "
          forward-delete-char
        !else
          forward-char
        !endif
        insert-string &spr "\n%n" #l1 " "
        set-variable #l0 &add #l0 1
      !endif
      &len #l2 forward-char
    !endif
  !done
!emacro

define-macro sql-format
  set-variable #l1 0
  !while &exi &spr ":*sql-format-%d*:" &inc #l1 1
  !done
  popup-window &spr "*sql-format-%d*" #l1
  !if &band @# 1
    yank
  !else
    !force set-variable #l1 @1
    !iif &not $status  set-variable #l1 @ml "SQL string"
    insert-string #l1
  !endif
  1 buffer-mode "magic"
  -1 buffer-mode "exact"
  beginning-of-buffer
  set-variable #l9 0
  !repeat
    !force 1 replace-string "'\\(\\([^']\\|''\\)*\\)'" &cat "'\x01" &inc #l9 1
    !iif &not $status  !break
    !if &gre &set #l2 &len @s1 2000
      ml-write "[SQL Error: String value too long, max length 2000]"
      !abort
    !endif
    set-variable &ind &cat ":a" #l9 @s1
    !iif &gre &set #l3 &sub #l2 2 0  insert-string &spr "%n" #l3 "X"
    insert-string "\x02'"
  !done
  beginning-of-buffer
  !force replace-string "\n" " "
  beginning-of-buffer
  !force replace-string "\t" " "
  beginning-of-buffer
  !force replace-string "  +" " "
  beginning-of-buffer
  !force replace-string " \\([,;)]\\)" "\\1"
  beginning-of-buffer
  !force replace-string "\\([,;(]\\) " "\\1"
  beginning-of-buffer
  !force replace-string " +$" ""
  beginning-of-buffer
  !if &band @# 2
    !force replace-string ";[; ]*" ";"
  !else
    !force replace-string ";[; ]*" ";\n\n"
    beginning-of-buffer
    !repeat
      !force search-buffer "mE" "\\<WITH [\\m]+ AS ?("
      !iif &not $status  !break
      !repeat
        insert-string "\n  "
        4 backward-char
        goto-matching-fence
        open-line
        forward-char
        !iif &not &xseq @wl "), ?\\([\\m]+ AS ?(\\).*"  !break
        2 forward-char
        open-line
        forward-char
        !iif &seq @wc " "  forward-delete-char
        &len @s1 forward-char
      !done
      forward-char
      open-line
      forward-char
      !iif &seq @wc " "  forward-delete-char
    !done
    set-variable #l0 120
    ; first trivial formating is to put all unions on their own line
    beginning-of-buffer
    !repeat
      !if &gre &set #l1 &len @wl #l0
        set-variable #l1 &sub #l1 &len &trl @wl
        set-variable #l2 $window-line
        -1 replace-string " \\(UNION\\( ALL\\)?\\) " &spr "\n%n\\1\n%n" #l1 " " #l1 " "
        ; if there was a UNION move the ORDER BY, which operated on the combined recordset onto its own line
        !iif &gre $window-line #l2  -1 replace-string " \\(ORDER BY\\) " &spr "\n%n\\1 " #l1 " "
      !endif
      !force forward-line
    !until &seq @wc ""
    ; second is to spit on any non-bracketed priority tags
    beginning-of-buffer
    !repeat
      !if &gre &len @wl #l0
        sql-format-line1
      !endif
      !force forward-line
    !until &seq @wc ""
    beginning-of-buffer
    !repeat
      set-variable #l1 &sub &len @wl &len &trl @wl
      1 sql-format-line2
    !until &seq @wc ""
  !endif
  set-variable #l1 0
  !while &gre #l9 &inc #l1 1
    beginning-of-buffer
    1 replace-string &spr "\x01%sX*\x02" #l1 ""
    insert-string &ind &spr ":%s:a%s" $buffer-bname #l1
  !done
  beginning-of-buffer
  !force fhook-sql
  set-mark
  end-of-buffer
  copy-region
  beginning-of-buffer
!emacro

buffer-init-fhook "sql"
