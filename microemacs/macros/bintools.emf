;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;
;
;  Author        : $Author: bill $
;  Created By    : Steven Phillips
;  Created       : 2009-10-09 11:49:57
;  Last Modified : <161109.0056>
;
;  Description
;
;  Notes
;
;  History
;
;  Copyright (c) 2009 Maxexam Ltd.
;
;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;

define-macro bin-txt-to-hex
  !force set-variable #l1 @1
  !if &not $status
    set-variable #l1 @ml "String"
  !endif
  !if &gre &len #l2 510
    ml-write "[ERROR: Invalid string given, too long to convert]"
    !abort
  !endif
  set-variable #l4 ""
  set-variable #l2 0
  !while &not &seq "" &set #l3 &mid #l1 &pinc #l2 1 1
    set-variable #l4 &spr "%s%02x" #l4 &atoi #l3
  !done
  set-variable $result &sup #l4
!emacro

define-macro bin-hex-to-txt
  !force set-variable #l1 @1
  !if &not $status
    set-variable #l1 @ml "Hex string"
  !endif
  !if &not &xse #l1 "\\h+"
    ml-write "[ERROR: Invalid hex string given, non-hex chars found]"
    !abort
  !elif &band &len #l1 1
    ml-write "[ERROR: Invalid hex string given, half byte given]"
    !abort
  !endif
  set-variable #l4 ""
  set-variable #l2 0
  !while &not &seq "" &set #l3 &mid #l1 &pinc #l2 2 2
    !if &not &seq #l3 "00"
      set-variable #l4 &cat #l4 &itoa &cat "0x" #l3
    !endif
  !done
  set-variable $result #l4
!emacro

0 define-macro bin-hex-to-dis-txt
  set-variable #l1 @1
  set-variable #l4 ""
  set-variable #l2 0
  0 set-char-mask "d"
  set-variable #l5 $result
  !while &not &seq "" &set #l3 &mid #l1 &pinc #l2 2 2
    !if &seq &set #l3 &itoa &cat "0x" #l3 ""
      set-variable #l4 &cat #l4 "."
    !elif &sin #l3 #l5
      set-variable #l4 &cat #l4 #l3
    !else
      set-variable #l4 &cat #l4 "."
    !endif
  !done
  set-variable $result #l4
!emacro

0 define-macro binary-get-line-info 
  !if &seq &trb @wl ""
    !if &not @?
      set-variable #p9 "||||"
    !elif @#
      !abort
    !else
      set-variable #p9 "|||0|"
    !endif
    !return
  !elif &not &xseq @wl "\\([\\h?]+\\):\\(\\( +\\h\\h\\)+ +\\)|  \\(.*\\)"
    !abort
  !endif
  set-variable #l1 @s1
  set-variable #l2 @s2
  set-variable #l3 @s4
  set-variable #l9 &rep #l2 " " ""
  set-variable #l8 &spr "|%s%n|%s|" &lef #l1 8 &sub 8 &len #l1 "?" #l9
  !if &not @?
    set-variable #p9 &cat #l8 "|"
  !elif &not @#
    set-variable #p9 &cat #l8 "0|"
  !elif &les @# &set #l4 &add 1 &len #l1
    !abort
  !elif &les @# &set #l5 &add #l4 &len #l2
    set-variable #l5 &len &rep &lef #l2 &sub @# #l4 " " ""
    !if &band #l5 1
      !abort
    !endif
    set-variable #p9 &cat #l8 &cat &div #l5 2 "|"
  !elif &les @# &set #l5 &add #l5 3
    !abort
  !else
    set-variable #l4 &sub @# #l5
    !if &gre &mul #l4 2 &len #l9
      !abort
    !endif
    set-variable #p9 &cat #l8 &cat #l4 "|"
  !endif
!emacro

0 define-macro binary-set-line
  set-variable #l1 @1
  set-variable #l2 @2
  bin-hex-to-dis-txt #l2
  set-variable #l3 $result
  set-variable #l2 &xrep #l2 "\\(..\\)" "\\1 "
  set-variable @wl &spr "%s?: %s%n |  %s" &lef #l1 7 #l2 &sub 48 &len #l2 " " #l3
!emacro

define-macro bin-move-backward
  !if &not &or &bmo "binary" &bmo "rbin"
    ml-write "[ERROR: Not a binary or rbin buffer]"
    !abort
  !endif
  !force set-variable #l5 @1
  !if &not $status
    set-variable #l5 @ml "Move backward by"
  !endif
  calc #l5
  set-variable #l5 .calc.result
  !if &bmo "binary"
    !force &set #l1 $window-col binary-get-line-info
    !if &not $status
      ml-write "[ERROR: Bad cursor position or badly formatted line]"
      !abort
    !endif
    set-variable #l5 &sub #l5 &lget #l9 3
    &div &add #l5 15 16 backward-line
    set-variable #l5 &sub 15 &mod &add #l5 15 16
    !if &gre #l1 58
      set-variable $window-col &add 62 #l5
    !else
      set-variable $window-col &add 10 &mul #l5 3
    !endif
  !else
    !if &band $window-col 1
      ml-write "[ERROR: Bad cursor position, currently in the middle of a character]"
      !abort
    !endif
    set-variable #l1 $window-line
    &mul #l5 2 backward-char
    set-variable #l2 $window-line
    &sub #l1 $window-line backward-char
    &sub #l2 $window-line backward-char
  !endif
!emacro       

define-macro bin-move-forward
  !if &not &or &bmo "binary" &bmo "rbin"
    ml-write "[ERROR: Not a binary or rbin buffer]"
    !abort
  !endif
  !force set-variable #l5 @1
  !if &not $status
    set-variable #l5 @ml "Move forward by"
  !endif
  calc #l5
  set-variable #l5 .calc.result
  !if &bmo "binary"
    !force &set #l1 $window-col binary-get-line-info
    !if &not $status
      ml-write "[ERROR: Bad cursor position or badly formatted line]"
      !abort
    !endif
    set-variable #l5 &add #l5 &lget #l9 3
    &div #l5 16 forward-line
    set-variable #l5 &mod #l5 16
    !if &gre #l1 58
      set-variable $window-col &add 62 #l5
    !else
      set-variable $window-col &add 10 &mul #l5 3
    !endif
  !else
    !if &band $window-col 1
      ml-write "[ERROR: Bad cursor position, currently in the middle of a character]"
      !abort
    !endif
    set-variable #l1 $window-line
    &mul #l5 2 forward-char
    set-variable #l2 $window-line
    &sub $window-line #l1 forward-char
    &sub $window-line #l2 forward-char
  !endif
!emacro       

define-macro bin-kill-region
  !if &not @#
    !return
  !elif &bmo "binary"
    !force $window-col binary-get-line-info
    !if &not $status
      ml-write "[ERROR: Bad cursor position or badly formatted line]"
      !abort
    !endif
    set-variable #l8 #l9
    set-variable #l0 $window-line
    exchange-point-and-mark
    !force $window-col binary-get-line-info
    !if &not $status
      exchange-point-and-mark
      ml-write "[ERROR: Bad mark position or badly formatted line]"
      !abort
    !endif
    !if &gre $window-line #l0
      exchange-point-and-mark
    !elif &gre #l0 $window-line
      set-variable #l7 #l8
      set-variable #l8 #l9
      set-variable #l9 #l7
    !elif &not &set #l1 &sub &lget #l9 3 &lget #l8 3
      !return
    !else
      !if &les #l1 0
        set-variable #l7 #l8
        set-variable #l8 #l9
        set-variable #l9 #l7
        set-variable #l1 &neg #l1
      !endif
      !if &equ &set #l1 &mul #l1 2 &len &lget #l8 2
        beginning-of-line
        set-mark
        forward-line
        -1 kill-region
        !if &gre @# 0
          set-variable @y &lget #l8 2
        !endif
      !else
        !if &gre @# 0
          set-variable @y &mid &lget #l8 2 &set #l7 &mul &lget #l8 3 2 #l1
        !endif
        set-variable #l2 &lget #l8 1
        set-variable #l3 &cat &lef &lget #l8 2 #l7 &rig &lget #l8 2 &add #l7 #l1
        binary-set-line #l2 #l3
        set-variable $window-col &add 10 &mul &lget #l8 3 3
        set-mark
        3 show-region
      !endif
      !return
    !endif
    !if &not &set #l0 &mul 2 &lget #l8 3
      set-variable #l1 ""
    !elif &les #l0 &len &lget #l8 2
      binary-set-line &lget #l8 1 &lef &lget #l8 2 #l0
      set-variable #l1 &rig &lget #l8 2 #l0
      forward-line
    !else
      set-variable #l1 ""
      forward-line
    !endif
    set-variable #l3 $window-line
    beginning-of-line
    exchange-point-and-mark
    !if &not &set #l4 &mul 2 &lget #l9 3
      set-variable #l2 ""
      set-variable #l5 0
    !elif &gre &set #l5 &sub &len &lget #l9 2 #l4 0
      !if &and #l0 &les &add #l0 #l5 33
        set-variable #l5 $window-line
        set-variable $window-line &dec #l3 1
        set-variable @wl ""
        set-mark
        set-variable $window-line #l5
        binary-set-line &lget #l8 1 &cat &lef &lget #l8 2 #l0 &rig &lget #l9 2 #l4
        set-variable #l5 &div #l0 2
      !else
        binary-set-line &lget #l9 1 &rig &lget #l9 2 #l4
        set-variable #l5 0
      !endif
      set-variable #l2 &lef &lget #l9 2 #l4
    !else
      set-variable #l2 ""
      set-variable #l5 0
      forward-line
    !endif
    beginning-of-line
    !if &gre $window-line #l3
      @# kill-region
      !if &gre @# 0
        set-position "\x82"
        !force 0 delete-buffer "*bin-del-tmp*"
        find-buffer "*bin-del-tmp*"
        yank
        beginning-of-buffer
        insert-string #l1
        insert-newline
        set-variable #l1 &len #l1
        !while &not &seq @wc "" 
          !if &seq &trb @wl ""
            beginning-of-line
            set-mark
            forward-line
            -1 kill-region
          !elif &not &xseq @wl "\\([\\h?]+\\):\\(\\( +\\h\\h\\)+ +\\)|  \\(.*\\)"
            set-variable #l0 &lef @wl 40
            !force 0 delete-buffer "*bin-del-tmp*"
            goto-position "\x82"
            ml-write &spr "[ERROR: Bad line found: %s...]" #l0
            !abort
          !else
            set-variable #l0 &rep @s2 " " ""
            !if &gre &add #l1 &len #l0 512
              set-variable @wl #l0
              set-variable #l1 &len #l0
              forward-line
            !else
              backward-line
              end-of-line
              insert-string #l0
              set-variable #l1 $window-col
              forward-line
              beginning-of-line
              set-mark
              forward-line
              -1 kill-region
            !endif
          !endif
        !done
        !if &seq #l2 ""
        !elif &gre &add #l1 &len #l2 512
          set-variable @wl #l2
        !else
          backward-line
          end-of-line
          insert-string #l2
        !endif
        beginning-of-buffer
        set-mark
        end-of-buffer
        -1 yank
        kill-region
        !force 0 delete-buffer "*bin-del-tmp*"
        goto-position "\x82"
      !endif
    !elif &gre @# 0
      set-variable @y &cat #l1 #l2
    !endif
    set-variable $window-col &add 10 &mul #l5 3
    set-mark
    3 show-region
  !elif &bmo "rbin"
    !if &band $window-col 1
      ml-write "[ERROR: Bad cursor position, currently in the middle of a character]"
      !abort
    !endif
    exchange-point-and-mark
    !if &band $window-col 1
      exchange-point-and-mark
      ml-write "[ERROR: Bad mark position, currently in the middle of a character]"
      !abort
    !endif
    exchange-point-and-mark
    @# kill-region
    !if &gre @# 0
      set-position "\x82"
      !force 0 delete-buffer "*bin-del-tmp*"
      find-buffer "*bin-del-tmp*"
      yank
      !if &gre $window-eline 1
        beginning-of-buffer
        !while &not &seq @wc ""
          end-of-line
          !if &equ $window-col 512
            forward-line
          !elif &gre $window-col 512
            set-variable $window-col 512
            insert-newline
          !elif &les &sub $window-eline $window-line 2
            forward-line
          !else
            forward-delete-char
          !endif
        !done
        beginning-of-buffer
        set-mark
        end-of-buffer
        -1 yank
        kill-region
      !endif
      !force 0 delete-buffer "*bin-del-tmp*"
      goto-position "\x82"
      end-of-line
      !if &gre $window-col 512
        set-variable $window-col 512
        insert-newline
        end-of-line
        set-variable #l4 $window-col
        forward-line
        end-of-line
        !if &seq @wc ""
        !elif &les &add $window-col #l4 513
          beginning-of-line
          backward-delete-char
        !endif
      !endif
      goto-position "\x82"
    !endif
  !else
    @# kill-region
    !if &gre @# 0
      set-position "\x82"
      !force 0 delete-buffer "*bin-del-tmp*"
      find-buffer "*bin-del-tmp*"
      yank
      !repeat
        !force backward-char
        !if $status
          !if &seq &set #l1 @wc "\n"
            insert-string "0A"
          !else
            forward-delete-char
            insert-string &spr "%02x" &atoi #l1
          !endif
          2 backward-char
        !endif
      !until &not $status
      !while &not &seq @wc "" 
        end-of-line
        !if &equ $window-col 512
          forward-line
        !elif &gre $window-col 512
          set-variable $window-col 512
          insert-newline
        !elif &les &sub $window-eline $window-line 2
          forward-line
        !else
          forward-delete-char
        !endif
      !done
      backward-char
      set-mark
      beginning-of-buffer
      -1 yank
      copy-region
      !force 0 delete-buffer "*bin-del-tmp*"
      goto-position "\x82"
    !endif
  !endif
!emacro
define-macro bin-yank-to-hex
  set-position "\x82"
  set-variable #l1 &cat &con &bmo "cr" "0D" "" &con &bmo "lf" "0A" ""
  !force 0 delete-buffer "*bin-tmp*"
  find-buffer "*bin-tmp*"
  yank
  !if @#
    beginning-of-buffer
    replace-string "^[\\h?]\\{8\\}: *" ""
    beginning-of-buffer
    replace-string "|.*$" ""
    beginning-of-buffer
    replace-string " " ""
    end-of-buffer
    backward-char
  !else
    !if &not $window-col
      insert-newline
    !endif
    insert-newline
    beginning-of-buffer
    !while &gre $window-eline &add $window-line 1
      !if &seq @wc "\n"
        insert-string #l1
      !else
        insert-string &spr "%02X" &ato @wc
      !endif
      forward-delete-char
      !if &gre $window-col 511
        insert-newline
      !endif
    !done
    backward-char
    end-of-line
    ; ignore last 0x0a
    &len #l1 backward-char
  !endif
  set-mark
  beginning-of-buffer
  copy-region
  !force 0 delete-buffer "*bin-tmp*"
  goto-position "\x82"
!emacro
define-macro bin-hex-yank-rbin-insert
  set-variable #l0 $window-line
  yank
  set-alpha-mark "\x81"
  set-variable #l1 $window-line
  set-variable $window-line #l0
  !repeat
    end-of-line
    !if &gre $window-col 512
      set-variable $window-col 512
      insert-newline
      set-variable #l1 &con &gre $window-line #l1 0 &add #l1 1
    !elif &les $window-col 512
      forward-delete-char
      set-variable #l1 &sub #l1 1
    !else
      forward-line
    !endif
  !until &gre $window-line #l1
  goto-alpha-mark "\x81"
!emacro
define-macro bin-hex-yank-bin-insert
  !force $window-col binary-get-line-info
  !if &not $status
    ml-write "[ERROR: Bad cursor position or badly formatted line]"
    !abort
  !elif &equ &mul &lget #l9 3 2 &len &lget #l9 2
    forward-line
    beginning-of-line
    set-variable #l9 "||0|"
  !endif
  set-position "\x82"
  !force 0 delete-buffer "*bin-tmp*"
  find-buffer "*bin-tmp*"
  yank
  exchange-point-and-mark
  !if &lget #l9 3
    insert-string &lef &lget #l9 2 &mul 2 &lget #l9 3
    end-of-buffer
    backward-char
    insert-string &rig &lget #l9 2 &mul 2 &lget #l9 3
    beginning-of-buffer
    set-variable #l0 &lget #l9 1
  !else
    set-variable #l0 "????????"
  !endif
  !while &not &seq @wc "" 
    end-of-line
    !if &band $window-col 1
      !force 0 delete-buffer "*bin-tmp*"
      goto-position "\x82"
      ml-write "[ERROR: Bad hex string found, odd number of chars]"
      !abort
    !elif &gre $window-col 31
      !if &gre $window-col 32
        set-variable $window-col 32
        insert-newline
        backward-char
      !endif
      binary-set-line #l0 @wl
      set-variable #l0 "????????"
      forward-line
    !elif &les &sub $window-eline $window-line 2
      !if $window-col
        binary-set-line #l0 @wl
      !endif
      forward-line
    !else
      forward-delete-char
    !endif
  !done
  set-mark
  beginning-of-buffer
  copy-region
  !force 0 delete-buffer "*bin-tmp*"
  goto-position "\x82"
  set-variable #l2 $window-line
  set-variable #l1 $window-col
  beginning-of-line
  !force !force yank
  !if &set #l0 $status
    !if &lget #l9 3
      set-mark
      forward-line
      -1 kill-region
    !endif
    set-mark
    set-variable $window-line #l2
    set-variable $window-col #l1
    exchange-point-and-mark
    !if &lget #l9 3
      &sub 17 &lget #l9 3 backward-char
      !if &les &set #l2 $window-col 62
        backward-line
        end-of-line
        &sub 62 #l2 backward-char
      !endif
      !if &les #l1 60
        binary-swap-section
      !endif
    !endif
  !endif
  -1 yank
  !return #l0
!emacro
define-macro bin-hex-yank-to-text
  set-position "\x82"
  !force 0 delete-buffer "*bin-tmp*"
  find-buffer "*bin-tmp*"
  yank
  insert-newline
  beginning-of-buffer
  set-variable #l2 0
  !while &gre $window-eline &add $window-line 1
    !if &set #l0 &isin @wc "0123456789abcdef"
      forward-delete-char
      !if &not &set #l1 &isin @wc "0123456789abcdef"
        ml-write "[ERROR: Current yank has bad hex byte code]"
        !abort
      !elif &and &equ #l2 13 &equ &set #l2 &add &mul &sub #l0 1 16 &sub #l1 1 10
      !elif &or &equ #l2 13 &equ #l2 10
        insert-newline
      !else
        insert-string &itoa #l2
      !endif
      forward-delete-char
    !elif &seq @wc "\n"
      forward-delete-char
    !else
      ml-write "[ERROR: Current yank has invalid hex character]"
      !abort
    !endif
  !done
  set-mark
  beginning-of-buffer
  copy-region
  !force 0 delete-buffer "*bin-tmp*"
  goto-position "\x82"
!emacro

define-macro bin-yank
  !if &les @# 1
    @# yank
    !return
  !elif &seq @y ""
    @# yank
    !return
  !elif &set #l0 &bmo "rbin"
    !if &band &set #l9 $window-col 1
      ml-write "[ERROR: Bad cursor position, currently in the middle of a character]"
      !abort
    !endif
  !elif &set #l0 &bmo "binary"
    !force $window-col binary-get-line-info
    !if &not $status
      ml-write "[ERROR: Bad cursor position or badly formatted line]"
      !abort
    !endif
  !endif
  !if &xseq @y "\\h\\h\\(\\h\\h\\|\n\\)*"
    set-variable #l1 2
  !elif &xseq &trb @y "\\h\\h\\( \\h\\h\\)+"
    set-variable #l1 1
  !elif &xseq @y "\\([?\\h]\\{8\\}:\\)? ?\\h\\h\\( \\h\\h\\)* +|  .\\{1,16\\}\n\\([?\\h]\\{8\\}:\\( \\h\\h\\)+ +|  .\\{1,16\\}\n\\)*\\([?\\h]\\{8\\}:\\( \\h\\h\\)+ ?\\)?"
    set-variable #l1 1
  !elif &xseq @y "\\([?\\h]\\{8\\}:\\)? ?\\h\\h\\( \\h\\h\\)* +|  .\\{1,16\\}\n\\([?\\h]\\{8\\}:\\( \\h\\h\\)+ +|  .\\{1,16\\}\n\\)\\{11\\}[\x01-\xff]*"
    set-variable #l1 1
  !elif &set #l1 &not #l0
    @# yank
    !return
  !endif
  !if &les #l1 2
    #l1 bin-yank-to-hex
  !endif
  !if &bmo "rbin"
    bin-hex-yank-rbin-insert
    !tjump &dec @# 1 -1
  !elif &bmo "binary"
    bin-hex-yank-bin-insert
    !tjump &dec @# 1 -1
  !else
    bin-hex-yank-to-text
    @# yank
    -1 yank
  !endif
  !if &les #l1 2
    -1 yank
  !endif
!emacro

define-macro bin-insert-hex-string
  !if &not &or &bmo "binary" &bmo "rbin"
    ml-write "[ERROR: Not a binary or rbin buffer]"
    !abort
  !endif
  !force set-variable #l1 @1
  !if &not $status
    set-variable #l1 @ml "Bin hex string"
  !endif
  set-variable #l1 &sup &rep #l1 "\n" ""
  !if &not &xse #l1 "\\h+"
    ml-write "[ERROR: Invalid hex string given, non-hex chars found]"
    !abort
  !elif &band &set #l2 &len #l1 1
    ml-write "[ERROR: Invalid hex string given, half byte given]"
    !abort
  !elif &bmo "binary"
    !force $window-col binary-get-line-info
    !if &not $status
      ml-write "[ERROR: Bad cursor position or badly formatted line]"
      !abort
    !endif
    set-variable #l0 &lget #l9 1
    set-variable #l3 &lget #l9 2
    set-variable #l4 &mul 2 &lget #l9 3
    set-variable #l5 &spr "%s%s%s" &lef #l3 #l4 #l1 &rig #l3 #l4
    set-variable #l3 &len #l5
    set-variable #l6 $window-line
    !while &gre #l3 0
      binary-set-line #l0 &lef #l5 32
      set-variable #l0 "????????"
      !if &gre &dec #l3 32 0
        end-of-line
        insert-newline
        set-variable #l5 &rig #l5 32
      !endif
    !done
    set-variable $window-line #l6
    binary-move-forward &add &lget #l9 3 &div #l2 2
  !else
    !if &band $window-col 1
      ml-write "[ERROR: Bad cursor position, currently in the middle of a character]"
      !abort
    !endif
    !while #l2
      !if &gre &set #l3 $window-col 511
        set-variable $window-col 512
        insert-newline
        set-variable $window-col &sub #l3 512
      !elif &gre &add #l3 #l2 512
        insert-string &lef #l1 &set #l4 &sub 512 #l3
        set-variable #l1 &rig #l1 #l4
        set-variable #l2 &sub #l2 #l4
        insert-newline
      !else
        insert-string #l1
        set-variable #l2 0
        set-alpha-mark "\x81"
        end-of-line
        !if &gre $window-col 512
          set-variable $window-col 512
          insert-newline
          end-of-line
          set-variable #l4 $window-col
          forward-line
          end-of-line
          !if &seq @wc ""
          !elif &les &add $window-col #l4 513
            beginning-of-line
            backward-delete-char
          !endif
        !endif
        goto-alpha-mark "\x81"
      !endif
    !done
  !endif
!emacro

define-macro bin-insert-string
  !if &not &or &bmo "binary" &bmo "rbin"
    ml-write "[ERROR: Not a binary or rbin buffer]"
    !abort
  !endif
  !force set-variable #l1 @1
  !if &not $status
    set-variable #l1 @ml "Bin String"
  !endif
  bin-txt-to-hex #l1
  set-variable #l2 $result
  bin-insert-hex-string #l2
!emacro


0 define-macro ibin-hex-search-forward
  set-variable #l1 @1
  !if &bmo "binary"
    set-variable #l3 &spr " %s " &lef #l1 2
    !repeat
      !force search-buffer "ME" #l3
      !if &not $status
        set-variable @# -1
      !else
        set-alpha-mark "\x82"
        set-variable #l4 2
        !while &les #l4 #l2
          !if &seq @wc " "
            forward-char
          !elif &seq @wc "|"
            beginning-of-line
            forward-line
            !force search-buffer "ME" ":"
          !elif &iseq @wc &mid #l1 &pinc #l4 1 1
            forward-char
            !tjump &not &iseq @wc &mid #l1 &pinc #l4 1 1 3
            forward-char
          !else
            ; not a match - move on
            goto-alpha-mark "\x82"
            backward-char
            set-variable #l4 &add #l2 1
          !endif
        !done
        !if &equ #l4 #l2
          ;found
          set-variable @# &sub @# 1
        !endif
      !endif
    !until &les @# 1
    !if @#
      goto-alpha-mark "\x81"
      ml-write "[Not found]"
      !abort
    !else
      set-alpha-mark "\x81"
      goto-alpha-mark "\x82"
      3 backward-char
      -3 show-region
      goto-alpha-mark "\x81"
      3 show-region
      ; 2 screen-update
    !endif
  !else
    set-alpha-mark "\x81"
    set-variable #l3 &lef #l1 2
    !repeat
      !force search-buffer "ME" #l3
      !if &not $status
        set-variable @# -1
      !elif &band $window-col 1
        backward-char
      !else
        set-alpha-mark "\x82"
        set-variable #l4 2
        !while &les #l4 #l2
          !if &seq @wc "\n"
            forward-char
          !elif &iseq @wc &mid #l1 &pinc #l4 1 1
            forward-char
          !else
            ; not a match - move on
            goto-alpha-mark "\x82"
            set-variable #l4 &add #l2 1
          !endif
        !done
        !if &equ #l4 #l2
          ;found
          set-variable @# &sub @# 1
        !endif
      !endif
    !until &les @# 1
    !if @#
      goto-alpha-mark "\x81"
      ml-write "[Not found]"
      !abort
    !else
      set-alpha-mark "\x81"
      goto-alpha-mark "\x82"
      2 backward-char
      -3 show-region
      goto-alpha-mark "\x81"
      3 show-region
      ; 2 screen-update
    !endif
  !endif
!emacro

define-macro bin-hex-search-forward
  !if &not &or &bmo "binary" &bmo "rbin"
    ml-write "[ERROR: Not a binary or rbin buffer]"
    !abort
  !elif &les @# 1
    ml-write "[ERROR: Numeric arg must be greater than zero]"
    !abort
  !endif
  !force set-variable #l1 @1
  !if &not $status
    set-variable #l1 @ml "Bin hex search"
  !endif
  set-variable #l1 &rep #l1 "\n" ""
  !if &not &xse #l1 "\\h+"
    ml-write "[ERROR: Invalid hex string given, non-hex chars found]"
    !abort
  !elif &band &set #l2 &len #l1 1
    ml-write "[ERROR: Invalid hex string given, half byte given]"
    !abort
  !endif
  @# ibin-hex-search-forward #l2
!emacro

define-macro bin-search-forward
  !if &not &or &bmo "binary" &bmo "rbin"
    ml-write "[ERROR: Not a binary or rbin buffer]"
    !abort
  !elif &les @# 1
    ml-write "[ERROR: Numeric arg must be greater than zero]"
    !abort
  !endif
  !force set-variable #l1 @1
  !if &not $status
    set-variable #l1 @ml15 "Bin search" @h4 0
  !endif
  bin-txt-to-hex #l1
  set-variable #l2 $result
  @# ibin-hex-search-forward #l2
!emacro
