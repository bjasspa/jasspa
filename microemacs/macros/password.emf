; -!- emf -!-
; This is part of the JASSPA MicroEmacs macro files
; Copyright (C) 1995-2023 JASSPA (www.jasspa.com)
; See the file me.emf for copying and conditions.
;
; Synopsis:    Password & random string utilites
; Authors:     Jon Green
;
; Convert a string of characters to the phonic alphabet.
; Typically used to convert a password into a unambiguous string.
define-macro password-to-phonic
  !force set-variable #l0 @1
  !iif &not $status  set-variable #l0 @ml0 "Enter password"
  set-variable #l1 0
  !while &set #l2 &atoi &mid #l0 &pinc #l1 1 1
    !iif &gre #l1 1 insert-string " - "
    !if &les #l2 32
      insert-string &cat "Control-" &itoa &add #l2 64
    !elif &les #l2 127
      insert-string &lget "|Space|Exclamation-mark|Quotation-mark|Hash-mark|Dollar-sign|Percent-sign|Ampersand|Apostrophe|Left-parenthesis|Right-paranthesis|Astrisk|Plus-sign|Comma|Hyphen|Period|Forward-slash|Zero|One|Two|Three|Four|Five|Six|Seven|Eight|Nine|Colon|Semicolon|Less-than|Equals-sign|Greater-than|Question-mark|Commercial-at-sign|ALPHA|BRAVO|CHARLIE|DELTA|ECHO|FOXTROT|GOLF|HOTEL|INDIA|JULIET|KILO|LIMA|MIKE|NOVEMBER|OSCAR|PAPA|QUEBEC|ROMEO|SIERRA|TANGO|UNIFORM|VICTOR|WHISKY|X-RAY|YANKEE|ZULU|Left-square-bracket|Backslash|Right-square-bracket|Caret|Underscore|Grave-accent|alpha|bravo|charlie|delta|echo|foxtrot|golf|hotel|india|juliet|kilo|lima|mike|november|oscar|papa|quebec|romeo|sierra|tango|uniform|victor|whisky|x-ray|yankee|zulu|Left-curly-brace|Vertical-bar|Right-curly-brace|Tilde|" &sub #l2 31
    !else
      insert-string &spr "UNKNOWN-CHAR-0x%x" #l2
    !endif
  !done
!emacro

define-macro generate-password
  !if @?
    set-variable #l1 &band @# 255
    set-variable @# &bxo @# #l1
  !else
    set-variable @# 0
    !force set-variable #l1 @1
    !iif &not $status set-variable #l1 @ml1 "Length" &band .lst 255
  !endif
  !if &or &les #l1 .wln &gre #l1 255
    ml-write &spr "[Password Error: Invalid length '%s', must be: %d <= len <= 255]" #l1 .wln
    !abort
  !endif
  !if &not @#
    !if @?
      !force set-variable #l3 @1
    !else
      !force set-variable #l3 @2
    !endif
    !if &not $status
      set-variable #l2 ""
      set-variable #l3 &div .lst 256
      set-variable #l4 1
      set-variable #l5 -1
      !while &len &set #l6 &mid "whdlu!bse" &inc #l5 1 1
        !iif &band #l4 #l3  set-variable #l2 &cat #l2 #l6
        set-variable #l4 &mul #l4 2
      !done
      set-variable #l3 @ml1 "Flags (whdlu!bseN)" #l2
    !endif
    set-variable #l2 0
    set-variable #l4 1
    set-variable #l5 -1
    !while &len &set #l6 &mid "whdlu!bseN" &inc #l5 1 1
      !iif &sin #l6 #l3  set-variable #l2 &add #l2 #l4
      set-variable #l4 &mul #l4 2
    !done
  !endif
  !if &and &band #l2 2 &band #l2 &bnot 530
    ml-write &spr "[Password Error: Cannot use hex (h) flag with any other]"
    !abort
  !elif &and &band #l2 1 &band #l2 8
    ml-write &spr "[Password Error: Cannot use word (w) flag with lowercase (l) flag]"
    !abort
  !endif
  !if &band #l2 512
  !elif &sub .lst &set #l9 &bor #l1 &mul #l2 256
    !force set-registry "/history/gen-passwd" &set .lst #l9
    !force mark-registry "/history" "u"
  !endif
  set-variable #l9 &cond &band #l2 128 " " ""
  !iif &band #l2 32  set-variable #l9 &cat #l9 "!\"#$%&'*+,-./:;=?@\\^_`|~"
  !iif &band #l2 64  set-variable #l9 &cat #l9 "()<>[]{}"
  !if &band #l2 1
    spell-rules-init
    !iif &seq #l9 ""  set-variable #l9 "-"
    !repeat
      set-variable #l4 ""
      set-variable #l3 #l1
      !repeat
        ;set-variable $debug 2
        !if &les #l3 &add .wln .wln
          set-variable #l5 #l3
        !else
          !repeat
            !iif &equ #l3 &set #l5 &add .wln &mod &div $random 0x800 &sub .wlx .wln  !break
            !if &les &sub #l3 #l5 .wln
            !elif &les #l5 #l3
              !break
            !endif
          !done
        !endif
        !if &not &set #l6 &lget .wlc #l5
          set-variable #l6 0
          set-variable $find-words &spr "%n" #l5 "\\l"
          !while &not &seq $find-words ""
            set-variable #l6 &add #l6 1
          !done
          !if &not #l6 
            ml-write &spr "[Password Error: No %d length words, setup spelling or don't use word (w) flag]" #l5
            !abort
          !endif
          set-variable .wlc &lset .wlc #l5 #l6
        !endif
        set-variable #l6 &mod &div $random 0x80 #l6
        set-variable $find-words &spr "%n" #l5 "\\l"
        !while &not &seq &set #l7 $find-words ""
        !until &les &dec #l6 1 0
        set-variable #l0 1
        set-variable #l3 &sub #l3 &len #l7
        !if &band #l2 16
          !iif &band 0x2000 $random  set-variable #l7 &cat &sup &lef #l7 1 &rig #l7 1
          !iif &band 0x0400 $random  set-variable #l7 &cat &lef #l7 -1 &sup &rig #l7 -1
        !endif
        !iif &len #l4  set-variable #l4 &cat #l4 &mid #l9 &mod &div $random 0x800 &len #l9 1
        set-variable #l4 &cat #l4 #l7
      !until &not #l3
      !if &not &band #l2 256
      !elif &and &band #l2 16 &not &xseq #l4 ".*[A-Z].*"
        set-variable #l4 ""
      !endif 
    !until &len #l4
    !iif &band #l2 4  set-variable #l4 &spr "%s%02d" #l4 &mod &div $random 0x800 100
    ml-write &spr "Len %d -> %s" #l1 #l4
    set-variable $result #l4
  !else
    !if &band #l2 2
      set-variable #l9 &cond &band #l2 16 "0123456789ABCDEF" "0123456789abcdef"
    !else
      !iif &band #l2 4   set-variable #l9 &cat #l9 "0123456789"
      !iif &band #l2 8   set-variable #l9 &cat #l9 "abcdefghijklmnopqrstuvwxyz"
      !iif &band #l2 16  set-variable #l9 &cat #l9 "ABCDEFGHIJKLMNOPQRSTUVWXYZ"
    !endif
    set-variable #l8 &len #l9
    set-variable #l4 ""
    !repeat
      set-variable #l3 #l1
      !repeat
        set-variable #l4 &cat #l4 &mid #l9 &mod &div $random 0x800 #l8 1
      !until &not &dec #l3 1
      !if &band #l2 256
        ; Test quality of password, must be comprised of lowercase, uppercase
        ; and numeric. 
        !if &and &band #l2 4 &not &xseq #l4 ".*[0-9].*"
          set-variable #l4 ""
        !elif &and &band #l2 8 &not &xseq #l4 ".*[a-z].*"
          set-variable #l4 ""
        !elif &and &band #l2 16 &not &xseq #l4 ".*[A-Z].*"
          set-variable #l4 ""
        !elif &and &band #l2 224 &not &xseq #l4 ".*[ -/:-@[-`{-~].*"
          set-variable #l4 ""
        !endif
      !endif
    !until &len #l4
    set-variable $result #l4
  !endif
!emacro

define-macro insert-password
  !if &band @# 1
    generate-password @1 @2
  !else
    .generate-password.lst generate-password
  !endif
  insert-string &set #l1 $result
  !if &band @# 2
  !elif &band .generate-password.lst 768
    ; don't do phonetic for word or hex passwords
  !elif &band .generate-password.lst 30720
    ; don't do phonetic if only numbers and spaces
    insert-string " ("
    password-to-phonic #l1
    insert-string ")"
  !endif
!emacro

set-variable .generate-password.lst &reg "/history/gen-passwd" 97288
set-variable .generate-password.wln 5
set-variable .generate-password.wlx 11
set-variable .generate-password.wlc &spr "|%n" .generate-password.wlx "|"
