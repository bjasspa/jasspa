; -!- emf -!-
;
; Copyright (C) 2000-2020 JASSPA (www.jasspa.com)
;
; This is part of JASSPA's MicroEmacs, see the LICENSE file for licensing and
; copying information.
;
; Synopsis:    Maven APT file hook
; Authors:     Jon Green
;
; Notes:       See docmacro.emf for documentation
;
define-macro fhook-apt
  @# buffer-init
  buffer-init-hooks
!emacro

; buffer-init variables
set-variable .fhook-apt.setup &reg "/history/fhook/apt" "ghijopsw"
set-variable .fhook-apt.setup-mask "aefghijkmnopstuwx"

; load docmacro.emf passing the apt setup flags
execute-file "docmacro" "apt"

!if &and &sin "h" .fhook-apt.setup &band .hilight.flags 0x02
  !iif &not &exist .hilight.apt  set-variable .hilight.apt  &pinc .hilight.next 1

  0 hilight &set #l0 .hilight.apt 0x00 $global-scheme
  ; Title
  hilight #l0 4 "^\\s+---+" "^\\s*$" "" .scheme.comment
  ; Headings
  hilight #l0 2 "^\\w.*" .scheme.function
  hilight #l0 2 "^\\*+ \\w.*" .scheme.function
  ; Lists
  hilight #l0 1 "^\\s+\\*" .scheme.operator
  hilight #l0 1 "^\\s+\\[\\[\\w+]]" .scheme.operator
  hilight #l0 1 "^\\s+\\[[^\\]]+]" .scheme.operator
  hilight #l0 1 "^\\s+\\[]" .scheme.type
  ; Verbatim
  hilight #l0 4 "^[-+]---[-+]+" "^[-+]---[-+]+" "" .scheme.quote
  ; Figure Caption
  hilight #l0 1 "^[[][^]]+[]]" .scheme.type
  ; Table
  hilight #l0 2 "^\\*--" .scheme.keyword
  ; Horizontal line
  hilight #l0 2 "^===+" .scheme.operator
  ; Page break
  hilight #l0 2 "^^L" .scheme.hlred
  ; Text styles
  hilight #l0 1 "<[^>]+>" .scheme.bold-italic
  hilight #l0 1 "<<[^>]+>>" .scheme.bold
  hilight #l0 1 "<<<[^>]+>>>" .scheme.string
  ; Links
  hilight #l0 1 "{[^}]+}" .scheme.prepro
  hilight #l0 1 "{{[^}]+}}" .scheme.keyword
  hilight #l0 1 "{{{[^}]+}\\w[^}]+}}" .scheme.keyword
  ; Bad links
  hilight #l0 1 "{[^}]+" .scheme.error
  hilight #l0 1 "{{[^}]+" .scheme.error
  hilight #l0 1 "{{{[^}]+" .scheme.error
  hilight #l0 1 "{{{[^}]+}\\w[^}]+" .scheme.error
  ; Line break
  hilight #l0 0 "\\\\\s*$" .scheme.bold-under
  ; Non breaking space
  hilight #l0 0 "\\\\ " .scheme.under
  ; Escaped special characters
  hilight #l0 0 "\\\\[[\\]<>{}=~+*\\\\]" .scheme.type
  hilight #l0 0 "\\\\-" .scheme.type
  hilight #l0 1 "\\\\\\w+" .scheme.type
  ; Comments
  hilight #l0 2 "^~~" .scheme.comment
!endif

buffer-init-fhook "apt"
