; -!- emf -!-
; This is part of the JASSPA MicroEmacs macro files
; Copyright (C) 1998-2004 JASSPA (www.jasspa.com)
; See the file me.emf for copying and conditions.
;
; Created:     Thu Sep 19 2002
; Synopsis:    Command-line macro for creating a Visual Basic tags file
; Authors:     Steven Phillips
;
; Usage:
;      me "@vbtags"
;      me "@vbtags" <files> <dirs>
;      me "@vbtags" "-v%tag-option=rm"
;      me "@vbtags" "-v%tag-option=r" <files> <dirs>
;
; Options:
;      v - variables, p - prototypes, r - recurse, a - append, m - multiple links to a tag
;
set-variable %tag-option   &cond &exi %tag-option   %tag-option   ""
set-variable %tag-filemask &cond &exi %tag-filemask %tag-filemask ":*.bas:*.cls:*.asp:"
set-variable %tag-ignore   &cond &exi %tag-ignore   %tag-ignore   ":SCCS/:CVS/:"

define-macro vbtags-add-tag
    set-variable #l1 @1
    set-variable #l3 @wl
    next-window
    !tjump &sin "m" %tag-option 4
    beginning-of-buffer
    !force search-forward &spr "^%s\t" #l1
    !if &not $status
        end-of-buffer
        insert-string #l1
        insert-string "\t"
        insert-string #p9
        insert-string "\t/^"
        insert-string #l3
        insert-string "$/"
    !endif
    next-window
!emacro
        
define-macro vbtags-add-file
    set-variable $buffer-mask "luh1"
    set-variable #l9 $buffer-fname
    !if &seq &lef #l9 #g1 #g2
        set-variable #l9 &rig #l9 #g1
    !endif
    ml-write &spr "[Parsing file %s]" #l9
    1 buffer-mode "magic"
    beginning-of-buffer
    !repeat
        !force search-forward "^[ \t]*\\(Private[ \t]+\\|Public[ \t]+\\)?\\(Function\\|Sub\\|Property[ \t]+\\(Get\\|Let\\)\\)[ \t]+\\(\\w+\\)[ \t]*("
        !if $status
            set-variable #l1 @s4
            vbtags-add-tag #l1
        !endif
    !until &not $status
    !if &sin "v" %tag-option
        beginning-of-buffer
        !repeat
            !force search-forward "^[ \t]*\\(Private\\|Public\\)[ \t]+\\(\\w+\\)[ \t]+As[ \t]"
            !if $status
                set-variable #l1 @s2
                vbtags-add-tag #l1
            !endif
        !until &not $status
        beginning-of-buffer
        !repeat
            !force search-forward "^[ \t]*\\(Private\\|Public\\)[ \t]+Const[ \t]+\\(\\w+\\)[ \t]+=[ \t]"
            !if $status
                set-variable #l1 @s2
                vbtags-add-tag #l1
            !endif
        !until &not $status
    !endif
    !if &sin "p" %tag-option
        beginning-of-buffer
        !repeat
            !force search-forward "^[ \t]*Declare[ \t]+\\(Function\\|Sub\\|Property[ \t]+\\(Get\\|Let\\)\\)[ \t]+\\(\\w+\\)[ \t]"
            !if $status
                set-variable #l1 @s3
                vbtags-add-tag #l1
            !endif
        !until &not $status
    !endif
!emacro

define-macro vbtags-parse-tags
    ml-write "All done! Parsing tags file."
    beginning-of-buffer
    set-mark
    replace-string "\\\\" "\\\\\\\\"
    end-of-buffer
    sort-lines
!emacro
    
define-macro vbtags-process-buffers
    !repeat
        set-variable #l0 $buffer-bname
        !if &xse #l0 "^.*<[0-9]*>$"
            set-variable #l1 &lef #l0 &sub &rsin "<" #l0 1
        !else
            set-variable #l1 #l0
        !endif
        !if &iseq #l1 "tags"
            !return
        !elif &lfind %tag-ignore #l1
        !elif &bmod "dir"
            set-variable #l2 $buffer-fname
            set-variable #l3 0
            !while &not &seq &set #l4 &lget %tag-filemask &inc #l3 1 ""
                !force find-file &cat #l2 #l4 
            !done
        !else
            beginning-of-buffer
            !force vbtags-add-file
            !if &not $status
                ml-write &spr "[Warning: vbtags-add-file failed on \"%s\"]" $buffer-fname
                !bell
                ;set-variable #l9 @cq
            !endif
        !endif
        0 delete-buffer #l0
    !until 0
!emacro

define-macro vbtags
    !if &seq &lget %tag-filemask 1 ""
        ml-write "[No input file mask set!]"
        !abort
    !endif
    delete-other-windows
    split-window-vertically
    find-buffer "*scratch*"
    find-file "tags"
    -1 buffer-mode "view"
    !force 0 delete-buffer "*scratch*"
    set-variable #g1 &rsin "/" $buffer-fname
    set-variable #g2 &lef $buffer-fname #g1
    !if &not &sin "a" %tag-option
        beginning-of-buffer
        set-mark
        end-of-buffer
        -1 kill-region
    !else
        end-of-buffer
    !endif
    !if &sin "r" %tag-option
        set-variable %tag-filemask &lins %tag-filemask -1 "[^.]*/"
    !endif
    1 buffer-mode "magic"
    1 buffer-mode "exact"
    next-window
    !if &iseq $buffer-bname "tags"
        !force find-file "."
    !endif
    vbtags-process-buffers
    vbtags-parse-tags
!emacro
  
define-macro start-up
    vbtags
    quick-exit
!emacro

; load in user extensions if found
!force execute-file "myvbtags"

