;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;
; bibtex hook - This file is invoked when a BibTeX file is loaded.
;
; Copyright (c) 1999-2001 JASSPA.
;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;

define-macro fhook-bibtex
    ; if arg is 0 this is a new file so add template 
    !if &not @#
        etfinsrt "bibtex"
    !endif
    buffer-initialize
    ; execute user extensions if enabled
    !if &exist my-fhook-bibtex
        my-fhook-bibtex
    !endif
!emacro

; buffer-initialize variables
set-variable .fhook-bibtex.name "BibTeX"
set-variable .fhook-bibtex.setup &reg "/history" "fhook/bibtex" "fhinsx"
set-variable .fhook-bibtex.setup-mask "aefhikmnstux"
set-variable .fhook-bibtex.command-flag  "|thbH|"
set-variable .fhook-bibtex.command-name  "|bibtex-swap-hilight|"
set-variable .fhook-bibtex.command-nbind "||"
set-variable .fhook-bibtex.command-kbind "|C-c C-h|"
set-variable .fhook-bibtex.command-desc  "|T&oggle hilighting|"

set-variable .fhook-bibtex.fold-open  "^@"
set-variable .fhook-bibtex.fold-close "^@"
set-variable .fhook-bibtex.fold-mopen "1"
set-variable .fhook-bibtex.fold-mnext "-1" 

!if &and &sin "h" .fhook-bibtex.setup &band .hilight.flags 0x02 
    !if &not &exist .hilight.bibtex
        set-variable .hilight.bibtex  &pinc .hilight.next 1
        set-variable .hilight.bibtexs &pinc .hilight.next 1
    !endif
    ; normal token hilighting    
    0 hilight .hilight.bibtex 0                       $global-scheme
    hilight .hilight.bibtex 0 "^\\s-*\\{@\\w*\\}{"    .scheme.bold
    hilight .hilight.bibtex 0 "^\\s-*\\{\\w*\\s-*\\}={*" .scheme.italic
    hilight .hilight.bibtex 0 "^\\s-*\\{\\w*\\s-*\\}=\\s-{*" .scheme.italic
    
    ; bibtex simulation hilighting 
    0 hilight .hilight.bibtexs 0                               $global-scheme
    hilight .hilight.bibtexs 0x44 "\\\\bibitem{" "" "}" "" ""  .scheme.bold
    hilight .hilight.bibtexs 0x44 "{\\\\em " "" "}" "" ""      .scheme.italic
    ;hilight .hilight.bibtexs 64 "\\\\newblock " ""       ; any idea ??
    ;hilight .hilight.bibtexs 0x44 "year = " "Y: " "," ""  .scheme.bold
    ;hilight .hilight.bibtexs 0x44 "@ARTICLE{" "*" "," "*" "" .scheme.hlred

    0 define-macro bibtex-swap-hilight
        !if &equal $buffer-hilight .hilight.bibtex
            set-variable $buffer-hilight .hilight.bibtexs
            1 buffer-mode "view"
        !else
            set-variable $buffer-hilight .hilight.bibtex
            -1 buffer-mode "view"
        !endif
    !emacro
!endif

ml-write "[BibTeX file hook loaded]"

; load in user extensions if found
!force execute-file "mybibtex"

