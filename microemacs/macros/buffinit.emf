; -!- emf -!-
; This is part of the JASSPA MicroEmacs macro files
; Copyright (C) 2000-2004 JASSPA (www.jasspa.com)
; See the file me.emf for copying and conditions.
;
; Created:     Fri Nov 24 2000
; Synopsis:    Buffer initialization routines
; Authors:     Steven Phillips
;
define-macro-file spellaut auto-spell auto-spell-ignore auto-spell-buffer auto-spell-reset
define-macro-file collapse collapse-current collapse-all
0 define-macro-file fold fold-setup

0 define-macro buffer-init-modes
    set-variable #l1 &ind &spr ".%s.setup" $buffer-fhook
    ; setup buffer modes
    !if &sin "e" #l1
        1 buffer-mode "exact"
    !elif &sin "E" #l1
        -1 buffer-mode "exact"
    !endif
    !if &sin "m" #l1
        1 buffer-mode "magic"
    !elif &sin "M" #l1
        -1 buffer-mode "magic"
    !endif
    !if &sin "d" #l1
        ; if auto-indent enabled don't enable indent mode
        !if &sin ">" &ind &spr ".%s.setup-mask" $buffer-fhook
            1 buffer-mode "cmode"
        !else
            set-variable $buffer-indent &ind &cat ".hilight." &rig $buffer-fhook 6
        !endif
    !elif &sin "i" #l1
        1 buffer-mode "indent"
    !elif &sin "I" #l1
        -1 buffer-mode "indent"
    !endif
    !if &sin "j" #l1
        1 buffer-mode "justify"
    !elif &sin "J" #l1
        -1 buffer-mode "justify"
    !endif
    !if &sin "w" #l1
        1 buffer-mode "wrap"
    !elif &sin "W" #l1
        -1 buffer-mode "wrap"
    !endif
    !if &sin "t" #l1
        1 buffer-mode "tab"
    !elif &sin "T" #l1
        -1 buffer-mode "tab"
    !endif
    !if &sin "x" #l1
        1 buffer-mode "time"
    !elif &sin "X" #l1
        -1 buffer-mode "time"
    !endif
    !if &sin "a" #l1
        1 buffer-mode "auto"
    !elif &sin "A" #l1
        -1 buffer-mode "auto"
    !endif
    !if &sin "k" #l1
        1 buffer-mode "backup"
    !elif &sin "K" #l1
        -1 buffer-mode "backup"
    !endif
    ; setup fence displaying - is the dmf extension being used?
    !if .display-matching-fence.dmf
        set-variable &ind &spr ".%s.dmf" $buffer-fhook &cond &sin "G" #l1 0 .display-matching-fence.dmf
    !elif &sin "G" #l1
        -1 buffer-mode "fence"
    !endif
    !if &sin "u" #l1
        1 buffer-mode "undo"
    !elif &sin "U" #l1
        -1 buffer-mode "undo"
    !endif
!emacro

; setup buffer bindings
; Create a buffer binding
;     buffer-bind-create <flag> <kbind> <narg> <command>
0 define-macro buffer-bind-create
    set-variable #l1 @1
    set-variable #l2 @2
    !if &not &sin "i" #l1
        !if &exi .buffer-bind-key.map-from
            !if &not &set #l0 &lfind .buffer-bind-key.map-from #l2
                !return
            !elif &seq &set #l2 &lget .buffer-bind-key.map-to #l0 ""
                !return
            !endif
        !endif
    !endif
    !if &not &sin "o" #l1
        !if &not &seq &cbind #l2 "ERROR"
            !return
        !endif
    !endif
    set-variable #l3 @3
    set-variable #l4 @4
    !if &seq #l3 ""
        buffer-bind-key #l4 #l2
    !else
        #l3 buffer-bind-key #l4 #l2
    !endif
!emacro

0 define-macro buffer-init
    set-variable $buffer-fhook &cat "fhook-" @1
    set-variable #l0 &ind &spr ".%s.setup" $buffer-fhook
    !if &not @#
        !if &sin "n" #l0
            !force set-variable #l1 @2
            set-variable #l1 &cond $status #l1 @1
            etfinsrt #l1
        !endif
    !endif
    set-variable #l1 &ind &spr ".%s.command-flag"  $buffer-fhook
    set-variable #l2 &ind &spr ".%s.command-kbind" $buffer-fhook
    set-variable #l3 &ind &spr ".%s.command-nbind" $buffer-fhook
    set-variable #l4 &ind &spr ".%s.command-name"  $buffer-fhook
    buffer-init-modes
    set-variable #l7 0
    !if &sin "h" #l0
        !if &band .hilight.flags 0x02 
            !jump 2
        !else &and &band .hilight.flags 0x01 &sin "1" &ind &spr ".%s.setup-mask" $buffer-fhook
            set-variable #l7 1
            set-variable $buffer-hilight &ind &cat ".hilight." &rig $buffer-fhook 6
        !endif
    !endif
    !if &sin "b" #l0
        !force buffer-abbrev-file &rig $buffer-fhook 6
    !endif
    set-variable #l6 0
    !while &not &seq "" &set #l5 &lget #l1 &inc #l6 1
        !if &sin "b" #l5
            !if &not &sin "H" #l5
                !jump 2
            !elif #l7
                !force buffer-bind-create #l5 &lget #l2 #l6 &lget #l3 #l6 &lget #l4 #l6
            !endif
        !endif
    !done
    !if &sin "d" #l0
        buffer-bind-create "bi" "C-tab" "" normal-tab
    !endif
    set-variable #l5 &ind &spr ".%s.comment" $buffer-fhook
    !if &not &seq &lget #l5 1 ""
        buffer-bind-create "b" "C-c C-s" "" comment-start
        buffer-bind-create "b" "C-c C-c" "" comment-line
        buffer-bind-create "b" "C-c C-d" "" uncomment-line
        buffer-bind-create "b" "C-c C-e" "" comment-to-end-of-line
        buffer-bind-create "b" "C-c C-r" "" comment-restyle
    !endif
    !if &or &exi &ind &spr ".%s.fold" $buffer-fhook &sin "fold-open:" $result
        !force 0 fold-setup $result
        !if &not $status
            !jump 3
        !endif
    !elif &sin "f" #l0
        buffer-bind-create "b" "f2" "" collapse-current 
        buffer-bind-create "b" "f3" "" collapse-all 
    !endif
    !if &sin "s" #l0
        !if &not &seq %language "Default"
            buffer-bind-create "b" "f5"     0  auto-spell
            buffer-bind-create "b" "S-f5"   "" auto-spell-buffer 
            buffer-bind-create "b" "esc f5" "" auto-spell-reset 
            buffer-bind-create "b" "f6"     1  auto-spell-ignore 
            buffer-bind-create "b" "S-f6"   2  auto-spell-ignore 
            !if &reg "/history" "spell/autospell" "0"
                1 auto-spell
            !endif
        !endif
    !endif
!emacro

0 define-macro buffer-init-hooks
    ; execute user extensions if defined
    set-variable #l0 &rig $buffer-fhook 6
    !if &exist &cat "my-fhook-" #l0
        execute-named-command &cat "my-fhook-" #l0
    !endif
    !if &exist &cat "my-bhook-" #l0
        !if &seq $buffer-bhook ""
            set-variable $buffer-bhook &cat "my-bhook-" #l0
        !endif
    !endif
    !if &exist &cat "my-ehook-" #l0
        !if &seq $buffer-ehook ""
            set-variable $buffer-ehook &cat "my-ehook-" #l0
        !endif
    !endif
!emacro

0 define-macro buffer-init-fhook
    set-variable #l9 @1
    !if &not &seq &set #l2 &reg "/history" &spr "fhook/%s-fomark" #l9 "" ""
        set-variable #l3 &reg "/history" &spr "fhook/%s-fcmark" #l9 ""
        !if &not &seq &set #l1 &lget &set #l4 &ind &spr ".fhook-%s.comment" #l9 1 ""
            set-variable #l0 &spr "\b%s %s\b%s %s\b%s\b" #l1 #l2 #l1 #l3 &lget #l4 2
            str-to-regex #l1
            str-to-regex #l2
            str-to-regex #l3
            set-variable #l0 &spr "%s%s[ \t]*%s\b%s[ \t]*%s\b" #l0 #l1 #l2 #l1 #l3
            set-variable &ind &spr ".fhook-%s.fold" #l9 #l0
        !endif
    !endif
    set-variable #l1 &ind &spr ".fhook-%s.name" #l9
    !if &seq #l1 "ERROR"
        set-variable #l1 &cat &sup &lef #l9 1 &rig #l9 1
    !endif
    ml-write &spr "[%s file hook loaded]" #l1
    ; load in user extensions if found
    !force execute-file &cat "my" #l9
!emacro
