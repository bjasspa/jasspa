; -!- emf -!-
; This is part of the JASSPA MicroEmacs macro files
; Copyright (C) 2000-2002 JASSPA (www.jasspa.com)
; See the file me.emf for copying and conditions.
;
; Created:     Thu Nov 23 2000
; Synopsis:    buffer-setup macros & GUI
; Authors:     Steven Phillips
; Notes:
;
;     The following flags are used in the .setup & .setup-mask variables:
;
;         a auto mode
;         b abbreviation
;         c Viewer or author
;         d auto indent
;         e exact mode
;         f fold support
;         g fence mode
;         h hilight
;         i indent
;         j justify
;         k backup
;         m magic mode
;         n New buffer header
;         o tool menu
;         p Help page
;         s auto spell
;         t tab mode
;         u undo
;         w wrap
;         x time mode
;         > Special internal flag indicating that indentation is
;           to be done by using cmode buffer mode (only add to .setup-mask).
;         1 Special internal flag indicating that hilighting is used
;           for reformatting, i.e. make readable 
;
;    In .setup, a 'A' means disable auto indent, a enables it.
;    In .setup-mask a lower case letter must be used.
;
;    For the buffer bindings, tool menu and help page the following list
;    variable are required, one item for each component:
;
;         .command-flag   - A flag indicating the components use
;         .command-name   - The command name
;         .command-nbind  - The numeric argument for the buffer binding
;         .command-kbind  - The key for the buffer binding
;         .command-desc   - Brief description of the command
;
;    The following flags are used for each component of .command-flag:
;
;         t Show in tool
;         h show in help
;         b binding
;         o override any existing binding
;         i ignore emulation key mappings
;         H Only create binding if Hilighting is enabled
;
!if &not &exi .osd.buffstp
    set-variable .osd.buffstp &pinc .osd.next 1
!endif

0 define-macro buffer-setup-option
    !if &les @# 0
        set-variable @# &abs @#
        set-variable #l0 &lget .buffer-setup.flags @#
        set-variable #l1 .buffer-setup.nsetup
        !force @# buffer-setup-option
        set-variable #l2 $status
        set-variable #l1 &rep #l1 #l0 ""
        set-variable #l1 &rep #l1 &sup #l0 ""
        !if #l2
            set-variable #l0 &sup #l0
        !endif
        set-variable .buffer-setup.nsetup &cat #l1 #l0
    !else
        set-variable #l0 &lget .buffer-setup.flags @#
        !if &sin #l0 .buffer-setup.nsetup
            !return
        !elif &sin &sup #l0 .buffer-setup.nsetup
            !abort
        !elif &seq &set #l1 &lget .buffer-setup.modes @# ""
            !abort
        !elif &not &gmod #l1
            !abort
        !endif
    !endif
!emacro
0 define-macro buffer-setup-save
    !if &not &seq .buffer-setup.nsetup .buffer-setup.osetup
        set-registry "/history" &cat "fhook/" &rig $buffer-fhook 6 .buffer-setup.nsetup
        set-variable &ind &spr ".%s.setup" $buffer-fhook .buffer-setup.nsetup
        set-variable .buffer-setup.osetup .buffer-setup.nsetup
    !endif
    !if &gre @# 1
        !force @# execute-file &cat "hk" &rig $buffer-fhook 6
        !if $status
            set-variable $buffer-hilight 0
            set-variable $buffer-indent 0
            execute-named-command $buffer-fhook
        !else
            osd-dialog "Buffer Setup" "Failed to re-initialize buffer hook!" "  &OK  "
        !endif
    !endif
!emacro
define-macro buffer-setup
    set-variable .mask &ind &spr ".%s.setup-mask" $buffer-fhook
    !if &seq .mask "ERROR"
        !bell
        ml-write "[buffer-setup not supported by current buffer type]"
        !abort
    !endif
    set-variable .osetup &ind &spr ".%s.setup" $buffer-fhook
    set-variable #l9 &ind &spr ".%s.name" $buffer-fhook
    !if &seq #l9 "ERROR"
        set-variable #l9 &cat &sup &mid $buffer-fhook 6 1 &rig $buffer-fhook 7
    !endif
    -1 osd .osd.buffstp
    osd .osd.buffstp 0 "batcdHs" 9 3 47 10 -1 -1 930 .scheme.osd-title &cat #l9 " Buffer Setup"
    osd .osd.buffstp 5 ""
    set-variable #l0 1
    set-variable #l1 0
    set-variable #l2 0
    set-variable #l3 0
    !repeat
        set-variable #l5 &lget .labels #l0
        !if &sin &lget .flags #l0 .mask
            !if &not &seq #l5 ""
                set-variable #l6 &sub 22 &add &len #l5 &cond &sin "&" #l5 0 1
                osd .osd.buffstp &mul #l0 10 "Sfh" &spr "%n%s: " #l6 " " #l5 &add 5 &mul #l0 10
                set-variable #l2 1
            !elif &gre &inc #l2 1 2
                osd .osd.buffstp &mul #l0 10 ""
                osd .osd.buffstp &add 1 &mul #l0 10 "fh" &spr "%n  " 21 " "
                set-variable #l2 1
            !endif
            set-variable #l6 "Ctfx"
            !if &set #l3 &seq &lget .labels &add #l0 1 ""
                set-variable #l6 &cat #l6 "h"
                osd .osd.buffstp &add 8 &mul #l0 10 "fh" "  "
            !endif
            !if &seq &set #l5 &lget .prefix #l0 ""
                osd .osd.buffstp &add 5 &mul #l0 10 #l6 &mul #l0 10 "^^NY^^" #l0 buffer-setup-option
            !else
                osd .osd.buffstp &add 5 &mul #l0 10 #l6 &mul #l0 10 &spr " [ *]^%s" #l5 #l0 buffer-setup-option
            !endif
            set-variable #l1 1
        !elif &not &seq #l5 ""
            set-variable #l7 #l0
            set-variable #l6 0
            !if &seq &lget .labels &inc #l7 1 ""
                !tjump &not &sin &lget .flags #l7 .mask -1
                set-variable #l6 1
            !endif
            !if #l6
                set-variable #l6 &sub 22 &add &len #l5 &cond &sin "&" #l5 0 1
                osd .osd.buffstp &mul #l0 10 "Sfh" &spr "%n%s: " #l6 " " #l5 &add 5 &mul #l0 10
                set-variable #l2 0
            !endif
        !elif #l3
            !if &not &seq &lget .labels &add #l0 1 ""
                osd .osd.buffstp &add -2 &mul #l0 10 ""
            !endif
        !endif
        !if &and #l1 &lget .blank #l0
            osd .osd.buffstp &add 9 &mul #l0 10 ""
            set-variable #l1 0
        !endif
    !until &seq &lget .flags &inc #l0 1 ""
    osd .osd.buffstp 900 ""
    osd .osd.buffstp 910 "BtcfHhx" 910 .scheme.osd-ebtt " &Save " 1 buffer-setup-save
    osd .osd.buffstp 920 "BtcfHh"  920 .scheme.osd-ebtt " &Current " 2 buffer-setup-save
    osd .osd.buffstp 930 "BtcfH"   930 .scheme.osd-ebtt " E&xit " f void
    set-variable .nsetup .osetup
    .osd.buffstp osd
!emacro
set-variable .buffer-setup.flags  "|p|o|c|n|g|h|d|s|f|b|e|m|a|k|i|j|t|x|u|w|"
set-variable .buffer-setup.blank  "|0|0|0|1|0|0|0|0|1|1|0|0|0|0|0|0|0|0|0|1|"
set-variable .buffer-setup.labels "|Create Help &Page|Create T&ools Menu|Use Author Mode|Insert &New Template|Fence Display|Setup &Hilighting|Setup Auto In&dent|Setup Auto Spe&ll|Setup &Folding|Add A&bbreviations|Search Modes||Buffer Modes||||||||x|"
set-variable .buffer-setup.prefix "|||||||||||  &Exact|  &Magic|   &Auto| Bac&kup| &Indent|&Justify|    &Tab|   Time|   &Undo|   &Wrap|"
set-variable .buffer-setup.modes  "|||||||||||exact|magic|auto|backup|indent|justify|tab|time|undo|wrap|"

; buffer-help-add-item <command-name> <narg> <desc>
; adds the item to the help page (.osd.tmp) using and incrementing
; the osd item counter in #p9
0 define-macro buffer-help-add-item
    set-variable #l1 @1
    set-variable #l2 @2
    set-variable #l3 @3
    !if &seq "" #l1
        set-variable #l8 ""
    !else
        !if &seq &set #l8 &kbin #l2 #l1 ""
            osd .osd.tmp &inc #p9 1 "" &spr "    %s%s%s  " #l2 &con &seq #l2 "" "" " " #l1
            set-variable #l8 "          - "
        !else
            set-variable #l6 &len #l8
            set-variable #l8 &spr "  %s%n - " &rep #l8 "&" "\\&" &sub 7 #l6 " "
        !endif
    !endif
    !if &not &seq "" #l3
        set-variable #l3 &rep #l3 "\\\\" "\bY"
        set-variable #l3 &rep #l3 "\\&" "\bZ"
        set-variable #l3 &rep #l3 "&" ""
        set-variable #l3 &rep #l3 "\bZ" "\\&"
        set-variable #l3 &rep #l3 "\bY" "\\\\"
        set-variable #l8 &cat #l8 #l3
    !endif
    osd .osd.tmp &inc #p9 1 "" &spr "  %s  " #l8
!emacro

define-macro buffer-help
    set-variable #l0 &ind &spr ".%s.setup" $buffer-fhook
    set-variable #l1 &ind &spr ".%s.command-flag"  $buffer-fhook
    set-variable #l2 &ind &spr ".%s.command-nbind" $buffer-fhook
    set-variable #l3 &ind &spr ".%s.command-name"  $buffer-fhook
    set-variable #l4 &ind &spr ".%s.command-desc"  $buffer-fhook
    !if &not &sin "p" #l0
        !if &sin "p" &ind &spr ".%s.setup-mask" $buffer-fhook
            ml-write "[Help page disabled]"
        !else
            ml-write "[Help page not available for this buffer]"
        !endif
        !abort
    !endif
    set-variable #l9 &ind &spr ".%s.name" $buffer-fhook
    !if &seq #l9 "ERROR"
        set-variable #l9 &cat &sup &mid $buffer-fhook 6 1 &rig $buffer-fhook 7
    !endif
    -1 osd .osd.tmp
    osd .osd.tmp 0  "batcdH" 9 3 1001 .scheme.osd-title &cat #l9 " Mode Help"
    osd .osd.tmp 1  "" 
    set-variable #l9 10
    !if &sin "p" #l0
        buffer-help-add-item "buffer-help" "" "View This Help Page"
        buffer-help-add-item "" "" ""
    !endif
    !if &sin "d" #l0
        buffer-help-add-item "normal-tab" "" "Insert a Normal Tab"
    !endif
    set-variable #l7 0
    !while &not &seq "" &set #l5 &lget #l1 &inc #l7 1
        !if &sin "h" #l5
            buffer-help-add-item &lget #l3 #l7 &lget #l2 #l7 &lget #l4 #l7
        !endif
    !done
    !if &not &seq &lget &ind &spr ".%s.comment" $buffer-fhook 1 ""
        buffer-help-add-item "" "" ""
        buffer-help-add-item "comment-start" "" "Start Comment"
        buffer-help-add-item "comment-line" "" "Comment Out Line"
        buffer-help-add-item "uncomment-line" "" "&Delete Comment"
        buffer-help-add-item "comment-to-end-of-line" "" "Comment to &End of Line"
        buffer-help-add-item "comment-restyle" "" "Restyle Current Comment"
    !endif
    !if &sin "f" #l0
        buffer-help-add-item "" "" ""
        buffer-help-add-item "fold-current" "" "Open/Close Current Fold"
        buffer-help-add-item "fold-all" "" "Open/Close All Folds"
    !endif
    !if &sin "s" #l0
        buffer-help-add-item "" "" ""
        buffer-help-add-item "auto-spell" "0" "Enable/Disable Auto-Spell"
        buffer-help-add-item "auto-spell-buffer" "" "Auto-Spell Buffer"
        buffer-help-add-item "auto-spell-reset" "" "Reset Auto-Spell"
    !endif
    !if &exi &ind &spr ".%s.item-list-s1" $buffer-fhook
        buffer-help-add-item "" "" ""
        buffer-help-add-item "item-list" "" "List Functions"
        buffer-help-add-item "item-list-close" "" "Close List Items window"
    !endif
    !if &exi &ind &spr ".%s.tags" $buffer-fhook
        buffer-help-add-item "" "" ""
        buffer-help-add-item "" "" "Tag generation is available under the Tools menu"
    !endif
    osd .osd.tmp 1000 "" 
    osd .osd.tmp 1001 "BcfH" .scheme.osd-ebtt "  &OK  " f void
    .osd.tmp osd
!emacro

; buffer-tool-add-item <command-name> <narg> <desc> <desc-len>
; adds the item to the tool menu (.osd.tmp) using and incrementing
; the osd item counter in #p9
0 define-macro buffer-tool-add-item
    set-variable #l1 @1
    set-variable #l2 @2
    set-variable #l3 @3
    !if &seq "" #l1
        !if &seq "" #l3
            osd .osd.tmp &inc #p9 1 "-"
        !else
            osd .osd.tmp &inc #p9 1 "c-" &spr " %s "#l3
        !endif
    !else
        !if &seq "" #l3
            set-variable #l3 #l1
        !endif
        !if &seq &set #l7 #l2 ""
            set-variable #l7 "f"
        !endif
        !if &sin " " #l1
            osd .osd.tmp &inc #p9 1 "i" #l3 #l7 #l1
        !else
            set-variable #l5 &rep #l3 "&" ""
            set-variable #l5 &spr "%s%n" #l3 &sub @4 &len #l5 " "
            set-variable #l6 &kbin #l2 #l1
            !if &not &seq #l6 ""
                set-variable #l5 &cat #l5 &rep #l6 "&" "\\&"
            !endif
            osd .osd.tmp &inc #p9 1 "" #l5 #l7 #l1
        !endif
    !endif
!emacro

0 define-macro buffer-tool
    set-variable #l0 &ind &spr ".%s.setup" $buffer-fhook
    set-variable #l5 &ind &spr ".%s.comment" $buffer-fhook
    set-variable #l1 &ind &spr ".%s.command-flag"  $buffer-fhook
    set-variable #l2 &ind &spr ".%s.command-nbind" $buffer-fhook
    set-variable #l3 &ind &spr ".%s.command-name"  $buffer-fhook
    set-variable #l4 &ind &spr ".%s.command-desc"  $buffer-fhook
    -1 osd .osd.tmp
    osd .osd.tmp 0  "b"
    set-variable #l7 0
    !if &sin "s" #l0
        set-variable #l8 25
    !elif &sin "f" #l0
        set-variable #l8 23
    !elif &not &seq &lget #l5 1 ""
        set-variable #l8 22
    !elif &sin "d" #l0
        set-variable #l8 19
    !elif &sin "p" #l0
        set-variable #l8 14
    !else
        set-variable #l8 0
    !endif
    !while &not &seq "" &set #l6 &lget #l1 &inc #l7 1
        !if &sin "t" #l6
            set-variable #l6 &rep &lget #l4 #l7 "&" ""
            !if &gre &set #l6 &len #l6 #l8
                set-variable #l8 #l6
            !endif
        !endif
    !done
    set-variable #l8 &add #l8 1
    set-variable #l9 1
    !if &sin "p" #l0
        buffer-tool-add-item "buffer-help" "" "View &Help Page" #l8
    !endif
    !if &sin "d" #l0
        buffer-tool-add-item "normal-tab" "" "Insert a Normal Tab" #l8
    !endif
    set-variable #l7 0
    !while &not &seq "" &set #l6 &lget #l1 &inc #l7 1
        !if &sin "t" #l6
            buffer-tool-add-item &lget #l3 #l7 &lget #l2 #l7 &lget #l4 #l7 #l8
        !endif
    !done
    !if &not &seq &lget #l5 1 ""
        buffer-tool-add-item "" "" "" #l8
        buffer-tool-add-item "comment-start" "" "Start Comment" #l8
        buffer-tool-add-item "comment-line" "" "Comment Out Line" #l8
        buffer-tool-add-item "uncomment-line" "" "&Delete Comment" #l8
        buffer-tool-add-item "comment-to-end-of-line" "" "Comment to &End of Line" #l8
        buffer-tool-add-item "comment-restyle" "" "Restyle Comment" #l8
    !endif
    !if &sin "f" #l0
        buffer-tool-add-item "" "" "" #l8
        buffer-tool-add-item "fold-current" "" "Open/Close Current Fold" #l8
        buffer-tool-add-item "fold-all" "" "Open/Close All Folds" #l8
    !endif
    !if &sin "s" #l0
        buffer-tool-add-item "" "" "" #l8
        buffer-tool-add-item "auto-spell" "0" "Enable/Disable Auto-Spell" #l8
        buffer-tool-add-item "auto-spell-buffer" "" "Auto-Spell Buffer" #l8
        buffer-tool-add-item "auto-spell-reset" "" "Reset Auto-Spell" #l8
    !endif
    !if &set #l7 &exi &ind &spr ".%s.item-list-s1" $buffer-fhook
        buffer-tool-add-item "" "" "" #l8
        buffer-tool-add-item "item-list" "" "List Functions" #l8
        buffer-tool-add-item "item-list-close" "" "Close List Items" #l8
    !endif
    !if &exi &ind &spr ".%s.tags" $buffer-fhook
        !if &not #l7
            buffer-tool-add-item "" "" "" #l8
        !endif
        buffer-tool-add-item "generate-tags-file" "0" "Generate &Tags File" #l8
    !endif
!emacro

