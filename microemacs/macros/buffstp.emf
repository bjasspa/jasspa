;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;
;
;  Created By    : Steven Phillips
;  Created       : Thu Nov 23 21:19:31 2000
;  Last Modified : <001203.1638>
;
;  Description
;
;  Notes
;
;     The following flags are used:
;
;         a auto mode
;         b abbreviation
;         c Viewer or author
;         d auto indent
;         e exact mode
;         f fold support
;         h hilight
;         i indent
;         j justify
;         k backup
;         m magic mode
;         n New buffer header
;         o tool menu
;         p Help page
;         s auto spell
;         t tab mode
;         u undo
;         w wrap
;         x time mode
;
;    A 'A' means disable auto indent, a enables it.
;
;  Copyright (c) 2000 JASSPA.
;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;

!if &not &exi .osd.buffstp
    set-variable .osd.buffstp &pinc .osd.next 1
!endif

define-macro buffer-setup-option
    !if &les @# 0
        set-variable @# &abs @#
        set-variable #l0 &lget .buffer-setup.flags @#
        set-variable #l1 .buffer-setup.nsetup
        !force @# buffer-setup-option
        set-variable #l2 $status
        var-str-sub #l1 #l0 ""
        var-str-sub #l1 &sup #l0 ""
        !if #l2
            set-variable #l0 &sup #l0
        !endif
        set-variable .buffer-setup.nsetup &cat #l1 #l0
    !else
        set-variable #l0 &lget .buffer-setup.flags @#
        !if &sin #l0 .buffer-setup.nsetup
            !return
        !elif &sin &sup #l0 .buffer-setup.nsetup
            !abort
        !elif &seq &set #l1 &lget .buffer-setup.modes @# ""
            !abort
        !elif &not &gmod #l1
            !abort
        !endif
    !endif
!emacro
define-macro buffer-setup-save
    !if &not &seq .buffer-setup.nsetup .buffer-setup.osetup
        set-registry "/history" &cat "fhook/" &rig $buffer-fhook 6 .buffer-setup.nsetup
        set-variable &ind &spr ".%s.setup" $buffer-fhook .buffer-setup.nsetup
        set-variable .buffer-setup.osetup .buffer-setup.nsetup
    !endif
    !if &gre @# 1
        !force @# execute-file &cat "hk" &rig $buffer-fhook 6
        !if $status
            set-variable $buffer-hilight 0
            set-variable $buffer-indent 0
            execute-named-command $buffer-fhook
        !else
            osd-dialog "Buffer Setup" "Failed to re-initialize buffer hook!" "  &OK  "
        !endif
    !endif
!emacro
define-macro buffer-setup
    set-variable .mask &ind &spr ".%s.setup-mask" $buffer-fhook
    !if &seq .mask "ERROR"
        !bell
        ml-write "[buffer-setup not supported by current buffer type]"
        !abort
    !endif
    set-variable .osetup &ind &spr ".%s.setup" $buffer-fhook
    -1 osd .osd.buffstp
    osd .osd.buffstp 0 "batcdHs" 9 3 47 10 -1 -1 930 .scheme.osd-title &spr "%s Buffer Setup" &rig $buffer-fhook 6
    osd .osd.buffstp 5 ""
    set-variable #l0 1
    set-variable #l1 0
    set-variable #l2 0
    set-variable #l3 0
    !repeat
        set-variable #l5 &lget .labels #l0
        !if &sin &lget .flags #l0 .mask
            !if &not &seq #l5 ""
                set-variable #l6 &sub 22 &add &len #l5 &cond &sin "&" #l5 0 1
                osd .osd.buffstp &mul #l0 10 "Sfh" &spr "%n%s: " #l6 " " #l5 &add 5 &mul #l0 10
                set-variable #l2 1
            !elif &gre &inc #l2 1 2
                osd .osd.buffstp &mul #l0 10 ""
                osd .osd.buffstp &add 1 &mul #l0 10 "fh" &spr "%n  " 21 " "
                set-variable #l2 1
            !endif
            set-variable #l6 "Ctfx"
            !if &set #l3 &seq &lget .labels &add #l0 1 ""
                set-variable #l6 &cat #l6 "h"
                osd .osd.buffstp &add 8 &mul #l0 10 "fh" "  "
            !endif
            !if &seq &set #l5 &lget .prefix #l0 ""
                osd .osd.buffstp &add 5 &mul #l0 10 #l6 &mul #l0 10 "^^NY^^" #l0 buffer-setup-option
            !else
                osd .osd.buffstp &add 5 &mul #l0 10 #l6 &mul #l0 10 &spr " [ *]^%s" #l5 #l0 buffer-setup-option
            !endif
            set-variable #l1 1
        !elif &not &seq #l5 ""
            set-variable #l7 #l0
            set-variable #l6 0
            !if &seq &lget .labels &inc #l7 1 ""
                !tjump &not &sin &lget .flags #l7 .mask -1
                set-variable #l6 1
            !endif
            !if #l6
                set-variable #l6 &sub 22 &add &len #l5 &cond &sin "&" #l5 0 1
                osd .osd.buffstp &mul #l0 10 "Sfh" &spr "%n%s: " #l6 " " #l5 &add 5 &mul #l0 10
                set-variable #l2 0
            !endif
        !elif #l3
            !if &not &seq &lget .labels &add #l0 1 ""
                osd .osd.buffstp &add -2 &mul #l0 10 ""
            !endif
        !endif
        !if &and #l1 &lget .blank #l0
            osd .osd.buffstp &add 9 &mul #l0 10 ""
            set-variable #l1 0
        !endif
    !until &seq &lget .flags &inc #l0 1 ""
    osd .osd.buffstp 900 ""
    osd .osd.buffstp 910 "BtcfHhx" 910 .scheme.osd-ebtt " &Save " 1 buffer-setup-save
    osd .osd.buffstp 920 "BtcfHh"  920 .scheme.osd-ebtt " &Current " 2 buffer-setup-save
    osd .osd.buffstp 930 "BtcfH"   930 .scheme.osd-ebtt " E&xit " f void
    set-variable .nsetup .osetup
    .osd.buffstp osd
!emacro
set-variable .buffer-setup.flags  "|p|o|c|n|h|d|s|f|b|e|m|a|k|i|j|t|x|u|w|"
set-variable .buffer-setup.blank  "|0|0|0|1|0|0|0|1|1|0|0|0|0|0|0|0|0|0|1|"
set-variable .buffer-setup.labels "|Create Help &Page|Create T&ools Menu|Use Author Mode|Insert &New Template|Setup &Hilighting|Setup Auto In&dent|Setup Auto Spe&ll|Setup &Folding|Add A&bbreviations|Search Modes||Buffer Modes||||||||x|"
set-variable .buffer-setup.prefix "||||||||||  &Exact|  &Magic|   &Auto| Bac&kup| &Indent|&Justify|    &Tab|   Time|   &Undo|   &Wrap|"
set-variable .buffer-setup.modes  "||||||||||exact|magic|auto|backup|indent|justify|tab|time|undo|wrap|"

