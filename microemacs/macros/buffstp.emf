; -!- emf -!-
; This is part of the JASSPA MicroEmacs macro files
; Copyright (C) 2000-2004 JASSPA (www.jasspa.com)
; See the file me.emf for copying and conditions.
;
; Created:     Thu Nov 23 2000
; Synopsis:    buffer-setup macros & GUI
; Authors:     Steven Phillips
; Notes:
;
;     The following flags are used in the .setup & .setup-mask variables:
;
;         a auto mode
;         b abbreviation
;         d auto indent
;         e exact mode
;         f fold support
;         g fence mode
;         h hilight
;         i indent
;         j justify
;         k backup
;         m magic mode
;         n New buffer header
;         o tool menu
;         p Help page
;         s auto spell
;         t tab mode
;         u undo
;         w wrap
;         x time mode
;         1 Special internal flag indicating that hilighting is used
;           for reformatting, i.e. make readable 
;         # special flag used to indicate commenting is supported (set at runtime)
;
;    The following flags can be used by individual buffer hooks (see hkhtml.emf)
;         c, l, q, r, v, y & z
;
;    In .setup, a 'A' means disable auto indent, a enables it.
;    In .setup-mask a lower case letter must be used.
;
;    For the buffer bindings, tool menu and help page the following list
;    variable are required, one item for each component:
;
;         .command-flag   - A flag indicating the components use
;         .command-name   - The command name
;         .command-nbind  - The numeric argument for the buffer binding
;         .command-kbind  - The key for the buffer binding
;         .command-desc   - Brief description of the command
;
;    The following flags are used for each component of .command-flag:
;
;         t Show in tool
;         h show in help
;         b binding
;         o override any existing binding
;         i ignore emulation key mappings
;         H Only create binding if Hilighting is enabled
;
!if &not &exi .osd.buffstp
    set-variable .osd.buffstp &pinc .osd.next 1
!endif

0 define-macro buffer-setup-option
    set-variable #l9 &abs @#
    !if &gre #l9 20
        set-variable #l0 &lget .buffer-setup.flags &sub #l9 20
    !else
        set-variable #l0 &lget &ind &spr ".%s.setup-flags" $buffer-fhook #l9
    !endif
    !if &les @# 0
        set-variable #l1 .buffer-setup.nsetup
        !force #l9 buffer-setup-option
        set-variable #l2 $status
        set-variable #l1 &rep #l1 #l0 ""
        set-variable #l1 &rep #l1 &sup #l0 ""
        !if #l2
            set-variable #l0 &sup #l0
        !endif
        set-variable .buffer-setup.nsetup &cat #l1 #l0
    !else
        !if &sin #l0 .buffer-setup.nsetup
            !return
        !elif &sin &sup #l0 .buffer-setup.nsetup
            !abort
        !elif &les #l9 21
            !abort
        !elif &seq &set #l1 &lget .buffer-setup.modes &sub #l9 20 ""
            !abort
        !elif &not &gmod #l1
            !abort
        !endif
    !endif
!emacro
0 define-macro buffer-setup-fmark
    set-variable #l0 &spr "fhook/%s-%s" &rig $buffer-fhook 6 &lget "|fomark|fcmark|tab-width|indent-width|" &abs @#
    set-variable #l1 &reg "/history" #l0 ""
    !if &les @# 0
        set-variable #l2 @ml2 "" #l1
        !if &not &seq #l2 #l1
            !if &seq #l2 ""
                !force delete-registry &cat "/history/" #l0
            !else
                set-registry "/history" #l0 #l2
            !endif
        !endif
    !else
        set-variable $result #l1
    !endif
!emacro
0 define-macro buffer-setup-save
    !if &not &seq .buffer-setup.nsetup .buffer-setup.osetup
        set-registry "/history" &cat "fhook/" &rig $buffer-fhook 6 .buffer-setup.nsetup
        set-variable &ind &spr ".%s.setup" $buffer-fhook .buffer-setup.nsetup
        set-variable .buffer-setup.osetup .buffer-setup.nsetup
    !endif
    !if &gre @# 1
        !force @# execute-file &cat "hk" &rig $buffer-fhook 6
        !if $status
            set-variable $buffer-hilight 0
            set-variable $buffer-indent 0
            execute-named-command $buffer-fhook
        !else
            osd-dialog "Buffer Setup" "Failed to re-initialize buffer hook!" "  \HOK  "
        !endif
    !endif
!emacro
define-macro buffer-setup
    set-variable .mask &ind &spr ".%s.setup-mask" $buffer-fhook
    !if &seq .mask "ERROR"
        !bell
        ml-write "[buffer-setup not supported by current buffer type]"
        !abort
    !endif
    set-variable .osetup &ind &spr ".%s.setup" $buffer-fhook
    set-variable #l9 &ind &spr ".%s.name" $buffer-fhook
    !if &seq #l9 "ERROR"
        set-variable #l9 &cat &sup &mid $buffer-fhook 6 1 &rig $buffer-fhook 7
    !endif
    -1 osd .osd.buffstp
    osd .osd.buffstp 0 "batcdHs" 9 3 48 10 -1 -1 970 .scheme.osd-title &cat #l9 " Buffer Setup"
    osd .osd.buffstp 5 ""
    !if &not &seq &set #l1 &ind &spr ".%s.setup-flags" $buffer-fhook "ERROR"
        set-variable #l2 &ind &spr ".%s.setup-labels" $buffer-fhook
        set-variable #l0 1
        !repeat
            osd .osd.buffstp &add 2 &mul #l0 10 "fh" "  "
            osd .osd.buffstp &add 5 &mul #l0 10 "Cptfx" &spr "%s\} %s" .osd.checkbox-chars &lget #l2 #l0 #l0 buffer-setup-option
        !until &seq &lget #l1 &inc #l0 1 ""
        osd .osd.buffstp 199 ""
    !endif
    set-variable #l0 1
    set-variable #l1 0
    set-variable #l3 0
    !repeat
        set-variable #l4 &lget .blank #l0
        !if &not &seq "" &set #l5 &lget .prefix #l0
            set-variable #l2 &cat "      " #l5
            osd .osd.buffstp &add 200 &mul #l0 10 "fh" #l2
        !endif
        !if &sin &lget .flags #l0 .mask
            set-variable #l7 &lget .labels #l0
            set-variable #l6 "Cptfx"
            !if &not &band #l4 6
                osd .osd.buffstp &add 202 &mul #l0 10 "fh" "  "
            !elif #l3
                osd .osd.buffstp &add 202 &mul #l0 10 "fh" &spr "%n" &sub 8 #l3 " "
                set-variable #l3 0
            !else
                !if &seq "" #l5
                    osd .osd.buffstp &add 200 &mul #l0 10 "fh" &spr "%n" &len #l2 " "
                !endif
                !if &band 4 &lget .blank &add #l0 1
                    set-variable #l3 &sub &len #l7 &cond &sin "\H" #l7 1 0
                    set-variable #l6 &cat #l6 "h"
                !endif
            !endif
            osd .osd.buffstp &add 205 &mul #l0 10 #l6 &spr "%s\} %s" .osd.checkbox-chars #l7 &add #l0 20 buffer-setup-option
            set-variable #l1 1
        !endif
        !if &band #l1 #l4
            !if #l3
                osd .osd.buffstp &add 208 &mul #l0 10 ""
                set-variable #l3 0
            !endif
            !if &band #l4 8
                osd .osd.buffstp &add 209 &mul #l0 10 ""
                set-variable #l1 0
            !endif
        !endif
    !until &seq &lget .flags &inc #l0 1 ""
    osd .osd.buffstp 900 "Sfh" "   Tab Width:       " 901
    osd .osd.buffstp 901 "EtRxHf" .scheme.osd-entry "##########" 3 buffer-setup-fmark
    osd .osd.buffstp 902 "Sfh" "   Indent Width:    " 903
    osd .osd.buffstp 903 "EtRxHf" .scheme.osd-entry "##########" 4 buffer-setup-fmark
    osd .osd.buffstp 904 ""
    !if &not &seq &lget &ind &spr ".%s.comment" $buffer-fhook 1 ""
        osd .osd.buffstp 909 "Sfh" "   Fold Open\H/Close: " 910
        osd .osd.buffstp 910 "EtRxHfh" .scheme.osd-entry "##########" 1 buffer-setup-fmark
        osd .osd.buffstp 915 "hf" "   "
        osd .osd.buffstp 920 "EtRxHf"  .scheme.osd-entry "##########" 2 buffer-setup-fmark
        osd .osd.buffstp 930 ""
    !endif        
    osd .osd.buffstp 940 ""
    osd .osd.buffstp 950 "BtcfHhx" .scheme.osd-ebtt " \HSave " 1 buffer-setup-save
    osd .osd.buffstp 960 "BtcfHh"  .scheme.osd-ebtt " \HCurrent " 2 buffer-setup-save
    osd .osd.buffstp 970 "BtcfH"   .scheme.osd-ebtt " E\Hxit " f void
    set-variable .nsetup .osetup
    .osd.buffstp osd
!emacro
set-variable .buffer-setup.flags  "|p|o|n|g|h|d|s|f|b|e|m|a|k|i|j|t|x|u|w|"
set-variable .buffer-setup.blank  "|0|0|9|0|0|0|0|9|9|2|5|2|4|4|4|4|4|4|13|"
set-variable .buffer-setup.labels "|Create Help \HPage|Create T\Hools Menu|Insert \HNew Template|Fence Display|Setup \HHilighting|Setup Auto In\Hdent|Setup Auto Spe\Hll|Setup \HFolding|Add A\Hbbreviations|\HExact|\HMagic|\HAuto|Bac\Hkup|\HIndent|\HJustify|\HTab|Time|\HUndo|\HWrap|"
set-variable .buffer-setup.prefix "||||||||||Search Modes: ||Buffer Modes: ||||||||"
set-variable .buffer-setup.modes  "||||||||||exact|magic|auto|backup|indent|justify|tab|time|undo|wrap|"

; buffer-help-add-item <command-name> <narg> <desc>
; adds the item to the help page (.osd.tmp) using and incrementing
; the osd item counter in #p9
0 define-macro buffer-help-add-item
    set-variable #l1 @1
    set-variable #l2 @2
    set-variable #l3 @3
    !if &seq "" #l1
        set-variable #l8 ""
    !else
        !if &seq &set #l8 &kbin #l2 #l1 ""
            osd .osd.tmp &inc #p9 1 "" &spr "    %s%s%s  " #l2 &con &seq #l2 "" "" " " #l1
            set-variable #l8 "          - "
        !else
            set-variable #l6 &len #l8
            set-variable #l8 &spr "  %s%n - " #l8 &sub 7 #l6 " "
        !endif
    !endif
    !if &not &seq "" #l3
        set-variable #l8 &cat #l8 &rep #l3 "\H" ""
    !endif
    osd .osd.tmp &inc #p9 1 "" &spr "  %s  " #l8
!emacro

define-macro buffer-help
    !if &exi &set #l1 &cat  "chook-" &set #l5 &rig $buffer-fhook 6
        !force execute-named-command #l1
    !endif
    !if &exi :chook
        set-variable #l5 :chook
    !endif
    set-variable #l0 &ind &spr ".fhook-%s.setup" #l5
    set-variable #l1 &ind &spr ".fhook-%s.command-flag" #l5
    set-variable #l2 &ind &spr ".fhook-%s.command-nbind" #l5
    set-variable #l3 &ind &spr ".fhook-%s.command-name" #l5
    set-variable #l4 &ind &spr ".fhook-%s.command-desc" #l5
    !if &not &sin "p" #l0
        !if &sin "p" &ind &spr ".fhook-%s.setup-mask" #l5
            ml-write "[Help page disabled]"
        !else
            ml-write "[Help page not available for this buffer]"
        !endif
        !abort
    !endif
    set-variable #l9 &ind &spr ".fhook-%s.name" #l5
    !if &seq #l9 "ERROR"
        set-variable #l9 &cat &sup &lef #l5 1 &rig #l5 1
    !endif
    -1 osd .osd.tmp
    osd .osd.tmp 0  "batcdH" 9 3 1001 .scheme.osd-title &cat #l9 " Mode Help"
    osd .osd.tmp 1  "" 
    set-variable #l9 10
    !if &sin "p" #l0
        buffer-help-add-item "buffer-help" "" "View This Help Page"
        buffer-help-add-item "" "" ""
    !endif
    !if &sin "d" #l0
        buffer-help-add-item "normal-tab" "" "Insert a Normal Tab"
    !endif
    set-variable #l7 0
    !while &not &seq "" &set #l6 &lget #l1 &inc #l7 1
        !if &sin "h" #l6
            buffer-help-add-item &lget #l3 #l7 &lget #l2 #l7 &lget #l4 #l7
        !endif
    !done
    !if &not &seq &lget &ind &spr ".fhook-%s.comment" #l5 1 ""
        buffer-help-add-item "" "" ""
        buffer-help-add-item "comment-start" "" "Start Comment"
        buffer-help-add-item "comment-line" "" "Comment Out Line"
        buffer-help-add-item "uncomment-line" "" "Delete Comment"
        buffer-help-add-item "comment-to-end-of-line" "" "Comment to End of Line"
        buffer-help-add-item "comment-restyle" "" "Restyle Current Comment"
    !endif
    !if &sin "f" #l0
        buffer-help-add-item "" "" ""
        buffer-help-add-item "collapse-current" "" "Open/Close Current Collapse"
        buffer-help-add-item "collapse-all" "" "Open/Close All Collapses"
    !endif
    !if &sin "s" #l0
        buffer-help-add-item "" "" ""
        buffer-help-add-item "auto-spell" "0" "Enable/Disable Auto-Spell"
        buffer-help-add-item "auto-spell-menu" "" "Auto-Spell Context Menu"
    !endif
    !if &exi &ind &spr ".fhook-%s.item-list-s1" #l5
        buffer-help-add-item "" "" ""
        buffer-help-add-item "item-list" "" "List Items"
        buffer-help-add-item "item-list-close" "" "Close List Items window"
    !endif
    !if &exi &ind &spr ".fhook-%s.tags" #l5
        buffer-help-add-item "" "" ""
        buffer-help-add-item "" "" "Tag generation is available under the Tools menu"
    !endif
    osd .osd.tmp 1000 "" 
    osd .osd.tmp 1001 "BcfH" .scheme.osd-ebtt "  \HOK  " f void
    .osd.tmp osd
!emacro

; buffer-tool-add-item <command-name> <narg> <desc> <desc-len>
; adds the item to the tool menu (.osd.tmp) using and incrementing
; the osd item counter in #p9
0 define-macro buffer-tool-add-item
    set-variable #l1 @1
    set-variable #l2 @2
    set-variable #l3 @3
    set-variable #l4 @4
    !if &seq "" #l1
        !if &seq #l2 "L"
            osd .osd.tmp &inc #p9 1 "" #l3
        !elif &seq "" #l3
            osd .osd.tmp &inc #p9 1 "-"
        !else
            osd .osd.tmp &inc #p9 1 "c-" &spr " %s "#l3
        !endif
    !else
        !if &seq "" #l3
            set-variable #l3 #l1
        !endif
        !if &seq &set #l7 #l2 ""
            set-variable #l7 "f"
        !endif
        !if &sin " " #l1
            osd .osd.tmp &inc #p9 1 "i" #l3 #l7 #l1
        !else
            set-variable #l5 &sub #l4 &len &rep #l3 "\H" ""
            set-variable #l5 &spr "%s%n" #l3 &cond &gre #l5 0 #l5 1 " "
            set-variable #l6 &kbin #l2 #l1
            !if &not &seq #l6 ""
                set-variable #l5 &cat #l5 #l6
            !endif
            osd .osd.tmp &inc #p9 1 "" #l5 #l7 #l1
        !endif
    !endif
!emacro

0 define-macro buffer-tool
    !if &exi &set #l1 &cat  "chook-" &set #l5 &rig $buffer-fhook 6
        !force execute-named-command #l1
    !endif
    !if &exi :chook
        set-variable #l5 :chook
    !endif
    set-variable #l0 &ind &spr ".fhook-%s.setup" #l5
    set-variable #l1 &ind &spr ".fhook-%s.command-flag" #l5
    set-variable #l2 &ind &spr ".fhook-%s.command-nbind" #l5
    set-variable #l3 &ind &spr ".fhook-%s.command-name" #l5
    set-variable #l4 &ind &spr ".fhook-%s.command-desc" #l5
    !if &not &seq &lget &ind &spr ".fhook-%s.comment" #l5 1 ""
        set-variable #l0 &cat #l0 "#"
    !endif
    -1 osd .osd.tmp
    osd .osd.tmp 0  "b"
    set-variable #l7 0
    !if &sin "s" #l0
        set-variable #l8 25
    !elif &sin "f" #l0
        set-variable #l8 23
    !elif &sin "#" #l0
        set-variable #l8 22
    !elif &sin "d" #l0
        set-variable #l8 19
    !elif &sin "p" #l0
        set-variable #l8 14
    !else
        set-variable #l8 0
    !endif
    !while &not &seq "" &set #l6 &lget #l1 &inc #l7 1
        !if &sin "t" #l6
            set-variable #l6 &rep &lget #l4 #l7 "\H" ""
            !if &gre &set #l6 &len #l6 #l8
                set-variable #l8 #l6
            !endif
        !endif
    !done
    set-variable #l8 &add #l8 1
    set-variable #l9 1
    !if &not &seq #l5 &rig $buffer-fhook 6 
        set-variable #l6 &ind &spr ".fhook-%s.name" #l5
        !if &seq #l6 "ERROR"
            set-variable #l6 &cat &sup &lef #l5 1 &rig #l5 1
        !endif
        buffer-tool-add-item "" "L" &cat #l6 " Tools:" #l8
        buffer-tool-add-item "" "L" "" #l8
    !endif
    !if &sin "p" #l0
        buffer-tool-add-item "buffer-help" "" "View \HHelp Page" #l8
    !endif
    !if &sin "d" #l0
        buffer-tool-add-item "normal-tab" "" "Insert a Normal Tab" #l8
    !endif
    set-variable #l7 0
    !while &not &seq "" &set #l6 &lget #l1 &inc #l7 1
        !if &sin "t" #l6
            buffer-tool-add-item &lget #l3 #l7 &lget #l2 #l7 &lget #l4 #l7 #l8
        !endif
    !done
    !if &sin "#" #l0
        buffer-tool-add-item "" "" "" #l8
        buffer-tool-add-item "comment-start" "" "Start Comment" #l8
        buffer-tool-add-item "comment-line" "" "Comment Out Line" #l8
        buffer-tool-add-item "uncomment-line" "" "\HDelete Comment" #l8
        buffer-tool-add-item "comment-to-end-of-line" "" "Comment to \HEnd of Line" #l8
        buffer-tool-add-item "comment-restyle" "" "Restyle Comment" #l8
    !endif
    !if &sin "f" #l0
        buffer-tool-add-item "" "" "" #l8
        buffer-tool-add-item "collapse-current" "" "Open/Close Current Collapse" #l8
        buffer-tool-add-item "collapse-all" "" "Open/Close All Collapses" #l8
    !endif
    !if &sin "s" #l0
        buffer-tool-add-item "" "" "" #l8
        buffer-tool-add-item "auto-spell" "0" "Enable/Disable Auto-Spell" #l8
        buffer-tool-add-item "auto-spell-menu" "" "Auto-Spell Context Menu" #l8
    !endif
    !if &set #l7 &exi &ind &spr ".fhook-%s.item-list-s1" #l5
        buffer-tool-add-item "" "" "" #l8
        buffer-tool-add-item "item-list" "" "List Items" #l8
        buffer-tool-add-item "item-list-close" "" "Close List Items" #l8
    !endif
    !if &exi &ind &spr ".%s.tags" $buffer-fhook
        !if &not #l7
            buffer-tool-add-item "" "" "" #l8
        !endif
        buffer-tool-add-item "generate-tags-file" "0" "Generate \HTags File" #l8
    !endif
!emacro

