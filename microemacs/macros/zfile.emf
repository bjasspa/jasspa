; -!- emf -!-
;
; Copyright (C) 1999-2024 JASSPA (www.jasspa.com)
;
; This is part of JASSPA's MicroEmacs, see the LICENSE file for licensing and
; copying information.
;
; Created:     Sun Jan 10 1999
; Synopsis:    Compressed file/archive support.
; Authors:     Steven Phillips & Jon Green
;
!iif &not &exi dirlst-sort-list  0 execute-file "hkdirlst"
!iif &not &lfin .fhook-dirlst.col-name "       Size"  set-variable .fhook-dirlst.col-name &lin .fhook-dirlst.col-name -1 "       Size"
set-variable #l0 &lfin .fhook-dirlst.col-name "       Size"

define-macro fhook-z-file
  buffer-init
  set-variable :tag-count 0
  set-variable :mouse-pick-1 dirlst-mouse-pick-1
  set-variable :mouse-drop-1 dirlst-mouse-drop-1
  set-variable :mouse-pick-3 dirlst-mouse-pick-3
  set-variable :mouse-drop-3 dirlst-mouse-drop-3
  set-variable :dirlst-mode .fhook-dirlst.mode
  buffer-init-hooks
!emacro
15 major-mode "z-file" "" "" "Z-File"
set-variable .fhook-z-file.setup &reg "/history/fhook/z-file" "ghmop"
set-variable .fhook-z-file.setup-mask    "eghkop"
set-variable .fhook-z-file.command-flag  "|thbio|thbio|th|thbio|thbio|thbio|thbio|thbio|thbio|th|thbio|thbio|thbio|thbio|thbio|thbio|th|thbio|h|thbio|thbio|thbio|thbio|th|thbio|bio|"
set-variable .fhook-z-file.command-name  "|dirlst-search|dirlst-search||dirlst-sort-list|dirlst-sort-list|dirlst-sort-list|dirlst-sort-list|dirlst-sort-list|dirlst-sort-list||dirlst-tag-toggle|dirlst-tag-mask|dirlst-tag-mask|dirlst-tag-invert|dirlst-tag-set|dirlst-tag-set||zfile-find-file||zfile-find-file|zfile-find-file|zfile-find-file|zfile-find-file||zfile-copy-file|zfile-copy-file|"
set-variable .fhook-z-file.command-nbind &spr "||0||5|6|4|%s|2|1|||1|-1||1|-1||5||1|9|4|3||1|1|" #l0
set-variable .fhook-z-file.command-kbind "|s|w||5|6|4|3|2|1||space|+|-|*|a|u||return||e|h|v|y||c|x|"
set-variable .fhook-z-file.command-desc  "|\HSearch for file by name|Search by order column||Sort by Name|Sort by Extension|Sort by Modified|Sort by Size|Sort by Attribute|Sort by Selected||Toggle Cur File Select|Select Files \H+|Unselect Files \H-|Invert Selection \H*|Select \HAll|\HUnselect All||O\Hpen File|            (Default file tool or Edit file)|\HEdit File|\HHandle File|\HView File|S\Hystem Open File||\HCopy Files||"

!if &and &sin "h" .fhook-z-file.setup &band .hilight.flags 0x02
  !iif &not &exist .hilight.z-file  set-variable .hilight.z-file &pinc .hilight.next 1
  0 hilight .hilight.z-file 0        $global-scheme
  hilight .hilight.z-file 0x102 "Directory: \\{" .scheme.link
  hilight .hilight.z-file 0x102 " Attr " .scheme.header
  hilight .hilight.z-file 0x102 " D" .scheme.dir
  hilight .hilight.z-file 0x102 "*D" .scheme.dir-select
  hilight .hilight.z-file 0x102 " d" .scheme.dir
  hilight .hilight.z-file 0x102 "*d" .scheme.dir-select
  hilight .hilight.z-file 0x102 "*\\." .scheme.select
!endif

define-macro find-zfile
  !force set-variable #l0 @1
  !if $status
    set-variable #l0 &stat "a" &cat &lef $buffer-fname &rsin "/" $buffer-fname #l0
  !else
    set-variable #l0 @ml01 "find-zfile"
  !endif
  ; see if this has already been loaded
  !force 2 find-buffer #l0
  !iif $status  !return
  !iif &not &seq "R" &set #l8 &stat "t" #l0  -8 ml-write &spr "[find-zfile Error: \"%s\" %s]" #l0 &con &seq #l8 "X" "does not exist" "is not a regular file"
  set-variable #l2 &rig #l0 &rsin "/" #l0
  ; Last added has highest priority, but as we don't know how many there are, loop through them all.
  set-variable #l9 ""
  set-variable #l1 0
  !while &len &reg &set #l3 &spr "/history/%s/zfl/%s" $platform &inc #l1 1 ""
    !if &xise #l2 &spr ".*\\.\\(%s\\)" &reg &cat #l3 "/mask" ""
      set-variable #l9 #l3
      set-variable #l8 @s1
    !endif
  !done
  !iif &seq #l9 ""  -8 ml-write &spr "[find-zfile Error: Unknown file type \"%s\", run zfile-setup]" #l2
  !if &set #l1 &seq "" &set #l7 &reg &cat #l9 "/list" ""
    !iif &seq "" &set #l7 &reg &cat #l9 "/xpipe" ""  -8 ml-write &spr "[ZFile-%s Error: List & xpipe commands not set, run zfile-setup]" &reg #l9 "NID"
    !iif &band 2 @#  -8 ml-write &spr "[ZFile-%s Error: File is not a multi-file compression file]" &reg #l9 "NID"
  !elif &band 4 @#
    -8 ml-write &spr "[ZFile-%s Error: File is not a single compressed file]" &reg #l9 "NID"
  !endif
  ; create a unique buffer name
  set-variable #l3 #l2
  set-variable #l4 0
  !repeat
    set-variable $buffer-names &cat "[*]" #l3
    !iif &seq $buffer-names ""  !break
    set-variable #l3 &spr "%s<%d>" #l2 &inc #l4 1
  !done
  set-variable #l3 &cat "*" #l3
  ; pop-up buffer and set the file name so the execution only needs the base name
  find-buffer #l3
  set-variable $buffer-fname #l0
  ; execute the list command into the buffer
  set-variable #l7 &rep &rep &rep #l7 "%%" "\bA" "%\"" "\bE" "%zf" #l2
  !iif &sin "%" #l7  set-variable #l7 &rep &rep &rep &rep #l7 "%zp" &set #l6 &lef #l0 &sub 0 &len #l2 "%zP" &rep #l6 "/" &lef %os-chars 1 "%zb" &lef #l2 &sub -1 &len #l8 "%ze" #l8
  set-variable #l7 &rep #l7 "\bA" "%"
  !while &set #l4 &sin "\bE" #l7
    !iif &not &set #l5 &sin "\bE" &rig #l7 &add #l4 1  -8 ml-write "[Error: Missing '%\"' in list command line]"
    !if &not &xse &set #l6 &mid #l7 &add #l4 1 &sub #l5 1 ".*[\\s(].*"
    !elif &band $system 0x100
      set-variable #l6 &spr "\"%s\"" #l6
    !else
      set-variable #l6 &spr "\"%s\"" &rep &rep #l6 "\\" "\\\\" "\"" "\\\""
    !endif
    set-variable #l7 &spr "%s%s%s" &lef #l7 &sub #l4 1 #l6 &rig #l7 &add &add #l4 #l5 2
  !done
  0x44 pipe-shell-command #l7 #l3 @mna
  set-variable #l4 $result
  set-variable $buffer-fname #l0
  !if #l1
    !iif #l4  ml-write &spr "[ZFile-%s Warning: Extract command returned %s]" &reg #l9 "NID" #l4
    !return
  !endif
  -1 buffer-mode "view"
  1 buffer-mode "magic"
  beginning-of-buffer
  !if &len &set #l7 &reg &cat #l9 "/rm-top" ""
    set-mark
    !force search-buffer "me" #l7
    !iif &not $status  -8 ml-write &spr "[ZFile-%s list command failed - top header not found]" &reg #l9 "NID"
    -1 kill-region
  !endif
  !if &not &seq "" &set #l6 &reg &cat #l9 "/smry-bot-s" ""
    end-of-buffer
    !force search-buffer "meb" #l6
    !if $status
      set-variable #l6 &xrep @wl #l6 &reg &cat #l9 "/smry-bot-r" ""
    !else
      set-variable #l6 ""
    !endif
  !endif
  !if &not &seq "" &set #l7 &reg &cat #l9 "/rm-bot" ""
    end-of-buffer
    set-mark
    !force search-buffer "meb" #l7
    !iif $status  -1 kill-region
  !endif
  set-variable #l1 1
  !while &not &seq "" &set #l2 &reg &spr "%s/line-s%s" #l9 #l1 ""
    beginning-of-buffer
    replace-string #l2 &reg &spr "%s/line-r%s" #l9 &pinc #l1 1 ""
  !done
  !if &reg &cat #l9 "/rplc-bckslsh" "0"
    beginning-of-buffer
    replace-string "\\\\" "/"
  !endif
  beginning-of-buffer
  !if &seq #l6 ""
    0 search-buffer "me" "/$"
    set-variable #l6 &spr "    %d files, %d dirs" &sub &sub $window-eline $result 1 $result
    beginning-of-buffer
  !endif
  insert-string &spr "ZFile-%s: %s\n%s\n\n%s\n" &reg #l9 "NID" #l0 #l6 &set #l2 &reg &cat #l9 "/header" ""
  -1 buffer-mode "edit"
  1 buffer-mode "view"
  beginning-of-buffer
  ; assume all non-single-file compress files have mulitple directories
  set-variable :mltd 1
  set-variable :fmpf &reg &cat &set :zfl #l9 "/fmpf" &sin " Name" #l2
  major-mode "z-file"
  !iif #l4  ml-write &spr "[ZFile-%s Warning: list command returned %s]" &reg #l9 "NID" #l4
!emacro

; usage: n zfile-extract-file <output-path> [<file-name>]
; where n is a bit based flag defined as follows:
;     0x01 : use <file-name> as file to extract
;     0x02 : use a .find-file var for the <file-name>, the <file-name> arg is the file number (i.e. 1,2,...)
;     0x04 : use all files in .find-file vars (i.e. %*[..] multi-file command), the <file-name> arg is not used
0 define-macro zfile-extract-file
  !iif &seq "" &set #l0 &reg &cat :zfl "/xtrct" ""  -8 ml-write &spr "[ZFile-%s missing file extraction command]" &reg :zfl "NID"
  set-variable #l1 &lef $buffer-fname &set #l2 &rsin "/" $buffer-fname
  set-variable #l2 &rig $buffer-fname #l2
  set-variable #l3 &rig #l2 &rsin #l2 "."
  !if &sin "%p" &set #l0 &rep &rep #l0 "%%" "\bA" "%\"" "\bE"
    set-variable #l4 #l1
    set-variable #l0 &rep #l0 "%p" &rep @1 "%" "\bA"
  !elif &not &add &sin "%zp" #l7 &sin "%zP" #l7
    -8 ml-write &spr "[ZFile-%s ERROR: No path in extraction command, %%o or %%zp must be used]" &reg :zfl "NID"
  !else
    set-variable #l4 @1
  !endif
  set-variable #l0 &rep &rep &rep &rep &rep #l0 "%zp" #l1 "%zP" &rep #l1 "/" &lef %os-chars 1 "%zf" #l2 "%ze" #l3 "%zb" &lef #l2 &sub -1 &len #l3
  set-position "\x8a"
  !force 0 delete-buffer "*zfile-tmp*"
  find-buffer "*zfile-tmp*"
  set-variable $buffer-fname #l4
  !if &band @# 2
    set-variable #l4 &rep &ind &cat ".find-file.file" @2 "%" "\bA"
  !elif &not &band @# 4
    set-variable #l4 &rep @2 "%" "\bA"
  !elif &equ 1 .find-file.file-count
    set-variable #l4 &rep &ind ".find-file.file1" "%" "\bA"
    set-variable @# 2
  !elif &not &set #l7 &sin "%*[" #l0
    -8 ml-write "[ZFile Error: Missing '%*[' in command line]"
  !endif
  !if &band @# 4
    set-variable #l4 &rig #l0 &add #l7 2
    !iif &not &set #l5 &sin "]" #l4  -8 ml-write "[ZFile Error: Missing ']' in %*[ multi-file command line]"
    set-variable #l4 &lef #l4 &sub #l5 1
    set-variable #l8 &rig #l0 &add &add #l7 #l5 2
    -1 insert-string &lef #l0 &sub #l7 1
    set-variable #l9 0
    !while &les &pinc #l9 1 .find-file.file-count
      set-variable #l1 &rep &ind &cat ".find-file.file" #l9 "%" "\bA"
      set-variable #l2 &rep #l4 "%f" #l1
      -1 insert-string #l2
    !done
    -1 insert-string #l8
    beginning-of-buffer
    !force replace-string "\bA" "%"
    beginning-of-buffer
    !repeat
      !force search-buffer "me" "\bE"
      !if $status
        backward-delete-char
        backward-delete-char
        set-mark
        !force search-buffer "me" "\bE"
        !iif &not $status   -8 ml-write "[Error: Missing '%\"' in command line]"
        backward-delete-char
        backward-delete-char
        kill-region
        !if &not &xse &set #l3 @y ".*[\\s(].*"
        !elif &band $system 0x100
          set-variable #l3 &spr "\"%s\"" #l3
        !else
          set-variable #l3 &spr "\"%s\"" &rep &rep #l3 "\\" "\\\\" "\"" "\\\""
        !endif
        -1 insert-string #l3
        -1 yank
      !endif
    !until &not $status
    ml-write &spr "[Launching: %s]" &lef @wl 500
    !force !force 0x105 shell-command "*zfile-tmp*"
  !else
    !if &sin "%*[" #l0
      !iif &not &sin "]" #l0  -8 ml-write "[ZFile Error: Missing ']' in %*[ multi-file command line]"
      set-variable #l0 &rep &rep #l0 "%*[" "" "]" ""
    !endif
    set-variable #l0 &rep &rep #l0 "%f" #l4 "\bA" "%"
    !while &set #l4 &sin "\bE" #l0
      !iif &not &set #l5 &sin "\bE" &rig #l0 &add #l4 1  -8 ml-write "[ZFile Error: Missing '%\"' in command line]"
      !if &not &xse &set #l7 &mid #l0 &add #l4 1 &sub #l5 1 ".*[\\s(].*"
      !elif &band $system 0x100
        set-variable #l7 &spr "\"%s\"" #l7
      !else
        set-variable #l7 &spr "\"%s\"" &rep &rep #l7 "\\" "\\\\" "\"" "\\\""
      !endif
      set-variable #l0 &spr "%s%s%s" &lef #l0 &sub #l4 1 #l7 &rig #l0 &add &add #l4 #l5 2
    !done
    ml-write &spr "[Launching: %s]" #l0
    !force !force 5 shell-command #l0
  !endif
  set-variable #l1 $status
  set-variable #p9 $result
  !force goto-position "\x8a"
  !force 0 delete-buffer "*zfile-tmp*"
  !iif &not #l1  ml-write &spr "[ZFile-%s Error: Failed to launch file extract]" &reg :zfl "NID"
  !return #l1
!emacro

0 define-macro zfile-tmp-rm
  set-variable #l0 &add .fcl 0
  set-variable #l4 &add .fc 0
  !while &les #l0 #l4
    set-variable #l3 &ind &cat ".fn" &inc #l0 1
    set-variable #l1 &lget #l3 1
    set-variable #l2 &lget #l3 2
    !repeat
      !force !force 0x20 file-op &cat #l1 #l2 @mna
      !iif &not &set #l3 &rsin "/" #l2  !break
      set-variable #l2 &lef #l2 &sub #l3 1
    !done
    unset-variable &ind &cat ".fn" #l0
  !done
  set-variable .fcl #l4
!emacro

define-macro zfile-find-file
  !iif &not &exi ":zfl"  -8 ml-write "[ZFile Error: Invalid buffer!]"
  !iif &or &les $window-line 5 &les &len @wl :fmpf  -8 ml-write "[ZFile Error: Invalid line!]"
  !iif &seq "/" &rig &set #l0 &rig @wl :fmpf -1  -8 ml-write "[ZFile Error: cannot edit/view directories!]"
  !if &exi &spr ":%s:" &set #l3 &spr "%s:%s" $buffer-bname &set #l2 #l0
    find-buffer #l3
    !return
  !endif
  set-variable #l5 1
  set-variable #l6 ""
  !if &band @# 8
    set-variable #l4 @mc5 "File read handler (?/b/c/C/l/r/t/u/z) ? " "bCclrtuz" "(b)inary, (c)rypted, (C)rypted+[blrtu], (l)ossless text (r)bin, (t)ext, (u)nicode, (z)file ? "
    !if &seq #l4 "C"
      set-variable #l5 &bor #l5 4
      set-variable #l4 @mc5 "CFile read handler (?/b/l/r/t/u) ? " "blrtu" "(b)inary, (l)ossless text, (r)bin, (t)ext, (u)nicode ? "
    !endif
    !if &seq #l4 "z"
      set-variable #l5 0
    !elif &seq #l4 "u"
      set-variable #l5 &bor #l5 8
      set-variable #l6 0x100
    !elif &seq #l4 "l"
      set-variable #l5 &bor #l5 8
      set-variable #l6 0
    !else
      set-variable #l5 &bor #l5 &sin #l4 "-b-c---r"
    !endif
  !endif
  set-variable #l7 :zfl
  !repeat
    !force 0x100 file-op &set #l1 &rep $temp-name "\\" "/"
  !until $status
  ; run zfile tool from the new sub-dir but then move path to zfile sub-path so tool is run in tmp folder and new sub-dir is removed
  1 zfile-extract-file #l1 #l0
  !while &seq &lef #l2 1 "/"
    set-variable #l2 &rig #l2 1
  !done
  set-variable #l2 &spr "%s/%s" &rig #l1 &rsin "/" #l1 #l2
  set-variable #l1 &lef #l1 &rsin "/" #l1
  ; launch the tool and don't wait, however we need to avoid deleting the tmp file before the system app has
  ; had a chance to load it, so throw in a long delay (20sec) before the del and hope!
  !if &set #l8 &seq "R" &stat t &cat #l1 #l2
    !if &set #l0 &band 4 @#
      !force &con &band 1 @# 1 3 file-tool-exec-default &cat #l1 #l2
      set-variable #l0 $status
    !elif &set #l0 &band 2 @#
      !if &not &xse &set #l4 &cat #l1 #l2 ".*[\\s(].*"
      !elif &band $system 0x100
        set-variable #l4 &spr "\"%s\"" #l4
      !else
        set-variable #l4 &spr "\"%s\"" &rep &rep #l4 "\\" "\\\\" "\"" "\\\""
      !endif
      !if &seq $platform "windows"
        !force !force 0x20 shell-command &rep #l4 "/" "\\"
      !elif &seq $platform "macos"
        !force !force 0x24 shell-command &cat "open " #l4
      !elif &seq $platform "linux"
        !force !force 0x24 shell-command &cat "xdg-open " #l4
      !else
        set-variable #l0 0
      !endif
    !endif
    !if &not #l0
      !if #l5
        #l5 find-file &cat #l1 #l2 @mna
        !iif &len #l6  #l6 translate-buffer
      !else
        find-zfile &cat #l1 #l2 @mna
      !endif
      set-variable $buffer-fname ""
      set-variable $buffer-bname #l3
    !endif
  !endif
  ; delay the remove of the tmp file by 20sec to allow command to execute
  set-variable &ind &cat ".zfile-tmp-rm.fn" &inc .zfile-tmp-rm.fc 1 &spr "|%s|%s|" #l1 #l2
  20000 create-callback zfile-tmp-rm
  !if &not #l8
    !force goto-position "\x8a"
    -8 ml-write &spr "[ZFile-%s Error: failed to extract requested file - command returned %s]" &reg #l7 "NID" #l9
  !elif #l9
    ml-write &spr "[ZFile-%s Warning: extract command returned %s]" &reg #l7 "NID" #l9
  !endif
!emacro

define-macro zfile-copy-file
  !iif &not &exi ":zfl"  -8 ml-write "[ZFile Error: Invalid buffer!]"
  !if :tag-count
    11 dirlst-tag-list "Copy Files"
    set-variable #l2 &con &sin "%*[" &reg &cat :zfl "/xtrct" "" 4 2
  !elif &or &les $window-line 5 &les &len @wl :fmpf
    -8 ml-write "[ZFile Error: Invalid line!]"
  !else
    set-variable #l1 &rig @wl :fmpf
    set-variable #l2 1
  !endif
  !force !force set-variable #l0 @ml21 "Copy to" &lef $buffer-fname &rsin "/" $buffer-fname
  set-variable #l3 $status
  !force -1 show-region
  !iif &not #l3  !abort
  !iif &not &seq &rig #l0 -1 "/"  set-variable #l0 &cat #l0 "/"
  !iif &not &seq "D" &stat "t" &lef &set #l0 &stat "a" &cat "file:" #l0 -1  -8 ml-write &spr "[ZFile-%s Error: Destination must be an existing directory]" &reg :zfl "NID"
  !if &equ #l2 2
    set-variable #l1 0
    !while &les &pinc #l1 1 .find-file.file-count
      2 zfile-extract-file #l0 #l1
    !done
  !else
    #l2 zfile-extract-file #l0 #l1
  !endif
!emacro

define-macro zfile-setup
  set-variable #l0 @ml "ZFile ID"
  !iif &not &xseq #l0 "\\m+"  -8 ml-write "[ZFile Error: An ID must be given and can only contain numbers and letters]"
  set-variable #l1 0
  set-variable #l2 -1
  !repeat
    !force find-registry &spr "/history/%s/zfl" $platform &pinc #l1 1
    !iif &not &set #l3 $status  !break
    !iif &set #l3 &seq #l0 &reg &set #l9 &spr "/history/%s/zfl/%s" $platform $result ""  !break
    !iif &gre $result #l2  set-variable #l2 $result
  !done
  !iif &not #l3  set-variable #l9 &spr "/history/%s/zfl/%s" $platform &add #l2 1
  set-variable #l1 @ml2 &spr "ZFile-%s extension regex" #l0 &reg &cat #l9 "/mask" ""
  !iif &seq #l1 ""  -8 ml-write "[ZFile Error: A file extension regex must be given]"
  set-variable #l2 @ml2 &spr "ZFile-%s list command (%%zp,%%zP,%%zf,%%zb,%%ze,%%\")" #l0 &reg &cat #l9 "/list" ""
  !if &seq #l2 ""
    set-variable #l2 @ml2 &spr "ZFile-%s piped extract" #l0 &reg &cat #l9 "/xpipe" ""
    !iif &seq #l2 "" -8 ml-write "[ZFile Error: The list or piped extract command line must be given]"
    set-registry #l9 #l0
    set-registry &cat #l9 "/mask" #l1
    !force delete-registry &cat #l9 "/list"
    set-registry &cat #l9 "/xpipe" #l2
    !return
  !endif
  set-variable #l3 @ml2 &spr "ZFile-%s top cut-to regex" #l0 &reg &cat #l9 "/rm-top" ""
  set-registry #l9 #l0
  set-registry &cat #l9 "/mask" #l1
  set-registry &cat #l9 "/list" #l2
  !if &len #l3
    set-registry &cat #l9 "/rm-top" #l3
  !else
    !force delete-registry &cat #l9 "/rm-top"
  !endif
  set-variable #l1 @ml2 &spr "ZFile-%s bottom cut-to regex" #l0 &reg &cat #l9 "/rm-bot" ""
  set-registry &cat #l9 "/rm-bot" #l1
  set-variable #l1 1
  !repeat
    set-variable #l2 @ml2 &spr "ZFile-%s search regex %d" #l0 #l1 &reg &spr "%s/line-s%d" #l9 #l1 ""
    !iif &seq #l2 ""  !break
    set-variable #l3 @ml2 &spr "ZFile-%s replace with %d" #l0 #l1 &reg &spr "%s/line-r%d" #l9 #l1 ""
    set-registry &spr "%s/line-s%d" #l9 #l1 #l2
    set-registry &spr "%s/line-r%d" #l9 #l1 #l3
    set-variable #l1 &add #l1 1
  !done
  !while &len &reg &spr "%s/line-s%d" #l9 #l1 ""
    !force delete-registry &spr "%s/line-s%d" #l9 #l1
    !force delete-registry &spr "%s/line-r%d" #l9 #l1
    set-variable #l1 &add #l1 1
  !done
  set-variable #l1 @ml2 &spr "ZFile-%s change \\ to / (1/0)" #l0 &reg &cat #l9 "/rplc-bckslsh" ""
  set-registry &cat #l9 "/rplc-bckslsh" #l1
  set-variable #l1 @ml2 &spr "ZFile-%s bottom summary regex" #l0 &reg &cat #l9 "/smry-bot-s" ""
  set-variable #l2 @ml2 &spr "ZFile-%s summary replace with" #l0 &reg &cat #l9 "/smry-bot-r" ""
  set-registry &cat #l9 "/smry-bot-s" #l1
  set-registry &cat #l9 "/smry-bot-r" #l2
  set-variable #l1 @ml2 &spr "ZFile-%s column header" #l0 &reg &cat #l9 "/header" ""
  set-registry &cat #l9 "/header" #l1
  set-variable #l1 @ml2 &spr "ZFile-%s extract command (%%zp,%%zP,%%zf,%%zb,%%ze,%%p,%%f,%%[..],%%\")" #l0 &reg &cat #l9 "/xtrct" ""
  set-registry &cat #l9 "/xtrct" #l1
!emacro
define-macro zfile-reset
  !if &band @# 1
    set-variable #l0 @mc5 &spr "This will reset all zfile settings for %s - continue (?/y/n) ? " $platform "nNyY" "(Y)es, (N)o, (C-g)Abort ? "
    !iif &not &iseq #l0 "y" !abort
  !endif
  !force delete-registry &spr "/history/%s/zfl" $platform
  execute-file "zfile"
!emacro

!force find-registry &spr "/history/%s/zfl" $platform 0
!if &not &set #l0 $status
  !if &not &seq &whi &set #l1 "7z" "ERROR"
  !elif &not &seq &whi &set #l1 "7za" "ERROR"
  !elif &not &seq &whi &set #l1 "7zz" "ERROR"
  !elif &not &seq &whi &set #l1 "7zzs" "ERROR"
  !else
    set-variable #l1 ""
  !endif
  !if &len #l1
    ; Add single-file handle first (no list cmd), then zfiles containing multiple files 
    set-registry &set #l2 &spr "/history/%s/zfl/%d" $platform &inc #l0 1 "7zs"
    set-registry &cat #l2 "/mask" "\\([bgx]?z\\|bz2\\)"
    set-registry &cat #l2 "/xpipe" &cat #l1 " e -so %\"%zf%\""
    set-registry &cat #l2 "/xtrct" &cat #l1 " x %\"%zf%\" %\"-o%p%\" -y"

    set-registry &set #l2 &spr "/history/%s/zfl/%d" $platform &inc #l0 1 "7z"
    set-registry &cat #l2 "/list" &cat #l1 " l %\"%zf%\""
    set-registry &cat #l2 "/mask" "\\(7z\\|zip\\|rar\\|tar\\|iso\\|apk\\|msi\\|cab\\|apk\\|ar[cj]?\\|epub\\|deb\\|dmg\\|rpm\\|ntfs\\|t[bgx]z\\)"
    set-registry &cat #l2 "/rm-top" "^----[- ]+----\n"
    set-registry &cat #l2 "/rm-bot" "^----[- ]+----\n"
    set-registry &cat #l2 "/line-s1" "^\\(\\d\\d\\d\\d-\\d\\d-\\d\\d \\d\\d:\\d\\d:\\d\\d\\) \\([.][.\\u]\\{4\\}\\) \\([ \\d]\\{11\\}\\d\\) [ \\d]\\{12\\}  \\(.+\\)$"
    set-registry &cat #l2 "/line-r1" " \\2 \\3  \\1 \\4"
    set-registry &cat #l2 "/line-s2" "^\\(\\d\\d\\d\\d-\\d\\d-\\d\\d \\d\\d:\\d\\d:\\d\\d\\) D\\([.\\u]\\{4\\}\\) \\([ \\d]\\{11\\}\\d\\) [ \\d]\\{12\\}  \\(.+\\)$"
    set-registry &cat #l2 "/line-r2" " D\\2 \\3  \\1 \\4/"
    set-registry &cat #l2 "/line-s3" "^\\(\\d\\d\\d\\d-\\d\\d-\\d\\d \\d\\d:\\d\\d:\\d\\d\\) D\\([.\\u]\\{4\\}\\) \\( \\{12\\}\\) [ \\d]\\{12\\}  \\(.+\\)$"
    set-registry &cat #l2 "/line-r3" " D\\2            -  \\1 \\4/"
    set-registry &cat #l2 "/line-s4" "^\\( \\{19\\}\\) \\([.][.\\u]\\{4\\}\\) \\([ \\d]\\{11\\}\\d\\) [ \\d]\\{12\\}  \\(.+\\)$"
    set-registry &cat #l2 "/line-r4" " \\2 \\3  XXXX-XX-XX XX:XX:XX \\4"
    set-registry &cat #l2 "/rplc-bckslsh" "1"
    set-registry &cat #l2 "/smry-bot-s" "^\\(\\d\\d\\d\\d-\\d\\d-\\d\\d \\d\\d:\\d\\d:\\d\\d\\) +\\(\\d+\\) +\\(\\d+\\) +\\(.+\\)$"
    set-registry &cat #l2 "/smry-bot-r" "    \\2 used in \\4 (Compressed to \\3)"
    set-registry &cat #l2 "/header" " Attr          Size             Modified Name  <Order by Extension>"
    set-registry &cat #l2 "/xtrct" &cat #l1 " x %\"%zf%\" %\"-o%p%\" -y --%*[ %\"%f%\"]"
  !elif &not &seq &whi "gunzip" "ERROR"
    set-registry &set #l2 &spr "/history/%s/zfl/%d" $platform &inc #l0 1 "Gzip"
    set-registry &cat #l2 "/mask" "gz"
    set-registry &cat #l2 "/xpipe" "gunzip -c %\"%zf%\""
  !endif
  !if &not &seq &whi "tar" "ERROR"
    set-registry &set #l2 &spr "/history/%s/zfl/%d" $platform &inc #l0 1 "tar"
    set-registry &cat #l2 "/list" "tar -tvf %\"%zf%\""
    set-registry &cat #l2 "/mask" "\\(tar\\|t[bgx]z\\|tar.[bgx]?z\\|tar.bz2\\)"
    set-registry &cat #l2 "/line-s1" "^\\([-\\l]\\{10\\}\\) [^\\s/]+/\\S+ +\\(\\d+\\) \\(.*\\)$"
    set-registry &cat #l2 "/line-r1" " \\1\\{2, ,,11\\}   \\3"
    set-registry &cat #l2 "/line-s2" "^\\([-\\l]\\{10\\}\\) +\\d+ +\\S+ +\\S+\\([ \\d]\\{9\\}\\) \\(.*\\)$"
    set-registry &cat #l2 "/line-r2" " \\1  \\2       \\3"
    set-registry &cat #l2 "/line-s3" "^\\([-\\l]\\{10\\}\\) +\\d+ +\\S+ +\\S+\\([ \\d]\\{5\\}\\) \\(.*\\)$"
    set-registry &cat #l2 "/line-r3" " \\1      \\2       \\3"
    set-registry &cat #l2 "/header" " Attr             Size           Modified Name  <Order by Extension>"
    set-registry &cat #l2 "/xtrct" "tar -xf %\"%zf%\" -C %\"%p%\"%*[ %\"%f%\"]"
  !endif
!endif
