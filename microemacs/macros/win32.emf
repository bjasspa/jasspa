;       Setup file for Microsoft Windows.
;
;       Author:         Jon Green
;   Last Modified : <000825.1824>
;
; Notes on the standard configuration
; ===================================
;
; The "Terminal" font is the standard DOS font used
; for the DOS window. This is an OEM fixed width
; character set which contains all of the conventional
; symbols found in the DOS shell.
;
; The fonts are the same as shown in the DOS window the
; last 2 arguments are the width x height, the terminal
; equivelents (Bit Mapped) are commented here.
;
; 8x8  regular weight seems to offer the best resolution
;      for 14/15" monitors.
;
; 7x12 A good replacement for 8x8 if it is not operating
;      correctly !!.
;
; 6x8  regular weight is more suitable for 17-21" monitors
;      which offer better resolutions.
;
; Standard Terminal Fonts - standard weight use  0 4
; Standard Terminal Fonts - heavy weight use     0 7
!if &band $system 0x01
    ; using a console interface
!else
    ; using our own window
    set-variable #l0 &reg "/history" &cat $platform "/font" ""
    set-variable #l1 .change-font.type
    set-variable #l2 &reg "/history" &cat $platform "/font-weight" "0"
    set-variable #l3 &reg "/history" &cat $platform "/font-width" "0"
    set-variable #l4 &reg "/history" &cat $platform "/font-depth" "0"
    1 change-font #l0 #l1 #l2 #l3 #l4
!endif
0 define-macro exec-me
    ipipe-shell-command &spr "\"%s\" %s" $progname @1 @mna
!emacro
; Add A-f4 as an exit binding
global-bind-key save-buffers-exit-emacs "A-f4"
;
; Setup the get-next-line stuff including grep and compiling
;
set-variable $line-template "[0-9]+"
set-variable $file-template "[a-zA-Z]?:?[-0-9a-zA-Z\\\\/_.~]+"
set-variable %grep-com "grep -n"
0 add-next-line "*grep*"
add-next-line "*grep*" "%f: %l:"
add-next-line "*grep*" "%f:%l:"
; These definitions are for the Microsoft MSCV 2.0+ compiler
set-variable %compile-com "nmake "
0 add-next-line "*compile*"
add-next-line "*compile*" "%f(%l) : error"
add-next-line "*compile*" "%f(%l) : warning"
add-next-line "*compile*" "%f:%l:"
; Load additional hooks required for win32.
define-macro-file hkipipe ishell
; Define the cygwin dummy macro. This reports an error if the cygwin environment
; has not been defined in the user start-up file.
define-macro cygnus
    !if &seq %cygnus-bin-path "ERROR"
        ; Bitch if the environment is not defined.
        5000 ml-write "[Error: %%cygnus-bin-path not set - see hkcygnus.emf]"
        !abort
    !else
        ; Load the macro file and execute the cygnus session.
        !force exec-file "hkcygnus"
        !if $status
            cygnus
        !endif
    !endif
!emacro
; Disable cygwin hilighting by default.
set-variable .cygnus.hilight 0
; Bind the windows Menu key to the menu. Users may choose to use this
; as an additional prefix key if they want.
0xa0000 global-bind-key osd "menu"

