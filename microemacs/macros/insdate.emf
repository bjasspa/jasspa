; -!- emf -!-
; This is part of the JASSPA MicroEmacs macro files
; Copyright (C) 1999-2004 JASSPA (www.jasspa.com)
; See the file me.emf for copying and conditions.
;
; Created:     Mon Sep 13 1999
; Synopsis:    Insert the date string into the current buffer. 
; Authors:     Steven Phillips
; Notes:
;     A  4 digit year
;     B  2 digit year
;     C  Month
;     D  3 letter month
;     E  2 digit month
;     F  2 digit day
;     G  1 digit day
;     H  Week Day
;     I  3 letter week day
;     J  2 digit 24 hour
;     K  2 digit minute
;     L  2 digit second
;
0 define-macro-file organize organizer-init

0 define-macro osd-insert-date-exec
    insert-string &rig $result 2
!emacro

0 define-macro osd-insert-date
    organizer-init
    set-variable #l0 $time
    -1 osd 31
    osd 31 0 "b"
    set-variable #l1 0
    !while &not &seq &set #l2 &lget .formats &inc #l1 1 ""
        ;        012345678901234567890123456789
        ; $time "1999255 91312145 2220"
        set-variable #l2 &rep #l2 "#A" &lef #l0 4
        set-variable #l2 &rep #l2 "#B" &mid #l0 2 2
        set-variable #l2 &rep #l2 "#C" &lget .org.months &mid #l0 7 2
        set-variable #l2 &rep #l2 "#D" &lef &lget .org.months &mid #l0 7 2 3
        set-variable #l2 &rep #l2 "#E" &spr "%02d" &mid #l0 7 2
        set-variable #l2 &rep #l2 "#F" &spr "%02d" &mid #l0 9 2
        set-variable #l2 &rep #l2 "#G" &add 0 &mid #l0 9 2
        set-variable #l2 &rep #l2 "#H" &lget .org.days &add 1 &mid #l0 11 1
        set-variable #l2 &rep #l2 "#I" &lef &lget .org.days &add 1 &mid #l0 11 1 3
        set-variable #l2 &rep #l2 "#J" &spr "%02d" &mid #l0 12 2
        set-variable #l2 &rep #l2 "#K" &spr "%02d" &mid #l0 14 2
        set-variable #l2 &rep #l2 "#L" &spr "%02d" &mid #l0 16 2
        set-variable #l2 &rep #l2 "##" "#"
        osd 31 #l1 "" &spr "\H%s %s" &itoa &add #l1 64 #l2 f osd-insert-date-exec
    !done
!emacro

!if &gre 4 &lfind %language-names %language
    ; American style
    set-variable .osd-insert-date.formats "|#E/#G/#B|#E.#G.#B|#E/#G/#A|#A-#E-#G|#G-#D-#B|#G #D #B|#G #D #A|#G #C #A|#H, #G #C #A|#E/#G/#B #J:#K|#E/#G/#B #J:#K:#L|"
!else
    set-variable .osd-insert-date.formats "|#G/#E/#B|#G.#E.#B|#G/#E/#A|#A-#E-#G|#G-#D-#B|#G #D #B|#G #D #A|#G #C #A|#H, #G #C #A|#G/#E/#B #J:#K|#G/#E/#B #J:#K:#L|"
!endif

