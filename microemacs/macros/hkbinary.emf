; -!- emf -!-
; This is part of the JASSPA MicroEmacs macro files
; Copyright (C) 1997-2009 JASSPA (www.jasspa.com)
; See the file me.emf for copying and conditions.
;
; Synopsis:    Binary file hook
; Authors:     Steven Phillips
;
define-macro-file bintools bin-move-backward bin-move-forward bin-copy-region bin-yank bin-insert-hex-string bin-insert-string bin-hex-search-forward bin-search-forward
define-macro fhook-binary
  !if &not &bmod "binary"
    !if &seq $buffer-fname ""
      ml-write "[Buffer has no file name]"
      !abort
    !endif
    !if &bmod "edit"
      ml-write "[Buffer has been changed]"
      !abort
    !endif
    beginning-of-buffer
    set-mark
    end-of-buffer
    -1 kill-region
    1 buffer-mode "binary"
    insert-file $buffer-fname
    -1 buffer-mode "edit"
    beginning-of-buffer
  !endif
  @# buffer-init "binary"
  buffer-init-hooks
!emacro

; buffer-init variables
set-variable .fhook-binary.setup &reg "/history/fhook/binary" "ghop"
set-variable .fhook-binary.setup-mask "aeghkmopu"
set-variable .fhook-binary.command-flag  "|thbio|th|thbo|thbo|thbo|thbo|th|th|th|th|thbio|thbio|"
set-variable .fhook-binary.command-name  "|bin-swap-section||bin-move-backward|bin-move-forward|bin-search-forward|bin-hex-search-forward||bin-insert-string|bin-insert-hex-string||bin-copy-region|bin-yank|"
set-variable .fhook-binary.command-nbind "|||||||||||||"
set-variable .fhook-binary.command-kbind "|tab||S-up|S-down|C-x s|esc s|||||A-C-w|A-C-y|"
set-variable .fhook-binary.command-desc  "|S\Hwap section||Move \Hbackward|Move \Hforward|\HSearch forward (txt)|Search forward (he\Hx)||\HInsert string|Insert \Hhex string||Binary \Hcopy region|Binary \Hyank|"

!if &and &sin "h" .fhook-binary.setup &band .hilight.flags 0x02 
  !if &not &exist .hilight.binary
    set-variable .hilight.binary &pinc .hilight.next 1
  !endif
  0 hilight .hilight.binary 0         $global-scheme
  hilight .hilight.binary 0x400  0  9 .scheme.no2
  hilight .hilight.binary 0x400 58 78 .scheme.no1
!endif

define-macro bin-swap-section
  !if &gre $window-col 9
    !if &les $window-col 62
      set-variable $window-col &add 62 &div &sub $window-col 10 3
    !else
      set-variable $window-col &add 10 &mul &sub $window-col 62 3
    !endif
  !endif
!emacro

; try to bind these globally if key is not already bound as these can be used in any buffer
0 describe-key "A-C-y"
!iif &seq $result ""  global-bind-key bin-yank "A-C-y"
0 describe-key "esc A-y"
!iif &seq $result ""  global-bind-key bin-copy-region "esc A-w"

buffer-init-fhook "binary"
