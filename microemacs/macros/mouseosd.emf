; -!- emf -!-
; This is part of the JASSPA MicroEmacs macro files
; Copyright (C) 1999-2009 JASSPA (www.jasspa.com)
; See the file me.emf for copying and conditions.
;
; Created:     Sat Jun 13 1998
; Synopsis:    Defines mouse opened dialogs
; Authors:     Steven Phillips
;
; define some external macros which are used first.
!if &not &exi osd-1-buffer
    exec-file osd
!endif
0 define-macro-file osdmisc osd-line-scheme
0 define-macro store-frame-size
    !force read-registry "/history" $user-name "cbr"
    set-registry &spr "/history/%s/frame-width" %platform $frame-width 
    set-registry &spr "/history/%s/frame-depth" %platform $frame-depth 
    save-registry "/history" ""
!emacro
; MENU #23 - MODE LINE pop-up menu
; The mode line pop-up menu is invoked when the mouse is right clicked
; on the mode line
;
osd 23 0  b
osd 23 1  "M" "Buffer \HModes"    24
osd 23 2  ""  "\HSave Buffer"      f save-buffer    ; Save the buffer
osd 23 3  ""  "\HClose Buffer"     f osd-close      ; Close the buffer
osd 23 4  "M" "\HBuffer "         15 osd-1-buffer   ; Set up the buffers
osd 23 5  "M" "\HQuick Open"      19                ; Open a file. 
osd 23 6  "M" "S\Hcroll Bars"     25
osd 23 7  ""  "Store Frame Size"  f store-frame-size
;
; MENU #24 - BUFFER MODES pop-up menu
; Show the buffer modes. These are check items on the popup menu, a callback
; macro is required to ascertain and set the state of each of the menu items.
;
0 define-macro osd-24-buffer-mode
    ; Pick up the name of the mode from $result generated by the caller.
    ; If this is a TRUE argument then toggle the current buffer
    ; mode to the new mode
    !if &les @# 0
        0 buffer-mode $result
    !elif &not &bmod $result
        ; Return the status of the call. Return FALSE if the mode not set
        ; otherwise true.
        ; osd 24 1 "CxH" .osd.revblue &cat "^^^^^^" $result 1 "osd-24-buffer-mode"
        osd 24 1 "Cx" &cat "^^^^^^" $result f "osd-24-buffer-mode"
        !abort
    !endif
    osd 24 1 "CxH" .scheme.osd-ebtt &cat "^^^^^^" $result f "osd-24-buffer-mode"
!emacro

osd 24 0 "bA"
set-variable $mode-names ".*"
!while &not &seq &set #l0 $mode-names ""
    osd 24 1 "Cx" &cat "^^^^^^" #l0 f "osd-24-buffer-mode"
!done
;
; Menu #25 - SCROLL BAR pop-up menu
; Enable the scroll bar modes to be toggled on and off, and changes the
; sizes of the scroll columns.
0 define-macro osd-25-scroll-bars
    set-variable $scroll-bar &bxor $scroll-bar @#
!emacro

0 define-macro osd-find-help
    !force help-item @y
    !if &not $status
        man @y
    !endif
!emacro
osd 25 0 "bB"
osd 25 1 "" "Toggle \HEnable"  0x100 osd-25-scroll-bars
osd 25 2 "" "Toggle \HWidth"   0x001 osd-25-scroll-bars
;
; MENU 27 - EDIT POPUP - Generic popup sub-menu
;
0 define-macro osd-mouse-yank
    !if .mouse-event.msshift
        -1 yank
        -1 kill-region
    !endif
    yank @mna
    set-variable @cc yank
!emacro

; NOTE Any item number changes may effect mouse-osd-multi
osd 27 0  "bB"
osd 27 10  ""  "Set \HMark"         f set-mark
osd 27 15  ""  "\HCopy Region"      f copy-region
osd 27 20  ""  "\HKill Region"      f kill-region
osd 27 25  ""  "\HDelete Region"   -1 kill-region
osd 27 30  ""  "\HPaste Region"     f yank
osd 27 40  "-"
osd 27 45  "i" "\HOccurrences"      f "2 occur @y"
osd 27 50  "i" "\HFind tag"         1 "3 find-tag @y"
osd 27 60  ""  "Find \Hhelp"        f osd-find-help
osd 27 65  "M" "File \HTools"    .osd.tmp file-tool-menu
osd 27 70  "M" "Sort \HLines"      28
osd 27 80  ""  "Restyle \HRegion "  f restyle-region
osd 27 90  "M" "H\Hilight Line"  .osd.tmp osd-line-scheme
osd 27 100 "-"
osd 27 120 ""  "Spell Buffer"       f spell-buffer
osd 27 130 ""  "\HSave Buffer"      f save-buffer    ; Save the buffer
osd 27 140 ""  "\HClose Buffer"     f osd-close      ; close the buffer
osd 27 150 "M" "Change \HBuffer "  15 osd-1-buffer   ; Set up the buffers
osd 27 160 "M" "\HQuick Open"      11
;
; MENU 28 - Sort lines.
; Simple menu for options for the sort lines item. Used in the Pop-up menus.
;
osd 28 0  b
osd 28 1  "" "\HCase Sensitive"     f sort-lines
osd 28 2  "" "\HIgnore Case"        f sort-lines-ignore-case

define-macro context-menu
    !if @#
        set-variable $mouse-x $cursor-x
        set-variable $mouse-y $cursor-y
        0x21 set-position "\x82"
        !if &exi :mouse-pick-3
            !force !force execute-line :mouse-pick-3
            !force !force execute-line :mouse-drop-3
            !return
        !endif
    !endif
    0 show-region
    set-variable #l2 $result
    set-variable #l0 20
    osd 27 5  "D"
    !if &seq $buffer-input "auto-spell-input"
        !if &equ @fss $mouse-y $mouse-x .scheme.spell-error
            0 auto-spell-test
            !if &seq &lef $result 1 "E"
                set-position "\x83"
                set-variable .auto-spell-osd.word $result
                osd 27 5  "M" "Auto Spell" .osd.spell-auto auto-spell-osd
                set-variable #l0 &add #l0 1
            !endif
        !endif
    !endif
    ; must goto the original position or cut etc wont work - must dup the position
    goto-position "\x82"
    osd 27 110 "i" &cond &band $window-flags 4 "Unlock \HWindow" "Lock \HWindow" f "set-variable $window-flags &bxor $window-flags 4"
    !if &band #l2 1
        !force show-region
    !endif
    !if &gre &add $mouse-y #l0 $frame-depth
        osd 27 0 "bBo" -1 &sub 1 #l0
    !else
        osd 27 0 "bBo" -1 1
    !endif
    27 osd
!emacro

;
; The following is the osd behaviour determined by the position of the mouse
0 define-macro mouse-osd-multi
    set-variable #l0 &band $mouse-pos 15
    !if &equ #l0   0
        ; Text window
        0 context-menu
    !elif &equ #l0 1
        ; Message line
        void
    !elif &or &equ #l0 2 &equ #l0 10
        ; Mouse on Mode line or corner - if this is a toolbar window execute the toolbar-setup
        !if &band $window-flags 0x1000
            toolbar-setup
        !else
            23 osd
        !endif
    !elif &and &gre #l0 2 &les #l0 10
        ; Mouse on scroll bar
        25 osd
    !elif &equ #l0 11
        ; menu line
        0 osd
    !endif
    ; goto the original window
    1 goto-position "\x82"
!emacro

define-macro mouse-osd
    ; goto the original position
    goto-position "\x82"
    0 osd
!emacro

