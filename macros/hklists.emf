;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;
;
;  Created By    : Stephen Pillips
;  Created       : Wed Mar 12 20:29:38 1997
;  Last Modified : <010228.1023>
;
;  Description   : Lists hook
;
;  Notes
;
;  History        :
;
;  000315 Jon Added keyboard short-cut to help page.
;
;  Copyright (c) 1997-2000 Jasspa
;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;

set-char-mask "2" "-$&#!%:@."

!if &band .hilight.flags 0x02
    !if &not &exist .hilight.lists
        set-variable .hilight.lists &pinc .hilight.next 1
    !endif
    0 hilight  .hilight.lists 0x000                         $global-scheme
    hilight .hilight.lists 0x102 "Buffer"                   .scheme.header
    hilight .hilight.lists 0x102 "Global"                   .scheme.header
    hilight .hilight.lists 0x102 "Ml"                       .scheme.header
    hilight .hilight.lists 0x102 "Register"                 .scheme.header
    hilight .hilight.lists 0x102 "System"                   .scheme.header
    hilight .hilight.lists 0x004 "\"" "\"" "\\"             .scheme.string
    hilight .hilight.lists 0x004 "[[:alpha:]]" "\\}\\s " "" .scheme.no1
    hilight .hilight.lists 0x004 "\\$\\w" "\\}\\s " ""      .scheme.no1
    hilight .hilight.lists 0x004 "%\\w" "\\}\\s " ""        .scheme.no1
    hilight .hilight.lists 0x004 ":\\w" "\\}\\s " ""        .scheme.no1
    hilight .hilight.lists 0x004 "#\\w" "\\}\\s " ""        .scheme.no1
!endif

define-macro-file hkehf ehf-process-link ehf-find-help

; Local macro to process the links from the keyboard.
; Bound to <RETURN>. Note that we process from the
; end of the line. This makes it easy to process
; the command where there are multiple key bindings.
0 define-macro lists-find-link
    set-mark
    end-of-line
    ; Search for the string on the end of the current line.
    ; Handles *bindings* buffer.
    ;
    ; "C-u" ......................... universal-argument
    ; "C-x" ......................... 2 prefix
    ;
    !force -1 search-backward " \\([#$@]?[-a-zA-Z0-9]+\\) *$"
    !if $status 
        ehf-find-help @s1
        !return
    !endif
    ; Search for the string at the start of the line.
    ; Handles *variables* and *commads* buffers.
    ; 
    ;     $buffer-bname ................. "hklists.emf"
    ;
    ;     backward-char ................. "C-b"
    ;                                     "kp-left"
    ;
    !force search-backward "^ +\\([#$@]?[-a-zA-Z0-9]+\\)"
    !if $status
        ehf-find-help @s1
        !return
    !endif
    ; Must be an error.
    exchange-point-and-mark
    ml-write "Error! Not in a link"
    !abort
!emacro

define-macro fhook-lists
    ; ZZZZ
    1 buffer-mode "justify"
    1 buffer-mode "indent"
    1 buffer-mode "wrap"
    1 buffer-mode "time"
    set-variable $buffer-mask "luh12"
    !if &band .hilight.flags 0x02
        set-variable $buffer-hilight .hilight.lists
    !endif
    set-variable :mouse-word-select ehf-process-link
    buffer-bind-key ehf-process-link "esc h"
    buffer-bind-key lists-find-link "return"
    ; execute user extensions if enabled
    !if &exist my-fhook-lists
        my-fhook-lists
    !endif
!emacro

; load in user extensions if found
!force execute-file "mylists"
