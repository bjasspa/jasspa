;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;
;  Created By    : Steven Phillips
;  Created       : Mon Sep 21 18:10:34 1998
;  Description   : HTML Hook File
;
;  Copyright (c) 1998-2001 JASSPA
;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;

define-macro fhook-html
    set-variable #l0 &mid $buffer-bname &rsin "." $buffer-bname 3
    !if &sin "c" .fhook-html.setup
        !jump 2
    !elif &or &seq #l0 "hts" &seq #l0 "htp"
        set-variable $buffer-mask "luh1"
        buffer-initialize
         1 buffer-mode "usr1"
    !else
        html-process-buffer
    !endif
    ; execute user extensions if enabled
    !if &exi my-fhook-html
        my-fhook-html
    !endif
!emacro

; buffer-initialize variables
set-variable .fhook-html.setup &reg "/history" "fhook/html" "cdhiopsx"
set-variable .fhook-html.setup-mask "acdehikmopstux"
set-variable .fhook-html.comment  "|<!|>|-|"
set-variable .fhook-html.command-flag  "|th|h|th|h|th|h|thb|thb|"
set-variable .fhook-html.command-name  "|||html-process-link||||html-show-buffer|html-toggle-mode|"
set-variable .fhook-html.command-nbind "|||||||||"
set-variable .fhook-html.command-kbind "|||||||C-c C-h|C-c C-i|"
set-variable .fhook-html.command-desc  "|Html Browser Mode||&Goto Link||Html Author Mode||Process Current Buffer|Toggle Indent To VB Mode|"

!if &and &sin "h" .fhook-html.setup &band .hilight.flags 0x02 
    !if &not &exist .hilight.html
        set-variable .hilight.html   &pinc .hilight.next 1
        set-variable .hilight.htmlT  &pinc .hilight.next 1
    !endif
    0 hilight .hilight.html 3 200                      $global-scheme
    hilight .hilight.html 0x80 "<" .hilight.htmlT      .scheme.keyword
    hilight .hilight.html 4 "</" ">" ""                .scheme.keyword
    hilight .hilight.html 4 "<!" ">" ""                .scheme.comment
    hilight .hilight.html 0 "&\\w\\w;"                 .scheme.operator
    hilight .hilight.html 0 "&\\w\\w\\w;"              .scheme.operator
    hilight .hilight.html 0 "&\\w\\w\\w\\w;"           .scheme.operator
    hilight .hilight.html 0x804 "\"" "\"" "\\"         .scheme.string
    hilight .hilight.html 4 "<script" "</script>" ""   .scheme.keyword
    hilight .hilight.html 4 "<%" "%>" ""               .scheme.keyword
    
    0 hilight .hilight.htmlT 0                         .scheme.keyword
    hilight .hilight.htmlT 4  "\"" "\"" "\\"           .scheme.string
    hilight .hilight.htmlT 0x80 ">" .hilight.html      .scheme.keyword
    
    !force exec-file "hkvb"
    !if $status
        hilight .hilight.html  0x80 "<%" .hilight.vb   .scheme.prepro
        hilight .hilight.htmlT 0x80 "<%" .hilight.vb   .scheme.prepro
        hilight .hilight.vb    0x90 "%>" 0             .scheme.prepro
        hilight .hilight.html  0x80 "<script\\s +language=VBScript[^>]*>" .hilight.vb .scheme.prepro
        hilight .hilight.vb    0x80 "</script>" 0      .scheme.prepro
        ;    indent .hilight.vb b "<" ">"
    !endif
    !force exec-file "hkjava"
    !if $status
        hilight .hilight.html 0x80 "<script\\s +language=javascript[^>]*>" .hilight.java .scheme.prepro
        hilight .hilight.html 0x80 "<script\\s +language=\"javascript\"[^>]*>" .hilight.java .scheme.prepro
        hilight .hilight.java 0x80 "</script>" 0       .scheme.prepro
        hilight .hilight.java 0x80 "</SCRIPT>" 0       .scheme.prepro
    !endif
!endif
!if &and &sin "h" .fhook-html.setup &band .hilight.flags 0x01 
    !if &not &exist .hilight.htmlN
        set-variable .hilight.htmlN  &pinc .hilight.next 1
        set-variable .hilight.htmlNL &pinc .hilight.next 1
    !endif
    0 hilight .hilight.htmlN 0                          $global-scheme
    hilight .hilight.htmlN 0x44 "\ecA" "" "\\}\e" "" "" $global-scheme
    hilight .hilight.htmlN 0x44 "\ecB" "" "\\}\e" "" "" .scheme.under    ; underline
    hilight .hilight.htmlN 0x44 "\ecC" "" "\\}\e" "" "" .scheme.italic   ; italic
    hilight .hilight.htmlN 0x44 "\ecD" "" "\\}\e" "" "" .scheme.bold     ; bold
    hilight .hilight.htmlN 0x44 "\ecE" "" "\\}\e" "" "" .scheme.header   ; Header
    hilight .hilight.htmlN 0x44 "\ecF" "" "\\}\e" "" "" .scheme.comment  ; image
    hilight .hilight.htmlN 0x44 "\ecG" "" "\\}\e" "" "" .scheme.type     ; TT (curior)
    hilight .hilight.htmlN 0xc0 "\els" "" .hilight.htmlNL $global-scheme ; link start
    hilight .hilight.htmlN 0x44 "\elm" "" "\ele" "" ""  .scheme.link     ; link name
    
    0 hilight .hilight.htmlNL 0                         $global-scheme
    hilight .hilight.htmlNL 0x80 "\\}\elm" .hilight.htmlN $global-scheme
    hilight .hilight.htmlNL 0x40 "." ""                 $global-scheme
!endif

!if &sin "d" .fhook-html.setup
    !if &not &exist .hilight.html
        set-variable .hilight.html   &pinc .hilight.next 1
    !endif
    ; Define the indentation tokens 
    0 indent .hilight.html 1 20 
    indent .hilight.html e "\"" "\"" "\\"
    indent .hilight.html b "<" ">"
    indent .hilight.html n "<html>" 2 
    indent .hilight.html o "</html>" -2 
    indent .hilight.html n "<head>" 2 
    indent .hilight.html o "</head>" -2 
    indent .hilight.html n "<table[^>]*>" 2 
    indent .hilight.html o "</table>" -2 
    indent .hilight.html n "<tr[^>]*>" 2 
    indent .hilight.html o "</tr>" -2 
    indent .hilight.html o "</td>" -2 
    indent .hilight.html n "<td[^>]*>" 2 
    indent .hilight.html o "</select>" -2 
    indent .hilight.html n "<select[^>]*>" 2 
    indent .hilight.html n "<%" 2 
    indent .hilight.html o "%>" -2 
!endif

define-macro-file htmltool html-to-ehf

0 define-macro html-process-link
    set-variable #l0 $window-col
    set-variable #l1 $window-line
    ; end of link must be found within 5 lines
    !force -5 search-forward "\ele"
    !if $status
        !force -5 search-backward "\els"
        !if &and $status &or &les $window-line #l1 &and &equ $window-line #l1 &not &gre $window-col #l0
            3 forward-char
            set-mark
            -1 search-forward "\elm"
            3 backward-char
            copy-region
            set-variable #l0 @y
            -1 yank
            !if &set #l1 &sin "#" #l0
                set-variable #l8 &rig #l0 #l1
                set-variable #l0 &lef #l0 &sub #l1 1
            !else
                set-variable #l8 ""
            !endif
            !if &not &seq #l0 ""
                !force find-file #l0
                !if &not $status
                    ml-write &spr "[Failed to find link \"%s\"]" #l0
                    !abort
                !endif
            !endif
            !if &not &seq #l8 ""
                !force search-forward &cat #l8 "\elm"
                beginning-of-line
            !endif
            !return
        !endif
    !endif
    set-variable $window-line #l1
    set-variable $window-col #l0
    ml-write "Error! Not in a link"
    !abort
!emacro

define-macro html-process-buffer
    set-variable :fill-col &sub $window-width 2
    beginning-of-buffer
    set-variable #g0 0
    html-to-ehf
    beginning-of-buffer
    !if &iseq &lef $buffer-fname 7 "http://"
        set-variable #l0 &lef $buffer-fname &add 7 &sin "/" &rig $buffer-fname 7
    !else
        set-variable #l0 "file:/"
    !endif
    1 buffer-mode "magic"
    replace-string "http:/\\([^/]\\)" &cat #l0 "\\1"
    beginning-of-buffer
    !if &and &sin "h" .fhook-html.setup &band .hilight.flags 0x01 
        set-variable $buffer-hilight .hilight.htmlN
    !endif
    -1 buffer-mode "edit"
    1 buffer-mode "view"
    set-variable :mouse-word-select html-process-link
    buffer-bind-create "bio" "return" "" html-process-link
    ml-write "Done!"
!emacro

0 define-macro html-show-buffer
    end-of-buffer
    set-mark
    beginning-of-buffer
    copy-region
    set-variable #l8 $buffer-fname
    !force 0 delete-buffer &cat "*" $buffer-bname
    find-buffer &cat "*" $buffer-bname
    set-variable $buffer-fname &lef #l8 &rsin "/" #l8
    yank
    -1 yank
    html-process-buffer
!emacro


0 define-macro html-spell-word
    ; simply skip any text in a <..> token - put a 5 line limit on it
    set-variable #l0 $window-col
    set-variable #l1 $window-line
    -2 show-region
    set-variable #l2 $window-col
    !force -5 regex-backward "[<>]"
    !if $status
        set-variable #l3 @wc
        set-variable $window-line #l1
        set-variable $window-col #l0
        !if &seq #l3 "<"
            ; this is a <..> token, skip all if spelling buffer (@# != 0)
            !if @#
                !force search-forward ">"
                set-variable .spell.check-word ""
            !endif
            !return
        !endif
    !endif
    ; must redefine the region as the search blows it away
    set-variable $window-col #l2
    -3 show-region
    set-variable $window-col #l0
    3 show-region
    !abort
!emacro

0 define-macro html-toggle-mode
    !if &not $buffer-indent
        !return
    !endif
    !if &bmod "usr1"
        set-variable :auto-spell &cond &seq $buffer-input "auto-spell-input" 1 -1
    !endif
    !if &equ @# 2
        -1 buffer-mode "usr1"
         1 buffer-mode "usr2"
         !if &sin "d" .fhook-html.setup
             set-variable $buffer-indent .hilight.vb
        !endif
        !if &sin "s" .fhook-html.setup
            -1 auto-spell
        !endif
    !else
         1 buffer-mode "usr1"
        -1 buffer-mode "usr2"
        !if &sin "d" .fhook-html.setup
            set-variable $buffer-indent .hilight.html
        !endif
        !if &sin "s" .fhook-html.setup
            :auto-spell auto-spell
        !endif
    !endif
!emacro

ml-write "[HTML file hook loaded]"

; load in user extensions if found
!force execute-file "myhtml"

