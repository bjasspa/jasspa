; -!- emf -!-
; This is part of the JASSPA MicroEmacs macro files
; Copyright (C) 1998-2002 JASSPA (www.jasspa.com)
; See the file me.emf for copying and conditions.
;
; Created:     Mon Sep 21 1998
; Synopsis:    HTML Hook File
; Authors:     Steven Phillips
;
define-macro fhook-html
    set-variable #l0 &mid $buffer-bname &rsin "." $buffer-bname 3
    !if &sin "c" .fhook-html.setup
        !jump 2
    !elif &or &seq #l0 "hts" &seq #l0 "htp"
        set-variable $buffer-mask "luh1"
        @# buffer-initialize "html"
         1 buffer-mode "usr1"
    !else
        html-process-buffer
    !endif
    buffer-initialize-hooks
!emacro

; buffer-initialize variables
set-variable .fhook-html.setup &reg "/history" "fhook/html" "cdghinopsx"
set-variable .fhook-html.setup-mask "acdeghikmnopstux"
set-variable .fhook-html.comment  "|<!|>|-| - | - ||"
set-variable .fhook-html.command-flag  "|th|h|th|h|th|h|thb|thb|"
set-variable .fhook-html.command-name  "|||html-process-link||||html-show-buffer|html-toggle-mode|"
set-variable .fhook-html.command-nbind "|||||||||"
set-variable .fhook-html.command-kbind "|||||||C-c C-h|C-c C-i|"
set-variable .fhook-html.command-desc  "|Html Browser Mode||\HGoto Link||Html Author Mode||Process Current Buffer|Toggle Indent To VB Mode|"

; setup item-list
set-variable .fhook-html.item-list-s1 "<[hH]1>\\(\\s *<[^>]*>\\)*\\s *\\([^<]+\\)"
set-variable .fhook-html.item-list-r1 "H1   \ecB\\2\ecA"
set-variable .fhook-html.item-list-s2 "<[hH]2>\\(\\s *<[^>]*>\\)*\\s *\\([^<]+\\)"
set-variable .fhook-html.item-list-r2 "H2   \ecB\\2\ecA"
set-variable .fhook-html.item-list-s3 "<[hH]3>\\(\\s *<[^>]*>\\)*\\s *\\([^<]+\\)"
set-variable .fhook-html.item-list-r3 "H3   \ecB\\2\ecA"
set-variable .fhook-html.item-list-s4 "<[hH][4-9]>\\(\\s *<[^>]*>\\)*\\s *\\([^<]+\\)"
set-variable .fhook-html.item-list-r4 "H4+  \ecB\\2\ecA"

!if &and &sin "h" .fhook-html.setup &band .hilight.flags 0x02 
    !if &not &exist .hilight.html
        set-variable .hilight.html   &pinc .hilight.next 1
        set-variable .hilight.htmlT  &pinc .hilight.next 1
    !endif
    0 hilight .hilight.html 3 200                      $global-scheme
    hilight .hilight.html 0x80 "<\\w*" .hilight.htmlT  .scheme.keyword
    hilight .hilight.html 4 "</" ">" ""                .scheme.keyword
    hilight .hilight.html 4 "<!" ">" ""                .scheme.comment
    hilight .hilight.html 0 "&\\w\\w;"                 .scheme.variable
    hilight .hilight.html 0 "&\\w\\w\\w;"              .scheme.variable
    hilight .hilight.html 0 "&\\w\\w\\w\\w;"           .scheme.variable
    hilight .hilight.html 0 "&\\w\\w\\w\\w\\w;"        .scheme.variable
    hilight .hilight.html 0 "&\\w\\w\\w\\w\\w;"        .scheme.variable
    hilight .hilight.html 0 "&\\w\\w\\w\\w\\w\\w;"     .scheme.variable
    hilight .hilight.html 0x804 "\"" "\"" "\\"         .scheme.string
    hilight .hilight.html 4 "<script" "</script>" ""   .scheme.keyword
    hilight .hilight.html 4 "<%" "%>" ""               .scheme.keyword
    
    0 hilight .hilight.htmlT 1                         $global-scheme
    hilight .hilight.htmlT 4  "=\\{\"" "\"" "\\"       .scheme.string
    hilight .hilight.htmlT 4  "=\\{" "\\}[ \t\n>]" ""  .scheme.string
    hilight .hilight.htmlT 1  "abbr"                   .scheme.operator
    hilight .hilight.htmlT 1  "above"                  .scheme.operator
    hilight .hilight.htmlT 1  "accept-charset"         .scheme.operator
    hilight .hilight.htmlT 1  "accesskey"              .scheme.operator
    hilight .hilight.htmlT 1  "action"                 .scheme.operator
    hilight .hilight.htmlT 1  "align"                  .scheme.operator
    hilight .hilight.htmlT 1  "alink"                  .scheme.operator
    hilight .hilight.htmlT 1  "alt"                    .scheme.operator
    hilight .hilight.htmlT 1  "archive"                .scheme.operator
    hilight .hilight.htmlT 1  "axis"                   .scheme.operator
    hilight .hilight.htmlT 1  "background"             .scheme.operator
    hilight .hilight.htmlT 1  "balance"                .scheme.operator
    hilight .hilight.htmlT 1  "behavior"               .scheme.operator
    hilight .hilight.htmlT 1  "behaviour"              .scheme.operator
    hilight .hilight.htmlT 1  "below"                  .scheme.operator
    hilight .hilight.htmlT 1  "bgcolor"                .scheme.operator
    hilight .hilight.htmlT 1  "bgproperties"           .scheme.operator
    hilight .hilight.htmlT 1  "border"                 .scheme.operator
    hilight .hilight.htmlT 1  "bordercolor"            .scheme.operator
    hilight .hilight.htmlT 1  "bordercolordark"        .scheme.operator
    hilight .hilight.htmlT 1  "bordercolorlight"       .scheme.operator
    hilight .hilight.htmlT 1  "bottommargin"           .scheme.operator
    hilight .hilight.htmlT 1  "cellpadding"            .scheme.operator
    hilight .hilight.htmlT 1  "cellspacing"            .scheme.operator
    hilight .hilight.htmlT 1  "challenge"              .scheme.operator
    hilight .hilight.htmlT 1  "char"                   .scheme.operator
    hilight .hilight.htmlT 1  "charoff"                .scheme.operator
    hilight .hilight.htmlT 1  "charset"                .scheme.operator
    hilight .hilight.htmlT 1  "checked"                .scheme.operator
    hilight .hilight.htmlT 1  "cite"                   .scheme.operator
    hilight .hilight.htmlT 1  "class"                  .scheme.operator
    hilight .hilight.htmlT 1  "classid"                .scheme.operator
    hilight .hilight.htmlT 1  "clear"                  .scheme.operator
    hilight .hilight.htmlT 1  "clip"                   .scheme.operator
    hilight .hilight.htmlT 1  "code"                   .scheme.operator
    hilight .hilight.htmlT 1  "codebase"               .scheme.operator
    hilight .hilight.htmlT 1  "codetype"               .scheme.operator
    hilight .hilight.htmlT 1  "color"                  .scheme.operator
    hilight .hilight.htmlT 1  "cols"                   .scheme.operator
    hilight .hilight.htmlT 1  "colspan"                .scheme.operator
    hilight .hilight.htmlT 1  "compact"                .scheme.operator
    hilight .hilight.htmlT 1  "content"                .scheme.operator
    hilight .hilight.htmlT 1  "coords"                 .scheme.operator
    hilight .hilight.htmlT 1  "data"                   .scheme.operator
    hilight .hilight.htmlT 1  "datafld"                .scheme.operator
    hilight .hilight.htmlT 1  "dataformatas"           .scheme.operator
    hilight .hilight.htmlT 1  "datapagesize"           .scheme.operator
    hilight .hilight.htmlT 1  "datasrc"                .scheme.operator
    hilight .hilight.htmlT 1  "datetime"               .scheme.operator
    hilight .hilight.htmlT 1  "declare"                .scheme.operator
    hilight .hilight.htmlT 1  "dir"                    .scheme.operator
    hilight .hilight.htmlT 1  "direction"              .scheme.operator
    hilight .hilight.htmlT 1  "disabled"               .scheme.operator
    hilight .hilight.htmlT 1  "dynsrc"                 .scheme.operator
    hilight .hilight.htmlT 1  "enctype"                .scheme.operator
    hilight .hilight.htmlT 1  "face"                   .scheme.operator
    hilight .hilight.htmlT 1  "for"                    .scheme.operator
    hilight .hilight.htmlT 1  "frame"                  .scheme.operator
    hilight .hilight.htmlT 1  "frameborder"            .scheme.operator
    hilight .hilight.htmlT 1  "framespacing"           .scheme.operator
    hilight .hilight.htmlT 1  "gutter"                 .scheme.operator
    hilight .hilight.htmlT 1  "headers"                .scheme.operator
    hilight .hilight.htmlT 1  "height"                 .scheme.operator
    hilight .hilight.htmlT 1  "hidden"                 .scheme.operator
    hilight .hilight.htmlT 1  "href"                   .scheme.operator
    hilight .hilight.htmlT 1  "hreflang"               .scheme.operator
    hilight .hilight.htmlT 1  "hspace"                 .scheme.operator
    hilight .hilight.htmlT 1  "http-equiv"             .scheme.operator
    hilight .hilight.htmlT 1  "id"                     .scheme.operator
    hilight .hilight.htmlT 1  "ismap"                  .scheme.operator
    hilight .hilight.htmlT 1  "label"                  .scheme.operator
    hilight .hilight.htmlT 1  "lang"                   .scheme.operator
    hilight .hilight.htmlT 1  "language"               .scheme.operator
    hilight .hilight.htmlT 1  "left"                   .scheme.operator
    hilight .hilight.htmlT 1  "leftmargin"             .scheme.operator
    hilight .hilight.htmlT 1  "link"                   .scheme.operator
    hilight .hilight.htmlT 1  "longdesc"               .scheme.operator
    hilight .hilight.htmlT 1  "loop"                   .scheme.operator
    hilight .hilight.htmlT 1  "lowsrc"                 .scheme.operator
    hilight .hilight.htmlT 1  "marginheight"           .scheme.operator
    hilight .hilight.htmlT 1  "marginwidth"            .scheme.operator
    hilight .hilight.htmlT 1  "maxlength"              .scheme.operator
    hilight .hilight.htmlT 1  "mayscript"              .scheme.operator
    hilight .hilight.htmlT 1  "media"                  .scheme.operator
    hilight .hilight.htmlT 1  "method"                 .scheme.operator
    hilight .hilight.htmlT 1  "methods"                .scheme.operator
    hilight .hilight.htmlT 1  "multiple"               .scheme.operator
    hilight .hilight.htmlT 1  "name"                   .scheme.operator
    hilight .hilight.htmlT 1  "noShade"                .scheme.operator
    hilight .hilight.htmlT 1  "nohref"                 .scheme.operator
    hilight .hilight.htmlT 1  "noresize"               .scheme.operator
    hilight .hilight.htmlT 1  "nowrap"                 .scheme.operator
    hilight .hilight.htmlT 1  "object"                 .scheme.operator
    hilight .hilight.htmlT 0x200 "on"
    hilight .hilight.htmlT 1  "onabort"                .scheme.operator
    hilight .hilight.htmlT 1  "onafterupdate"          .scheme.operator
    hilight .hilight.htmlT 1  "onbeforeunload"         .scheme.operator
    hilight .hilight.htmlT 1  "onbeforeupdate"         .scheme.operator
    hilight .hilight.htmlT 1  "onblur"                 .scheme.operator
    hilight .hilight.htmlT 1  "onchange"               .scheme.operator
    hilight .hilight.htmlT 1  "onclick"                .scheme.operator
    hilight .hilight.htmlT 1  "ondataavailable"        .scheme.operator
    hilight .hilight.htmlT 1  "ondatasetchanged"       .scheme.operator
    hilight .hilight.htmlT 1  "ondatasetcomplete"      .scheme.operator
    hilight .hilight.htmlT 1  "ondblclick"             .scheme.operator
    hilight .hilight.htmlT 1  "ondragdrop"             .scheme.operator
    hilight .hilight.htmlT 1  "ondragstart"            .scheme.operator
    hilight .hilight.htmlT 1  "onerror"                .scheme.operator
    hilight .hilight.htmlT 1  "onerrorupdate"          .scheme.operator
    hilight .hilight.htmlT 1  "onfilterchange"         .scheme.operator
    hilight .hilight.htmlT 1  "onfocus"                .scheme.operator
    hilight .hilight.htmlT 1  "onhelp"                 .scheme.operator
    hilight .hilight.htmlT 1  "onkeydown"              .scheme.operator
    hilight .hilight.htmlT 1  "onkeypress"             .scheme.operator
    hilight .hilight.htmlT 1  "onkeyup"                .scheme.operator
    hilight .hilight.htmlT 1  "onload"                 .scheme.operator
    hilight .hilight.htmlT 1  "onmousedown"            .scheme.operator
    hilight .hilight.htmlT 1  "onmousemove"            .scheme.operator
    hilight .hilight.htmlT 1  "onmouseout"             .scheme.operator
    hilight .hilight.htmlT 1  "onmouseover"            .scheme.operator
    hilight .hilight.htmlT 1  "onmouseup"              .scheme.operator
    hilight .hilight.htmlT 1  "onmove"                 .scheme.operator
    hilight .hilight.htmlT 1  "onreadystatechange"     .scheme.operator
    hilight .hilight.htmlT 1  "onreset"                .scheme.operator
    hilight .hilight.htmlT 1  "onresize"               .scheme.operator
    hilight .hilight.htmlT 1  "onrowenter"             .scheme.operator
    hilight .hilight.htmlT 1  "onrowexit"              .scheme.operator
    hilight .hilight.htmlT 1  "onscroll"               .scheme.operator
    hilight .hilight.htmlT 1  "onselect"               .scheme.operator
    hilight .hilight.htmlT 1  "onselectstart"          .scheme.operator
    hilight .hilight.htmlT 1  "onsubmit"               .scheme.operator
    hilight .hilight.htmlT 1  "onunload"               .scheme.operator
    hilight .hilight.htmlT 1  "pagex"                  .scheme.operator
    hilight .hilight.htmlT 1  "pagey"                  .scheme.operator
    hilight .hilight.htmlT 1  "palette"                .scheme.operator
    hilight .hilight.htmlT 1  "pluginspage"            .scheme.operator
    hilight .hilight.htmlT 1  "pluginurl"              .scheme.operator
    hilight .hilight.htmlT 1  "point-size"             .scheme.operator
    hilight .hilight.htmlT 1  "profile"                .scheme.operator
    hilight .hilight.htmlT 1  "prompt"                 .scheme.operator
    hilight .hilight.htmlT 1  "readonly"               .scheme.operator
    hilight .hilight.htmlT 1  "rel"                    .scheme.operator
    hilight .hilight.htmlT 1  "rev"                    .scheme.operator
    hilight .hilight.htmlT 1  "rightmargin"            .scheme.operator
    hilight .hilight.htmlT 1  "rows"                   .scheme.operator
    hilight .hilight.htmlT 1  "rowspan"                .scheme.operator
    hilight .hilight.htmlT 1  "rules"                  .scheme.operator
    hilight .hilight.htmlT 1  "runat"                  .scheme.operator
    hilight .hilight.htmlT 1  "scheme"                 .scheme.operator
    hilight .hilight.htmlT 1  "scope"                  .scheme.operator
    hilight .hilight.htmlT 1  "scrollamount"           .scheme.operator
    hilight .hilight.htmlT 1  "scrolldelay"            .scheme.operator
    hilight .hilight.htmlT 1  "scrolling"              .scheme.operator
    hilight .hilight.htmlT 1  "selected"               .scheme.operator
    hilight .hilight.htmlT 1  "shape"                  .scheme.operator
    hilight .hilight.htmlT 1  "shapes"                 .scheme.operator
    hilight .hilight.htmlT 1  "size"                   .scheme.operator
    hilight .hilight.htmlT 1  "span"                   .scheme.operator
    hilight .hilight.htmlT 1  "src"                    .scheme.operator
    hilight .hilight.htmlT 1  "standby"                .scheme.operator
    hilight .hilight.htmlT 1  "start"                  .scheme.operator
    hilight .hilight.htmlT 1  "style"                  .scheme.operator
    hilight .hilight.htmlT 1  "summary"                .scheme.operator
    hilight .hilight.htmlT 1  "tabindex"               .scheme.operator
    hilight .hilight.htmlT 1  "target"                 .scheme.operator
    hilight .hilight.htmlT 1  "text"                   .scheme.operator
    hilight .hilight.htmlT 1  "title"                  .scheme.operator
    hilight .hilight.htmlT 1  "top"                    .scheme.operator
    hilight .hilight.htmlT 1  "topmargin"              .scheme.operator
    hilight .hilight.htmlT 1  "truespeed"              .scheme.operator
    hilight .hilight.htmlT 1  "type"                   .scheme.operator
    hilight .hilight.htmlT 1  "urn"                    .scheme.operator
    hilight .hilight.htmlT 1  "usemap"                 .scheme.operator
    hilight .hilight.htmlT 1  "valign"                 .scheme.operator
    hilight .hilight.htmlT 1  "value"                  .scheme.operator
    hilight .hilight.htmlT 1  "valuetype"              .scheme.operator
    hilight .hilight.htmlT 1  "version"                .scheme.operator
    hilight .hilight.htmlT 1  "visibility"             .scheme.operator
    hilight .hilight.htmlT 1  "vlink"                  .scheme.operator
    hilight .hilight.htmlT 1  "volume"                 .scheme.operator
    hilight .hilight.htmlT 1  "vspace"                 .scheme.operator
    hilight .hilight.htmlT 1  "weight"                 .scheme.operator
    hilight .hilight.htmlT 1  "width"                  .scheme.operator
    hilight .hilight.htmlT 1  "wrap"                   .scheme.operator
    hilight .hilight.htmlT 1  "z-index"                .scheme.operator
    hilight .hilight.htmlT 0  "\\w+"                   .scheme.error
    hilight .hilight.htmlT 0x80 ">" .hilight.html      .scheme.keyword
    
    !force exec-file "hkvb"
    !if $status
        hilight .hilight.html  0x80 "<%" .hilight.vb   .scheme.prepro
        hilight .hilight.htmlT 0x80 "<%" .hilight.vb   .scheme.prepro
        hilight .hilight.vb    0x80 "%>" .hilight.html .scheme.prepro
        hilight .hilight.html  0x80 "<script\\s +language=VBScript[^>]*>" .hilight.vb .scheme.prepro
        hilight .hilight.vb    0x80 "</script>" 0      .scheme.prepro
        ;    indent .hilight.vb b "<" ">"
    !endif
    !force exec-file "hkjava"
    !if $status
        hilight .hilight.html 0x80 "<script\\s +language=javascript[^>]*>" .hilight.java .scheme.prepro
        hilight .hilight.html 0x80 "<script\\s +language=\"javascript\"[^>]*>" .hilight.java .scheme.prepro
        hilight .hilight.java 0x80 "</script>" 0       .scheme.prepro
        hilight .hilight.java 0x80 "</SCRIPT>" 0       .scheme.prepro
    !endif
!endif
!if &and &sin "h" .fhook-html.setup &band .hilight.flags 0x01 
    !if &not &exist .hilight.htmlN
        set-variable .hilight.htmlN  &pinc .hilight.next 1
        set-variable .hilight.htmlNL &pinc .hilight.next 1
    !endif
    0 hilight .hilight.htmlN 0                          $global-scheme
    hilight .hilight.htmlN 0x44 "\ecA" "" "\\}\e" "" "" $global-scheme
    hilight .hilight.htmlN 0x44 "\ecB" "" "\\}\e" "" "" .scheme.under    ; underline
    hilight .hilight.htmlN 0x44 "\ecC" "" "\\}\e" "" "" .scheme.italic   ; italic
    hilight .hilight.htmlN 0x44 "\ecD" "" "\\}\e" "" "" .scheme.bold     ; bold
    hilight .hilight.htmlN 0x44 "\ecE" "" "\\}\e" "" "" .scheme.header   ; Header
    hilight .hilight.htmlN 0x44 "\ecF" "" "\\}\e" "" "" .scheme.comment  ; image
    hilight .hilight.htmlN 0x44 "\ecG" "" "\\}\e" "" "" .scheme.type     ; TT (curior)
    hilight .hilight.htmlN 0xc0 "\els" "" .hilight.htmlNL $global-scheme ; link start
    hilight .hilight.htmlN 0x44 "\elm" "" "\ele" "" ""  .scheme.link     ; link name
    
    0 hilight .hilight.htmlNL 0                         $global-scheme
    hilight .hilight.htmlNL 0x80 "\\}\elm" .hilight.htmlN $global-scheme
    hilight .hilight.htmlNL 0x40 "." ""                 $global-scheme
!endif

!if &sin "d" .fhook-html.setup
    !if &not &exist .hilight.html
        set-variable .hilight.html   &pinc .hilight.next 1
    !endif
    ; Define the indentation tokens 
    0 indent .hilight.html 1 20 
    indent .hilight.html e "\"" "\"" "\\"
    indent .hilight.html b "<" ">"
    indent .hilight.html n "<html>" 2 
    indent .hilight.html o "</html>" -2 
    indent .hilight.html n "<head>" 2 
    indent .hilight.html o "</head>" -2 
    indent .hilight.html n "<ul>" 2 
    indent .hilight.html o "</ul>" -2 
    indent .hilight.html n "<table[^>]*>" 2 
    indent .hilight.html o "</table>" -2 
    indent .hilight.html n "<tr[^>]*>" 2 
    indent .hilight.html o "</tr>" -2 
    indent .hilight.html o "</td>" -2 
    indent .hilight.html n "<td[^>]*>" 2 
    indent .hilight.html o "</select>" -2 
    indent .hilight.html n "<select[^>]*>" 2 
    indent .hilight.html e "<%" "%>" ""
    
    !if &sin "d" .fhook-vb.setup
        indent .hilight.vb n "<%" 2 
        indent .hilight.vb o "%>" -2 
    !endif
!endif

define-macro-file htmltool html-to-ehf

0 define-macro html-process-link
    set-variable #l0 $window-col
    set-variable #l1 $window-line
    ; end of link must be found within 5 lines
    !force -5 search-forward "\ele"
    !if $status
        !force -5 search-backward "\els"
        !if &and $status &or &les $window-line #l1 &and &equ $window-line #l1 &not &gre $window-col #l0
            3 forward-char
            set-mark
            -1 search-forward "\elm"
            3 backward-char
            copy-region
            set-variable #l0 @y
            -1 yank
            !if &set #l1 &sin "#" #l0
                set-variable #l8 &rig #l0 #l1
                set-variable #l0 &lef #l0 &sub #l1 1
            !else
                set-variable #l8 ""
            !endif
            !if &not &seq #l0 ""
                !if &and &seq &lef #l0 1 "/" &set #l2 &sin "/" &rig $buffer-fname 7
                    set-variable #l0 &cat &lef $buffer-fname &add 6 #l2 #l0
                !endif
                !force find-file #l0
                !if &not $status
                    ml-write &spr "[Failed to find link \"%s\"]" #l0
                    !abort
                !endif
            !endif
            !if &not &seq #l8 ""
                !force search-forward &cat #l8 "\elm"
                beginning-of-line
            !endif
            !return
        !endif
    !endif
    set-variable $window-line #l1
    set-variable $window-col #l0
    ml-write "Error! Not in a link"
    !abort
!emacro

define-macro html-process-buffer
    set-variable :fill-col &sub $window-width 2
    beginning-of-buffer
    set-variable #g0 0
    html-to-ehf
    beginning-of-buffer
    !if &iseq &lef $buffer-fname 7 "http://"
        set-variable #l0 &lef $buffer-fname &add 7 &sin "/" &rig $buffer-fname 7
    !else
        set-variable #l0 "file:/"
    !endif
    1 buffer-mode "magic"
    replace-string "http:/\\([^/]\\)" &cat #l0 "\\1"
    beginning-of-buffer
    !if &and &sin "h" .fhook-html.setup &band .hilight.flags 0x01 
        set-variable $buffer-hilight .hilight.htmlN
    !endif
    -1 buffer-mode "edit"
    1 buffer-mode "view"
    set-variable :mouse-word-select html-process-link
    buffer-bind-create "bio" "return" "" html-process-link
    ml-write "Done!"
!emacro

0 define-macro html-show-buffer
    end-of-buffer
    set-mark
    beginning-of-buffer
    copy-region
    set-variable #l8 $buffer-fname
    !force 0 delete-buffer &cat "*" $buffer-bname
    find-buffer &cat "*" $buffer-bname
    set-variable $buffer-fname &lef #l8 &rsin "/" #l8
    yank
    -1 yank
    html-process-buffer
!emacro


0 define-macro html-spell-word
    ; simply skip any text in a <..> token - put a 5 line limit on it
    set-variable #l0 $window-col
    set-variable #l1 $window-line
    -2 show-region
    set-variable #l2 $window-col
    !force -5 regex-backward "[<>]"
    !if $status
        set-variable #l3 @wc
        set-variable $window-line #l1
        set-variable $window-col #l0
        !if &seq #l3 "<"
            ; this is a <..> token, skip all if spelling buffer (@# != 0)
            !if @#
                !force search-forward ">"
                set-variable .spell.check-word ""
            !endif
            !return
        !endif
    !endif
    ; must redefine the region as the search blows it away
    set-variable $window-col #l2
    -3 show-region
    set-variable $window-col #l0
    3 show-region
    !abort
!emacro

0 define-macro html-toggle-mode
    !if &seq $buffer-fhook "fhook-html"
        set-variable :auto-spell &cond &seq $buffer-input "auto-spell-input" 1 -1
        !if &sin "s" .fhook-html.setup
            -1 auto-spell
        !endif
        set-variable $buffer-fhook "fhook-vb"
        !if &sin "d" .fhook-html.setup
            set-variable $buffer-indent .hilight.vb
        !endif
    !else
        set-variable $buffer-fhook "fhook-html"
        !if &sin "d" .fhook-html.setup
            set-variable $buffer-indent .hilight.html
        !endif
        !if &sin "s" .fhook-html.setup
            :auto-spell auto-spell
        !endif
    !endif
    !if .toolbar.open
        ; flag that the buffer type has changed
        set-variable .toolbar-redraw.bc &bor .toolbar-redraw.bc 0x004
    !endif
!emacro

0 define-macro html-get-color-schm
    set-variable #l8 @1
    add-color @# &cat "0x" &lef #l8 2 &cat "0x" &mid #l8 2 2 &cat "0x" &rig #l8 4
    add-color-scheme @# 0 @# 0 @# 0 @# 0 @#
!emacro

0 define-macro html-get-color-com
    set-variable #l8 .html-get-color.icolor
    set-variable #l0 &band &abs @# 3
    set-variable #l9 &mul &sub #l0 1 2
    set-variable #l1 &cat "0x" &mid #l8 #l9 2
    !if &band &abs @# 0xff000
        ; incramental button press
        !if &band 0x100 @#
            set-variable #l1 &sub #l1 &div @# 0x1000
        !else
            set-variable #l1 &add #l1 &div @# 0x1000
        !endif
    !else
        !if &band &abs @# 0x010
            set-variable #l1 &rig #l1 2
        !else
            set-variable #l1 &spr "%3d" #l1
        !endif
        !if &gre @# 0
            set-variable $result #l1
            !return
        !endif
        set-variable #l1 @ml2 "" #l1
        !if &band &abs @# 0x010
            set-variable #l1 &cat "0x" &add #l1 0
        !endif
    !endif
    !if &les #l1 0
        set-variable #l1 0
    !elif &gre #l1 255
        set-variable #l1 255
    !endif
    set-variable #l8 &spr "%s%02x%s" &lef #l8 #l9 #l1 &rig #l8 &add #l9 2
    set-variable .html-get-color.icolor #l8
    127 html-get-color-schm #l8
!emacro

0 define-macro html-get-color-pal
    !if &not @?
        -1 osd .osd.tmp
        osd .osd.tmp 0 "btcHds" 38 12 -1 -1 610 .scheme.osd-title "Set HTML Pallette Color" 
        osd .osd.tmp 100 ""
        osd .osd.tmp 200 "Ic" .osd.htmlcolr
        osd .osd.tmp 300 ""
        osd .osd.tmp 610 "BtcfH" 61 .scheme.osd-ebtt " \HCancel " f void
        set-variable .mode 1
        !force .osd.tmp osd
        set-variable .mode 0
    !elif .mode
        set-registry "/history" &cat "fhook/html-col" @# .html-get-color.icolor
        &add @# 128 html-get-color-schm .html-get-color.icolor
        !abort
    !else
        set-variable #l8 &reg "/history" &cat "fhook/html-col" @# &lget .html-get-color.default &add @# 1
        set-variable .html-get-color.icolor #l8
        127 html-get-color-schm #l8
    !endif
!emacro

set-variable #l0 &mid $window-chars  6 1
set-variable #l1 &mid $window-chars 10 1
!if &not &exist .osd.htmlcol
    set-variable .osd.htmlcol &pinc .osd.next 1
    add-color 127 0 0 0
    add-color-scheme 127 0 127 0 127 0 127 0 127
!endif
set-variable .osd.htmlcoll &pinc .osd.next 1
set-variable .osd.htmlcolr &pinc .osd.next 1
osd .osd.htmlcoll 0   "s" 9 9 -1 -1
osd .osd.htmlcoll 110 "fh"  "          "
osd .osd.htmlcoll 111 "RTfxh" #l0 0x64001 html-get-color-com
osd .osd.htmlcoll 112 "RTfxh" #l0 0x0a001 html-get-color-com
osd .osd.htmlcoll 113 "RTfxh" #l0 0x01001 html-get-color-com
osd .osd.htmlcoll 120 "fh"  "   "
osd .osd.htmlcoll 121 "RTfxh" #l0 0x10011 html-get-color-com
osd .osd.htmlcoll 122 "RTfx"  #l0 0x01011 html-get-color-com
osd .osd.htmlcoll 130 "Sfh" "    \HRed : " 140
osd .osd.htmlcoll 140 "REtxHfh" 14 .scheme.osd-entry "####" 0x001 html-get-color-com
osd .osd.htmlcoll 145 "fh"  "  "
osd .osd.htmlcoll 150 "REtxHf" 15 .scheme.osd-entry "###"  0x011 html-get-color-com
osd .osd.htmlcoll 160 "fh"  "          "
osd .osd.htmlcoll 161 "RTfxh" #l1 0x64101 html-get-color-com
osd .osd.htmlcoll 162 "RTfxh" #l1 0x0a101 html-get-color-com
osd .osd.htmlcoll 163 "RTfxh" #l1 0x01101 html-get-color-com
osd .osd.htmlcoll 170 "fh"  "   "
osd .osd.htmlcoll 171 "RTfxh" #l1 0x10111 html-get-color-com
osd .osd.htmlcoll 172 "RTfx"  #l1 0x01111 html-get-color-com
osd .osd.htmlcoll 210 "fh"  "          "
osd .osd.htmlcoll 211 "RTfxh" #l0 0x64002 html-get-color-com
osd .osd.htmlcoll 212 "RTfxh" #l0 0x0a002 html-get-color-com
osd .osd.htmlcoll 213 "RTfxh" #l0 0x01002 html-get-color-com
osd .osd.htmlcoll 220 "fh"  "   "
osd .osd.htmlcoll 221 "RTfxh" #l0 0x10012 html-get-color-com
osd .osd.htmlcoll 222 "RTfx"  #l0 0x01012 html-get-color-com
osd .osd.htmlcoll 230 "Sfh" "  \HGreen : " 240
osd .osd.htmlcoll 240 "REtxHfh" 24 .scheme.osd-entry "####" 0x002 html-get-color-com
osd .osd.htmlcoll 245 "fh"  "  "
osd .osd.htmlcoll 250 "REtxHf"  25 .scheme.osd-entry "###"  0x012 html-get-color-com
osd .osd.htmlcoll 260 "fh"  "          "
osd .osd.htmlcoll 261 "RTfxh" #l1 0x64102 html-get-color-com
osd .osd.htmlcoll 262 "RTfxh" #l1 0x0a102 html-get-color-com
osd .osd.htmlcoll 263 "RTfxh" #l1 0x01102 html-get-color-com
osd .osd.htmlcoll 270 "fh"  "   "
osd .osd.htmlcoll 271 "RTfxh" #l1 0x10112 html-get-color-com
osd .osd.htmlcoll 272 "RTfx"  #l1 0x01112 html-get-color-com
osd .osd.htmlcoll 310 "fh"  "          "
osd .osd.htmlcoll 311 "RTfxh" #l0 0x64003 html-get-color-com
osd .osd.htmlcoll 312 "RTfxh" #l0 0x0a003 html-get-color-com
osd .osd.htmlcoll 313 "RTfxh" #l0 0x01003 html-get-color-com
osd .osd.htmlcoll 320 "fh"  "   "
osd .osd.htmlcoll 321 "RTfxh" #l0 0x10013 html-get-color-com
osd .osd.htmlcoll 322 "RTfx"  #l0 0x01013 html-get-color-com
osd .osd.htmlcoll 330 "Sfh" "   \HBlue : " 340
osd .osd.htmlcoll 340 "REtxHfh" 34 .scheme.osd-entry "####" 0x003 html-get-color-com
osd .osd.htmlcoll 345 "fh"  "  "
osd .osd.htmlcoll 350 "REtxHf"  35 .scheme.osd-entry "###"  0x013 html-get-color-com
osd .osd.htmlcoll 360 "fh"  "          "
osd .osd.htmlcoll 361 "RTfxh" #l1 0x64103 html-get-color-com
osd .osd.htmlcoll 362 "RTfxh" #l1 0x0a103 html-get-color-com
osd .osd.htmlcoll 363 "RTfxh" #l1 0x01103 html-get-color-com
osd .osd.htmlcoll 370 "fh"  "   "
osd .osd.htmlcoll 371 "RTfxh" #l1 0x10113 html-get-color-com
osd .osd.htmlcoll 372 "RTfx"  #l1 0x01113 html-get-color-com

osd .osd.htmlcolr 0 "G"

osd .osd.htmlcol 0   "batcHds" 9 3 40 16 -1 -1 610 .scheme.osd-title "HTML Color" 
osd .osd.htmlcol 10  ""
osd .osd.htmlcol 20  ""
osd .osd.htmlcol 100 "fh" " "
osd .osd.htmlcol 110 "Ih" .osd.htmlcoll
osd .osd.htmlcol 120 "fh" "    "
osd .osd.htmlcol 130 "Hczh" 127 10 9 ""
osd .osd.htmlcol 140 "fh" "    "
osd .osd.htmlcol 200 ""
osd .osd.htmlcol 210 ""
osd .osd.htmlcol 300 "Ic" .osd.htmlcolr
osd .osd.htmlcol 500 ""
osd .osd.htmlcol 510 ""
osd .osd.htmlcol 600 "RBtcxfHh" 60 .scheme.osd-ebtt " Set \HPal " f html-get-color-pal
osd .osd.htmlcol 620 "BtcfH"  62 .scheme.osd-ebtt " \HCancel " 0 void

define-macro html-get-color
    !force set-variable #l0 @1
    !if &not $status
        set-variable #l0 " &Done "
    !endif
    set-variable .html-get-color-pal.mode 0
    osd .osd.htmlcol 610 "BtcfHh" 61 .scheme.osd-ebtt #l0 f void
    set-variable .icolor &rig .color 1
    127 html-get-color-schm .icolor
    -1 osd .osd.htmlcolr
    osd .osd.htmlcolr 0 "G"
    set-variable #l0 0
    !repeat
        set-variable #l1 &reg "/history" &cat "fhook/html-col" #l0 &lget .default &add #l0 1
        &add #l0 128 html-get-color-schm #l1
        osd .osd.htmlcolr &add 100 #l0 &cond &mod &add #l0 1 8 "RGHfxhz" "RGHfxz" &add 128 #l0 3 2 "" #l0 html-get-color-pal
    !until &not &les &inc #l0 1 16
    .osd.htmlcol osd
    set-variable .color &cat "#" .icolor
!emacro
set-variable .html-get-color.color "#000000"
set-variable .html-get-color.default "|000000|c80000|00c800|c8c800|0000c8|c800c8|00c8c8|c8c8c8|000000|ff0000|00ff00|ffff00|0000ff|ff00ff|00ffff|ffffff|"

define-macro html-forward-tag
    forward-char
    set-variable #l9 &bmod "magic"
    1 buffer-mode "magic"
    !force search-forward "[<>]"
    !if &not $status
        end-of-buffer
        !force backward-char
    !else
        backward-char
        !if &seq @s0 ">"
            forward-char
        !endif
    !endif
    &cond #l9 1 -1 buffer-mode "magic"
!emacro

define-macro html-backward-tag
    backward-char
    set-variable #l9 &bmod "magic"
    1 buffer-mode "magic"
    !force search-backward "[<>]"
    !if &not $status
        beginning-of-buffer
    !else
        forward-char
        !if &seq @s0 "<"
            backward-char
        !endif
    !endif
    &cond #l9 1 -1 buffer-mode "magic"
!emacro

define-macro html-goto-matching-tag
    set-position "\x81"
    !if &seq @wc "<"
        forward-char
    !endif
    forward-char
    html-backward-tag
    !if &not &seq @wc "<"
        goto-position "\x81"
        !abort
    !endif
    set-variable #l0 &add $window-col 1
    !if &set #l1 &seq &mid @wl #l0 1 "/"
        set-variable #l0 &add #l0 1
    !else
        forward-char
    !endif
    set-variable #l2 &rig @wl #l0
    !if &set #l0 &sin ">" #l2
        set-variable #l2 &lef #l2 &sub #l0 1
    !endif
    !if &set #l0 &sin " " #l2
        set-variable #l2 &lef #l2 &sub #l0 1
    !endif
    set-variable #l2 &cat "<\\(/?\\)" #l2
    set-variable #l9 &bmod "magic"
    set-variable #l8 &bmod "exact"
    1 buffer-mode "magic"
    -1 buffer-mode "exact"
    set-variable #l0 1
    !repeat
        !if #l1
            !force search-backward #l2
        !else
            !force search-forward #l2
        !endif
        !if $status
            set-variable #l0 &add #l0 &con &bxor #l1 &seq @s1 "/" -1 1
        !else
            set-variable #l0 -1
        !endif
    !until &les #l0 1
    &cond #l9 1 -1 buffer-mode "magic"
    &cond #l8 1 -1 buffer-mode "exact"
    !if #l0
        goto-position "\x81"
        !if &seq @p ""
            ml-write "[matching tag not found]"
        !endif
        !abort
    !else
        -2 show-region
        -1 show-region
    !endif
!emacro

0 define-macro html-collapse-tag-markup
    !if &equ @# 1
        set-variable $result &cat &lef @wl 40 " ..."
    !else
        !abort
    !endif
!emacro

define-macro html-collapse-tag
    set-position "\x83"
    set-variable #l0 $window-line
    !force html-goto-matching-tag
    !if &not $status
        ml-write "[Failed to find matching tag]"
        !abort
    !endif
    set-variable #l1 $window-line
    !if &equ #l0 #l1
        goto-position "\x83"
        ml-write "[tag is on one line - whats the point?]"
        !abort
    !endif
    !if &gre #l1 #l0
        html-forward-tag
        set-variable #l5 &rig @wl $window-col
        backward-char
    !else
        set-variable #l4 &lef @wl $window-col
    !endif
    !force html-goto-matching-tag
    !if &not &and $status &equ $window-line #l0
        goto-position "\x83"
        ml-write "[Weird tag mismatch problem - abort]"
        !abort
    !endif
    !if &gre #l0 #l1
        html-forward-tag
        set-variable #l5 &rig @wl $window-col
        forward-line
    !else
        set-variable #l4 &lef @wl $window-col
    !endif
    !if &not &seq &trb #l4 ""
        goto-position "\x83"
        ml-write "[Extra text at the start of tag - abort]"
        !abort
    !elif &not &seq &trb #l5 ""
        goto-position "\x83"
        ml-write "[Extra text at the end of tag - abort]"
        !abort
    !endif
    set-mark
    set-variable $window-line #l1
    !if &gre #l1 #l0
        forward-line
    !endif
    0x34 narrow-buffer .scheme.fold-close html-collapse-tag-markup
!emacro

; n html-find-tag "name"
; If n is 0 then the search starts at the top of the buffer
define-macro html-find-tag
    !force set-variable #l0 @1
    !if &not $status
        set-variable #l0 @ml "Find HTML Tag"
    !endif
    !if &not @#
        beginning-of-buffer
    !endif
    set-variable #l9 &bmod "exact"
    -1 buffer-mode "exact"
    !force search-forward &cat "<" #l0
    set-variable #l1 $status
    &cond #l9 1 -1 buffer-mode "exact"
    !if &not #l1
        !if &seq @p ""
            ml-write "[Tag not found]"
        !endif
        !abort
    !endif
!emacro

; n html-insert-tag "name"
; n is a bitwise flag where:
;      0x01  insert </name> end tag
;      0x02  ignore the current region
;      0x04  leave cursor at end of start tag
define-macro html-insert-tag
    set-variable #l2 0
    !if &not &band 0x02 @#
        0 show-region
        !if &equ &band $result 0x05 0x05
            set-variable #l2 $result
            3 show-region
        !endif
    !endif
    !force set-variable #l0 @1
    !if &not $status
        set-variable #l0 @ml "Insert HTML Tag"
    !endif
    !if #l2
        ; get the end of the current  region
        &cond &band #l2 0x08 -2 2 show-region
        set-variable #l3 $window-line
        set-variable #l4 $window-col
        &cond &band #l2 0x08 2 -2 show-region
    !endif
    set-position "\x81"
    !force forward-char
    !force html-backward-tag
    !if &not $status
        goto-position "\x81"
    !elif &seq @wc "<"
        !force html-forward-tag
    !else
        goto-position "\x81"
    !endif
    insert-string &spr "<%s>" #l0
    !if &band @# 1
        set-variable #l5 $window-line
        set-variable #l6 $window-col
        !if #l2
            !if &equ $window-line #l3
                set-variable $window-col &add #l4 &add &len #l0 2
            !else
                set-variable $window-line #l3
                set-variable $window-col #l4
            !endif
            set-position "\x81"
            !force forward-char
            !force html-backward-tag
            !if &not $status
                goto-position "\x81"
            !elif &seq @wc "<"
                !force html-forward-tag
            !else
                goto-position "\x81"
            !endif
        !endif
        set-variable #l1 $window-col
        insert-string &spr "</%s>" #l0
        !if &band @# 4
            set-variable $window-line #l5
            set-variable $window-col #l6
        !else
            set-variable $window-col #l1
        !endif
    !endif
!emacro

; n html-insert-attrib "name"
; n is a bitwise flag where:
;      0x01  search for existing value
;      0x02  insert an =
;      0x04  insert an =""
define-macro html-insert-attrib
    !force set-variable #l0 @1
    !if &not $status
        set-variable #l0 @ml "HTML Attribute"
    !endif
    0x13f set-position "\x81"
    !force html-backward-tag
    !if &not &and $status &seq @wc "<"
        0x0bf goto-position "\x81"
        ml-write "[Error: Not in a tag]"
        !abort
    !endif
    set-mark
    !force html-forward-tag
    !if &not $status
        !jump 3
    !elif &seq @wc "<"
        backward-char
        search-backward "\\S "
        forward-char
        !if &seq @wc " "
            forward-char
        !endif
    !else
        backward-char
        !if &not &seq @wc ">"
            forward-char
        !endif
    !endif
    !if &band @# 1
        copy-region
        !if &xis @y &spr ".*\\s %s%s\\(.*\\)" #l0 &cond &band @# 2 "=\"?" ""
            set-variable #l1 &len @s1
            #l1 backward-char
            -1 yank
            0x500 goto-position "\x81"
            !return
        !endif
        -1 yank
    !endif
    0x100 goto-position "\x81"
    !if &not $window-col
    !elif &not &seq &mid @wl &sub $window-col 1 1 " "
        insert-space
    !endif
    insert-string #l0
    !if &band @# 2
        insert-string "="
    !elif &band @# 4
        insert-string "=\"\""
        backward-char
    !endif
!emacro

0 define-macro html-insert-main-tag
    set-variable #l9 &bmod "exact"
    -1 buffer-mode "exact"
    beginning-of-buffer
    !force search-forward &cat "<" &lget .tags @#
    !if $status
        backward-char
        !force html-forward-tag
    !elif &seq &lef &lget .tags @# 1 "/"
        &sub @# 1 html-insert-main-tag
        search-forward &cat "<" &lget .tags @#
        forward-char
    !else
        !if &not &set #l1 &lget "|0|1|2|2|3|" @#
            end-of-buffer
            insert-string &spr "</%s>" &lget .tags @#
            beginning-of-buffer
            insert-string &spr "<%s>\n" &lget .tags @#
            backward-char
        !else
            #l1 html-insert-main-tag
            insert-string &spr "\n<%s>" &lget .tags @#
            set-variable #l2 $window-line
            set-variable #l3 $window-col
            !if &set #l1 &lget "|0|5|0|0|1|" @#
                set-variable #l1 &cat "</" &lget .tags #l1
                !force search-forward #l1
                !if $status
                    &add &len #l1 1 backward-char
                !endif
            !endif
            !if &not &equ @# 4
                insert-newline
            !endif
            insert-string &spr "</%s>" &lget .tags @#
            set-variable $window-line #l2
            set-variable $window-col #l3
        !endif
    !endif
    &cond #l9 1 -1 buffer-mode "exact"
!emacro
set-variable .html-insert-main-tag.tags "|HTML|HEAD|/HEAD|TITLE|BODY|"

define-macro html-insert-color
    html-get-color " &Insert "
    insert-string .html-get-color.color
!emacro

define-macro html-create-table
    !force set-variable #l1 @2
    !if $status
        set-variable #l0 @1
    !else
        set-variable #l0 @ml1 "Number of Columns" 2
        set-variable #l1 @ml1 "Number of Rows" 2
    !endif
    set-variable #l2 $window-col
    set-variable #l4 0
    insert-string "<TABLE>\n" @mna
    !while &gre &pdec #l1 1 0
        !if $buffer-indent
            tab
        !else
            &add #l2 2 insert-space
        !endif
        insert-string "<TR>\n"
        !if &not #l4
            set-variable #l4 $window-line
        !endif
        set-variable #l3 #l0
        !if &gre &pdec #l3 1 0
            !if $buffer-indent
                tab
            !else
                &add #l2 4 insert-space
            !endif
            insert-string "<TD></TD>\n"
            !jump -7
        !endif
        !if $buffer-indent
            insert-string "</TR>"
            newline
        !else
            insert-string &spr "%n</TR>\n" &add #l2 2 " "
        !endif
    !done
    !if $buffer-indent
        insert-string "</TABLE>"
        tab
    !else
        insert-string &spr "%n</TABLE>" #l2 " "
    !endif
    !if #l4
        set-variable $window-line #l4
        end-of-line
        5 backward-char
    !endif
!emacro

0 define-macro html-view-launch
    !if &seq $buffer-fname ""
        ml-write "[Current buffer has no file name]"
        !abort
    !endif
    !if &bmod "edit"
        !if &iseq @mc1 "Save buffer first [y/n]? " "nNyY" "y"
            save-buffer
        !endif
    !endif
    set-variable #l1 $buffer-fname
    !if &seq &lef #l1 7 "http://"
    !elif &seq &lef #l1 6 "ftp://"
    !else
        !if &band $system 0x100
            set-variable #l1 &rep #l1 "/" "\\"
        !endif
        set-variable #l1 &cat "file:" #l1
    !endif
    set-variable #l0 &rep @1 "%%" "\b"
    set-variable #l0 &rep #l0 "%f" #l1
    set-variable #l0 &rep #l0 "\b" "%"
    2 ipipe-shell-command #l0 "*html-viewer*" @mna
!emacro
define-macro html-view-iexplorer
    set-variable #l0 &reg "/history" &cat $platform "/html-iexplorer" "iexplore \"%s\""
    html-view-launch #l0
!emacro
define-macro html-view-netscape
    set-variable #l0 &reg "/history" &cat $platform "/html-netscape" "netscape \"%s\""
    html-view-launch #l0
!emacro

define-macro html-config-iexplorer
    set-variable #l0 &reg "/history" &cat $platform "/html-iexplorer" "iexplore \"%s\""
    set-variable #l0 @ml2 "IExplorer Command-line" #l0
    set-registry "/history" &cat $platform "/html-iexplorer" #l0
!emacro
define-macro html-config-netscape
    set-variable #l0 &reg "/history" &cat $platform "/html-netscape" "netscape \"%s\""
    set-variable #l0 @ml2 "Netscape Command-line" #l0
    set-registry "/history" &cat $platform "/html-netscape" #l0
!emacro

ml-write "[HTML file hook loaded]"

; load in user extensions if found
!force execute-file "myhtml"

