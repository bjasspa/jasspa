;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;
;  Binary file hook
;
;  Last Modified : <010228.1410>
;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;
; buffer-setup flags
set-variable #l0 &reg "/history" "fhook/binary" "hop"
set-variable #l1 "aehkmopu"

!if &and &sin "h" #l0 &band .hilight.flags 0x02 
    !if &not &exist .hilight.binary
        set-variable .hilight.binary &pinc .hilight.next 1
    !endif
    0 hilight .hilight.binary 0         $global-scheme
    hilight .hilight.binary 0x400  0  9 .scheme.no2
    hilight .hilight.binary 0x400 58 78 .scheme.no1
!endif

define-macro binary-move-backward
    set-variable #l5 @ml "Move backward by"
    calc #l5
    set-variable #l5 .calc.result
    !if &gre $window-col 57
        !if &gre $window-col 62
            set-variable #l5 &sub #l5 &sub $window-col 62
        !endif
        set-variable $window-col 62
    !else
        !if &gre $window-col 10
            set-variable #l5 &sub #l5 &div &sub $window-col 10 3
        !endif
        set-variable $window-col 10
    !endif
    &div &add #l5 15 16 backward-line
    set-variable #l5 &sub 15 &mod &add #l5 15 16
    !if &gre $window-col 57
        #l5 forward-char
    !else
        &mul #l5 3 forward-char
    !endif
!emacro       

define-macro binary-move-forward
    set-variable #l5 @ml "Move forward by"
    calc #l5
    set-variable #l5 .calc.result
    !if &gre $window-col 57
        !if &gre $window-col 62
            set-variable #l5 &add #l5 &sub $window-col 62
        !endif
        set-variable $window-col 62
    !else
        !if &gre $window-col 10
            set-variable #l5 &add #l5 &div &sub $window-col 10 3
        !endif
        set-variable $window-col 10
    !endif
    &div #l5 16 forward-line
    set-variable #l5 &mod #l5 16
    !if &gre $window-col 57
        #l5 forward-char
    !else
        &mul #l5 3 forward-char
    !endif
!emacro       

define-macro binary-swap-section
    !if &gre $window-col 9
        !if &les $window-col 62
            set-variable $window-col &add 62 &div &sub $window-col 10 3
        !else
            set-variable $window-col &add 10 &mul &sub $window-col 62 3
        !endif
    !endif
!emacro
    
define-macro fhook-binary
    !if &not &bmod "binary"
        !if &seq $buffer-fname ""
            ml-write "[Buffer has no file name]"
            !abort
        !endif
        !if &bmod "edit"
            ml-write "[Buffer has been changed]"
            !abort
        !endif
        beginning-of-buffer
        set-mark
        end-of-buffer
        kill-region
        -1 yank
        1 buffer-mode "binary"
        insert-file $buffer-fname
        -1 buffer-mode "edit"
        beginning-of-buffer
    !endif
    buffer-initialize
    ; execute user extensions if enabled
    !if &exist my-fhook-binary
        my-fhook-binary
    !endif
!emacro
set-variable .fhook-binary.setup #l0
set-variable .fhook-binary.setup-mask #l1
set-variable .fhook-binary.command-flag  "|thbio|th|thbo|thbo|"
set-variable .fhook-binary.command-name  "|binary-swap-section||binary-move-backward|binary-move-forward|"
set-variable .fhook-binary.command-nbind "|||||"
set-variable .fhook-binary.command-kbind "|tab||A-up|A-down|"
set-variable .fhook-binary.command-desc  "|&Swap section||Move &backward|Move &forward|"

ml-write "[Binary file hook loaded]"

; load in user extensions if found
!force execute-file "mybinary"
