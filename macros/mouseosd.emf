; -!- emf -!-
; This is part of the JASSPA MicroEmacs macro files
; Copyright (C) 1999-2002 JASSPA (www.jasspa.com)
; See the file me.emf for copying and conditions.
;
; Created:     Sat Jun 13 1998
; Synopsis:    Defines mouse opened dialogs
; Authors:     Steven Phillips
;
; define some external macros which are used first.
0 define-macro-file osd osd-1-buffer osd-close
0 define-macro-file search osd-line-scheme
;
; MENU #23 - MODE LINE pop-up menu
; The mode line pop-up menu is invoked when the mouse is right clicked
; on the mode line
;
osd 23 0  b
osd 23 1  "M"  "Buffer &Modes" 24
osd 23 2  ""   "&Save Buffer"   f save-buffer    ; Save the buffer
osd 23 3  ""   "&Close Buffer"  f osd-close      ; Close the buffer
osd 23 4  "Md" "&Buffer"       20 osd-1-buffer   ; Set up the buffers
osd 23 5  "Md" "&Quick Open"   19                ; Open a file. 
osd 23 6  "M"  "S&croll Bars"  25
;
; MENU #24 - BUFFER MODES pop-up menu
; Show the buffer modes. These are check items on the popup menu, a callback
; macro is required to ascertain and set the state of each of the menu items.
;
0 define-macro osd-24-buffer-mode
    ; Pick up the name of the mode from $result generated by the caller.
    ; If this is a TRUE argument then toggle the current buffer
    ; mode to the new mode
    !if &les @# 0
        0 buffer-mode $result
    !elif &not &bmod $result
        ; Return the status of the call. Return FALSE if the mode not set
        ; otherwise true.
        ; osd 24 1 "CxH" .osd.revblue &cat "^^^^^^" $result 1 "osd-24-buffer-mode"
        osd 24 1 "Cx" &cat "^^^^^^" $result f "osd-24-buffer-mode"
        !abort
    !endif
    osd 24 1 "CxH" .scheme.osd-ebtt &cat "^^^^^^" $result f "osd-24-buffer-mode"
!emacro

osd 24 0 "bA"
set-variable $mode-names ".*"
!while &not &seq &set #l0 $mode-names ""
    osd 24 1 "Cx" &cat "^^^^^^" #l0 f "osd-24-buffer-mode"
!done
;
; Menu #25 - SCROLL BAR pop-up menu
; Enable the scroll bar modes to be toggled on and off, and changes the
; sizes of the scroll columns.
;
0 define-macro osd-25-scroll-bars
    ; If the argument is true then change the scroll bar mode.
    !if &equ @# 0
        !if @?
            set-variable $scroll-bar &bxor $scroll-bar 0x100
;            !if &not &band $scroll-bar 0x10
;                set-variable $scroll-bar &bor $scroll-bar 0x10
;            !else
;                set-variable $scroll-bar &band $scroll-bar &bnot 0x10
;            !endif
        !endif
        !if &not &band $scroll-bar 0x10
            !abort
        !endif
    !else
        !if @?
            set-variable $scroll-bar &bxor $scroll-bar 0x01
        !endif
        !if &not &band $scroll-bar 0x01
            !abort
        !endif
    !endif
!emacro

0 define-macro osd-find-help
    !force help-item @y
    !if &not $status
        man @y
    !endif
!emacro
osd 25 0 b
osd 25 1 "" "&Enabled"  0 osd-25-scroll-bars
osd 25 2 "" "&Wide"     1 osd-25-scroll-bars
;
; MENU 27 - EDIT POPUP - Generic popup sub-menu
;
0 define-macro osd-mouse-yank
    !if .mouse-event.msshift
        -1 yank
        -1 kill-region
    !endif
    yank @mna
    set-variable @cc yank
!emacro

; NOTE Any item number changes may effect mouse-osd-multi
osd 27 0  "bB"
osd 27 1  ""   "Cu&t"              f kill-region    ; Kill off the region
osd 27 2  ""   "&Copy"             f copy-region    ; Copy the region
osd 27 3  ""   "&Paste"            f osd-mouse-yank ; Paste text
osd 27 4  "-"
osd 27 5  "i"  "&Find tag"         1 "3 find-tag @y"
osd 27 6  ""   "Find &help"        f osd-find-help
osd 27 7  "Md" "Sort &Lines"      28
osd 27 8  ""   "Restyle &Region "  f restyle-region
osd 27 9  "Md" "H&ilight Line"     .osd.tmp osd-line-scheme
osd 27 10  "-"
osd 27 11 ""   "Spell Buffer"      f spell-buffer
osd 27 12 ""   "&Save Buffer"      f save-buffer    ; Save the buffer
osd 27 13 ""   "&Close Buffer"     f osd-close      ; close the buffer
osd 27 14 "Md" "&Buffer"          20 osd-1-buffer   ; Set up the buffers
osd 27 15 "Md" "&Quick Open"      19                ; Open a file. 
;
; MENU 28 - Sort lines.
; Simple menu for options for the sort lines item. Used in the Pop-up menus.
;
osd 28 0  b
osd 28 1  "" "&Case Sensitive"       f sort-lines
osd 28 2  "" "&Ignore Case"          f sort-lines-ignore-case
;
; The following is the osd behaviour determined by the position of the mouse
define-macro mouse-osd-multi
    set-variable #l0 &band $mouse-pos 15
    !if &equ #l0   0
        ; Text window - must goto the original position or cut etc wont work - must dup the position
        0 show-region
        set-variable #l2 $result
        goto-position "\x82"
        !if &and &gre $mouse-x 20 &les &sub $screen-width $mouse-x 33 
            osd 27 7  "Mdw" "Sort &Lines"      28
            osd 27 9  "Mdw" "H&ilight Line"    .osd.tmp osd-line-scheme
            osd 27 14 "Mdw" "&Buffer"          20 osd-1-buffer
            osd 27 15 "Mdw" "&Quick Open"      19
        !else
            osd 27 7  "Md"  "Sort &Lines"      28
            osd 27 9  "Md"  "H&ilight Line"    .osd.tmp osd-line-scheme
            osd 27 14 "Md"  "&Buffer"          20 osd-1-buffer
            osd 27 15 "Md"  "&Quick Open"      19
        !endif
        !if &band #l2 1
            !force show-region
        !endif
        27 osd
    !elif &equ #l0 1
        ; Message line
        void
    !elif &equ #l0 2
        ; Mouse on Mode line
        23 osd
    !elif &and &gre #l0 2 &les #l0 10
        ; Mouse on scroll bar
        25 osd
    !elif &equ #l0 10
        ; Mouse on corner
        23 osd
    !elif &equ #l0 11
        ; menu line
        0 osd
    !endif
    ; goto the original window
    1 goto-position "\x82"
!emacro

define-macro mouse-osd
    ; goto the original position
    goto-position "\x82"
    0 osd
!emacro

