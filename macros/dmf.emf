; -!- emf -!-
; This is part of the JASSPA MicroEmacs macro files
; Copyright (C) 2001-2009 JASSPA (www.jasspa.com)
; See the file me.emf for copying and conditions.
;
; Created:     Tue Aug 28 2001
; Synopsis:    Display matching brackets emacs style
; Authors:     Steven Phillips
;
; Notes:
;  user-setup may bind this to idle-pick event, this may cause some issues
;
;  Bits in arg n have the following meaning
;
;      0x01 Display fence (if not set nothing is done)
;      0x02 use pos 0x85 instead of 0x84 (internal)
;      0x04 Don't Jump when off screen
;      0x08 Jump when closing brase is off screen
;      0x10 Always jump when closing brase
;      0x20 Give preference close fence to left of cursor
;
;  if arg of 0 given the use value of variable .<fhook>.dmf if defined or
;  .display-matching-fence.dmf
define-macro display-matching-fence
    ; must preserve the show-region status
    0 show-region
    set-variable #l9 &band $result 0x07
    !if @#
    !elif &exi &set #l8 &spr ".%s.dmf" $buffer-fhook
        set-variable @# &ind #l8
    !else
        set-variable @# .dmf
    !endif
    !if &or &not @# &seq @wc ""
        ; not enabled or at the very end of the buffer
        !return
    !endif
    set-variable #l8 &cond &band @# 2 "\x87" "\x88"
    set-variable #l7 &cond &seq @cck "idle-pick" 0 -1
    set-variable #l6 &sin @cl "})]"
    set-position #l8
    set-variable #l0 $window-x-scroll
    set-variable #l1 $window-xcl-scroll
    set-variable #l2 $window-y-scroll
    !if &equ #l9 7
        show-region
    !elif &equ #l9 5
        4 show-region
    !endif
    !if &and &band @# 32 $window-col
        !if &sin &set #l3 &mid @wl &sub $window-col 1 1 "})]"
            backward-char
            set-variable #l4 &sub $cursor-x 1
        !elif &sin &set #l3 @wc "{([])}"
            set-variable #l4 $cursor-x
        !else
            !return
        !endif
    !elif &sin &set #l3 @wc "{([])}"
        set-variable #l4 $cursor-x
    !else
        !return
    !endif
    set-variable #l5 $cursor-y
    !force 0 goto-matching-fence
    !if &not $status
        goto-position #l8
        !if &equ #l9 5
            4 show-region
        !endif
        2 screen-update
        0x40 screen-poke #l5 #l4 .scheme.dmf #l3
        #l7 screen-update
        !return
    !endif
    3 screen-update
    !if &equ #l0 $window-x-scroll
        !if &equ #l1 $window-xcl-scroll
            !if &equ #l2 $window-y-scroll
                set-variable #l0 $cursor-y
                set-variable #l1 $cursor-x
                set-variable #l2 @wc
                !if &and #l6 &band @# 16
                    !if &equ #l9 7
                        show-region
                    !elif &equ #l9 5
                        4 show-region
                    !endif
                    2 screen-update
                    0x02 screen-poke #l0 #l1 .scheme.dmf #l2
                    0x00 screen-poke #l5 #l4 .scheme.dmf #l3
                    !force !force &neg $pause-time command-wait
                !endif
                goto-position #l8
                !if &equ #l9 7
                    show-region
                !elif &equ #l9 5
                    4 show-region
                !endif
                2 screen-update
                0x02 screen-poke #l0 #l1 .scheme.dmf #l2
                0x00 screen-poke #l5 #l4 .scheme.dmf #l3
                #l7 screen-update
                !return
            !endif
        !endif
    !endif
    !if &not &band @# 4
        !jump 2
    !elif &and #l6 &band @# 8
        !if &equ #l9 7
            show-region
        !elif &equ #l9 5
            4 show-region
        !endif
        2 screen-update
        7 display-matching-fence
        !force !force &neg $pause-time command-wait
    !endif
    goto-position #l8
    !if &equ #l9 7
        show-region
    !elif &equ #l9 5
        4 show-region
    !endif
    2 screen-update
    0x20 screen-poke #l5 #l4 .scheme.dmf #l3
    #l7 screen-update
!emacro
