; -!- emf -!-
; This is part of the JASSPA MicroEmacs macro files
; Copyright (C) 1999-2009 JASSPA (www.jasspa.com)
; See the file me.emf for copying and conditions.
;
; Created:     Tue Aug 10 1999
; Synopsis:    Favorite file list macros.
; Authors:     Steven Phillips
;
0 define-macro osd-favorites-add
    set-variable #l0 $buffer-bname
    set-variable #l1 $window-line
    set-variable #l2 $window-y-scroll
    set-variable #l3 $window-col
    set-variable #l4 $buffer-fname
    !force find-file .osd-favorites.fname
    1 buffer-mode "magic"
    beginning-of-buffer
    !force search-forward "^[ \t]*$"
    !while $status
        beginning-of-line
        set-mark
        forward-line
        -1 kill-region
        !force search-forward "^[ \t]*$"
    !done
    end-of-buffer
    set-variable #l5 &rep &rig #l4 &rsin "/" #l4 "\\" "\\\\"
    !if &les $window-line 18
        insert-string &spr "\\H%d " &add 1 &div $window-line 2
    !endif
    insert-string &spr "%s\n%s" #l5 #l4
    find-buffer #l0
    set-variable $window-line #l1
    set-variable $window-y-scroll #l2
    set-variable $window-col #l3
    !if .toolbar-favorites.update
        toolbar-favorites
    !endif
!emacro

0 define-macro osd-favorites-edit
    !force find-file .osd-favorites.fname
    beginning-of-buffer
!emacro

0 define-macro osd-favorites-sel
    !force find-file .osd-favorites.fname
    !if $status
        beginning-of-buffer
        !while @#
            !if &not &seq &trl @wl ""
                !if &and &dec @# 1 &not &seq @wl "-"
                    forward-line
                !endif
            !endif
            forward-line
        !done
        set-variable #l1 @wl
        -1 find-buffer $buffer-bname
        !force file-tool-exec-default #l1
        !if &not $status
            find-file #l1
        !endif
    !endif
!emacro

0 define-macro favorites-load
    !if &not &exi .osd-favorites.fname
        !if &seq &set .osd-favorites.fname &find $user-name ".eff" "ERROR"
            set-variable .osd-favorites.fname &spr "%s%s.eff" $user-path $user-name
        !endif
        set-variable .osd-favorites.fname &cat "file:" .osd-favorites.fname
    !endif
    !force find-file .osd-favorites.fname
    !if &not $status
        ml-write &spr "[Failed to load %s]" #l0
        !abort
    !endif
    1 buffer-mode "hide"
    beginning-of-buffer
!emacro

0 define-macro toolbar-favorites-pick
    set-variable .toolbar-favorites-drop.line $window-line
    goto-position "\x82"
!emacro

0 define-macro toolbar-favorites-drop
    !if &equ .line $window-line
        !if &not &seq @wl ""
            set-variable #l0 $window-line
            goto-position "\x82"
            #l0 osd-favorites-sel
            !return
        !endif
    !endif
    goto-position "\x82"
!emacro

0 define-macro toolbar-favorites
    set-position "\x86"
    !force !force favorites-load
    !if &not $status
        goto-position "\x86"
        !return
    !endif
    set-variable #l0 $buffer-bname
    find-buffer "*favorites*" 
    -1 buffer-mode "undo"
    -1 buffer-mode "view"
    beginning-of-buffer
    set-mark
    end-of-buffer
    -1 kill-region
    find-buffer #l0
    !while &not &seq @wc ""
        !if &not &seq &trl @wl ""
            !if &seq @wl "-"
                set-variable #l1 "\n"
            !else
                set-variable #l1 &xrep @wl "\\\\\\(H\\|\\(.\\)\\)" "\\2"
                forward-line
            !endif
            find-buffer "*favorites*"
            end-of-buffer
            insert-string #l1
            find-buffer #l0
        !endif
        forward-line
    !done
    find-buffer "*favorites*" 
    beginning-of-buffer
    set-variable :mouse-pick-1 toolbar-favorites-pick
    set-variable :mouse-drop-1 toolbar-favorites-drop
    -1 buffer-mode "edit"
    1 buffer-mode "view"
    goto-position "\x86"
    set-variable .update 1
!emacro

0 define-macro osd-favorites-set
    favorites-load
    -1 osd @#
    osd @# 0 "boI" 0 -3 10
    !if &or &seq &lef $buffer-bname 1 "*" &seq $buffer-fname ""
        osd @# 1 "S" "Add"
    !else
        osd @# 1 "" "\HAdd" f osd-favorites-add
    !endif
    osd @# 2 "" "\HEdit  "  f osd-favorites-edit
    osd @# 3  "-"
    set-variable #l0 1
    !while &not &seq @wc ""
        !if &not &seq &trl @wl ""
            !if &seq @wl "-"
                osd @# &mul &pinc #l0 1 10 "-"
            !else
                execute-line &spr "set-variable #l1 \"%s\"" @wl 
                osd @# &mul #l0 10 "" #l1 &pinc #l0 1 osd-favorites-sel
                forward-line
            !endif
        !endif
        forward-line
    !done
    !if &equ #l0 1
        osd @# 10 "" "<no favorites>"
    !endif
!emacro

0 define-macro osd-favorites
    set-position "\x81"
    !force @# osd-favorites-set
    goto-position "\x81"
!emacro


