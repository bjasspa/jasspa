; -!- emf -!-
; This is part of the JASSPA MicroEmacs macro files
; Copyright (C) 2002 JASSPA (www.jasspa.com)
; See the file me.emf for copying and conditions.
;
; Created:     Thu May 30 2002
; Synopsis:    OSD Find Buffer menu using File Types
; Authors:     Detlef Groth & Steven Phillips
;
!if &not &exi .osd.find-buffer
    set-variable .osd.find-buffer &pinc .osd.next 1
!endif

0 define-macro osd-find-buffer
    !if &equ @# .osd.find-buffer
        ; called as a sub menu
        osd .osd.find-buffer 0 "bo" 0 -3
        !return
    !endif
    !if &band @# 1
        osd .osd.find-buffer 0 "ba" &add $cursor-x 1 &sub $cursor-y 3
    !else
        osd .osd.find-buffer 0 "bo" 0 -3
    !endif
    .osd.find-buffer osd
!emacro
0 define-macro osd-find-buffer-submenu
    !if &seq $result "* Buffers"
        set-variable #l0 "\\*.*"
    !elif &seq $result "All Buffers"
        set-variable #l0 ".*"
    !else
        !if &not &set #l1 &lfi &reg "/history" "file-type" "\b" $result
            !abort
        !endif
        set-variable #l0 &lget &reg "/history" "file-mask" "\b" #l1
        filemask-to-regex #l0
        set-variable #l0 &cat &rep #l0 "," "\\(<[0-9]+>\\)?\\|" "\\(<[0-9]+>\\)?"
    !endif
    -1 osd .osd.tmp
    osd .osd.tmp 0 "b"
    set-variable #l1 0
    set-variable $buffer-names ".*" 
    !while &not &seq &set #l2 $buffer-names "" 
        !if &not &nbmod #l2 "hide"
            !if &xis #l2 #l0
                set-variable :fileflag &inc :fileflag 1
                osd .osd.tmp &inc #l1 1 "" #l2 f osd-find-buffer-select
            !endif
        !endif
    !done
    !if &not #l1
        osd .osd.tmp 1 "" "None found"
    !endif
!emacro

0 define-macro osd-find-buffer-select
    find-buffer $result
!emacro

-1 osd .osd.find-buffer
osd .osd.find-buffer 0 "bo" 0 -3
osd .osd.find-buffer 1 "M" "All Buffers" .osd.tmp osd-find-buffer-submenu
osd .osd.find-buffer 2 "M" "&* Buffers"  .osd.tmp osd-find-buffer-submenu
osd .osd.find-buffer 3 "-"  
set-variable #l0 &reg "/history" "file-type" "\b"
set-variable #l1 0
!while &not &seq &set #l2 &lget #l0 &inc #l1 1 ""
    osd .osd.find-buffer &add #l1 3 "M" #l2 .osd.tmp osd-find-buffer-submenu
!done
    

