;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;
;
;  Created By    : Steven Phillips
;  Created       : Fri Aug 10 10:28:53 2001
;  Last Modified : <010810.1351>
;  Description   : Create a list of abbreviations
;
;  Notes
;
;  Copyright (c) 2001 Jasspa
;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;

!if &band .hilight.flags 0x01 
    !if &not &exist .hilight.abbrev-item
        set-variable .hilight.abbrev-item &pinc .hilight.next 1
    !endif
    0 hilight .hilight.abbrev-item 0 $global-scheme
    hilight .hilight.abbrev-item 0x40 ":EXSTR:.*" "" .scheme.hide
    hilight .hilight.abbrev-item 0x44 "\ecB" "" "\ecA" "" "" .scheme.function
    hilight .hilight.abbrev-item 0x44 "\ecC" "" "\ecA" "" "" .scheme.keyword
    hilight .hilight.abbrev-item 0x44 "\ecD" "" "\ecA" "" "" .scheme.operator
    hilight .hilight.abbrev-item 0x44 "\ecE" "" "\ecA" "" "" .scheme.variable
    hilight .hilight.abbrev-item 0x44 "\ecF" "" "\ecA" "" "" .scheme.string
!endif

0 define-macro abbrev-list-add
    !force set-variable #l0 @3
    !if $status
        goto-position "\x85"
        insert-string &spr "%s:EXSTR:%s\n" #l0 @2
        set-position "\x85"
        goto-position "\x84"
    !endif
!emacro

0 define-macro abbrev-list-load
    set-variable #l0 &rig $buffer-fhook 6
    !force 0 find-buffer "*abbrev-list*" 
    !if $status
        !if &seq :hook #l0
            !return
        !endif
        change-buffer-name &cat "*abbrev-list*" :hook
        1 buffer-mode "hide"
        -1 find-buffer $buffer-bname
    !endif
    !force 0 find-buffer &cat "*abbrev-list*" #l0
    !if $status
        change-buffer-name "*abbrev-list*"
        -1 buffer-mode "hide"
    !endif
    ml-write &spr "[Loading abbrev file \"%s.eaf\"]" #l0 
    !force 0 find-file &find #l0 ".eaf"
    !if &not $status
        !abort
    !endif
    beginning-of-buffer
    set-position "\x84"
    !force 0 delete-buffer "*abbrev-list*"
    find-buffer "*abbrev-list*"
    -1 buffer-mode "undo"
    set-position "\x85"
    goto-position "\x84"
    !while &not &seq @wl ""
        !if &seq &lef @wl 1 " "
            execute-line &cat "abbrev-list-add A " @wl
        !else
            execute-line &cat "abbrev-list-add " @wl
        !endif
        forward-line
        set-position "\x84"
    !done
    !force 0 delete-buffer $buffer-bname
    goto-position "\x85"
    beginning-of-buffer
    -1 buffer-mode "edit"
    1 buffer-mode "view"
    set-variable :hook #l0
    set-variable $buffer-hilight .hilight.abbrev-item
    set-variable :mouse-pick-1 void
    set-variable :mouse-drop-1 abbrev-list-execute
!emacro

define-macro abbrev-list
    !if &not &sin "b" &ind &spr ".%s.setup-mask" $buffer-fhook
        ml-write "[Abbreviations not supported in current buffer]"
        !abort
    !endif
    set-position "\x83"
    !force abbrev-list-load
    !if &not $status
        goto-position "\x83"
        ml-write "[Failed to load abbreviation list for current buffer]"
        !abort
    !endif
    goto-position "\x83"
    popup-window "*abbrev-list*"
    goto-position "\x83"
!emacro

define-macro abbrev-list-execute
    set-variable #l0 &rig @wl &add &sin ":EXSTR:" @wl 6
    goto-position "\x82"
    execute-string #l0
!emacro
