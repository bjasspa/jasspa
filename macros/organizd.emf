; -!- emf -!-
; This is part of the JASSPA MicroEmacs macro files
; Copyright (C) 1998-2009 JASSPA (www.jasspa.com)
; See the file me.emf for copying and conditions.
;
; Created:     Sat Jul 26 1997
; Synopsis:    Organizer day page macros.
; Authors:     Steven Phillips
;
; Key binding definitions
set-variable .org.bind-k2 "|C-left|C-right|C-up|C-down|" 
set-variable .org.bind-c2 "|-1 org-day-inc-day|1 org-day-inc-day|-1 org-day-inc-year|1 org-day-inc-year|" 
set-variable .org.bind-r2 "|1|1|1|1|" 

0 define-macro org-day-init
    set-variable .org.page 2
    set-variable .org.week 0
    !if &not .org.day
        set-variable .org.day 1
    !endif
    org-cal-info
    ; create the month selection menu
    osd .osd.org-m 0 "b"
    set-variable #l1 0
    !while &not &equ &inc #l1 1 13
        osd .osd.org-m #l1 "" &lget .org.months #l1 #l1 org-day-change-month
    !done
    ; set teh top line
    osd .osd.orgd 50 "fh" &spr "      %s " &lget .org.days &add 1 &mid .org.info 4 1
    osd .osd.orgd 70 "Mdtmxsfh" &lget .org.months .org.month .osd.org-m
!emacro     

0 define-macro org-day-change-year
    !if &les @# 0
        set-variable #l0 @ml2 "" .org.year
        !if &and &equ #l0 0 &not &seq #l0 "*"
            osd-dialog "Organizer" "Error: Invalid year!" "  \HOK  "
            !return
        !endif
        set-variable .org.year #l0
        !if &equ .org.month 2
            !if &gre .org.day 28
                org-leap-year
                set-variable .org.day &add 28 #l9
            !endif
        !endif
        org-day-init
    !else
        set-variable $result .org.year
    !endif
!emacro

0 define-macro org-day-change-month
    set-variable .org.month @#
    !if &gre .org.day &lget .org.dinm .org.month 
        !if &equ .org.month 2
            org-leap-year
            set-variable .org.day &add 28 #l9
        !else
            set-variable .org.day &lget .org.dinm .org.month
        !endif
    !endif
    org-day-init
!emacro

0 define-macro org-day-change-day
    !if &les @# 0
        set-variable .org.day @ml2 "" .org.day
        !if &gre .org.day &lget .org.dinm .org.month 
            !if &equ .org.month 2
                org-leap-year
                set-variable .org.day &add 28 #l9
            !else
                set-variable .org.day &lget .org.dinm .org.month
            !endif
        !endif
        org-day-init
    !else
        set-variable $result .org.day
    !endif
!emacro


0 define-macro org-day-inc-year
    @# org-inc-year
    org-day-init
!emacro

0 define-macro org-day-inc-day
    set-variable .org.day &add .org.day @#
    !if &les .org.day 1
        set-variable .org.month &sub .org.month 1
        !if &les .org.month 1
            set-variable .org.month 12
            -1 org-inc-year
        !endif
        !if &equ .org.month 2
            org-leap-year
            set-variable .org.day &add 28 #l9
        !else
            set-variable .org.day &lget .org.dinm .org.month
        !endif
    !elif &gre .org.day &lget .org.dinm .org.month 
        !if &equ .org.month 2
            org-leap-year
            !if &gre .org.day &add 28 #l9
                !jump 3
            !endif
        !else
            set-variable .org.day 1
            set-variable .org.month &add .org.month 1
            !if &gre .org.month 12
                set-variable .org.month 1
                1 org-inc-year
            !endif
        !endif
    !endif
    org-day-init
!emacro

0 define-macro org-day-set-notes
    !if &les @# 0
        set-variable #l0 &reg &spr "/org/cal/%s/%s/%s" .org.year .org.month .org.day &reg &spr "/org/cal/*/%s/%s" .org.month .org.day ""
        set-variable #l1 @ml2 "" #l0
        !if &not &seq #l0 #l1
            !if &seq #l1 &reg &spr "/org/cal/*/%s/%s" .org.month .org.day ""
                delete-registry &spr "/org/cal/%s/%s/%s" .org.year .org.month .org.day #l1
            !elif &seq #l1 ""
                set-registry &spr "/org/cal/%s/%s/%s" .org.year .org.month .org.day " "
            !else
                set-registry &spr "/org/cal/%s/%s/%s" .org.year .org.month .org.day #l1
            !endif
            mark-registry "/org" "u"
        !endif
    !else
        set-variable $result &reg &spr "/org/cal/%s/%s/%s" .org.year .org.month .org.day &reg &spr "/org/cal/*/%s/%s" .org.month .org.day ""
    !endif
!emacro

0 define-macro org-day-buffer
    beginning-of-line
    insert-string &spr "       %s %d %s %d\n\n" &lget .org.days &add 1 &mid .org.info 4 1 .org.day &lget .org.months .org.month .org.year
    insert-string &spr "%s\n\n" &reg &spr "/org/cal/%s/%s/%s" .org.year .org.month .org.day &reg &spr "/org/cal/*/%s/%s" .org.month .org.day ""
    screen-update
!emacro

osd .osd.orgd 10 ""
osd .osd.orgd 20 "hf" "    "
osd .osd.orgd 30 "BtxfRhT" " \H<- " -1 org-day-inc-day
osd .osd.orgd 60 "Etxfh" "###" f org-day-change-day
osd .osd.orgd 75 "fh" " "
osd .osd.orgd 80 "Etxfh" "#####" f org-day-change-year
osd .osd.orgd 100 "BtxrfRhT" " -\H> "  1 org-day-inc-day
osd .osd.orgd 110 "rf" "    "
osd .osd.orgd 120 "" 
osd .osd.orgd 190 "Sf" "    \HNotes" 200 
osd .osd.orgd 200 "EtNcfHxz" .scheme.osd-entry 56 27 "" f org-day-set-notes
osd .osd.orgd 600 "" 
osd .osd.orgd 605 "" 
osd .osd.orgd 610 "BtxhcfH" .scheme.osd-ebtt " \HSave "    f organizer-save
osd .osd.orgd 620 "BtxhcfH" .scheme.osd-ebtt " Day to \HBuffer "  f org-day-buffer
osd .osd.orgd 630 "BtcfH"   .scheme.osd-ebtt " \HClose "   1 void

