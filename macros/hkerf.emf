; -!- emf -!-
; This is part of the JASSPA MicroEmacs macro files
; Copyright (C) 1999-2009 JASSPA (www.jasspa.com)
; See the file me.emf for copying and conditions.
;
; Synopsis:    erf hook - invoked when a MicroEmacs registry file is loaded.
; Authors:     Jon Green & Steven Phillips
;
define-macro fhook-erf
    set-variable $buffer-mask "luh1"
    @# buffer-init "erf"
    buffer-init-hooks
!emacro

; buffer-init variables
set-variable .fhook-erf.name "ME Registry"
set-variable .fhook-erf.setup &reg "/history/fhook/erf" "dfghiopx"
set-variable .fhook-erf.setup-mask "adefghikmoptux"
set-variable .fhook-erf.comment "|;||;|; | ;|r|"
set-variable .fhook-erf.indent-width 1

; setup erf collapse support
set-variable .fhook-erf.collapse-open  "^\".*{$"
set-variable .fhook-erf.collapse-close "^}"
set-variable .fhook-erf.collapse-mclose "1"
set-variable .fhook-erf.collapse-mnext "-1"

!if &not &exist .hilight.erf
    set-variable .hilight.erf &pinc .hilight.next 1
!endif

!if &and &sin "h" .fhook-erf.setup &band .hilight.flags 0x02 
    0 hilight  .hilight.erf 0              $global-scheme
    hilight .hilight.erf 4 "\"" "\"" "\\"  .scheme.keyword
    hilight .hilight.erf 0     "="         .scheme.operator
    hilight .hilight.erf 0x2   ";"         .scheme.comment ; Comment from SOL to EOL.
!endif

!if &sin "d" .fhook-erf.setup
    0 indent .hilight.erf 0 10
    indent .hilight.erf n "{"  t
    indent .hilight.erf o "}" -t
    indent .hilight.erf e "\"" "\"" "\\"
    indent .hilight.erf i ";"  
!endif

buffer-init-fhook "erf"

