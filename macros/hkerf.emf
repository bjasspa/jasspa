;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;
; erf hook - This file is invoked when a MicroEmacs registry file is loaded.
;
; Copyright (c) 1999-2001 JASSPA.
;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;

define-macro fhook-erf
    set-variable $buffer-mask "luh1"
    buffer-initialize
    ; execute user extensions if enabled
    !if &exist my-fhook-erf
        my-fhook-erf
    !endif
!emacro

; buffer-initialize variables
set-variable .fhook-erf.name "ME Registry"
set-variable .fhook-erf.setup &reg "/history" "fhook/erf" "dfhiopx"
set-variable .fhook-erf.setup-mask "adefhikmoptux"
set-variable .fhook-erf.comment "|;||;|; ||"

; setup erf folding support
set-variable .fhook-erf.fold-open  "^\".*{$"
set-variable .fhook-erf.fold-close "^}"
set-variable .fhook-erf.fold-mopen "1"

!if &not &exist .hilight.erf
    set-variable .hilight.erf &pinc .hilight.next 1
!endif

!if &and &sin "h" .fhook-erf.setup &band .hilight.flags 0x02 
    0 hilight  .hilight.erf 0              $global-scheme
    hilight .hilight.erf 4 "\"" "\"" "\\"  .scheme.keyword
    hilight .hilight.erf 0     "="         .scheme.operator
    hilight .hilight.erf 0x2   ";"         .scheme.comment ; Comment from SOL to EOL.
!endif

!if &sin "d" .fhook-erf.setup
    0 indent .hilight.erf 0 10
    indent .hilight.erf n "{"  1
    indent .hilight.erf o "}" -1
    indent .hilight.erf e "\"" "\"" "\\"
    indent .hilight.erf i ";"  
!endif

ml-write "[ME Registry file hook loaded]"

; load in user extensions if found
!force execute-file "myerf"

