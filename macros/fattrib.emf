; -!- emf -!-
; This is part of the JASSPA MicroEmacs macro files
; Copyright (C) 1997-2009 JASSPA (www.jasspa.com)
; See the file me.emf for copying and conditions.
;
; Synopsis:    OSD based file system file attribute setter.
; Authors:     Steven Phillips
;
!if &seq .osd.fattr "ERROR"
    set-variable .osd.fattr &pinc .osd.next 1
!endif

0 define-macro fattr-type
    set-variable #l0 &lget .file-attrib.info 4
    !if &les @# 0
        !if &gre #l0 -1
            set-variable .file-attrib.info &lset .file-attrib.info 4 &abs @#
        !endif
    !elif &not &equ @# #l0
        !abort
    !endif
!emacro

-1 osd .osd.fattr
osd .osd.fattr 0  "batcDHs" 10 3 50 0 -1 -1 610 .scheme.osd-title "File attributes"
osd .osd.fattr 10  ""
osd .osd.fattr 55  "fh" "      "
osd .osd.fattr 65  "fh" "  "
osd .osd.fattr 72  "fh" "  "
osd .osd.fattr 77  "fh" "  "
osd .osd.fattr 100 ""

0 define-macro fattr-attr
    !if &les @# 0
        ; set the mode
        set-variable .file-attrib.info &lset .file-attrib.info 2 &bxor &lget .file-attrib.info 2 &abs @#
    !elif &not &band &lget .file-attrib.info 2 @#
        !abort
    !endif
!emacro

osd .osd.fattr 110 ""   "  Attributes:"
!if &seq $platform "dos"
    osd .osd.fattr 115 "fh" "      "
    osd .osd.fattr 120 "CfxRpt" &cat .osd.checkbox-chars "\} \HRead-only"  1  fattr-attr
    osd .osd.fattr 125 "fh" "      "
    osd .osd.fattr 130 "CfxRpt" &cat .osd.checkbox-chars "\} \HHidden   "  2  fattr-attr
    osd .osd.fattr 135 "fh" "      "
    osd .osd.fattr 140 "CfxRpt" &cat .osd.checkbox-chars "\} S\Hystem   "  4  fattr-attr
    osd .osd.fattr 145 "fh" "      "
    osd .osd.fattr 150 "SCfxRp" &cat .osd.checkbox-chars "\} Directory"    16 fattr-attr
    osd .osd.fattr 155 "fh" "      "
    osd .osd.fattr 160 "CfxRpt" &cat .osd.checkbox-chars "\} \HArchive  "  32 fattr-attr
!elif &seq $platform "win32"
    osd .osd.fattr 115 "fh" "      "
    osd .osd.fattr 120 "CfxRpt" &cat .osd.checkbox-chars "\} \HRead-only "    1 fattr-attr
    osd .osd.fattr 125 "fh" "      "
    osd .osd.fattr 130 "CfxRpt" &cat .osd.checkbox-chars "\} \HHidden    "    2 fattr-attr
    osd .osd.fattr 135 "fh" "      "
    osd .osd.fattr 140 "CfxRpt" &cat .osd.checkbox-chars "\} S\Hystem    "    4 fattr-attr
    osd .osd.fattr 145 "fh" "      "
    osd .osd.fattr 150 "SCfxRp" &cat .osd.checkbox-chars "\} Directory "     16 fattr-attr
    osd .osd.fattr 155 "fh" "      "
    osd .osd.fattr 160 "CfxRpt" &cat .osd.checkbox-chars "\} \HArchive   "   32 fattr-attr
    osd .osd.fattr 165 "fh" "      "
    osd .osd.fattr 170 "CfxRpt" &cat .osd.checkbox-chars "\} \HNormal    "  128 fattr-attr
    osd .osd.fattr 175 "fh" "      "
    osd .osd.fattr 180 "CfxRpt" &cat .osd.checkbox-chars "\} \HTemporary "  256 fattr-attr
    osd .osd.fattr 185 "fh" "      "
    osd .osd.fattr 190 "CfxRpt" &cat .osd.checkbox-chars "\} Com\Hpressed" 2048 fattr-attr
!else
    osd .osd.fattr 120 ""
    osd .osd.fattr 130 "hf" "    User:   "
    osd .osd.fattr 140 "CfxRpth" &cat .osd.checkbox-chars "\} Read(\H1) "  256 fattr-attr
    osd .osd.fattr 150 "CfxRpth" &cat .osd.checkbox-chars "\} Write(\H2) " 128 fattr-attr
    osd .osd.fattr 160 "CfxRpt"  &cat .osd.checkbox-chars "\} Exec(\H3)  "  64 fattr-attr
    osd .osd.fattr 170 ""
    osd .osd.fattr 180 "hf" "    Group:  "
    osd .osd.fattr 190 "CfxRpth" &cat .osd.checkbox-chars "\} Read(\H4) "  32 fattr-attr
    osd .osd.fattr 200 "CfxRpth" &cat .osd.checkbox-chars "\} Write(\H5) " 16 fattr-attr
    osd .osd.fattr 210 "CfxRpt"  &cat .osd.checkbox-chars "\} Exec(\H6)  "  8 fattr-attr
    osd .osd.fattr 220 ""
    osd .osd.fattr 230 "hf" "    Global: "
    osd .osd.fattr 240 "CfxRpth" &cat .osd.checkbox-chars "\} Read(\H7) "  4 fattr-attr
    osd .osd.fattr 250 "CfxRpth" &cat .osd.checkbox-chars "\} Write(\H8) " 2 fattr-attr
    osd .osd.fattr 260 "CfxRpt"  &cat .osd.checkbox-chars "\} Exec(\H9)  " 1 fattr-attr
!endif

osd .osd.fattr 590 ""
osd .osd.fattr 595 ""
osd .osd.fattr 605 "hf" "  "
osd .osd.fattr 610 "BHrfhti" .scheme.osd-ebtt "  \HOK  "  f "set-variable .file-attrib.ok 1"
osd .osd.fattr 615 "hf" "  "
osd .osd.fattr 620 "BHrfht" .scheme.osd-ebtt " \HCancel " 0 void
osd .osd.fattr 625 "f"  "  "

; .info is a list "|<orig-file-mod>|<mod-file-mod>|<mod-file-type>|<mod-file-type>|"
define-macro file-attrib
    !if &band @# 1
        set-variable #l0 $buffer-fname
        !if &band $buffer-fmod 0x10000
            set-variable #l2 "-1"
        !else
            set-variable #l2 &add &add &bmod "cr" &mul &bmod "lf" 2 &mul &bmod "ctrlz" 4
        !endif
        set-variable .info &spr "|%d|%d|%d|%d|" $buffer-fmod $buffer-fmod #l2 #l2
        osd .osd.fattr 20  "" &cat "  Buffer:  " $buffer-bname
    !else
        !force set-variable #l0 @1
        !if &not $status
            set-variable #l0 @ml01 "File"
        !endif
        !if &seq #l0 ""
            !abort
        !elif &not &seq "L" &lget &set #l1 &stat i #l0 1
            ml-write "[Only local files supported]"
            !abort
        !elif &seq "X" &lget #l1 2
            ml-write "[No such file exists]"
            !abort
        !elif &seq "L" &lget #l1 2
            ml-write "[Symbolic links not supported]"
            !abort
        !endif
        set-variable .info &spr "|%d|%d|-1|-1|" &lget #l1 4 &lget #l1 4
        osd .osd.fattr 20  "" "  File: "
    !endif
    !if &not &set #l4 &len &set #l3 #l0
        set-variable #l3 "<no file name>"
    !elif &gre #l4 70
        set-variable #l3 &cat "..." &rig #l3 &sub #l4 67
    !endif
    osd .osd.fattr 30  "" &spr "    %s  " #l3
    !if &les &lget .info 3 0
        osd .osd.fattr 40  "D"
        osd .osd.fattr 50  "D"
        osd .osd.fattr 60  "D"
        osd .osd.fattr 70  "D"
        osd .osd.fattr 75  "D"
        osd .osd.fattr 80  "D"
        osd .osd.fattr 600 "D"
    !else
        osd .osd.fattr 40  ""
        osd .osd.fattr 50  "" "  File Type:"
        osd .osd.fattr 60  "CxfRpht" &cat .osd.checkbox-chars "\} \HUnix"    1 fattr-type
        osd .osd.fattr 70  "CxfRpht" &cat .osd.checkbox-chars "\} \HMac"     2 fattr-type
        osd .osd.fattr 75  "CxfRpht" &cat .osd.checkbox-chars "\} \HWindows" 3 fattr-type
        osd .osd.fattr 80  "CxfRpt"  &cat .osd.checkbox-chars "\} \HDos"     7 fattr-type
        osd .osd.fattr 600 "BHrfhti" .scheme.osd-ebtt " \HSave Changes "  f "set-variable .file-attrib.ok 2"
    !endif
    set-variable .ok 0
    !force .osd.fattr osd
    !if .ok
        ; file fmod
        !if &equ &lget .info 1 &set #l2 &lget .info 2
        !elif &les &lget .info 3 0
            0x200 file-op #l0 #l2
        !else
            set-variable $buffer-fmod #l2
            !if &band 0x100 $system
                &cond &band 1 #l2 1 -1 buffer-mode "view"
            !else
                &cond &band 148 #l2 -1 1 buffer-mode "view"
            !endif
        !endif
        ; file type
        !if &equ &lget .info 3 &set #l4 &lget .info 4
        !else
            &con &band 1 #l4 1 -1 buffer-mode "cr"
            &con &band 2 #l4 1 -1 buffer-mode "lf"
            &con &band 4 #l4 1 -1 buffer-mode "ctrlz"
        !endif
        !if &equ .ok 2
            0 save-buffer
        !endif
    !endif
!emacro
