;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;
;
; Last Modified:    <991125.1029>
;
; Comments:         Typically used for Microsoft Windows environments
;                   primarily for the initalisation files. The syntax is
;                   slightly more liberal that a true .ini file but allows
;                   the template to be used by slightly different file
;                   formats.
;
; Usage:            .ini - Microsoft Windows initialisation files
;                   .hpj - Microsoft help project files.
;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;
; buffer-setup flags
set-variable #l0 &reg "/history" "fhook/ini" "fhix"
set-variable #l1 "efhimtux"

!if &and &sin "h" #l0 &band .hilight.flags 0x02 
    !if &not &exist .hilight.ini
        set-variable .hilight.ini &pinc .hilight.next 1
    !endif
    0 hilight  .hilight.ini 0                     .scheme.keyword
    hilight .hilight.ini 0     "/"             $global-scheme
    hilight .hilight.ini 2     "="             $global-scheme
    hilight .hilight.ini 0     ":="            $global-scheme
    hilight .hilight.ini 2     "#"             .scheme.comment  ; Comment from SOL to EOL.
    hilight .hilight.ini 0x102 ";"             .scheme.comment  ; Comment from SOL to EOL.
    ; Obscure file format extensions
    hilight .hilight.ini 2     "//"            .scheme.comment  ; Comment to EOL.
    hilight .hilight.ini 20    "\\[" "]" ""    .scheme.string
    hilight .hilight.ini 1     "ROOT"          .scheme.variable
    hilight .hilight.ini 1     "USER"          .scheme.variable
    hilight .hilight.ini 1     "LOCAL"         .scheme.variable
    hilight .hilight.ini 1     "CURRENT"       .scheme.variable
!endif

define-macro fhook-ini
    set-variable $buffer-mask "luh1"
    buffer-modes-init .fhook-ini.setup
    !if &and &sin "h" .fhook-ini.setup &band .hilight.flags 0x02 
        set-variable $buffer-hilight .hilight.ini
    !endif
    fold-init .fhook-ini.setup
    ; execute user extensions if enabled
    !if &exist my-fhook-ini
        my-fhook-ini
    !endif
!emacro
set-variable .fhook-ini.setup #l0
set-variable .fhook-ini.setup-mask #l1

; setup ini file folding
set-variable .fhook-ini.fold-open  "^\\["
set-variable .fhook-ini.fold-close "^\\["
set-variable .fhook-ini.fold-mopen "1"
set-variable .fhook-ini.fold-mnext "-1"

ml-write "[ini file hook loaded]"

; load in user extensions if found
!force execute-file "myini"

