; -!- emf -!-
; This is part of the JASSPA MicroEmacs macro files
; Copyright (C) 1999-2009 JASSPA (www.jasspa.com)
; See the file me.emf for copying and conditions.
;
; Created:     Mon Sep 13 1999
; Synopsis:    Insert the date string into the current buffer. 
; Authors:     Steven Phillips
; Notes:
;   Format is compliant to standard strftime C function, the following
;   Codes are supported:
;
;     %%   % char
;     %Y   4 digit year
;     %y   2 digit year
;     %B   Month
;     %b   3 letter month
;     %m   2 digit month
;     %#m  1 digit month
;     %d   2 digit day
;     %#d  1 digit day
;     %A   Week Day
;     %a   3 letter week day
;     %H   2 digit 24 hour
;     %M   2 digit minute
;     %S   2 digit second
;
0 define-macro-file organize organizer-init

0 define-macro osd-insert-date-exec
    insert-string &rig $result 2
!emacro

0 define-macro osd-insert-date
    organizer-init
    set-variable #l0 $time
    ascii-time
    -1 osd @#
    osd @# 0 "b"
    osd @# 1 "" &spr "\HA %s" #l9 f osd-insert-date-exec
    set-variable #l4 &rep #l0 " " "0"
    set-variable #l1 0
    !while &not &seq &set #l2 &lget .formats &inc #l1 1 ""
        ;        012345678901234567890123456789
        ; $time "1999255 91312145 2220"
        set-variable #l2 &rep #l2 "%%" "\b"
        set-variable #l2 &rep #l2 "%Y" &lef #l0 4
        set-variable #l2 &rep #l2 "%y" &mid #l0 2 2
        set-variable #l2 &rep #l2 "%B" &lget .org.months &mid #l0 7 2
        set-variable #l2 &rep #l2 "%b" &lef &lget .org.months &mid #l0 7 2 3
        set-variable #l2 &rep #l2 "%m" &mid #l4 7 2
        set-variable #l2 &rep #l2 "%#m" &trb &mid #l0 7 2
        set-variable #l2 &rep #l2 "%d" &mid #l4 9 2
        set-variable #l2 &rep #l2 "%#d" &trb &mid #l0 9 2
        set-variable #l2 &rep #l2 "%A" &lget .org.days &add 1 &mid #l0 11 1
        set-variable #l2 &rep #l2 "%a" &lef &lget .org.days &add 1 &mid #l0 11 1 3
        set-variable #l2 &rep #l2 "%H" &mid #l4 12 2
        set-variable #l2 &rep #l2 "%M" &mid #l4 14 2
        set-variable #l2 &rep #l2 "%S" &mid #l4 16 2
        set-variable #l2 &rep #l2 "\b" "%"
        osd @# &add #l1 1 "" &spr "\H%s %s" &itoa &add #l1 65 #l2 f osd-insert-date-exec
    !done
!emacro

!if &seq .spell.language "enus"
    ; American style
    set-variable .osd-insert-date.formats "|%m/%d/%y|%m.%d.%y|%m/%d/%Y|%Y-%m-%d|%#d-%b-%y|%#d %b %y|%#d %b %Y|%#d %B %Y|%A, %#d %B %Y|%m/%d/%y %H:%M|%m/%d/%y %H:%M:%S|"
!else
    set-variable .osd-insert-date.formats "|%d/%m/%y|%d.%m.%y|%#d/%m/%Y|%Y-%m-%d|%#d-%b-%y|%#d %b %y|%#d %b %Y|%#d %B %Y|%A, %#d %B %Y|%d/%m/%y %H:%M|%d/%m/%y %H:%M:%S|"
!endif

